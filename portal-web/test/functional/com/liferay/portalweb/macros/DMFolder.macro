definition {

	var assetType = "Documents Folder";

	@summary = "Default summary"
	macro addCP(dmFolderDescription = null, dmFolder = null, siteName = null, dmFolderName = null) {
		var key_dmFolderName = ${dmFolderName};

		LexiconEntry.gotoAdd();

		AssertClick.assertTextClickAt(
			key_menuItem = "Folder",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Folder");

		AssertElementPresent.assertVisible(locator1 = "TextInput#NAME");

		PortletEntry.inputName(name = ${dmFolderName});

		if (isSet(dmFolderDescription)) {
			PortletEntry.inputDescription(description = ${dmFolderDescription});
		}

		Button.clickSave();

		if (${dmFolder} == "Duplicate") {
			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_1",
				value1 = "Please enter a unique folder name.");
		}
		else {
			Alert.viewSuccessMessage();
		}

		if (isSet(siteName)) {
			Navigator.openSiteURL(siteName = ${siteName});

			ProductMenu.gotoPortlet(
				category = "Content & Data",
				portlet = "Documents and Media",
				site = ${siteName});
		}

		DMFolder.viewEntryPresent(dmFolderName = ${dmFolderName});
	}

	@summary = "Default summary"
	macro addDMFolderLoginUserPG(viewableBy = null, dmFolderDescription = null, siteName = null, dmFolder = null, dmFolderName = null, userLoginFullName = null, userLoginEmailAddress = null, pageName = null) {
		User.logoutAndLoginPG(
			userLoginEmailAddress = "test@liferay.com",
			userLoginFullName = "Test Test");

		if (isSet(siteName)) {
			Navigator.gotoSitePage(
				pageName = ${pageName},
				siteName = ${siteName});
		}
		else {
			Navigator.gotoPage(pageName = ${pageName});
		}

		DMFolder.addPG(
			dmFolder = ${dmFolder},
			dmFolderDescription = ${dmFolderDescription},
			dmFolderName = ${dmFolderName},
			viewableBy = ${viewableBy});

		User.logoutAndLoginPG(
			userLoginEmailAddress = ${userLoginEmailAddress},
			userLoginFullName = ${userLoginFullName});
	}

	@summary = "Default summary"
	macro addPG(viewableBy = null, addOrRemoveViewPermission = null, roleTitle = null, dmFolderDescription = null, permissionDefinitionKeyList = null, dmFolder = null, dmFolderName = null) {
		if (isSet(contentPage)) {
			LexiconEntry.gotoAdd();
		}
		else {
			Portlet.clickPortletPlusIconPG(portletName = "Documents and Media");
		}

		AssertClick.assertTextClickAt(
			key_menuItem = "Folder",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Folder");

		AssertElementPresent.assertVisible(locator1 = "TextInput#NAME");

		PortletEntry.inputName(name = ${dmFolderName});

		PortletEntry.inputDescription(description = ${dmFolderDescription});

		if (isSet(viewableBy)) {
			Panel.expandPanel(panel = "Permissions");

			Select(
				locator1 = "Select#PERMISSIONS",
				value1 = ${viewableBy});

			if (isSet(permissionDefinitionKeyList)) {
				PermissionsInline.addOrRemoveEntryPermissionsFromAllRoles(
					addOrRemoveViewPermission = ${addOrRemoveViewPermission},
					key_roleTitle = ${roleTitle},
					permissionDefinitionKeyList = ${permissionDefinitionKeyList});
			}
		}

		Button.clickSave();

		if (${dmFolder} == "Duplicate") {
			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_1",
				value1 = "Please enter a unique folder name.");
		}
		else if (${dmFolder} == "Invalid") {
			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_1",
				value1 = "The folder name cannot contain the following invalid characters: \ // : * ? \" < > | ../ /...");
		}
		else {
			Alert.viewSuccessMessage();
		}
	}

	@summary = "Default summary"
	macro addPGViaMG(mgFolderName = null, mgFolderDescription = null) {
		Portlet.clickPortletPlusIconPG(portletName = "Media Gallery");

		AssertClick.assertTextClickAt(
			key_menuItem = "Folder",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Folder");

		AssertElementPresent.assertVisible(locator1 = "TextInput#NAME");

		PortletEntry.inputName(name = ${mgFolderName});

		PortletEntry.inputDescription(description = ${mgFolderDescription});

		PortletEntry.save();

		var key_mgFolderName = ${mgFolderName};

		DMFolder.viewEntryPresent(dmFolderName = ${mgFolderName});
	}

	@summary = "Default summary"
	macro assertExpectedFileNames(key_fileName = null) {
		var tempDir = PropsUtil.get("output.dir.name");

		var fileName = FileUtil.exists("${tempDir}/${key_fileName}");

		if (${fileName} == "false") {
			fail("Filename "${fileName}" was not expected.");
		}
	}

	@summary = "Default summary"
	macro assertExpectedFileNumber(key_folder = null, expectedFileNumber = null) {
		var tempDir = PropsUtil.get("output.dir.name");

		var folderPath = ${tempDir};

		if (isSet(key_folder)) {
			var folderPath = "${folderPath}${key_folder}";
		}

		var fileList = FileUtil.listFiles(${folderPath});

		var listSize = ListUtil.size(${fileList});

		if (${listSize} != ${expectedFileNumber}) {
			fail("Number of files on "${key_folder}" differ from expected.");
		}
	}

	@summary = "Default summary"
	macro cannotViewCP(dmFolderName = null) {
		var key_dmFolderName = ${dmFolderName};

		LexiconCard.viewCardNotPresent(card = ${dmFolderName});
	}

	@summary = "Default summary"
	macro configureWorkflowCP(workflowRestriction = null, workflowDefinition = null, dmDocumentTypeName = null, dmFolderName = null) {
		if (${dmFolderName} == "homeFolder") {
			Portlet.gotoPortletOptions(portletOption = "Edit");
		}
		else {
			LexiconEntry.gotoEntryMenuItem(
				menuItem = "Edit",
				rowEntry = ${dmFolderName});

			if (IsElementNotPresent.isNotVisible(locator1 = "Radio#WORKFLOW_PARENT_FOLDER")) {
				Click(locator1 = "DocumentsAndMediaEditFolder#WORKFLOW_DOCUMENT_TYPE_RESTRICTIONS_TOGGLE");
			}
		}

		if (${workflowRestriction} == "Parent Folder") {
			Click(locator1 = "Radio#WORKFLOW_PARENT_FOLDER");

			Button.clickSave();
		}
		else if (${workflowRestriction} == "Specific Restrictions") {
			Click(locator1 = "Radio#WORKFLOW_SPECIFIC_RESTRICTIONS");

			AssertClick(
				locator1 = "DocumentsAndMediaEditFolder#WORKFLOW_SPECIFIC_RESTRICTIONS_SELECT_DOCUMENT_TYPE_BUTTON",
				value1 = "Select Document Type");

			SelectFrame(value1 = "relative=top");

			SelectFrame(locator1 = "DocumentsAndMediaSelectDocument#SELECT_DOCUMENT_IFRAME");

			var key_dmDocumentTypeName = ${dmDocumentTypeName};

			AssertClick(
				locator1 = "DocumentsAndMediaSelectDocument#DOCUMENTS_TABLE_CHOOSE",
				value1 = "Choose");

			SelectFrameTop(value1 = "relative=top");

			Select(
				locator1 = "DocumentsAndMediaEditFolder#WORKFLOW_SPECIFIC_RESTRICTIONS_TYPE_WORKFLOW_SELECT",
				value1 = ${workflowDefinition});

			AssertClickNoError(
				key_text = "Save",
				locator1 = "Button#ANY",
				value1 = "Save");

			AssertConfirm(value1 = "You are making changes to the document types. If a document in this folder or its subfolders is currently in draft or pending workflow states, you may lose some or all of its metadata. A new version will be created for all other documents. Are you sure you wish to continue?");
		}
		else if (${workflowRestriction} == "Default Workflow") {
			if (!(isSet(homeFolder))) {
				Click(locator1 = "Radio#WORKFLOW_DEFAULT_WORKFLOW");
			}

			Select(
				locator1 = "Select#WORKFLOW_DEFAULT",
				value1 = ${workflowDefinition});

			PortletEntry.save();
		}
	}

	@summary = "Default summary"
	macro copy(depotName = null,targetFolderName = null,siteName = null,dmFolderName = null) {
		if (!(isSet(bulkCopy))) {
			LexiconEntry.gotoEntryMenuItem(
				menuItem = "Copy To",
				rowEntry = ${dmFolderName});
		}

		Alert.viewWarningSpecific(warningMessage = "The copying process may take some time. Only categories, tags, ratings, and document types in the global scope or asset libraries connected to both sites will be copied. Document type fields will be displayed empty.");

		AssertElementPresent(
			key_fieldLabel = "Copy From",
			locator1 = "FormFields#FIELD_DISABLED");

		AssertElementPresent(
			key_fieldLabel = "Copy To",
			locator1 = "FormFields#FIELD_DISABLED");

		AssertVisible(
			key_text = "Copy",
			locator1 = "Button#ANY_DISABLED");

		Button.clickSelect();

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		WaitForVisible(
			key_breadcrumbName = "Sites and Libraries",
			locator1 = "Breadcrumb#BREADCRUMB_ENTRY");

		if (isSet(siteName) || isSet(depotName)) {
			AssertClick(
				key_breadcrumbName = "Sites and Libraries",
				locator1 = "Breadcrumb#BREADCRUMB_ENTRY",
				value1 = "Sites and Libraries");
		}

		if (isSet(siteName)) {
			LexiconCard.clickCard(card = ${siteName});
		}

		if (isSet(depotName)) {
			AssertClick(
				key_groupTab = "Asset Library",
				locator1 = "ItemSelector#NAVIGATION_GROUP_TAB",
				value1 = "Asset Library");

			LexiconCard.clickCard(card = ${depotName});
		}

		if (isSet(homeFolder)) {
			AssertClick(
				key_text = "Select This Folder",
				locator1 = "Button#ANY",
				value1 = "Select This Folder");
		}
		else {
			AssertClick(
				key_dmFolderName = ${targetFolderName},
				locator1 = "DocumentsAndMedia#SELECT_RESOURCE_BUTTON",
				value1 = "Select");
		}

		SelectFrame(locator1 = "relative=top");

		Button.click(button = "Copy");

		if (isSet(copyDuplicatedDocument)) {
			Alert.viewErrorMessage(errorMessage = "A folder already exists with name ${dmFolderName}");
		}
		else if (isSet(copyToUnconnectedSite)) {
			Alert.viewErrorMessage(errorMessage = "The item is not copied because the site and asset library are not connected.");
		}
		else if (isSet(copyToStagingSite)) {
			Alert.viewErrorMessage(errorMessage = "Cannot copy to the live version of a site. Make your changes in staging and publish them to Live afterwards to make them public.");
		}
		else {
			Alert.viewSuccessMessage();
		}
	}

	@summary = "Default summary"
	macro deleteCP(dmFolderName = null) {
		var key_dmFolderName = ${dmFolderName};

		Click(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_VERTICAL_ELLIPSIS");

		if (isSet(recycleBinDisabled)) {
			MenuItem.clickNoError(menuItem = "Delete");

			AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

			Alert.viewSuccessMessage();
		}
		else {
			PortletEntry.clickMoveToRecycleBin();

			VerifyElementPresent(locator1 = "Message#SUCCESS");
		}
	}

	@summary = "Default summary"
	macro deletePG(dmFolderName = null) {
		var key_dmFolderName = ${dmFolderName};

		AssertTextEquals(
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_TITLE",
			value1 = ${dmFolderName});

		Check(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_CHECKBOX");

		Click(
			key_text = "trash",
			locator1 = "ManagementBar#ANY_ICON");

		VerifyElementPresent(locator1 = "Message#SUCCESS");

		AssertElementNotPresent(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_ICON");
	}

	@summary = "Default summary"
	macro downloadViaEllipsisMenu(dmFolderName = null) {
		Click(
			key_rowEntry = ${dmFolderName},
			locator1 = "Icon#ROW_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "Download");
	}

	@summary = "Default summary"
	macro editCP(dmFolderNameEdit = null, dmFolderName = null, dmFolderDescriptionEdit = null) {
		var key_dmFolderName = ${dmFolderName};

		DMFolder.viewEntryPresent(dmFolderName = ${dmFolderName});

		AssertElementPresent(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_VERTICAL_ELLIPSIS");

		Click(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "Edit");

		PortletEntry.inputName(name = ${dmFolderNameEdit});

		PortletEntry.inputDescription(description = ${dmFolderDescriptionEdit});

		PortletEntry.save();

		DMFolder.viewEntryPresent(dmFolderName = ${dmFolderName});
	}

	@summary = "Default summary"
	macro editPG(dmFolderNameEdit = null, dmFolderName = null, dmFolderDescriptionEdit = null) {
		var key_dmFolderName = ${dmFolderName};

		AssertTextEquals(
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_TITLE",
			value1 = ${dmFolderName});

		Click(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "Edit");

		PortletEntry.inputName(name = ${dmFolderNameEdit});

		PortletEntry.inputDescription(description = ${dmFolderDescriptionEdit});

		Button.clickSave();

		var key_dmFolderNameEdit = ${dmFolderNameEdit};

		Alert.viewSuccessMessage();

		AssertTextEquals.assertPartialText(
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_EDITED_TITLE",
			value1 = ${dmFolderNameEdit});
	}

	@summary = "Default summary"
	macro editPGViaMG(mgFolderNameEdit = null, mgFolderName = null, mgFolderDescriptionEdit = null) {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = ${mgFolderName});

		PortletEntry.inputName(name = ${mgFolderNameEdit});

		PortletEntry.inputDescription(description = ${mgFolderDescriptionEdit});

		PortletEntry.save();

		var key_mgFolderName = ${mgFolderName};

		DMFolder.viewEntryPresent(dmFolderName = ${mgFolderNameEdit});
	}

	@summary = "Default summary"
	macro gotoEditCP(folderName = null) {
		var key_dmFolderName = ${folderName};

		DMFolder.viewEntryPresent(dmFolderName = ${folderName});

		Click(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "Edit");
	}

	@summary = "Default summary"
	macro gotoEntry(dmFolderName = null) {
		LexiconCard.clickCard(card = ${dmFolderName});
	}

	@summary = "Default summary"
	macro gotoPGViaMG(mgFolderName = null) {
		var key_mgFolderName = ${mgFolderName};

		DMFolder.gotoEntry(dmFolderName = ${mgFolderName});

		AssertTextEquals(
			locator1 = "MediaGallery#BREADCRUMB",
			value1 = ${mgFolderName});
	}

	@summary = "Default summary"
	macro moveToFolderCP(moveSubfolder = null, dmFolderName1 = null, moveFailure = null, dmFolderName = null, dmFolderName2 = null) {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		var key_dmFolderName = ${dmFolderName};

		AssertElementPresent(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_TITLE");

		Click(
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_VERTICAL_ELLIPSIS",
			value1 = ${dmFolderName2});

		MenuItem.click(menuItem = "Move");

		SelectFrame.selectFrameNoLoading(locator1 = "IFrame#MODAL_BODY");

		var siteName = TestCase.getSiteName();

		echo("## * siteName: ${siteName}");

		if (${moveSubfolder} == "true") {
			AssertClick(
				key_breadcrumbName = ${siteName},
				locator1 = "BreadcrumbPortlet#BREADCRUMB_FOLDER",
				value1 = ${siteName});

			AssertClick(
				key_text = "Select This Folder",
				locator1 = "Button#ANY",
				value1 = "Select This Folder");

			SelectFrame(locator1 = "relative=top");

			if (${moveFailure} == "true") {
				AssertTextEquals.assertPartialText(
					locator1 = "Message#ERROR",
					value1 = "Error:The folder you selected already has an entry with this name. Please select a different folder.");
			}
			else {
				Alert.viewSuccessMessage();
			}
		}
		else {
			var key_dmFolderName = ${dmFolderName1};

			AssertClick(
				locator1 = "DocumentsAndMediaSelectFolder#SELECT_BUTTON",
				value1 = "Select");

			SelectFrame(locator1 = "relative=top");

			Alert.viewSuccessMessage();

			AssertElementNotPresent(
				key_dmFolderName = ${dmFolderName2},
				locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_TITLE");

			Click(
				key_dmFolderName = ${dmFolderName1},
				locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_TITLE",
				value1 = ${dmFolderName1});

			AssertElementPresent(
				key_dmFolderName = ${dmFolderName2},
				locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_TITLE");
		}
	}

	@summary = "Default summary"
	macro moveToRecycleBinCP(dmFolderName = null) {
		var key_dmFolderName = ${dmFolderName};

		DMFolder.viewEntryPresent(dmFolderName = ${dmFolderName});

		LexiconCard.checkCardCheckbox(card = ${dmFolderName});

		Click(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_VERTICAL_ELLIPSIS");

		PortletEntry.clickMoveToRecycleBin();

		VerifyElementPresent(locator1 = "Message#SUCCESS");

		DMFolder.viewEntryNotPresent(dmFolderName = ${dmFolderName});
	}

	@summary = "Default summary"
	macro removePermissions(roleTitle = null, dmFolder = null, permissionNameList = null) {
		DMFolder.viewFolderPermissionsPG(
			dmFolder = ${dmFolder},
			key_roleTitle = ${roleTitle});

		var key_roleTitle = ${roleTitle};

		for (var permissionName : list ${permissionNameList}) {
			ScrollWebElementIntoView(locator1 = "Permissions#CONTENT_PERMISSIONS_${permissionName}_CHECKBOX");

			Uncheck.uncheckNotVisible(locator1 = "Permissions#CONTENT_PERMISSIONS_${permissionName}_CHECKBOX");
		}

		PortletEntry.save();
	}

	@summary = "Default summary"
	macro selectFolders(dmFolderName = null) {
		for (var key_dmFolderName : list ${dmFolderName}) {
			Check.checkNotVisible(locator1 = "DocumentsAndMedia#ICON_FOLDER_CHECKBOX");
		}
	}

	@summary = "Default summary"
	macro subscribePG() {
		Click(locator1 = "Icon#INFO_DM");

		Click(locator1 = "Icon#SUBSCRIBE_BELL");

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro tearDownCP(site = null) {
		var key_dmDocumentTitle = "tree.png";

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Documents and Media",
			site = ${site});

		if (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			Click.clickNoMouseOverNoWaitForVisible(locator1 = "DocumentsAndMedia#TOOLBAR_SELECT_ALL_CHECKBOX");

			if (IsElementPresent(locator1 = "DocumentsAndMedia#ICON_DOCUMENT_CHECKBOX")) {
				Click.clickNoMouseOverNoWaitForVisible(locator1 = "DocumentsAndMedia#ICON_DOCUMENT_CHECKBOX");
			}

			if (IsElementPresent(locator1 = "DocumentsAndMedia#TOOLBAR_ACTIONS")) {
				Click(locator1 = "DocumentsAndMedia#TOOLBAR_ACTIONS");

				MenuItem.viewPresent(menuItem = "Delete");

				PortletEntry.clickMoveToRecycleBin();
			}
		}

		JSONRecycleBin.emptyRecycleBin();
	}

	@summary = "Default summary"
	macro tearDownPGViaMG(pageName = null) {
		Navigator.gotoPage(pageName = ${pageName});

		while (IsElementPresent(locator1 = "MediaGallery#ENTRIES_FOLDER")) {
			Click(locator1 = "MediaGallery#ENTRIES_FOLDER");

			AssertClick(
				locator1 = "MediaGallery#ENTRY_RECYCLE_BIN_ICON",
				value1 = "Delete");

			VerifyElementPresent(locator1 = "Message#SUCCESS");
		}

		JSONRecycleBin.emptyRecycleBin();
	}

	@summary = "Default summary"
	macro viewCP(displayStyle = null, dmFolderName = null, specificNameFolder = null) {
		var key_dmFolderName = ${dmFolderName};

		if (IsElementPresent(key_displayStyle = "list", locator1 = "Icon#CURRENT_DISPLAY_STYLE_ACTIVE")) {
			AssertElementPresent(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_ICON");

			AssertTextEquals(
				locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_TITLE",
				value1 = ${dmFolderName});
		}
		else {
			DMFolder.viewEntryPresent(dmFolderName = ${dmFolderName});
		}

		if (${specificNameFolder} == "true") {
			var rowEntry = ${dmFolderName};

			AssertClick(
				locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_TITLE",
				value1 = ${dmFolderName});
		}
		else {
			LexiconEntry.gotoEntry(
				displayStyle = ${displayStyle},
				rowEntry = ${dmFolderName});
		}

		AssertTextEquals(
			locator1 = "DocumentsAndMedia#BREADCRUMB_FOLDER_NAME",
			value1 = ${dmFolderName});
	}

	@summary = "Default summary"
	macro viewDefaultCP() {
		AssertElementNotPresent(locator1 = "DocumentsAndMedia#ICON_DOCUMENT_GENERIC");

		AssertTextEquals.assertPartialText(
			locator1 = "Message#EMPTY_INFO",
			value1 = "There are no documents or media files in this folder.");
	}

	@summary = "Default summary"
	macro viewDetailsInListView(authorName = null, dmFolderName = null, contextualBreadcrumb = null) {
		var key_dmFolderName = ${dmFolderName};

		AssertTextEquals(
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_TITLE",
			value1 = ${dmFolderName});

		AssertElementPresent(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_ICON");

		var key_authorName = ${authorName};

		AssertElementPresent(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_AUTHOR_AND_DATE");

		var key_contextualBreadcrumb = ${contextualBreadcrumb};

		AssertTextEquals(
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_BREADCRUMB",
			value1 = ${contextualBreadcrumb});
	}

	@summary = "Default summary"
	macro viewEntryNotPresent(dmFolderName = null) {
		LexiconCard.viewCardNotPresent(card = ${dmFolderName});
	}

	@summary = "Default summary"
	macro viewEntryPresent(dmFolderName = null) {
		LexiconCard.viewCardPresent(card = ${dmFolderName});
	}

	@summary = "Default summary"
	macro viewFolderPermissionsPG(dmFolder = null) {
		var key_dmFolderName = ${dmFolder};
		var key_roleTitle = StringUtil.lowerCase(${key_roleTitle});

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		Click(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "Permissions");

		IFrame.selectPermissionsFrame();
	}

	@summary = "Default summary"
	macro viewNoSubscribe() {
		Click(locator1 = "Icon#INFO");

		AssertElementNotPresent(locator1 = "Icon#SUBSCRIBE");
	}

	@summary = "Default summary"
	macro viewPG(dmFolderName = null) {
		var key_dmFolderName = ${dmFolderName};

		AssertTextEquals.assertPartialText(
			locator1 = "DocumentsAndMedia#BREADCRUMB_FOLDER_NAME",
			value1 = ${dmFolderName});
	}

	@summary = "Default summary"
	macro viewPGViaMG(mgFolderName = null) {
		var key_mgFolderName = ${mgFolderName};

		DMFolder.gotoEntry(dmFolderName = ${mgFolderName});

		AssertTextEquals(
			locator1 = "MediaGallery#FOLDER_HEADER_TITLE",
			value1 = ${mgFolderName});
	}

}