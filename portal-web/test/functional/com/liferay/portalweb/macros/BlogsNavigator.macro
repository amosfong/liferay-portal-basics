definition {

	@summary = "Default summary"
	macro gotoAddEntry() {
		if (IsElementPresent(locator1 = "Blogs#ADD_BLOGS_ENTRY")) {
			Click(locator1 = "Blogs#ADD_BLOGS_ENTRY");
		}
		else {
			Click(
				key_text = "New Entry",
				locator1 = "Button#ANY");
		}

		WaitForPageLoad();

		AssertVisible(locator1 = "BlogsEntry#ENTRY_COVER_IMAGE_SELECT_FILE");
	}

	@summary = "Default summary"
	macro gotoBlogsEntriesPermissionsCP() {
		LexiconEntry.gotoHeaderMenuItem(menuItem = "Entries Permissions");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");
	}

	@summary = "Default summary"
	macro gotoBlogsEntryPermissionsCP(entryTitle = null) {
		LexiconCard.clickEllipsisItem(
			card = ${entryTitle},
			item = "Permissions");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");
	}

	@summary = "Default summary"
	macro gotoBlogsEntryPermissionsPG(entryTitle = null) {
		Portlet.mouseOverPortletTitle(portletTitleName = "Blogs");

		Blogs.clickEllipsisItemPG(
			entryTitle = ${entryTitle},
			item = "Permissions");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");
	}

	@summary = "Default summary"
	macro gotoBrowseImage() {
		WaitForPageLoad();

		Click(locator1 = "Button#BROWSE_IMAGE");
	}

	@summary = "Default summary"
	macro gotoConfigurationScopePG() {
		Navigator.gotoNavItem(navItem = "Scope");
	}

	@summary = "Default summary"
	macro gotoConfigurationSetupPG() {
		Portlet.gotoPortletOptions(portletOption = "Configuration");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");
	}

	@summary = "Default summary"
	macro gotoEditCP(entryTitle = null) {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		var key_entryTitle = ${entryTitle};

		Blogs.viewEntryTitle(entryTitle = ${entryTitle});

		Click(
			key_listEntry = ${entryTitle},
			locator1 = "LexiconList#LIST_ENTRY_ELLIPSIS");

		MenuItem.click(menuItem = "Edit");

		AssertElementPresent(locator1 = "Icon#BACK");

		AssertVisible(
			key_text = "Title",
			locator1 = "TextArea#ANY");
	}

	@summary = "Default summary"
	macro gotoEditPG(entryTitle = null) {
		var key_entryTitle = ${entryTitle};

		AssertTextEquals(
			locator1 = "BlogsEntry#TITLE",
			value1 = ${entryTitle});

		if (IsElementPresent(locator1 = "Button#EDIT_PENCIL")) {
			Click(locator1 = "Button#EDIT_PENCIL");
		}
		else {
			Portlet.mouseOverPortletTitle(portletTitleName = "Blogs");

			Blogs.clickEllipsisItemPG(
				entryTitle = ${entryTitle},
				item = "Edit");
		}

		AssertVisible(
			key_text = "Title",
			locator1 = "TextArea#ANY");
	}

	@summary = "Default summary"
	macro gotoEntryCP(entryStatus = null, entryTitle = null) {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		var key_entryTitle = ${entryTitle};

		if (isSet(entryStatus)) {
			AssertTextEquals(
				locator1 = "Blogs#DESCRIPTIVE_VIEW_ENTRY_STATUS",
				value1 = ${entryStatus});
		}

		LexiconList.clickListEntryTitle(listEntry = ${entryTitle});
	}

	@summary = "Default summary"
	macro gotoEntryPG(entryTitle = null, entryContent = null) {
		var key_entryContent = ${entryContent};
		var key_entryTitle = ${entryTitle};

		AssertTextEquals(
			locator1 = "BlogsEntry#TITLE",
			value1 = ${entryTitle});

		AssertTextEquals(
			locator1 = "BlogsEntry#CONTENT",
			value1 = ${entryContent});

		AssertClick.assertTextClickAt(
			locator1 = "BlogsEntry#TITLE_LINK",
			value1 = ${entryTitle});
	}

	@summary = "Default summary"
	macro gotoEntryWithAbstractPG(entryAbstractDescription = null, entryTitle = null) {
		var key_entryAbstractDescription = ${entryAbstractDescription};
		var key_entryTitle = ${entryTitle};

		AssertTextEquals(
			locator1 = "BlogsEntry#TITLE",
			value1 = ${entryTitle});

		AssertTextEquals.assertPartialText(
			locator1 = "BlogsEntry#CONTENT_ABSTRACT",
			value1 = ${entryAbstractDescription});

		AssertClick(
			locator1 = "BlogsEntry#TITLE_LINK",
			value1 = ${entryTitle});
	}

	@summary = "Default summary"
	macro gotoFriendlyURLHistory() {
		Click(locator1 = "Icon#FRIENDLY_URL_HISTORY");
	}

	@summary = "Default summary"
	macro gotoPG(blogEntryURL = null, entryTitle = null) {
		var key_entryTitle = ${entryTitle};

		if (isSet(blogEntryURL)) {
			AssertLocation(value1 = ${blogEntryURL});
		}
		else {
			Click(
				locator1 = "BlogsEntry#TITLE_LINK",
				value1 = ${entryTitle});
		}

		AssertTextEquals(
			locator1 = "BlogsEntry#TITLE",
			value1 = ${entryTitle});
	}

	@summary = "Default summary"
	macro gotoSelectFile() {
		WaitForElementPresent(
			locator1 = "BlogsEntry#ENTRY_COVER_IMAGE_SELECT_FILE",
			value1 = "Select File");

		AssertClick(
			locator1 = "BlogsEntry#ENTRY_COVER_IMAGE_SELECT_FILE",
			value1 = "Select File");
	}

	@summary = "Default summary"
	macro gotoShare(entryTitle = null) {
		WaitForLiferayEvent.initializeLiferayEventLog();

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Share",
			rowEntry = ${entryTitle});
	}

	@summary = "Default summary"
	macro moveToRecycleBinCP(entryTitle = null) {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		var key_entryTitle = ${entryTitle};

		Blogs.viewEntryTitle(entryTitle = ${entryTitle});

		Click(
			key_listEntry = ${entryTitle},
			locator1 = "LexiconList#LIST_ENTRY_ELLIPSIS");

		ClickNoError(
			key_menuItem = "Delete",
			locator1 = "MenuItem#DROPDOWN_MENU_ITEM");

		VerifyElementPresent(locator1 = "Message#SUCCESS");
	}

	@summary = "Default summary"
	macro openBlogsAdmin(baseURL = null, siteURLKey = null) {
		Navigator.openWithAppendToBaseURL(
			baseURL = ${baseURL},
			urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_blogs_web_portlet_BlogsAdminPortlet");
	}

	@summary = "Default summary"
	macro openBlogsImages(baseURL = null, siteURLKey = null) {
		Navigator.openWithAppendToBaseURL(
			baseURL = ${baseURL},
			urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_blogs_web_portlet_BlogsAdminPortlet&_com_liferay_blogs_web_portlet_BlogsAdminPortlet_navigation=images");
	}

	// You can use this navigation when you need to access the configuration quickly without checking the UI.

	@summary = "Default summary"
	macro openToAddEntry(siteURLKey = null, layoutName = null) {
		var baseURL = ${baseURL};

		if (!(isSet(baseURL))) {
			var baseURL = JSONCompany.getPortalURL();
		}

		// Entries accessed in site pages will be redirected back to the page after publishing. Those accessed in admin will stay in admin after publishing.

		if (isSet(layoutName)) {
			Open(value1 = "${baseURL}/group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_blogs_web_portlet_BlogsAdminPortlet&_com_liferay_blogs_web_portlet_BlogsAdminPortlet_mvcRenderCommandName=%2Fblogs%2Fedit_entry&_com_liferay_blogs_web_portlet_BlogsAdminPortlet_redirect=http%3A%2F%2F${baseURL}%3A8080%2Fweb%2F${siteURLKey}%2F${layoutName}");
		}
		else {
			Open(value1 = "${baseURL}/group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_blogs_web_portlet_BlogsAdminPortlet&_com_liferay_blogs_web_portlet_BlogsAdminPortlet_mvcRenderCommandName=%2Fblogs%2Fedit_entry");
		}
	}

	@summary = "Default summary"
	macro openToBlogEntry(siteURLKey = null) {
		var entryTitle = StringUtil.replace(${entryTitle}, " ", "-");

		var entryTitle = StringUtil.lowerCase(${entryTitle});
		var layoutName = StringUtil.replace(${layoutName}, " ", "-");

		var layoutName = StringUtil.lowerCase(${layoutName});
		var baseURL = ${baseURL};

		if (!(isSet(baseURL))) {
			var baseURL = JSONCompany.getPortalURL();
		}

		Open(value1 = "${baseURL}/web/${siteURLKey}/${layoutName}/-/blogs/${entryTitle}");
	}

	@summary = "Default summary"
	macro openToConfigInInstanceSettings(portlet = null) {
		var baseURL = ${baseURL};

		if (!(isSet(baseURL))) {
			var baseURL = JSONCompany.getPortalURL();
		}

		if (${portlet} == "Blogs Service") {
			var portletId = "com.liferay.blogs.configuration.BlogsGroupServiceConfiguration";
		}

		Open(value1 = "${baseURL}/group/control_panel/manage?p_p_id=com_liferay_configuration_admin_web_portlet_InstanceSettingsPortlet&_com_liferay_configuration_admin_web_portlet_InstanceSettingsPortlet_factoryPid=${portletId}&_com_liferay_configuration_admin_web_portlet_InstanceSettingsPortlet_mvcRenderCommandName=%2Fconfiguration_admin%2Fedit_configuration");
	}

	@summary = "Default summary"
	macro openToEditEntryInSite(groupName = null, site = null, siteURLKey = null, friendlyURL = null, entryTitle = null) {
		var entryId = JSONBlog.getBlogEntryId(
			entryTitle = ${entryTitle},
			friendlyURL = ${friendlyURL},
			groupName = ${groupName},
			site = ${site});
		var baseURL = ${baseURL};

		if (!(isSet(baseURL))) {
			var baseURL = JSONCompany.getPortalURL();
		}

		Open(value1 = "${baseURL}/group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_blogs_web_portlet_BlogsAdminPortlet&p_p_lifecycle=0&p_p_state=maximized&refererPlid=12&_com_liferay_blogs_web_portlet_BlogsAdminPortlet_mvcRenderCommandName=%2Fblogs%2Fedit_entry&_com_liferay_blogs_web_portlet_BlogsAdminPortlet_entryId=${entryId}");
	}

	@summary = "Default summary"
	macro openToNotPublishedTab(siteURLKey = null, layoutName = null) {
		Navigator.openWithAppendToBaseURL(urlAppend = "web/${siteURLKey}/${layoutName}?p_p_id=com_liferay_blogs_web_portlet_BlogsPortlet&_com_liferay_blogs_web_portlet_BlogsPortlet_mvcRenderCommandName=%2Fblogs%2Fview_not_published_entries");
	}

	@summary = "Default summary"
	macro openToStagingCP(groupName = null, siteURLKey = null) {
		if (!(isSet(site))) {
			var site = "true";
		}

		var stagingId = JSONGroupAPI._getGroupIdByName(
			groupName = ${groupName},
			site = ${site});
		var baseURL = ${baseURL};

		if (!(isSet(baseURL))) {
			var baseURL = JSONCompany.getPortalURL();
		}

		Open(value1 = "${baseURL}/group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_blogs_web_portlet_BlogsAdminPortlet&p_v_l_s_g_id=${stagingId}");
	}

}