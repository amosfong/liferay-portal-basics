definition {

	@summary = "Default summary"
	macro assertSuccessfulConnection() {
		ACDXPSettings.NavigateBetweenWizardSteps(wizardStepName = "Connect");

		AssertTextEquals(
			locator1 = "Message#SUCCESS",
			value1 = "Connected");
	}

	@summary = "Default summary"
	macro assertToggleOnOff(boolean = null, toggleType = null) {
		AssertElementPresent(
			key_boolean = ${boolean},
			key_toggleType = ${toggleType},
			locator1 = "ACUtils#TOGGLE_ICON_ON_OFF");
	}

	@summary = "Default summary"
	macro changeAssignModalPagination(itemsPerPage = null) {
		if (IsElementPresent(locator1 = "Message#SUCCESS_DISMISSIBLE")) {
			Click(locator1 = "Icon#CLOSE_ALERT");
		}

		Click(locator1 = "ACDXPSettings#ASSIGN_MODAL_PAGINATION");

		MenuItem.click(menuItem = ${itemsPerPage});
	}

	@summary = "Default summary"
	macro clickToAssignSitesAndChannel(propertyName = null) {
		Click(
			key_propertyName = ${propertyName},
			locator1 = "ACDXPSettings#SYNC_ANY_PROPERTIES");

		WaitForPageLoad();
	}

	@summary = "Default summary"
	macro clickToSelectAttributes(attributeCategory = null) {
		Click(
			key_attributeCategory = ${attributeCategory},
			locator1 = "ACDXPSettings#SELECT_ATTRIBUTES_BUTTON");

		WaitForPageLoad();
	}

	@summary = "Default summary"
	macro completeWizardConfiguration() {
		while (IsElementPresent(key_text = "Next", locator1 = "Button#ANY")) {
			Click(
				key_text = "Next",
				locator1 = "Button#ANY");
		}

		Click(
			key_text = "Finish",
			locator1 = "Button#ANY");
	}

	@summary = "Default summary"
	macro connectAnalyticsCloud(staticACToken = null, acToken = null, staticExistingSourceToken = null) {
		ACDXPSettings.disconnectDXPFromAnalyticsCloud();

		Click(locator1 = "ACDXPSettings#TOKEN_INPUT_FIELD");

		if (isSet(acToken)) {
			Type(
				locator1 = "ACDXPSettings#TOKEN_INPUT_FIELD",
				value1 = ${acToken});
		}
		else if (isSet(staticExistingSourceToken)) {
			Type(
				locator1 = "ACDXPSettings#TOKEN_INPUT_FIELD",
				value1 = ${staticExistingSourceToken});
		}
		else {
			Type(
				locator1 = "ACDXPSettings#TOKEN_INPUT_FIELD",
				value1 = ${staticACToken});
		}

		WaitForElementNotPresent(locator1 = "ACDXPSettings#CONNECT_BUTTON_DISABLED");

		Click(locator1 = "ACDXPSettings#CONNECT_BUTTON");

		WaitForVisible(
			key_text = "Next",
			locator1 = "Button#ANY");

		WaitForElementNotPresent(locator1 = "ACSettings#LOADING_ANIMATION");
	}

	@summary = "Default summary"
	macro connectDXPtoAnalyticsCloud(orgName = null, acToken = null, accountGroup = null, siteName = null, channelName = null, userGroup = null) {
		if (isSet(acToken)) {
			ACDXPSettings.connectAnalyticsCloud(acToken = ${acToken});
		}
		else {
			ACDXPSettings.connectAnalyticsCloud();
		}

		var assignedPropertyName = ACDXPSettings.getAssignedPropertyName();

		if (isSet(channelName)) {
			ACDXPSettings.syncChannel(
				channelName = ${channelName},
				propertyName = ${assignedPropertyName});
		}

		ACDXPSettings.syncSites(
			propertyName = ${assignedPropertyName},
			siteName = ${siteName});

		Click(
			key_text = "Next",
			locator1 = "Button#ANY");

		if (isSet(userGroup) || isSet(orgName) || isSet(contacts)) {
			ACDXPSettings.syncContactsData(
				orgName = ${orgName},
				userGroup = ${userGroup});
		}
		else if (isSet(accountGroup) || isSet(accounts)) {
			ACDXPSettings.syncAccountsData(accountGroup = ${accountGroup});
		}
		else {
			ACDXPSettings.syncContactsAndAccountstData();
		}

		Click(locator1 = "ACDXPSettings#AC_GENERIC_ADD_OR_CONFIRMATION_BUTTON");

		Click(locator1 = "ACDXPSettings#AC_GENERIC_ADD_OR_CONFIRMATION_BUTTON");

		return ${assignedPropertyName};
	}

	@summary = "Default summary"
	macro connectDXPtoAnalyticsCloudWithTryAgain(orgName = null, acToken = null, accountGroup = null, siteName = null, channelName = null, userGroup = null) {
		if (isSet(acToken)) {
			ACDXPSettings.connectAnalyticsCloud(acToken = ${acToken});
		}
		else {
			ACDXPSettings.connectAnalyticsCloud();
		}

		WaitForVisible(
			key_text = "Next",
			locator1 = "Button#ANY");

		WaitForElementNotPresent(locator1 = "ACSettings#LOADING_ANIMATION");

		if (IsElementPresent(key_text = "Try Again", locator1 = "Button#ANY")) {
			Click(
				key_text = "Next",
				locator1 = "Button#ANY");

			if (isSet(userGroup) || isSet(orgName) || isSet(contacts)) {
				ACDXPSettings.syncContactsData(
					orgName = ${orgName},
					userGroup = ${userGroup});
			}
			else if (isSet(accountGroup) || isSet(accounts)) {
				ACDXPSettings.syncAccountsData(accountGroup = ${accountGroup});
			}
			else {
				ACDXPSettings.syncContactsAndAccountstData();
			}

			Click(locator1 = "ACDXPSettings#AC_GENERIC_ADD_OR_CONFIRMATION_BUTTON");

			Click(locator1 = "ACDXPSettings#AC_GENERIC_ADD_OR_CONFIRMATION_BUTTON");

			Navigator.gotoNavTab(navTab = "Properties");

			var assignedPropertyName = ACDXPSettings.getAssignedPropertyName();

			if (isSet(channelName)) {
				ACDXPSettings.syncChannel(
					channelName = ${channelName},
					propertyName = ${assignedPropertyName});
			}

			ACDXPSettings.syncSites(
				propertyName = ${assignedPropertyName},
				siteName = ${siteName});
		}
		else {
			var assignedPropertyName = ACDXPSettings.getAssignedPropertyName();

			if (isSet(channelName)) {
				ACDXPSettings.syncChannel(
					channelName = ${channelName},
					propertyName = ${assignedPropertyName});
			}

			ACDXPSettings.syncSites(
				propertyName = ${assignedPropertyName},
				siteName = ${siteName});

			Click(
				key_text = "Next",
				locator1 = "Button#ANY");

			if (isSet(userGroup) || isSet(orgName) || isSet(contacts)) {
				ACDXPSettings.syncContactsData(
					orgName = ${orgName},
					userGroup = ${userGroup});
			}
			else if (isSet(accountGroup) || isSet(accounts)) {
				ACDXPSettings.syncAccountsData(accountGroup = ${accountGroup});
			}
			else {
				ACDXPSettings.syncContactsAndAccountstData();
			}

			Click(locator1 = "ACDXPSettings#AC_GENERIC_ADD_OR_CONFIRMATION_BUTTON");

			Click(locator1 = "ACDXPSettings#AC_GENERIC_ADD_OR_CONFIRMATION_BUTTON");
		}

		return ${assignedPropertyName};
	}

	@summary = "Default summary"
	macro createNewProperty(newPropertyNameList = null) {
		for (var newPropertyName : list ${newPropertyNameList}) {
			Click(
				ariaLabel = "Create a New Property",
				locator1 = "Button#ANY_WITH_ARIA_LABEL");

			Type(
				locator1 = "ACDXPSettings#NEW_PROPERTY_NAME",
				value1 = ${newPropertyName});

			Click(
				key_text = "Create",
				locator1 = "Button#ANY");

			WaitForElementNotPresent(locator1 = "Modal#MODAL_CONTENT");
		}
	}

	@summary = "Default summary"
	macro desyncAttributes(attributeCategory = null, attributeNameList = null) {
		if ((IsElementNotPresent(locator1 = "ACDXPSettings#AC_CONNECTION_NAV_SIDEBAR")) && (IsElementNotPresent(key_wizardStepName = "Attributes", locator1 = "ACDXPSettings#WIZARD_STEP_BUTTON"))) {
			ACDXPSettings.goToInstanceSettingsAC();

			ACDXPSettings.navToSyncedAttributes();
		}

		ACDXPSettings.clickToSelectAttributes(attributeCategory = ${attributeCategory});

		for (var attributeName : list ${attributeNameList}) {
			var key_attributeName = ${attributeName};

			ACUtils.searchBar(searchItem = ${attributeName});

			if (IsElementPresent(locator1 = "ACDXPSettings#CHECK_SELECT_ATTRIBUTES_ENABLE")) {
				Click.clickAtNotVisible(locator1 = "ACDXPSettings#SELECT_ATTRIBUTES_CHECKBOX");
			}
		}

		Click(
			key_text = "Sync",
			locator1 = "Button#ANY");

		WaitForElementNotPresent(locator1 = "ACSettings#LOADING_ANIMATION");
	}

	@summary = "Default summary"
	macro disconnectDXPFromAnalyticsCloud() {
		ACDXPSettings.goToInstanceSettingsAC();

		if (IsElementPresent(locator1 = "ACDXPSettings#DISCONNECT_BUTTON")) {
			ClickNoError(locator1 = "ACDXPSettings#DISCONNECT_BUTTON");

			Click(locator1 = "ACSettings#DISCONNECT_CONFIRMATION_BUTTON");
		}
	}

	@summary = "Default summary"
	macro getAssignedPropertyName() {
		WaitForVisible(
			key_text = "Assign",
			locator1 = "Button#ANY");

		static var assignedPropertyName = selenium.getText("ACDXPSettings#FIRST_AVAILABLE_PROPERTY");

		if (${assignedPropertyName} == "Beryl Commerce") {
			static var assignedPropertyName = selenium.getText("ACDXPSettings#SECOND_AVAILABLE_PROPERTY");
		}

		return ${assignedPropertyName};
	}

	@summary = "Default summary"
	macro getSyncNumberPreview(attributeCategory = null) {
		var key_syncFieldName = ${attributeCategory};
		var syncNumber = selenium.getText("ACDXPSettings#SYNC_NUMBER_PREVIEW");

		var syncNumber = StringUtil.extractDigits(${syncNumber});

		return ${syncNumber};
	}

	@summary = "Default summary"
	macro goToInstanceSettingsAC() {
		PortalSettings.openInstanceSettingsAdmin();

		Click(locator1 = "ACDXPSettings#ANALYTICS_CLOUD_DXP_OPTION");
	}

	@summary = "Default summary"
	macro NavigateBetweenWizardSteps(wizardStepName = null) {
		var key_wizardStepName = ${wizardStepName};

		Click(locator1 = "ACDXPSettings#WIZARD_STEP_BUTTON");
	}

	@summary = "Default summary"
	macro navToAnalyticsCloudConnectionMenu() {
		Click(locator1 = "ACDXPSettings#AC_CONNECTION_NAV_SIDEBAR");
	}

	@summary = "Default summary"
	macro navToSyncedAttributes() {
		Click(locator1 = "ACDXPSettings#SYNCED_ATTRIBUTES_DATA_NAV_SIDEBAR");
	}

	@summary = "Default summary"
	macro navToSyncedContactDataMenu() {
		Click(locator1 = "ACDXPSettings#SYNCED_CONTACT_DATA_NAV_SIDEBAR");
	}

	@summary = "Default summary"
	macro navToSyncedSitesMenu() {
		Click(locator1 = "ACDXPSettings#SYNCED_SITES_NAV_SIDEBAR");
	}

	@summary = "Default summary"
	macro searchACItemInAssignModal(searchItem = null) {
		Click(locator1 = "ACDXPSettings#SYNCED_SITES_SEARCH_FIELD");

		Type(
			locator1 = "ACDXPSettings#SYNCED_SITES_SEARCH_FIELD",
			value1 = ${searchItem});

		KeyPress(
			locator1 = "ACDXPSettings#SYNCED_SITES_SEARCH_FIELD",
			value1 = "\ENTER");
	}

	@summary = "Default summary"
	macro setFilterAndOrderInAssignModal(orderBy = null) {
		var menuItem = ${orderBy};

		Click.pauseClickAt(locator1 = "ACDXPSettings#ASSIGN_MODAL_FILTER_AND_ORDER");

		WaitForLiferayEvent.initializeLiferayEventLog();

		MenuItem.click(menuItem = ${menuItem});
	}

	@summary = "Default summary"
	macro setSortDirection(sortDirection = null) {
		if (IsElementNotPresent(key_sortDirection = ${sortDirection}, locator1 = "ACDXPSettings#SORT_DIRECTION")) {
			Click(locator1 = "ACDXPSettings#SORT_BUTTON");
		}
	}

	@summary = "Default summary"
	macro setSortDirectionInAssignModal(sortDirection = null) {
		if (IsElementNotPresent(key_sortDirection = ${sortDirection}, locator1 = "ACDXPSettings#ASSIGN_MODAL_SORT_DIRECTION")) {
			Click(locator1 = "ACDXPSettings#ASSIGN_MODAL_SORT_BUTTON");
		}
	}

	@summary = "Default summary"
	macro switchTab(tabName = null) {
		var key_tab = ${tabName};

		Click(locator1 = "Modal#TAB_LINK");
	}

	@summary = "Default summary"
	macro syncAccountsData(accountGroup = null) {
		if (IsElementPresent(key_panel = "Select Accounts", locator1 = "Panel#PANEL_COLLAPSED")) {
			Click(
				key_panel = "Select Accounts",
				locator1 = "Panel#PANEL_COLLAPSED");
		}

		if (isSet(accountGroup)) {
			Click(
				key_option = "Sync by Account Groups",
				locator1 = "ACDXPSettings#CONTACT_SYNC_OPTIONS");

			Click.clickAtNotVisible(
				key_entry = ${accountGroup},
				locator1 = "ACDXPSettings#SELECT_OPTION_CHECKBOX");

			Click(
				key_text = "Add",
				locator1 = "Button#ANY");

			Alert.viewSuccessMessage();
		}
		else {
			if (IsElementPresent(index = 3, locator1 = "ACDXPSettings#TOGGLE_ICON_ENABLE")) {
				Check.checkToggleSwitch(
					key_syncFieldName = "Sync All Accounts",
					locator1 = "ACDXPSettings#SYNC_TOGGLE_ICON");
			}

			Check.checkToggleSwitch(
				key_syncFieldName = "Sync All Accounts",
				locator1 = "ACDXPSettings#SYNC_TOGGLE_ICON");
		}
	}

	@summary = "Default summary"
	macro syncAttributes(attributeCategory = null, attributeNameList = null) {
		if ((IsElementNotPresent(locator1 = "ACDXPSettings#AC_CONNECTION_NAV_SIDEBAR")) && (IsElementNotPresent(key_wizardStepName = "Attributes", locator1 = "ACDXPSettings#WIZARD_STEP_BUTTON"))) {
			ACDXPSettings.goToInstanceSettingsAC();

			ACDXPSettings.navToSyncedAttributes();
		}

		ACDXPSettings.clickToSelectAttributes(attributeCategory = ${attributeCategory});

		for (var attributeName : list ${attributeNameList}) {
			var key_attributeName = ${attributeName};

			ACUtils.searchBar(searchItem = ${attributeName});

			if (IsElementNotPresent(locator1 = "ACDXPSettings#CHECK_SELECT_ATTRIBUTES_ENABLE")) {
				Click.clickAtNotVisible(locator1 = "ACDXPSettings#SELECT_ATTRIBUTES_CHECKBOX");
			}
		}

		Click(
			key_text = "Sync",
			locator1 = "Button#ANY");

		WaitForElementNotPresent(locator1 = "ACSettings#LOADING_ANIMATION");
	}

	@summary = "Default summary"
	macro syncChannel(newPropertyName = null, channelName = null) {
		if (isSet(newPropertyName)) {
			ACDXPSettings.createNewProperty(newPropertyNameList = ${newPropertyName});

			var propertyName = ${newPropertyName};
		}

		ACUtils.waitForSendingRequest();

		Check.checkToggleSwitch(
			key_propertyName = ${propertyName},
			locator1 = "ACDXPSettings#COMMERCE_PROPERTY_TOGGLE");

		ACDXPSettings.clickToAssignSitesAndChannel(propertyName = ${propertyName});

		ACDXPSettings.switchTab(tabName = "Channel");

		if (isSet(channelName)) {
			ACDXPSettings.searchACItemInAssignModal(searchItem = ${channelName});
		}

		Click.clickAtNotVisible(locator1 = "ACDXPSettings#AC_GENERIC_CHECKBOX");

		Click(
			key_name = "Assign",
			locator1 = "Button#GENERAL_BUTTON_MODAL_FOOTER");

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro syncContactsAndAccountstData() {
		if (IsElementPresent(index = 1, locator1 = "ACDXPSettings#TOGGLE_ICON_ENABLE")) {
			Check.checkToggleSwitch(locator1 = "ACUtils#TOGGLE_ICON");
		}

		Check.checkToggleSwitch(locator1 = "ACUtils#TOGGLE_ICON");
	}

	@summary = "Default summary"
	macro syncContactsData(orgName = null, userGroup = null) {
		if (IsElementPresent(key_panel = "Select Contacts", locator1 = "Panel#PANEL_COLLAPSED")) {
			Click(
				key_panel = "Select Contacts",
				locator1 = "Panel#PANEL_COLLAPSED");
		}

		if (isSet(orgName)) {
			Click(
				key_option = "Organizations",
				locator1 = "ACDXPSettings#CONTACT_SYNC_OPTIONS");

			Click.clickAtNotVisible(
				key_entry = ${orgName},
				locator1 = "ACDXPSettings#SELECT_OPTION_CHECKBOX");

			Click(
				key_text = "Add",
				locator1 = "Button#ANY");

			Alert.viewSuccessMessage();
		}
		else if (isSet(userGroup)) {
			Click(
				key_option = "User Groups",
				locator1 = "ACDXPSettings#CONTACT_SYNC_OPTIONS");

			Click.clickAtNotVisible(
				key_entry = ${userGroup},
				locator1 = "ACDXPSettings#SELECT_OPTION_CHECKBOX");

			Click(
				key_text = "Add",
				locator1 = "Button#ANY");

			Alert.viewSuccessMessage();
		}
		else {
			if (IsElementPresent(index = 2, locator1 = "ACDXPSettings#TOGGLE_ICON_ENABLE")) {
				Check.checkToggleSwitch(
					key_syncFieldName = "Sync All Contacts",
					locator1 = "ACDXPSettings#SYNC_TOGGLE_ICON");
			}

			Check.checkToggleSwitch(
				key_syncFieldName = "Sync All Contacts",
				locator1 = "ACDXPSettings#SYNC_TOGGLE_ICON");
		}
	}

	@summary = "Default summary"
	macro syncNewContactData() {
		ACDXPSettings.goToInstanceSettingsAC();

		ACDXPSettings.navToSyncedContactDataMenu();

		ACDXPSettings.syncContactsAndAccountstData();
	}

	@summary = "Default summary"
	macro syncSites(newPropertyName = null, siteName = null) {
		if (isSet(newPropertyName)) {
			ACDXPSettings.createNewProperty(newPropertyNameList = ${newPropertyName});

			var propertyName = ${newPropertyName};
		}

		ACDXPSettings.clickToAssignSitesAndChannel(propertyName = ${propertyName});

		ACDXPSettings.switchTab(tabName = "Sites");

		if (isSet(siteName)) {
			ACDXPSettings.searchACItemInAssignModal(searchItem = ${siteName});
		}

		Click.clickAtNotVisible(locator1 = "ACDXPSettings#AC_GENERIC_CHECKBOX");

		Click(
			key_name = "Assign",
			locator1 = "Button#GENERAL_BUTTON_MODAL_FOOTER");

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro viewAssignModalListItemInOrder() {
		var itemValueList = ListUtil.newListFromString(${itemValueList});
		var indexList = ListUtil.newListFromString(${indexList});
		var size = ListUtil.size(${itemValueList});
		var i = 0;

		while (${i} != ${size}) {
			var itemValue = ListUtil.get(${itemValueList}, ${i});
			var index = ListUtil.get(${indexList}, ${i});

			AssertTextEquals.assertPartialText(
				locator1 = "ACDXPSettings#ASSIGN_MODAL_VIEW_LIST_ITEM_ORDER",
				value1 = ${itemValue});

			var i = ${i} + 1;

			var i = StringUtil.valueOf(${i});
		}
	}

	@summary = "Default summary"
	macro viewAssignModalResultsMessage(results = null) {
		AssertTextEquals(
			locator1 = "ACDXPSettings#ASSIGN_MODAL_RESULT_MESSAGE",
			value1 = ${results});
	}

	@summary = "Default summary"
	macro viewGenericListItem(itemValueList = null) {
		for (var itemValue : list ${itemValueList}) {
			var key_itemValue = ${itemValue};

			AssertElementPresent(locator1 = "ACDXPSettings#VIEW_LIST_ITEM");
		}
	}

	@summary = "Default summary"
	macro viewGenericListItemInOrder() {
		var itemValueList = ListUtil.newListFromString(${itemValueList});
		var indexList = ListUtil.newListFromString(${indexList});
		var size = ListUtil.size(${itemValueList});
		var i = 0;

		while (${i} != ${size}) {
			var itemValue = ListUtil.get(${itemValueList}, ${i});
			var index = ListUtil.get(${indexList}, ${i});

			AssertTextEquals(
				locator1 = "ACDXPSettings#VIEW_LIST_ITEM_ORDER",
				value1 = ${itemValue});

			var i = ${i} + 1;

			var i = StringUtil.valueOf(${i});
		}
	}

	@summary = "Default summary"
	macro viewGenericListItemNotPresent(itemValueList = null) {
		for (var itemValue : list ${itemValueList}) {
			var key_itemValue = ${itemValue};

			AssertElementNotPresent(locator1 = "ACDXPSettings#VIEW_LIST_ITEM");
		}
	}

	@summary = "Default summary"
	macro viewIncreaseOrDecreaseSyncNumber(attributeCategory = null, decreaseAttributeNumber = null, currentAttributeNumber = null, increaseAttributeNumber = null) {
		if (isSet(increaseAttributeNumber)) {
			var expectedAttributeNumber = ${currentAttributeNumber} + ${increaseAttributeNumber};
		}

		if (isSet(decreaseAttributeNumber)) {
			var expectedAttributeNumber = ${currentAttributeNumber} - ${decreaseAttributeNumber};
		}

		var actualAttributeNumber = ACDXPSettings.getSyncNumberPreview(attributeCategory = ${attributeCategory});

		TestUtils.assertEquals(
			actual = ${actualAttributeNumber},
			expected = ${expectedAttributeNumber});
	}

	@summary = "Default summary"
	macro viewSyncedChannelsAndSitesNumber(syncedChannelsNumber = null, propertyName = null, syncedSitesNumber = null) {
		if (isSet(syncedChannelsNumber)) {
			AssertTextEquals(
				index = 1,
				key_propertyName = ${propertyName},
				locator1 = "ACDXPSettings#CHANNELS_OR_SITES_SYNCED_NUMBER",
				value1 = ${syncedChannelsNumber});
		}

		if (isSet(syncedSitesNumber)) {
			AssertTextEquals(
				index = 2,
				key_propertyName = ${propertyName},
				locator1 = "ACDXPSettings#CHANNELS_OR_SITES_SYNCED_NUMBER",
				value1 = ${syncedSitesNumber});
		}
	}

	@summary = "Default summary"
	macro viewSyncNumberPreview(syncNumber = null, syncFieldName = null) {
		var key_syncFieldName = ${syncFieldName};

		AssertTextEquals(
			locator1 = "ACDXPSettings#SYNC_NUMBER_PREVIEW",
			value1 = ${syncNumber});
	}

}