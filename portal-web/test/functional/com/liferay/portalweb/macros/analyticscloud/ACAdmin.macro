definition {

	@summary = "Default summary"
	macro changeACUserPassword(userScreenName) {
		var analyticsCloudURL = PropsUtil.get("analytics.cloud.url");

		User.openUsersAdmin(baseURL = ${analyticsCloudURL});

		UserNavigator.gotoUser(userScreenName = ${userScreenName});

		UserNavigator.gotoPassword();

		User.resetPassword();
	}

	@summary = "Default summary"
	macro configureMailSettings(incomingPOPServer = null, userPassword = null, outgoingSMTPServer = null, userName = null, incomingPort = null, outgoingPort = null,) {
		ACAdmin.goToServerAdministration();

		Navigator.gotoNavItem(navItem = "Mail");

		if (isSet(incomingPOPServer)) {
			Type(
				locator1 = "ServerAdministrationMail#INCOMING_POP_SERVER",
				value1 = ${incomingPOPServer});
		}

		if (isSet(incomingPort)) {
			Type(
				locator1 = "ServerAdministrationMail#INCOMING_PORT",
				value1 = ${incomingPort});

			Check(locator1 = "ServerAdministrationMail#INCOMING_SECURE_NETWORK_CONNECTION_CHECKBOX");
		}

		if (isSet(userName)) {
			Type(
				locator1 = "ServerAdministrationMail#INCOMING_USER_NAME",
				value1 = ${userName});
		}

		if (isSet(userPassword)) {
			Type(
				locator1 = "ServerAdministrationMail#INCOMING_PASSWORD",
				value1 = ${userPassword});
		}

		if (isSet(outgoingSMTPServer)) {
			Type(
				locator1 = "ServerAdministrationMail#OUTGOING_SMTP_SERVER",
				value1 = ${outgoingSMTPServer});
		}

		if (isSet(outgoingPort)) {
			Type(
				locator1 = "ServerAdministrationMail#OUTGOING_PORT",
				value1 = ${outgoingPort});

			Check(locator1 = "ServerAdministrationMail#OUTGOING_SECURE_CONNECTION_CHECKBOX");
		}

		if (isSet(userName)) {
			Type(
				locator1 = "ServerAdministrationMail#OUTGOING_USER_NAME",
				value1 = ${userName});
		}

		if (isSet(userPassword)) {
			Type(
				locator1 = "ServerAdministrationMail#OUTGOING_PASSWORD",
				value1 = ${userPassword});
		}

		AssertClick(
			locator1 = "Button#SAVE",
			value1 = "Save");

		AssertTextEquals.assertPartialText(
			locator1 = "Message#SUCCESS",
			value1 = "Your request completed successfully.");
	}

	@summary = "Default summary"
	macro getUserID() {
		var userID = selenium.getElementValue("UsersAndOrganizationsEditUser#DETAILS_USER_ID_FIELD");

		return ${userID};
	}

	@summary = "Default summary"
	macro goToACAdmin(userScreenNameToResetPassword = null,) {
		var analyticsCloudURL = PropsUtil.get("analytics.cloud.url");

		Navigator.openSpecificURL(url = "${analyticsCloudURL}/admin");

		WaitForPageLoad();

		ACUtils.waitForLoading();

		if (IsElementPresent(locator1 = "TextInput#PASSWORD_1")) {
			User.resetPassword(newPassword = "test1");

			if (IsElementPresent(locator1 = "Modal#HEADER")) {
				Click(locator1 = "Button#DONE");
			}

			if (isSet(userScreenNameToResetPassword)) {
				ACAdmin.changeACUserPassword(userScreenName = ${userScreenNameToResetPassword});
			}
		}
	}

	@summary = "Default summary"
	macro goToServerAdministration() {
		ProductMenuHelper.expandPanel(panel = "control panel");

		ProductMenuHelper.expandCategory(category = "Configuration");

		ProductMenuHelper.gotoPortlet(portlet = "Server Administration");
	}

}