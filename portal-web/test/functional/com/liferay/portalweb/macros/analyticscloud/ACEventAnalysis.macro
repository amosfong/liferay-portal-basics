definition {

	@summary = "Default summary"
	macro addBreakdown(attributeNameList = null) {
		for (var attributeName : list ${attributeNameList}) {
			ACEventAnalysis.clickToAddBreakdownButton();

			ACEventAnalysis.searchEventAnalysis(searchItem = ${attributeName});

			Click(
				key_buttonName = ${attributeName},
				locator1 = "ACUtils#GENERIC_BUTTON");
		}
	}

	@summary = "Default summary"
	macro addEvent(customEventName = null) {
		var key_buttonName = ${customEventName};

		if (!(isSet(tabName))) {
			var tabName = "All";
		}

		ACEventAnalysis.clickToAddEventButton();

		ACNavigation.switchTabInCard(tabName = ${tabName});

		ACEventAnalysis.searchEventAnalysis(searchItem = ${customEventName});

		while ((IsElementNotPresent(locator1 = "ACUtils#GENERIC_BUTTON")) && (maxIterations = "50")) {
			TestUtils.hardRefresh();

			ACEventAnalysis.clickToAddEventButton();

			ACEventAnalysis.searchEventAnalysis(searchItem = ${customEventName});
		}

		Click(locator1 = "ACUtils#GENERIC_BUTTON");
	}

	@summary = "Default summary"
	macro addFilter(inputEndValue = null, month = null, year = null, dropdownOptionName = null, optionValue = null, filterName = null, inputValue = null) {
		ACEventAnalysis.clickToAddFilterButton();

		ACEventAnalysis.searchEventAnalysis(searchItem = ${filterName});

		Click(
			key_filterName = ${filterName},
			locator1 = "ACEventAnalysis#CHOOSE_FILTER");

		ACUtils.selectGenericOption(optionValue = ${optionValue});

		if (isSet(dropdownOptionName)) {
			ACEventAnalysis.selectAutocompleteOption(dropdownOptionName = ${dropdownOptionName});

			ACUtils.selectGenericOption(optionValue = ${optionValue});
		}
		else if ((IsElementNotPresent(key_text = "calendar", locator1 = "Icon#ANY")) && (${optionValue} != "True") && (${optionValue} != "False")) {
			Type(
				locator1 = "ACEventAnalysis#INPUT_CONDITION",
				value1 = ${inputValue});

			if (${optionValue} == "between") {
				Type(
					locator1 = "ACEventAnalysis#INPUT_CONDITION_END_VALUE",
					value1 = ${inputEndValue});
			}
		}

		if (IsElementPresent(key_text = "calendar", locator1 = "Icon#ANY")) {
			Click(
				key_text = "calendar",
				locator1 = "Icon#ANY");

			Select(
				key_indexCalendar = 1,
				locator1 = "ACSegments#MONTH_AND_YEAR_CALENDAR",
				value1 = ${month});

			Select(
				key_indexCalendar = 2,
				locator1 = "ACSegments#MONTH_AND_YEAR_CALENDAR",
				value1 = ${year});

			Click(
				key_day = ${inputValue},
				locator1 = "ACSegments#DAY_CALENDAR_OPTION");

			if (${optionValue} == "is between") {
				Click(
					key_day = ${inputEndValue},
					locator1 = "ACSegments#DAY_CALENDAR_OPTION");
			}

			Click(
				key_text = "calendar",
				locator1 = "Icon#ANY");
		}

		Click(locator1 = "ACEventAnalysis#FILTER_APPLY_BUTTON");
	}

	@summary = "Default summary"
	macro changeAttributeDataType(attributeName = null, editOption = null, newDataType = null) {
		var key_buttonName = ${attributeName};
		var key_attributeName = ${attributeName};

		MouseOver(locator1 = "ACUtils#GENERIC_BUTTON");

		if (${editOption} == "Description") {
			Click(locator1 = "ACEventAnalysis#EDIT_PENCIL");

			ACCustomEvents.changeAttributeDataType(newDataType = ${newDataType});
		}

		if (${editOption} == "Filter") {
			Click(locator1 = "ACEventAnalysis#FILTER_BUTTON");

			Click(locator1 = "ACEventAnalysis#EDIT_PENCIL");

			ACCustomEvents.changeAttributeDataType(newDataType = ${newDataType});
		}
	}

	@summary = "Default summary"
	macro changeBreakdownPosition(indexEnd = null, indexStart = null) {
		DragAndDrop.javaScriptDragAndDropToObject(
			indexEnd = ${indexEnd},
			indexStart = ${indexStart},
			locator1 = "ACEventAnalysis#BREAKDOWN_ORIGINAL_POSITION",
			locator2 = "ACEventAnalysis#BREAKDOWN_NEW_POSITION");
	}

	@summary = "Default summary"
	macro changeCustomEventDisplayName(newDisplayName = null, editOption = null, elementName = null) {
		var key_buttonName = ${elementName};

		MouseOver(locator1 = "ACUtils#GENERIC_BUTTON");

		if (${editOption} == "Description") {
			Click(locator1 = "ACEventAnalysis#EDIT_PENCIL");

			ACCustomEvents.changeCustomEventDisplayNameOrDescription(newDisplayName = ${newDisplayName});
		}

		if (${editOption} == "Filter") {
			Click(locator1 = "ACEventAnalysis#FILTER_BUTTON");

			Click(locator1 = "ACEventAnalysis#EDIT_PENCIL");

			ACCustomEvents.changeCustomEventDisplayNameOrDescription(newDisplayName = ${newDisplayName});
		}
	}

	@summary = "Default summary"
	macro clickToAddBreakdownButton() {
		var key_sectionType = "breakdown";

		Click(locator1 = "ACEventAnalysis#ADD_ATTRIBUTE_BUTTON");
	}

	@summary = "Default summary"
	macro clickToAddEventButton() {
		var key_definitionsOptions = "event-analysis/create";

		if (IsElementPresent(locator1 = "ACSettings#DEFINITIONS_OPTIONS")) {
			Click(locator1 = "ACSettings#DEFINITIONS_OPTIONS");
		}

		Click(locator1 = "ACEventAnalysis#ADD_EVENT_BUTTON");
	}

	@summary = "Default summary"
	macro clickToAddFilterButton() {
		var key_sectionType = "filter";

		Click(locator1 = "ACEventAnalysis#ADD_ATTRIBUTE_BUTTON");
	}

	@summary = "Default summary"
	macro deleteAllEventAnalysis() {
		var key_actionButton = "Delete";

		KeyPress(
			locator1 = "ACSettings#SELECT_ALL_CHECKBOX",
			value1 = "\SPACE");

		Click(locator1 = "ACSettings#DELETE_USER");

		ACUtils.clickAnyButton(button = ${key_actionButton});
	}

	@summary = "Default summary"
	macro getColumnBreakdownValue() {
		var index = ${index};
		var breakdownValue = selenium.getText("ACEventAnalysis#COLUMN_BREAKDOWN_VALUES");

		return ${breakdownValue};
	}

	@summary = "Default summary"
	macro removeFromAnalysis(removeValue = null) {
		var key_removeValue = ${removeValue};

		Click(locator1 = "ACEventAnalysis#REMOVE_BUTTON");
	}

	@summary = "Default summary"
	macro searchEventAnalysis(searchItem = null) {
		Type(
			locator1 = "ACEventAnalysis#SEARCH_BAR_EVENT_ANALYSE",
			value1 = ${searchItem});
	}

	@summary = "Default summary"
	macro selectAutocompleteOption(dropdownOptionName) {
		Click(locator1 = "ACEventAnalysis#INPUT_CONDITION");

		ACUtils.waitForLoading();

		WaitForElementPresent(
			key_optionName = ${dropdownOptionName},
			locator1 = "ACEventAnalysis#DROPDOWN_BY_OPTION");

		AssertClick(
			key_optionName = ${dropdownOptionName},
			locator1 = "ACEventAnalysis#DROPDOWN_BY_OPTION",
			value1 = ${dropdownOptionName});
	}

	@summary = "Default summary"
	macro sortAnalysis(attributeName = null) {
		var key_attributeName = ${attributeName};

		Click(locator1 = "ACEventAnalysis#SORT_ANALYSIS");
	}

	@summary = "Default summary"
	macro viewAnalysisColumnInOrder(columnName = null) {
		var index = ${index};

		AssertTextEquals.assertPartialText(
			locator1 = "ACEventAnalysis#VIEW_ORDER_ANALYSIS_COLUMN",
			value1 = ${columnName});
	}

	@summary = "Default summary"
	macro viewAnalysisDashboard(attributeNameList = null, eventName = null, filterNameList = null) {
		if (isSet(eventName)) {
			AssertElementPresent(
				key_buttonName = ${eventName},
				locator1 = "ACUtils#GENERIC_BUTTON");
		}

		if (isSet(attributeNameList)) {
			for (var attributeName : list ${attributeNameList}) {
				AssertElementPresent(
					key_attributeName = ${attributeName},
					locator1 = "ACEventAnalysis#VIEW_DASHBOARD_INFORMATION");
			}
		}

		if (isSet(filterNameList)) {
			for (var filterName : list ${filterNameList}) {
				AssertElementPresent(
					key_attributeName = ${filterName},
					locator1 = "ACEventAnalysis#VIEW_DASHBOARD_INFORMATION");
			}
		}
	}

	@summary = "Default summary"
	macro viewAnalysisInformation(informationValueList = null) {
		for (var informationValue : list ${informationValueList}) {
			var key_informationValue = ${informationValue};

			ACUtils.waitForLoading();

			AssertElementPresent(locator1 = "ACEventAnalysis#VIEW_ANALYSIS_INFORMATION");
		}
	}

	@summary = "Default summary"
	macro viewAnalysisInformationInOrder(commonInformation = null, informationValue = null) {
		var key_commonInformation = ${commonInformation};
		var index = ${index};

		AssertVisible(
			locator1 = "ACEventAnalysis#VIEW_ANALYSIS_INFORMATION_SPECIFIC_ORDER",
			value1 = ${informationValue});
	}

	@summary = "Default summary"
	macro viewAnalysisInformationNotPresent(informationValueList = null) {
		for (var informationValue : list ${informationValueList}) {
			var key_informationValue = ${informationValue};

			AssertElementNotPresent(locator1 = "ACEventAnalysis#VIEW_ANALYSIS_INFORMATION");
		}
	}

	@summary = "Default summary"
	macro viewChangedAttributeDataType(attributeName = null) {
		var key_buttonName = ${attributeName};
		var key_attributeName = ${attributeName};
		var newDataType = StringUtil.upperCase(${newDataType});

		MouseOver(locator1 = "ACUtils#GENERIC_BUTTON");

		ACCustomEvents.viewDataType(typeValue = ${newDataType});

		if ((${newDataType} != "STRING") && (${newDataType} != "BOOLEAN")) {
			Click(locator1 = "ACEventAnalysis#FILTER_BUTTON");
		}

		if ((${newDataType} == "NUMBER") || (${newDataType} == "DURATION")) {
			AssertElementPresent(locator1 = "ACEventAnalysis#INPUT_CONDITION");
		}

		if (${newDataType} == "DATE") {
			AssertElementPresent(locator1 = "Select#SELECT");
		}
	}

	@summary = "Default summary"
	macro viewEventAnalysisList(eventList = null) {
		for (var name : list ${eventList}) {
			var key_buttonName = ${name};

			while ((IsElementNotPresent(locator1 = "ACUtils#GENERIC_BUTTON")) && (maxIterations = "100")) {
				ACEventAnalysis.searchEventAnalysis(searchItem = ${name});
			}

			AssertTextEquals(
				locator1 = "ACUtils#GENERIC_BUTTON",
				value1 = ${name});
		}
	}

	@summary = "Default summary"
	macro viewEventAnalysisListNotPresent(eventList = null) {
		for (var name : list ${eventList}) {
			var key_buttonName = ${name};

			AssertElementNotPresent(locator1 = "ACUtils#GENERIC_BUTTON");
		}
	}

	@summary = "Default summary"
	macro viewTotalNumberRelatedToColumnInformation(columnInformationValue = null, totalEventsValue = null) {
		var key_columnInformationValue = ${columnInformationValue};
		var key_totalEventsValue = ${totalEventsValue};

		AssertElementPresent(locator1 = "ACEventAnalysis#VIEW_TOTAL_NUMBER_RELATED_COLUMN_INFORMATION");
	}

}