definition {

	@summary = "Default summary"
	macro addComment(commentValue = null) {
		Type(
			locator1 = "PublicationsChanges#COMMENT_BODY",
			value1 = ${commentValue});

		AssertClick(
			locator1 = "Button#REPLY",
			value1 = "Reply");

		AssertElementPresent(
			key_commentValue = ${commentValue},
			locator1 = "PublicationsChanges#COMMENT_ACTIONS");
	}

	@summary = "Default summary"
	macro assertPublicationsCommentsCRUDPermissions() {
		PublicationsChanges.openCommentsSidebar();

		PublicationsChanges.addComment(commentValue = "Good to Publish");

		PublicationsChanges.editComment(
			commentValue = "Good to Publish",
			commentValueEdit = "It is not ready to Publish",
			userName = "Test Test");

		PublicationsChanges.deleteComment(commentValue = "It is not ready to Publish");
	}

	@summary = "Default summary"
	macro deleteComment(commentValue = null) {
		Click.mouseOverClick(
			key_commentValue = ${commentValue},
			locator1 = "PublicationsChanges#COMMENT_ACTIONS");

		AssertClickNoError(
			key_menuItem = "Delete",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Delete");

		AssertElementPresent(locator1 = "PublicationsChanges#COMMENT_DELETING_MESSAGE");

		Click.javaScriptClick(locator1 = "Button#DELETE");

		AssertTextNotPresent(value1 = ${commentValue});
	}

	@summary = "Default summary"
	macro discardChanges(conflictingChanges = null, changeCategory = null, changeTitle = null) {
		var key_changeCategory = ${changeCategory};
		var key_changeTitle = ${changeTitle};

		if (${conflictingChanges} == "true") {
			if (IsElementPresent(locator1 = "PublicationsChanges#PUBLICATIONS_CHANGE_DROPDOWN")) {
				Click(locator1 = "PublicationsChanges#PUBLICATIONS_CHANGE_DROPDOWN");

				MenuItem.click(menuItem = "Discard Change");
			}
			else if (IsElementPresent(locator1 = "PublicationsChanges#DISCARD_CHANGE_BUTTON")) {
				Click(locator1 = "PublicationsChanges#DISCARD_CHANGE_BUTTON");
			}
			else {
				Click(locator1 = "PublicationsChangeDetails#DISCARD_CHANGE_BUTTON");
			}
		}
		else {
			Click(locator1 = "PublicationsChanges#PUBLICATIONS_CHANGE_TITLE");

			Click(locator1 = "PublicationsChanges#CONFLICTING_CHANGES_DISCARD_ELLIPSIS");

			MenuItem.click(menuItem = "Discard");
		}

		AssertElementPresent(
			key_changeTitle = "Discarded Changes",
			locator1 = "PublicationsChanges#CHANGE_TITLE");

		while (IsElementNotPresent(locator1 = "PublicationsChanges#CONTEXT_CHANGE_TITLE")) {
			Click(locator1 = "Pagination#NEXT_LINK");
		}

		Click(
			key_text = "Discard",
			locator1 = "Button#ANY");

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro editComment(commentValueEdit = null, commentValue = null, userName = null) {
		Click.mouseOverClick(
			key_commentValue = ${commentValue},
			locator1 = "PublicationsChanges#COMMENT_ACTIONS");

		MenuItem.click(menuItem = "Edit");

		Type.clickAtType(
			key_userName = ${userName},
			locator1 = "PublicationsChanges#COMMENT_BODY_SPECIFIC",
			value1 = ${commentValueEdit});

		SelectFrame(value1 = "relative=top");

		Click.javaScriptClick(locator1 = "Button#SAVE");

		AssertElementPresent(
			key_commentValue = ${commentValueEdit},
			locator1 = "PublicationsChanges#COMMENT_VALUE",
			value1 = ${commentValueEdit});
	}

	@summary = "Default summary"
	macro filterChangesByItem(checkboxName = null, menuItem = null) {
		if (IsElementPresent(key_text = "Reset Filters", locator1 = "Button#ANY")) {
			Click(
				key_text = "Reset Filters",
				locator1 = "Button#ANY");
		}

		WaitForElementPresent(locator1 = "PublicationsChanges#CHANGE_TYPE_BUTTON");

		Click(
			key_name = "Filter",
			locator1 = "PublicationsChanges#SORT_BUTTON");

		MenuItem.clickNoError(menuItem = ${menuItem});

		Check.checkNotVisible(
			key_checkboxName = ${checkboxName},
			locator1 = "MenuItem#ANY_CHECKBOX");

		Click(locator1 = "Publications#ADD_FILTER_BUTTON");
	}

	@summary = "Default summary"
	macro gotoEditChanges(changeCategory = null, changeTitle = null, editInLocation = null) {
		Click(
			key_changeCategory = ${changeCategory},
			key_changeTitle = ${changeTitle},
			locator1 = "PublicationsChanges#PUBLICATIONS_CHANGE_DROPDOWN");

		MenuItem.click(menuItem = "Edit in ${editInLocation}");
	}

	@summary = "Default summary"
	macro gotoMoveChangeToAnotherPublication(changeCategory = null, targetPublicationName = null, changeTitle = null) {
		Search.searchCP(searchTerm = ${changeTitle});

		Click(
			key_changeCategory = ${changeCategory},
			key_changeTitle = ${changeTitle},
			locator1 = "PublicationsChanges#PUBLICATIONS_CHANGE_TITLE");

		Click(locator1 = "PublicationsChanges#REVIEW_CHANGE_DROPDOWN");

		Click(
			key_menuItem = "Move Changes",
			locator1 = "PublicationsChanges#REVIEW_CHANGE_DROPDOWN_ITEM");

		Select(
			ariaLabel = "Publication",
			locator1 = "Select#ANY_ARIA_LABEL",
			value1 = ${targetPublicationName});
	}

	@summary = "Default summary"
	macro moveChangeToAnotherPublication(moveFailed = null) {
		Click(
			key_text = "Move Changes",
			locator1 = "Button#ANY");

		if (${moveFailed} == "true") {
			AssertTextEquals.assertPartialText(
				key_alertType = "danger",
				locator1 = "ClayAlert#ALERT_STATIC",
				value1 = "One or more changes conflict with existing changes in the destination publication.");
		}
		else {
			AssertElementPresent(
				key_uniqueSuccess = "Your request completed successfully.",
				locator1 = "Message#UNIQUE_SUCCESS");
		}
	}

	@summary = "Default summary"
	macro openCommentsSidebar() {
		Click.javaScriptClick(locator1 = "PublicationsChanges#COMMENT_BUTTON");
	}

	@summary = "Default summary"
	macro sortChanges(sortName = null, sort = null) {
		Click(
			key_name = ${sortName},
			locator1 = "PublicationsChanges#SORT_BUTTON");

		if (${sort} == "descending") {
			if (IsElementPresent(locator1 = "PublicationsChanges#SORTED_ASCENDING_ICON")) {
				Click(locator1 = "PublicationsChanges#SORTED_ASCENDING_ICON");
			}
		}

		if (${sort} == "ascending") {
			if (IsElementPresent(locator1 = "PublicationsChanges#SORTED_DESCENDING_ICON")) {
				Click(locator1 = "PublicationsChanges#SORTED_DESCENDING_ICON");
			}
		}
	}

	@summary = "Default summary"
	macro viewChanges(changeCategory = null, changeUser = null, publishedPublication = null, changeType = null, changeSite = null, changeTitle = null, changeStatus = null, publicationName = null, showSystemChanges = null) {
		if (${publishedPublication} == "true") {
			Click(
				key_publicationName = ${publicationName},
				locator1 = "Publications#PUBLICATION_NAME_VIEW");
		}

		WaitForElementPresent(locator1 = "PublicationsChanges#CHANGE_TYPE_BUTTON");

		if (${showSystemChanges} == "true") {
			Click(locator1 = "PublicationsChanges#ELLIPSIS_IN_PUBLICATION");

			MenuItem.click(menuItem = "Show System Changes");
		}

		var key_changeCategory = ${changeCategory};
		var key_changeUser = ${changeUser};
		var key_changeSite = ${changeSite};
		var key_changeTitle = ${changeTitle};
		var key_changeType = ${changeType};

		WaitForElementPresent(locator1 = "PublicationsChanges#PUBLICATIONS_CHANGE_CATEGORY");

		while (IsElementNotPresent(locator1 = "PublicationsChanges#PUBLICATIONS_CHANGE_CATEGORY")) {
			Click(locator1 = "//*[contains(@class, 'page-link')]//*[contains(@class,'right')]");
		}

		AssertTextEquals.assertPartialText(
			locator1 = "PublicationsChanges#PUBLICATIONS_CHANGE_USER",
			value1 = ${changeUser});

		AssertTextEquals.assertPartialText(
			locator1 = "PublicationsChanges#PUBLICATIONS_CHANGE_SITE",
			value1 = ${changeSite});

		if (isSet(changeStatus)) {
			var key_changeStatus = ${changeStatus};

			AssertTextEquals.assertPartialText(
				locator1 = "PublicationsChanges#PUBLICATIONS_CHANGE_STATUS",
				value1 = ${changeStatus});
		}

		AssertTextEquals.assertPartialText(
			locator1 = "PublicationsChanges#PUBLICATIONS_CHANGE_TITLE",
			value1 = ${changeTitle});

		AssertTextEquals.assertPartialText(
			locator1 = "PublicationsChanges#PUBLICATIONS_CHANGE_TYPE",
			value1 = ${changeType});
	}

	@summary = "Default summary"
	macro viewChangesInRelationships(changeTitle = null) {
		PublicationsNavigator.gotoNavbar(navBarTitle = "Relationships");

		AssertTextEquals.assertPartialText(
			key_changeTitle = ${changeTitle},
			locator1 = "PublicationsChanges#RELATIONSHIPS_CHANGE_TITLE",
			value1 = ${changeTitle});
	}

	@summary = "Default summary"
	macro viewChangesResultNotPresent(changeCategory = null, changeTitle = null) {
		var key_changeCategory = ${changeCategory};
		var key_changeTitle = ${changeTitle};

		AssertElementNotPresent(
			locator1 = "PublicationsChanges#PUBLICATIONS_CHANGE_TITLE",
			value1 = ${changeTitle});
	}

	@summary = "Default summary"
	macro viewChangesResultsOrder(changeCategory = null, changesList = null) {
		var changeOrder = 0;

		for (var changeTitle : list ${changesList}) {
			var changeOrder = ${changeOrder} + 1;
			var key_changeCategory = ${changeCategory};
			var key_changeTitle = ${changeTitle};

			AssertTextEquals(
				changeOrder = ${changeOrder},
				locator1 = "PublicationsChanges#PUBLICATIONS_CHANGE_TITLE",
				value1 = ${changeTitle});
		}
	}

}