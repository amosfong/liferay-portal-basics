definition {

	@summary = "Default summary"
	macro cleanWorkflowIndex(workflowIndexName = null) {
		var companyId = JSONCompany.getCompanyId();
		var portalHostname = PropsUtil.get("portal.hostname");

		var cleanCurl = '''
			http://${portalHostname}:9200/liferay-${companyId}-${workflowIndexName}/_delete_by_query?pretty \
				-H 'Content-Type: application/json' \
				-d '{
					"query": {
						"match_all": {
						}
					}
				}'
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post(${cleanCurl});
	}

	@summary = "Default summary"
	macro getNumberOfSuccessfulWriteRequests(indexName = null) {
		var companyId = JSONCompany.getCompanyId();
		var portalHostname = PropsUtil.get("portal.hostname");

		var curl = "http://${portalHostname}:9202/liferay-${companyId}-${indexName}/_ccr/stats?pretty";

		var ccrSynonymInfo = JSONCurlUtil.get(${curl});

		var successfulWriteRequests = RegexUtil.replace(${ccrSynonymInfo}, "successful_write_requests\" : (\d+)", 1);

		echo("Number of successful_write_requests: ${successfulWriteRequests}");

		return ${successfulWriteRequests};
	}

	@summary = "Default summary"
	macro setupCrossClusterReplication(portalCluster = null) {
		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "System Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "Search",
			configurationName = "Cross-Cluster Replication",
			configurationScope = "System Scope");

		FormFields.enableCheckbox(fieldName = "ccrEnabled");

		var elasticsearchHostname = PropsUtil.get("selenium.remote.driver.url");

		var elasticsearchHostname = Regex.removeHTTP(url = ${elasticsearchHostname});

		var elasticsearchHostname = StringUtil.regexReplaceFirst(${elasticsearchHostname}, "_chrome(.*?):[0-9]{4,}.*", "$1");

		if (${portalCluster} == "true") {
			FormFields.editRepeatableTextMultiline(
				fieldName = "ccrLocalClusterConnectionConfigurations",
				fieldValue = "${elasticsearchHostname}:8080,localcluster",
				repeatableFieldNumber = 1);

			FormFields.addRepeatableField(
				fieldName = "ccrLocalClusterConnectionConfigurations",
				repeatableFieldNumber = 1);

			FormFields.editRepeatableTextMultiline(
				fieldName = "ccrLocalClusterConnectionConfigurations",
				fieldValue = "${elasticsearchHostname}:9080,localcluster",
				repeatableFieldNumber = 2);
		}
		else {
			FormFields.editTextMultiline(
				fieldName = "ccrLocalClusterConnectionConfigurations",
				fieldValue = "${elasticsearchHostname}:8080,localcluster");
		}

		SystemSettings.saveConfiguration();

		echo("##");

		echo("## Local Cluster Index List");

		echo("##");

		var localClusterIndexList = JSONCurlUtil.get("http://${elasticsearchHostname}:9202/_cat/indices?h=index");

		echo("##");

		echo("## Remote Cluster Index List");

		echo("##");

		var remoteClusterIndexList = JSONCurlUtil.get("http://${elasticsearchHostname}:9200/_cat/indices?h=index");

		var remoteClusterIndexList = RegexUtil.replace(${remoteClusterIndexList}, "\n", ",");

		for (var indexName : list ${remoteClusterIndexList}) {
			if (!(contains(${indexName}, ".")) && !(contains(${indexName}, "ilm-history")) && !(contains(${localClusterIndexList}, ${indexName}))) {
				fail("Failed to create a follower index for ${indexName}.");
			}
		}

		echo("##");

		echo("## Local Cluster Health");

		echo("##");

		var localClusterHealth = JSONCurlUtil.get("http://${elasticsearchHostname}:9202/_cluster/health?pretty");

		if (contains(${localClusterHealth}, "red")) {
			echo("##");

			echo("## Local Cluster Index Status");

			echo("##");

			JSONCurlUtil.get("http://${elasticsearchHostname}:9202/_cat/indices?pretty");

			fail("A follower index is in red status.");
		}
	}

	@summary = "Default summary"
	macro setupWorkflow() {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Configuration",
			portlet = "Workflow");

		Workflow.configureWorkflow(
			workflowDefinition = "Single Approver",
			workflowResourceValue = "Blogs Entry");

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			panel = "Site Administration",
			portlet = "Blogs");

		Blogs.addWithWorkflow(
			entryContent = "Blogs Entry Content",
			entrySubtitle = "Subtitle",
			entryTitle = "Blogs Entry Title");

		Navigator.openURL();
	}

}