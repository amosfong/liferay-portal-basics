definition {

	@summary = "Default summary"
	macro addServiceAccessPolicy(serviceClass = null, defaultSwitch = null, policyName = null, methodName = null, title = null, enabled = null) {
		LexiconEntry.gotoAdd();

		FormFields.editText(
			fieldName = "name",
			fieldValue = ${policyName});

		FormFields.editText(
			fieldName = "title",
			fieldValue = ${title});

		if (isSet(serviceClass)) {
			FormFields.editText(
				fieldName = "serviceClass",
				fieldValue = ${serviceClass});
		}

		if (isSet(methodName)) {
			FormFields.editText(
				fieldName = "MethodName",
				fieldValue = ${methodName});
		}

		if (${enabled} == "true") {
			FormFields.enableCheckbox(fieldName = "enabled");
		}

		if (${defaultSwitch} == "true") {
			FormFields.enableCheckbox(fieldName = "default");
		}

		PortletEntry.save();
	}

	@summary = "Default summary"
	macro editServiceAccessPolicy(newName = null, serviceClass = null, policyName = null, methodName = null, allowedServiceSignature = null, title = null, enabled = null) {
		ServiceAccessPolicy.gotoServiceAccessPolicy(policyName = ${policyName});

		if (isSet(allowedServiceSignature)) {
			Click(
				locator1 = "ServiceAccessPolicy#SWITCH_TO_ADVANCED_MODE",
				value1 = "Switch to Advanced Mode");

			Type(
				locator1 = "ServiceAccessPolicy#ALLOWED_SERVICE_SIGNATURES_FIELD",
				value1 = ${allowedServiceSignature});
		}

		if (${enabled} == "true") {
			FormFields.enableCheckbox(fieldName = "enabled");
		}
		else if (${enabled} == "false") {
			FormFields.disableCheckbox(fieldName = "enabled");
		}

		if (isSet(newName)) {
			FormFields.editText(
				fieldName = "name",
				fieldValue = ${newName});
		}

		if (isSet(title)) {
			FormFields.editText(
				fieldName = "title",
				fieldValue = ${title});
		}

		if (isSet(serviceClass)) {
			FormFields.editText(
				fieldName = "serviceClass",
				fieldValue = ${serviceClass});
		}

		if (isSet(methodName)) {
			FormFields.editText(
				fieldName = "MethodName",
				fieldValue = ${methodName});
		}

		PortletEntry.save();
	}

	@summary = "Default summary"
	macro gotoServiceAccessPolicy(policyName = null) {
		AssertClick(
			key_policyName = ${policyName},
			locator1 = "ServiceAccessPolicy#POLICY_NAME",
			value1 = ${policyName});
	}

	@summary = "Default summary"
	macro openServiceAccessPolicyAdmin(baseURL = null) {
		Navigator.openWithAppendToBaseURL(
			baseURL = ${baseURL},
			urlAppend = "group/control_panel/manage?p_p_id=com_liferay_portal_security_service_access_policy_web_portlet_SAPPortlet");
	}

	@summary = "Default summary"
	macro openToServiceAccessPolicy(groupName = null, baseURL = null, siteURLKey = null) {
		var groupId = JSONGroupSetter.setGroupId(groupName = ${groupName});

		Navigator.openWithAppendToBaseURL(
			baseURL = ${baseURL},
			urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_portal_security_service_access_policy_web_portlet_SAPPortlet&p_p_lifecycle=0&p_p_state=maximized&p_v_l_s_g_id=${groupId}");
	}

}