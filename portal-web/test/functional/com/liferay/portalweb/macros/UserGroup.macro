definition {

	@summary = "Default summary"
	macro addCP(selectSiteTemplate = null, userGroupName = null, userGroupNameVariation = null, siteTemplateName = null) {
		var key_userGroupName = ${userGroupName};

		LexiconEntry.gotoAdd();

		Type.type(
			locator1 = "TextInput#NAME",
			value1 = ${userGroupName});

		if (${selectSiteTemplate} == "true") {
			Select(
				locator1 = "UserGroupsEditUserGroup#PUBLIC_PAGES_SELECT",
				value1 = ${siteTemplateName});
		}

		Click(
			locator1 = "Button#SAVE",
			value1 = "Save");

		if (${userGroupNameVariation} == "Invalid") {
			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_1",
				value1 = "The User Group Name cannot be blank, numeric or a reserved word such as null.");

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_1",
				value1 = "The User Group Name cannot contain the following invalid characters: , *.");
		}
		else if (${userGroupNameVariation} == "Duplicate") {
			Alert.viewRequestFailedToComplete();

			Alert.viewDuplicateErrorMessage();
		}
		else if (${userGroupNameVariation} == "Null") {
			Alert.viewSpecificRequiredField(fieldFieldLabel = "Name");
		}
		else {
			Alert.viewSuccessMessage();

			AssertTextEquals(
				locator1 = "UserGroups#USER_GROUP_TABLE_NAME",
				value1 = ${userGroupName});
		}
	}

	@summary = "Default summary"
	macro assignAllMembersCP(userGroupName = null) {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Assign Members",
			rowEntry = ${userGroupName});

		LexiconEntry.gotoAdd();

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		PortletEntry.selectAll();

		SelectFrameTop();

		Button.clickAdd();

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro assignMemberCP(userGroupName = null, userScreenName = null) {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Assign Members",
			rowEntry = ${userGroupName});

		LexiconEntry.gotoAdd();

		AssertElementPresent.assertVisible(
			key_text = "Add",
			locator1 = "Button#ANY");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		User.searchCP(searchTerm = ${userScreenName});

		Check(
			key_userScreenName = ${userScreenName},
			locator1 = "UserGroupsAssignUsers#USER_TABLE_CHECKBOX");

		SelectFrameTop();

		AssertClick.assertTextClickAtWaitForLastScript(
			key_text = "Add",
			locator1 = "Button#ANY",
			value1 = "Add");

		Alert.viewSuccessMessage();

		AssertTextEquals(
			key_userScreenName = ${userScreenName},
			locator1 = "UserGroupsAssignUsers#USER_TABLE_SCREEN_NAME",
			value1 = ${userScreenName});
	}

	@summary = "Default summary"
	macro deleteCP(userGroupName = null) {
		AssertTextEquals(
			key_userGroupName = ${userGroupName},
			locator1 = "UserGroups#USER_GROUP_TABLE_NAME",
			value1 = ${userGroupName});

		LexiconEntry.gotoEntryMenuItemNoError(
			key_userGroupName = ${userGroupName},
			menuItem = "Delete",
			rowEntry = ${userGroupName});

		if (isSet(withUser)) {
			AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_1",
				value1 = "You cannot delete user groups that have users.");
		}
		else {
			AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

			Alert.viewSuccessMessage();

			AssertElementNotPresent(
				key_userGroupName = ${userGroupName},
				locator1 = "UserGroups#USER_GROUP_TABLE_NAME");
		}
	}

	@summary = "Default summary"
	macro editCP(userGroupSite = null, checkEnablePropagationCheckbox = null, userGroupDescriptionEdit = null, userGroupName = null, userGroupNameEdit = null) {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = ${userGroupName});

		if (isSet(userGroupNameEdit)) {
			PortletEntry.inputName(name = ${userGroupNameEdit});
		}
		else if (isSet(userGroupDescriptionEdit)) {
			PortletEntry.inputDescription(description = ${userGroupDescriptionEdit});
		}
		else if (isSet(userGroupSite)) {
			Select(
				locator1 = "UserGroupsEditUserGroup#PUBLIC_PAGES_SELECT",
				value1 = ${userGroupSite});

			if (${checkEnablePropagationCheckbox} == "true") {
				AssertChecked(locator1 = "UserGroupsEditUserGroup#ENABLE_PROPAGATION_PUBLIC_PAGE_CHECKBOX");
			}
			else {
				Uncheck(locator1 = "UserGroupsEditUserGroup#ENABLE_PROPAGATION_PUBLIC_PAGE_CHECKBOX");
			}
		}

		PortletEntry.save();
	}

	@summary = "Default summary"
	macro gotoAddPageCP(userGroupName = null) {
		Search.searchCP(searchTerm = ${userGroupName});

		Click.waitForMenuToggleJSClick(
			key_tableEntry = ${userGroupName},
			locator1 = "LexiconTable#TABLE_ENTRY_ELLIPSIS");

		WaitForLiferayEvent.initializeLiferayEventLog();

		MenuItem.click(menuItem = "Manage Pages");
	}

	@summary = "Default summary"
	macro gotoProfilePages(userGroupName = null, siteName = null) {
		Search.searchCP(searchTerm = ${userGroupName});

		Click.waitForMenuToggleJSClick(
			key_tableEntry = ${userGroupName},
			locator1 = "LexiconTable#TABLE_ENTRY_ELLIPSIS");

		WaitForLiferayEvent.initializeLiferayEventLog();

		MenuItem.click(menuItem = "Go to Profile Pages");

		SelectWindow(value1 = "Home - ${userGroupName} - ${siteName}");
	}

	@summary = "Default summary"
	macro openUserGroupsAdmin() {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/control_panel/manage?p_p_id=com_liferay_user_groups_admin_web_portlet_UserGroupsAdminPortlet");
	}

	@summary = "Default summary"
	macro removeMemberCP(userGroupName = null, userScreenName = null) {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Assign Members",
			rowEntry = ${userGroupName});

		Check(
			key_userScreenName = ${userScreenName},
			locator1 = "UserGroupsAssignUsers#USER_TABLE_CHECKBOX");

		ClickNoError(
			key_text = "times-circle",
			locator1 = "ManagementBar#ANY_ICON");

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro searchCP(userGroupName = null) {
		Search.searchCP(searchTerm = ${userGroupName});

		LexiconEntry.viewEntryName(rowEntry = ${userGroupName});

		Search.searchCP(searchTerm = "UG1 UserGroup1 Name1");

		LexiconEntry.viewNoEntry(rowEntry = ${userGroupName});
	}

	@summary = "Default summary"
	macro tearDownCP() {
		UserGroup.openUserGroupsAdmin();

		if (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			PortletEntry.selectAll();

			ClickNoError(
				key_text = "trash",
				locator1 = "ManagementBar#ANY_ICON");

			AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

			Alert.viewSuccessMessage();

			AssertTextEquals(
				locator1 = "Message#EMPTY_INFO",
				value1 = "No user groups were found.");
		}
	}

	@summary = "Default summary"
	macro viewAssignMemberCP(userGroupName = null, userScreenName = null) {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Assign Members",
			rowEntry = ${userGroupName});

		AssertTextEquals(
			key_userScreenName = ${userScreenName},
			locator1 = "UserGroupsAssignUsers#USER_TABLE_SCREEN_NAME",
			value1 = ${userScreenName});
	}

	@summary = "Default summary"
	macro viewCP(userGroupName = null) {
		LexiconEntry.viewEntryName(rowEntry = ${userGroupName});

		LexiconEntry.viewEntryMenu(rowEntry = ${userGroupName});

		AssertElementPresent(locator1 = "Checkbox#LEXICON_SELECT_ALL_INPUT");

		AssertElementPresent(
			key_userGroupName = ${userGroupName},
			locator1 = "UserGroups#USER_GROUP_TABLE_CHECKBOX");
	}

	@summary = "Default summary"
	macro viewDefault() {
		AssertTextEquals.assertPartialText(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No user groups were found.");
	}

	@summary = "Default summary"
	macro viewPortletCP() {
		AssertElementPresent(locator1 = "Button#PLUS");

		AssertElementPresent(locator1 = "TextInput#SEARCH");

		AssertElementPresent(locator1 = "Button#SUBMIT");

		AssertElementPresent(locator1 = "Icon#HEADER_VERTICAL_ELLIPSIS");
	}

	@summary = "Default summary"
	macro viewRemoveMemberCP(userGroupName = null, userFirstName = null) {
		LexiconEntry.viewEntryName(rowEntry = ${userGroupName});

		LexiconEntry.gotoEntry(rowEntry = ${userGroupName});

		AssertTextNotPresent(value1 = ${userFirstName});
	}

}