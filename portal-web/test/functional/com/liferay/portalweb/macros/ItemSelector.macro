definition {

	@summary = "Default summary"
	macro addAudioFromURL(navTab = null, audioSourceURL = null) {
		var key_navTab = ${navTab};

		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		AssertClick(
			locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
			value1 = "URL");

		AssertElementPresent(locator1 = "ItemSelector#URL_LABEL");

		Type(
			locator1 = "TextInput#URL",
			value1 = ${audioSourceURL});

		AssertTextEquals(
			locator1 = "TextInput#URL",
			value1 = ${audioSourceURL});

		WaitForVisible(
			key_text = "Add",
			locator1 = "Button#ANY_ENABLED");

		Button.clickAdd();

		SelectFrameTop(value1 = "relative=top");
	}

	@summary = "Default summary"
	macro addImageFromURL(navTab = null, imageSourceURL = null) {
		var key_imageSourceURL = ${imageSourceURL};
		var key_navTab = ${navTab};

		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		if (isSet(wikiAdmin)) {
			AssertElementNotPresent(locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB");
		}
		else {
			AssertClick(
				locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
				value1 = "URL");
		}

		AssertElementPresent(locator1 = "ItemSelector#URL_LABEL");

		AssertElementNotPresent(locator1 = "ItemSelector#URL_IMAGE_PREVIEW");

		AssertVisible(
			key_text = "Add",
			locator1 = "Button#ANY_DISABLED");

		Type(
			locator1 = "TextInput#URL",
			value1 = ${imageSourceURL});

		AssertTextEquals(
			locator1 = "TextInput#URL",
			value1 = ${imageSourceURL});

		if (contains(${imageSourceURL}, "/documents/")) {
			AssertVisible(locator1 = "ItemSelector#URL_IMAGE_SOURCE");

			if (IsElementPresent(locator1 = "ItemSelector#URL_IMAGE_EXAMPLE")) {
				Click(locator1 = "ItemSelector#URL_IMAGE_EXAMPLE");
			}
		}

		WaitForElementPresent(
			key_text = "Add",
			locator1 = "Button#ANY_ENABLED");

		Button.clickAdd();

		SelectFrameTop(value1 = "relative=top");
	}

	@summary = "Default summary"
	macro addVideo(videoShortcutTitle = null, extractedVideoShortcutTitle = null, videoURL = null, videoShortcutDescription = null, videoShortcutPreview = null) {
		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		AssertClick(
			key_navTab = "Documents and Media",
			locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
			value1 = "Documents and Media");

		VideoShortcut.addCP(
			extractedVideoShortcutTitle = ${extractedVideoShortcutTitle},
			videoShortcutDescription = ${videoShortcutDescription},
			videoShortcutPreview = ${videoShortcutPreview},
			videoShortcutTitle = ${videoShortcutTitle},
			videoURL = ${videoURL});

		Click(
			key_imageFileName = ${videoShortcutTitle},
			locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");

		SelectFrameTop(value1 = "relative=top");
	}

	@summary = "Default summary"
	macro addVideoFromDM(navTab = null, videoShortcutTitle = null, depotName = null, videoShortcutPreview = null) {
		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		AssertClick(
			key_navTab = ${navTab},
			locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
			value1 = ${navTab});

		if (isSet(depotName)) {
			AssertClick(
				key_breadcrumbName = "Sites and Libraries",
				locator1 = "Breadcrumb#BREADCRUMB_ENTRY",
				value1 = "Sites and Libraries");

			AssertClick(
				key_groupTab = "Asset Library",
				locator1 = "ItemSelector#NAVIGATION_GROUP_TAB",
				value1 = "Asset Library");

			LexiconCard.clickCard(card = ${depotName});
		}

		if (isSet(videoShortcutPreview)) {
			Click(
				key_cardTitle = ${videoShortcutTitle},
				locator1 = "Icon#EYE_PREVIEW_SPECIFIC");

			AssertVisible(
				key_videoShortcutPreview = ${videoShortcutPreview},
				locator1 = "DocumentsAndMediaDocument#VIDEO_SHORTCUT_PREVIEW");

			AssertTextEquals(
				key_imageFileName = ${videoShortcutTitle},
				locator1 = "ItemSelector#VIEWER_FOOTER_IMAGE",
				value1 = ${videoShortcutTitle});

			AssertClick(
				locator1 = "ItemSelector#ADD_BUTTON",
				value1 = "Add");
		}
		else {
			Click(
				key_imageFileName = ${videoShortcutTitle},
				locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");
		}

		SelectFrameTop(value1 = "relative=top");
	}

	@summary = "Default summary"
	macro addVideoFromURL(navTab = null, videoSourceURL = null) {
		var key_navTab = ${navTab};

		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		AssertClick(
			locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
			value1 = "URL");

		AssertElementPresent(locator1 = "ItemSelector#URL_LABEL");

		AssertElementNotPresent(locator1 = "IFrame#EXTERNAL_VIDEO_IFRAME");

		Type(
			locator1 = "TextInput#VIDEO_URL",
			value1 = ${videoSourceURL});

		if (!(isSet(invalidURL))) {
			WaitForElementPresent(
				key_src = "https://",
				locator1 = "IFrame#IFRAME_SRC");
		}

		if (!(isSet(unpublish))) {
			WaitForVisible(
				key_text = "Add",
				locator1 = "Button#ANY_ENABLED");

			ClickNoError(
				key_text = "Add",
				locator1 = "Button#ANY_ENABLED");
		}

		SelectFrameTop(value1 = "relative=top");
	}

	@summary = "Default summary"
	macro changeWorkspaces(navTab = null, workspacesType = null) {
		if (!(isSet(customImage))) {
			SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");
		}

		if (isSet(navTab)) {
			AssertClick(
				key_navTab = ${navTab},
				locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
				value1 = ${navTab});
		}

		AssertClick(
			key_breadcrumbName = "Sites and Libraries",
			locator1 = "Breadcrumb#BREADCRUMB_ENTRY",
			value1 = "Sites and Libraries");

		AssertClick(
			key_groupTab = ${workspacesType},
			locator1 = "ItemSelector#NAVIGATION_GROUP_TAB",
			value1 = ${workspacesType});
	}

	@summary = "Default summary"
	macro clearFilterResults(webContentTitle = null, currentSiteWebContentTitle = null) {
		Click(locator1 = "Link#CLEAR");

		AssertElementNotPresent(locator1 = "Search#SEARCH_INFO");

		AssertVisible(
			key_rowEntry = ${currentSiteWebContentTitle},
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");

		AssertElementNotPresent(
			key_rowEntry = ${webContentTitle},
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");
	}

	@summary = "Default summary"
	macro configureFilterViaSelectFile(navTab = null, filterBy = null) {
		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		AssertClick(
			key_navTab = ${navTab},
			locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
			value1 = ${navTab});

		ManagementBar.setFilterAndOrder(filterBy = ${filterBy});
	}

	@summary = "Default summary"
	macro gotoImageSelectorViaSelectButton() {
		Click(locator1 = "FormFields#UPLOAD_ON_WIDGET");
	}

	@summary = "Default summary"
	macro gotoItemSelectorViaAlloyEditor() {
		AlloyEditor.clickContentField();

		Click(locator1 = "AlloyEditor#CONTENT_ADD_BUTTON");

		if (isSet(video)) {
			Click(locator1 = "AlloyEditor#CONTENT_ADD_MENUBAR_VIDEO");
		}
		else {
			Click.clickNoMouseOver(locator1 = "AlloyEditor#CONTENT_ADD_MENUBAR_IMAGE");
		}
	}

	@summary = "Default summary"
	macro gotoItemSelectorViaCKEditor() {
		if (isSet(video)) {
			WaitForLiferayEvent.initializeLiferayEventLog();

			Click(locator1 = "CKEditor#TOOLBAR_VIDEO_BUTTON");
		}
		else {
			Click(locator1 = "CKEditor#TOOLBAR_IMAGE_BUTTON");

			SelectFrame(value1 = "relative=top");
		}
	}

	@summary = "Default summary"
	macro gotoItemSelectorViaSelectButton() {
		Click(locator1 = "FormFields#SELECT_UPLOAD_FIELD");
	}

	@summary = "Default summary"
	macro gotoItemSelectorViaSelectButtonByGuest(fieldName = null) {
		Click(
			key_fieldName = ${fieldName},
			locator1 = "FormFields#UPLOAD_FIELD_GUEST_1");
	}

	@summary = "Default summary"
	macro gotoItemSelectorViaSelectButtonByIndex(index = null) {
		Click(
			key_index = ${index},
			locator1 = "FormFields#SELECT_UPLOAD_FIELD_BY_INDEX");
	}

	@summary = "Default summary"
	macro gotoPreviewViaSelectFile(navTab = null) {
		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		AssertClick(
			key_navTab = ${navTab},
			locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
			value1 = ${navTab});

		Click(locator1 = "Icon#EYE_PREVIEW");
	}

	@summary = "Default summary"
	macro previewEmbeddedVideo(invalidURL = null, validURL = null) {
		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		WaitForPageLoad();

		if (isSet(invalidURL)) {
			if (contains(${invalidURL}, "youtube")) {
				SelectFrame.selectFrameNoLoading(locator1 = "IFrame#EXTERNAL_VIDEO_IFRAME");

				Click(locator1 = "Button#YOUTUBE_PLAY");

				AssertTextEquals.assertPartialText(
					locator1 = "Message#ERROR_INVALID_YOUTUBE_VIDEO",
					value1 = "An error occurred. Please try again later.");
			}
			else if (contains(${invalidURL}, "facebook")) {
				SelectFrame.selectFrameNoLoading(locator1 = "IFrame#EXTERNAL_VIDEO_IFRAME");

				AssertTextEquals.assertPartialText(
					locator1 = "Message#ERROR_INVALID_FACEBOOK_VIDEO",
					value1 = "Video Unavailable");
			}
			else if (contains(${invalidURL}, "vimeo")) {
				SelectFrame.selectFrameNoLoading(locator1 = "IFrame#EXTERNAL_VIDEO_IFRAME");

				AssertTextEquals.assertPartialText(
					locator1 = "Message#ERROR_INVALID_VIMEO_VIDEO",
					value1 = "This video does not exist.");
			}
			else {
				AssertTextEquals(
					locator1 = "Message#INFO_EXTERNAL_VIDEO_PREVIEW",
					value1 = "Sorry, this platform is not supported.");
			}
		}
		else {
			AssertVisible(
				key_text = "Add",
				locator1 = "Button#ANY_ENABLED");

			AssertVisible(
				key_videoShortcutPreview = ${validURL},
				locator1 = "DocumentsAndMediaDocument#VIDEO_SHORTCUT_PREVIEW");
		}

		SelectFrameTop(value1 = "relative=top");
	}

	@summary = "Default summary"
	macro previewVideoFromDM(navTab = null, videoShortcutTitle = null, depotName = null, validURL = null) {
		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		AssertClick(
			key_navTab = ${navTab},
			locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
			value1 = ${navTab});

		if (isSet(depotName)) {
			AssertClick(
				key_breadcrumbName = "Sites and Libraries",
				locator1 = "Breadcrumb#BREADCRUMB_ENTRY",
				value1 = "Sites and Libraries");

			AssertClick(
				key_groupTab = "Asset Library",
				locator1 = "ItemSelector#NAVIGATION_GROUP_TAB",
				value1 = "Asset Library");

			LexiconCard.clickCard(card = ${depotName});
		}

		Click(
			key_cardTitle = ${videoShortcutTitle},
			locator1 = "Icon#EYE_PREVIEW_SPECIFIC");

		AssertVisible(
			key_videoShortcutPreview = ${validURL},
			locator1 = "DocumentsAndMediaDocument#VIDEO_SHORTCUT_PREVIEW");

		AssertTextEquals(
			key_imageFileName = ${videoShortcutTitle},
			locator1 = "ItemSelector#VIEWER_FOOTER_IMAGE",
			value1 = ${videoShortcutTitle});
	}

	@summary = "Default summary"
	macro searchBlogImages(imageFileName = null) {
		var key_imageFileName = ${imageFileName};

		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		var key_navTab = "Documents and Media";

		AssertClick(
			locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
			value1 = "Documents and Media");

		AssertElementNotPresent(locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");

		var key_navTab = "Blog Images";

		AssertClick(
			locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
			value1 = "Blog Images");

		Type(
			locator1 = "TextInput#SEARCH",
			value1 = ${imageFileName});

		KeyPress(
			locator1 = "TextInput#SEARCH",
			value1 = "\RETURN");

		AssertTextEquals(
			locator1 = "Search#SEARCH_INFO",
			value1 = "1 Result Found for \"${imageFileName}\"");

		AssertElementPresent(locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");

		SelectFrameTop(value1 = "relative=top");
	}

	@summary = "Default summary"
	macro searchDMImages(imageFileName = null) {
		var key_imageFileName = ${imageFileName};
		var key_uploadFileName = ${imageFileName};

		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		var key_navTab = "Blog Images";

		AssertClick(
			locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
			value1 = "Blog Images");

		LexiconCard.viewCardNotPresent(card = ${imageFileName});

		var key_navTab = "Documents and Media";

		AssertClick(
			locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
			value1 = "Documents and Media");

		Type(
			locator1 = "TextInput#SEARCH",
			value1 = ${imageFileName});

		KeyPress(
			locator1 = "TextInput#SEARCH",
			value1 = "\RETURN");

		AssertTextEquals(
			locator1 = "Search#SEARCH_INFO",
			value1 = "1 Result Found for \"${imageFileName}\"");

		LexiconCard.viewCardPresent(card = ${imageFileName});

		SelectFrameTop(value1 = "relative=top");
	}

	@summary = "Default summary"
	macro selectBlogImage(navTab = null, imageFileName = null) {
		var key_imageFileName = ${imageFileName};
		var key_navTab = ${navTab};
		var key_uploadFileName = ${imageFileName};

		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		AssertClick(
			locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
			value1 = "Blog Images");

		Click(locator1 = "ItemSelector#SELECT_FILE_IMAGE_VIEW_BUTTON");

		AssertTextEquals(
			locator1 = "ItemSelector#FOOTER",
			value1 = ${imageFileName});

		AssertClick(
			locator1 = "ItemSelector#ADD_BUTTON",
			value1 = "Add");

		SelectFrameTop();
	}

	@summary = "Default summary"
	macro selectDMImage(navTab = null, dmSubFolderName = null, imageFileName = null, dmFolderName = null) {
		var key_navTab = ${navTab};
		var key_uploadFileName = ${imageFileName};

		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		if (IsElementPresent(locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB")) {
			AssertClick(
				locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
				value1 = ${navTab});
		}

		if (isSet(dmFolderName)) {
			var key_dmFolderName = ${dmFolderName};

			AssertClick(
				locator1 = "DocumentsAndMedia#ICON_FOLDER_TITLE",
				value1 = ${dmFolderName});
		}

		if (isSet(dmSubFolderName)) {
			var key_dmFolderName = ${dmSubFolderName};

			AssertClick(
				locator1 = "DocumentsAndMedia#ICON_FOLDER_TITLE",
				value1 = ${dmSubFolderName});
		}

		var key_imageFileName = StringUtil.replace(${imageFileName}, " ", "+");

		LexiconCard.clickCard(card = ${imageFileName});

		SelectFrameTop();
	}

	@summary = "Default summary"
	macro selectRepositoryImage(navTab = null, depotName = null, imageFileName = null, customImage = null, folderName = null) {
		var key_imageFileName = ${imageFileName};

		ItemSelector.changeWorkspaces(
			customImage = ${customImage},
			navTab = ${navTab},
			workspacesType = "Asset Library");

		LexiconCard.clickCard(card = ${depotName});

		if (isSet(folderName)) {
			LexiconCard.clickCard(card = ${folderName});
		}

		if (isSet(noViewPermission)) {
			AssertElementNotPresent(locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");
		}
		else {
			Click(locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");
		}

		SelectFrameTop();
	}

	@summary = "Default summary"
	macro selectSiteImage(imageFileName = null, siteName = null) {
		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		Click(
			key_breadcrumbName = "Sites and Libraries",
			locator1 = "Breadcrumb#BREADCRUMB_ENTRY");

		Click(
			key_siteName = ${siteName},
			locator1 = "ItemSelector#SITE_SELECTOR");

		Click(
			key_imageFileName = ${imageFileName},
			locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");

		SelectFrameTop();
	}

	@summary = "Default summary"
	macro switchTab(navTab = null) {
		AssertClick(
			key_navTab = ${navTab},
			locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
			value1 = ${navTab});

		AssertElementPresent(
			key_tab = ${navTab},
			locator1 = "NavTab#ACTIVE_TAB_LINK");
	}

	@summary = "Default summary"
	macro uploadFile(navTab = null, uploadFileName = null, invalidFileValidation = null) {
		var key_navTab = ${navTab};
		var key_uploadFileName = ${uploadFileName};

		AssertVisible(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		if (IsElementPresent(locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB")) {
			AssertClick(
				locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
				value1 = ${navTab});
		}

		UploadDependencyFile.uploadFile(fileName = ${uploadFileName});

		if (isSet(invalidFileValidation)) {
			AssertTextEquals(
				locator1 = "Message#ERROR_FORM_FIELD_REQUIRED",
				value1 = ${invalidFileValidation});

			AssertElementNotPresent(
				locator1 = "ItemSelector#ADD_BUTTON",
				value1 = "Add");

			SelectFrame(value1 = "relative=top");

			Click.clickNoMouseOver(locator1 = "Modal#CLOSE_BUTTON");
		}
		else {
			WaitForVisible(locator1 = "ItemSelector#ADD_BUTTON");

			AssertClick(
				locator1 = "ItemSelector#ADD_BUTTON",
				value1 = "Add");

			SelectFrameTop();
		}
	}

	@summary = "Default summary"
	macro uploadFileWithSelectFrameNoLoading(navTab = null, uploadFileName = null, invalidFileValidation = null) {
		var key_navTab = ${navTab};
		var key_uploadFileName = ${uploadFileName};

		SelectFrame.selectFrameNoLoading(locator1 = "IFrame#MODAL_IFRAME");

		if (IsElementPresent(locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB")) {
			AssertClick(
				locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
				value1 = ${navTab});
		}

		UploadCommonFile.uploadCommonFileHiddenNoMouseOver(
			locator1 = "TextInput#FILE",
			value1 = ${uploadFileName});

		if (isSet(invalidFileValidation)) {
			AssertTextEquals(
				key_alertMessage = ${invalidFileValidation},
				locator1 = "Message#ERROR_ENTER_A_VALID_VALUE",
				value1 = ${invalidFileValidation});

			AssertElementNotPresent(
				locator1 = "ItemSelector#ADD_BUTTON",
				value1 = "Add");

			SelectFrame(value1 = "relative=top");

			Click.clickNoMouseOver(locator1 = "Modal#CLOSE_BUTTON");
		}
		else {
			WaitForVisible(locator1 = "ItemSelector#ADD_BUTTON");

			AssertClick(
				locator1 = "ItemSelector#ADD_BUTTON",
				value1 = "Add");
		}

		SelectFrameTop();
	}

	@summary = "Default summary"
	macro viewBlogImageDetails(imageFormat = null, uploadFileName = null, imageFileName = null, lastModifiedBy = null, imageSize = null, imageStatus = null, versionNumber = null) {
		var key_imageFileName = ${imageFileName};
		var key_uploadFileName = ${uploadFileName};

		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		AssertElementPresent(locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");

		Click(locator1 = "Icon#EYE_PREVIEW");

		AssertElementPresent.assertVisible(locator1 = "ItemSelector#VIEWER_INFO");

		Click(locator1 = "ItemSelector#VIEWER_INFO");

		AssertElementPresent(
			key_tab = "File Info",
			locator1 = "NavTab#TAB_LINK");

		AssertElementPresent(
			key_tab = "Version",
			locator1 = "NavTab#TAB_LINK");

		AssertTextEquals(
			locator1 = "ItemSelector#SIDEBAR_FILE_INFO_FORMAT",
			value1 = ${imageFormat});

		AssertTextEquals(
			locator1 = "ItemSelector#SIDEBAR_FILE_INFO_SIZE",
			value1 = ${imageSize});

		AssertTextEquals(
			locator1 = "ItemSelector#SIDEBAR_FILE_INFO_NAME",
			value1 = ${uploadFileName});

		AssertTextEquals.assertPartialText(
			locator1 = "ItemSelector#SIDEBAR_FILE_INFO_MODIFIED",
			value1 = ${lastModifiedBy});

		var key_tab = "Version";

		AssertClick(
			locator1 = "NavTab#TAB_LINK",
			value1 = "Version");

		AssertTextEquals(
			locator1 = "ItemSelector#SIDEBAR_VERSION_VERSION",
			value1 = ${versionNumber});

		AssertTextEquals(
			locator1 = "ItemSelector#SIDEBAR_VERSION_STATUS",
			value1 = ${imageStatus});

		SelectFrameTop(value1 = "relative=top");
	}

	@summary = "Default summary"
	macro viewCardThumbnails(navTab = null, cardList = null, depotName = null, customImage = null) {
		if (isSet(depotName)) {
			ItemSelector.changeWorkspaces(
				customImage = ${customImage},
				navTab = ${navTab},
				workspacesType = "Asset Library");

			LexiconCard.clickCard(card = ${depotName});
		}
		else {
			SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");
		}

		LexiconEntry.changeDisplayStyle(displayStyle = "cards");

		for (var card : list ${cardList}) {
			LexiconCard.viewCardThumbnail(card = ${card});
		}
	}

	@summary = "Default summary"
	macro viewCurrentSiteFilterResults(navTab = null, imageFileName = null, noImageFileName = null, viewNoLocationInfo = null) {
		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		if (isSet(navTab)) {
			AssertClick(
				key_navTab = ${navTab},
				locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
				value1 = ${navTab});
		}

		LexiconEntry.changeDisplayStyle(displayStyle = "cards");

		AssertVisible(
			key_imageFileName = ${imageFileName},
			locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");

		AssertElementNotPresent(
			key_imageFileName = ${noImageFileName},
			locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");

		ManagementBar.setFilterAndOrder(filterBy = "Current Site");

		AssertVisible(
			key_imageFileName = ${imageFileName},
			locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");

		AssertElementNotPresent(
			key_imageFileName = ${noImageFileName},
			locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");

		if (isSet(viewNoLocationInfo)) {
			AssertElementNotPresent(
				key_locationName = ${viewNoLocationInfo},
				key_title = ${imageFileName},
				locator1 = "ItemSelector#LOCATION_INFO_CARDS",
				value1 = ${viewNoLocationInfo});
		}

		AssertElementNotPresent(locator1 = "Search#SEARCH_INFO");

		AssertVisible(
			key_imageFileName = ${imageFileName},
			locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");
	}

	@summary = "Default summary"
	macro viewEverywhereFilterResults(navTab = null, imageFileName = null, dmFolderName = null) {
		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		if (isSet(navTab)) {
			AssertClick(
				key_navTab = ${navTab},
				locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
				value1 = ${navTab});
		}

		ManagementBar.setFilterAndOrder(filterBy = "Everywhere");

		LexiconEntry.changeDisplayStyle(displayStyle = "cards");

		if (isSet(dmFolderName)) {
			Click(
				key_card = ${dmFolderName},
				locator1 = "Card#CARD_TITLE");
		}

		AssertVisible(
			key_imageFileName = ${imageFileName},
			locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");
	}

	@summary = "Default summary"
	macro viewExternalVideoInDifferentViews(navTab = null, videoShortcutList = null) {
		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		if (isSet(navTab)) {
			AssertClick(
				key_navTab = ${navTab},
				locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
				value1 = ${navTab});
		}

		LexiconEntry.changeDisplayStyle(displayStyle = "cards");

		for (var videoShortcutTitle : list ${videoShortcutList}) {
			LexiconCard.viewCardTitle(card = ${videoShortcutTitle});
		}

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		for (var videoShortcutTitle : list ${videoShortcutList}) {
			LexiconList.viewListEntryTitle(listEntry = ${videoShortcutTitle});
		}

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		for (var videoShortcutTitle : list ${videoShortcutList}) {
			AssertTextEquals.assertPartialText(
				key_tableEntry = ${videoShortcutTitle},
				locator1 = "LexiconTable#TABLE_ENTRY_TITLE",
				value1 = ${videoShortcutTitle});
		}
	}

	@summary = "Default summary"
	macro viewFilteredResultsViaAssetLists(itemType = null, assetTitle = null, itemSubtype = null, assetListName = null, assetType = null) {
		if (isSet(assetListName)) {
			Click(
				key_title = ${assetListName},
				locator1 = "AssetLists#ASSET_LIST_ENTRY_TITLE");
		}

		if (IsElementPresent(locator1 = "APConfiguration#SOURCE_SCOPE_ASSET_TYPE_SELECT")) {
			AssetListsAdmin.configureItemTypeInManualCollection(
				itemSubtype = ${itemSubtype},
				itemType = ${itemType});
		}

		Button.clickSelect();

		MenuItem.click(menuItem = ${assetType});

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		AssertTextEquals(
			key_assetTitle = ${assetTitle},
			locator1 = "AssetLists#ASSET_TITLE_ROW",
			value1 = ${assetTitle});

		ManagementBar.setFilterAndOrder(filterBy = "Current Asset Library");

		AssertTextEquals(
			key_assetTitle = ${assetTitle},
			locator1 = "AssetLists#ASSET_TITLE_ROW",
			value1 = ${assetTitle});

		ManagementBar.setFilterAndOrder(filterBy = "Everywhere");

		AssertTextEquals(
			key_assetTitle = ${assetTitle},
			locator1 = "AssetLists#ASSET_TITLE_ROW",
			value1 = ${assetTitle});

		Click(locator1 = "Link#CLEAR");

		AssertElementNotPresent(locator1 = "Search#SEARCH_INFO");

		AssertTextEquals(
			key_assetTitle = ${assetTitle},
			locator1 = "AssetLists#ASSET_TITLE_ROW",
			value1 = ${assetTitle});
	}

	@summary = "Default summary"
	macro viewListThumbnails(navTab = null, depotName = null, customImage = null, listEntry = null) {
		if (isSet(depotName)) {
			ItemSelector.changeWorkspaces(
				customImage = ${customImage},
				navTab = ${navTab},
				workspacesType = "Asset Library");

			LexiconCard.clickCard(card = ${depotName});
		}
		else {
			SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");
		}

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		for (var list : list ${listEntry}) {
			AssertVisible(
				key_list = ${list},
				locator1 = "DocumentsAndMediaDocument#VIDEO_SHORTCUT_THUMBNAIL_LIST");
		}
	}

	@summary = "Default summary"
	macro viewLocationInfoViaBlogs(navTab = null, locationName = null, titleList = null) {
		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		if (isSet(navTab)) {
			AssertClick(
				key_navTab = ${navTab},
				locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
				value1 = ${navTab});
		}

		ManagementBar.setFilterAndOrder(filterBy = "Everywhere");

		LexiconEntry.changeDisplayStyle(displayStyle = "cards");

		for (var title : list ${titleList}) {
			AssertTextEquals(
				key_locationName = ${locationName},
				key_title = ${title},
				locator1 = "ItemSelector#LOCATION_INFO_CARDS",
				value1 = ${locationName});
		}

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		for (var title : list ${titleList}) {
			AssertTextEquals(
				key_locationName = ${locationName},
				key_title = ${title},
				locator1 = "ItemSelector#LOCATION_INFO_LIST",
				value1 = ${locationName});
		}

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		for (var title : list ${titleList}) {
			AssertTextEquals(
				key_locationName = ${locationName},
				key_title = ${title},
				locator1 = "ItemSelector#LOCATION_INFO_TABLE",
				value1 = ${locationName});
		}
	}

	@summary = "Default summary"
	macro viewLocationInfoViaWCD(depotName = null, locationName = null, titleList = null, portletName = null) {
		WebContentDisplayPortlet.gotoSelectWebContent(
			depotName = ${depotName},
			portletName = ${portletName});

		ManagementBar.setFilterAndOrder(filterBy = "Everywhere");

		LexiconEntry.changeDisplayStyle(displayStyle = "cards");

		for (var title : list ${titleList}) {
			AssertTextEquals(
				key_locationName = ${locationName},
				key_title = ${title},
				locator1 = "ItemSelector#LOCATION_INFO_CARDS_2",
				value1 = ${locationName});
		}

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		for (var title : list ${titleList}) {
			AssertTextEquals(
				key_locationName = ${locationName},
				key_title = ${title},
				locator1 = "ItemSelector#LOCATION_INFO_LIST",
				value1 = ${locationName});
		}

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		for (var title : list ${titleList}) {
			AssertTextEquals(
				key_locationName = ${locationName},
				key_title = ${title},
				locator1 = "ItemSelector#LOCATION_INFO_TABLE",
				value1 = ${locationName});
		}
	}

	@summary = "Default summary"
	macro viewPreview(footer = null, imageFileName = null) {
		AssertElementPresent(
			key_footer = ${footer},
			locator1 = "ItemSelector#VIEWER_FOOTER");

		AssertElementPresent(
			key_imageFileName = ${imageFileName},
			locator1 = "ItemSelector#VIEWER_FOOTER_IMAGE");
	}

	@summary = "Default summary"
	macro viewRelevantSelections() {
		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		if (isSet(addDocumentOptions)) {
			ItemSelector.switchTab(navTab = "Documents and Media");

			LexiconEntry.gotoAdd();

			SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

			for (var menuItem : list "File Upload,Folder,External Video Shortcut") {
				MenuItem.viewPresent(menuItem = ${menuItem});
			}
		}

		if (isSet(addImageOptions)) {
			LexiconEntry.gotoAdd();

			SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

			for (var menuItem : list "File Upload,Folder") {
				MenuItem.viewPresent(menuItem = ${menuItem});
			}

			for (var menuItem : list "External Video Shortcut,Google Drive Shortcut") {
				MenuItem.viewNotPresent(menuItem = ${menuItem});
			}
		}

		if (isSet(addVideoOptions)) {
			ItemSelector.switchTab(navTab = "Documents and Media");

			LexiconEntry.gotoAdd();

			SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

			for (var menuItem : list "File Upload,Folder,External Video Shortcut") {
				MenuItem.viewPresent(menuItem = ${menuItem});
			}

			for (var menuItem : list "Google Drive Shortcut") {
				MenuItem.viewNotPresent(menuItem = ${menuItem});
			}
		}
	}

	@summary = "Default summary"
	macro viewWCCurrentSiteFilterResults(webContentTitle = null, noWebContentTitle = null, viewNoLocationInfo = null) {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		AssertVisible(
			key_rowEntry = ${webContentTitle},
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");

		AssertElementNotPresent(
			key_rowEntry = ${noWebContentTitle},
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");

		ManagementBar.setFilterAndOrder(filterBy = "Current Site");

		AssertVisible(
			key_rowEntry = ${webContentTitle},
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");

		AssertElementNotPresent(
			key_rowEntry = ${noWebContentTitle},
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");

		if (isSet(viewNoLocationInfo)) {
			AssertElementNotPresent(
				key_locationName = ${viewNoLocationInfo},
				key_title = ${webContentTitle},
				locator1 = "ItemSelector#LOCATION_INFO_CARDS_2",
				value1 = ${viewNoLocationInfo});
		}

		AssertElementNotPresent(locator1 = "Search#SEARCH_INFO");

		AssertVisible(
			key_rowEntry = ${webContentTitle},
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");
	}

	@summary = "Default summary"
	macro viewWCEverywhereFilterResults(siteName = null, folderWebContentTitle = null, wcFolderName = null) {
		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		ManagementBar.setFilterAndOrder(filterBy = "Everywhere");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		if (isSet(draft)) {
			AssertElementNotPresent(
				key_rowEntry = ${webContentTitle},
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");

			AssertTextEquals.assertPartialText(
				locator1 = "Message#EMPTY_INFO",
				value1 = "No web content was found.");
		}
		else {
			for (var webContentTitle : list ${webContentTitle}) {
				AssertVisible(
					key_rowEntry = ${webContentTitle},
					locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");
			}
		}

		if (isSet(viewFolderWc)) {
			Click(
				key_rowEntry = ${wcFolderName},
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");

			AssertVisible(
				key_breadcrumbName = ${siteName},
				locator1 = "Breadcrumb#BREADCRUMB_ENTRY");

			Breadcrumb.viewActiveEntry(breadcrumbName = ${wcFolderName});

			AssertVisible(
				key_rowEntry = ${folderWebContentTitle},
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");
		}
	}

	@summary = "Default summary"
	macro viewWikiAttachment(imageFileName = null) {
		var key_imageFileName = ${imageFileName};

		ItemSelector.gotoItemSelectorViaCKEditor();

		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		LexiconEntry.changeDisplayStyle(displayStyle = "cards");

		AssertElementPresent(
			key_imageFileName = ${imageFileName},
			locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");
	}

}