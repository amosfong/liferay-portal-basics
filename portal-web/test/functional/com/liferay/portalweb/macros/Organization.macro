definition {

	var orgName = "Organization Name";

	@summary = "Default summary"
	macro addComment(orgComment = null) {
		Type(
			locator1 = "TextArea#COMMENTS",
			value1 = ${orgComment});

		PortletEntry.save();
	}

	@summary = "Default summary"
	macro addContactInformation(country = null, extension = null, address = null, city = null, addressType = null, postalCode = null, mondayClose = null, contactType = null, mondayOpen = null, makePrimary = null, url = null, number = null, street1 = null, region = null) {
		if (isSet(contactType)) {
			var key_contactType = ${contactType};

			Click.mouseDownMouseUp(locator1 = "UsersAndOrganizationsEditOrganization#CONTACT_ADD_BUTTON");

			if (${makePrimary} == "true") {
				Check(
					checkboxName = "Make Primary",
					locator1 = "Checkbox#ANY_CHECKBOX");
			}

			// Addresses

			if (isSet(addressType)) {
				Select(
					locator1 = "Select#TYPE",
					value1 = ${addressType});
			}

			if (isSet(street1)) {
				Type(
					locator1 = "TextInput#STREET_1",
					value1 = ${street1});
			}

			if (isSet(city)) {
				Type(
					locator1 = "TextInput#CITY",
					value1 = ${city});
			}

			if (isSet(country)) {
				Select(
					locator1 = "Select#COUNTRY",
					value1 = ${country});
			}

			if (isSet(region)) {
				Select(
					locator1 = "Select#REGION",
					value1 = ${region});
			}

			if (isSet(postalCode)) {
				Type(
					locator1 = "TextInput#POSTAL_CODE",
					value1 = ${postalCode});
			}

			// Phone Numbers

			if (isSet(number)) {
				Type(
					locator1 = "TextInput#NUMBER",
					value1 = ${number});
			}

			if (isSet(extension)) {
				Type(
					locator1 = "TextInput#EXTENSION",
					value1 = ${extension});
			}

			// Additional Email Addresses

			if (isSet(address)) {
				Type(
					locator1 = "TextInput#ADDRESS",
					value1 = ${address});
			}

			// Websites

			if (isSet(url)) {
				Type(
					locator1 = "TextInput#URL",
					value1 = ${url});
			}

			// Opening Hours

			if (isSet(mondayOpen)) {
				Select(
					locator1 = "Select#MONDAY_OPEN",
					value1 = ${mondayOpen});
			}

			if (isSet(mondayClose)) {
				Select(
					locator1 = "Select#MONDAY_CLOSE",
					value1 = ${mondayClose});
			}
		}

		PortletEntry.save();
	}

	@summary = "Default summary"
	macro addCP(orgName = null) {
		LexiconEntry.gotoAdd();

		Type(
			locator1 = "TextInput#NAME",
			value1 = ${orgName});

		Button.clickSave();

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro addSecurityQuestionsCP(securityQuestions = null, localizedLanguage = null, localizedSecurityQuestions = null) {
		MenuBar.gotoSecurityQuestions();

		Type(
			locator1 = "TextArea#SECURITY_QUESTIONS",
			value1 = ${securityQuestions});

		if (isSet(localizedLanguage)) {
			PortletEntry.changeLocale(
				fieldName = "Security Questions",
				locale = ${localizedLanguage});

			Type(
				locator1 = "TextArea#SECURITY_QUESTIONS",
				value1 = ${localizedSecurityQuestions});
		}

		PortletEntry.save();

		MenuBar.gotoSecurityQuestions();

		AssertTextEquals(
			locator1 = "TextArea#SECURITY_QUESTIONS",
			value1 = ${securityQuestions});

		if (isSet(localizedLanguage)) {
			PortletEntry.changeLocale(
				fieldName = "Security Questions",
				locale = ${localizedLanguage});

			AssertTextEquals(
				locator1 = "TextArea#SECURITY_QUESTIONS",
				value1 = ${securityQuestions});
		}
	}

	@summary = "Default summary"
	macro addSiteCP(checkEnablePropagationCheckbox = null, orgName = null, siteTemplateName = null) {
		Organization.gotoEdit(orgName = ${orgName});

		UsersAndOrganizationsNavigator.gotoMenuBarOrganizationSite();

		Check.checkToggleSwitch(
			key_toggleSwitchLabel = "Create Site",
			locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

		if (isSet(siteTemplateName)) {
			Select(
				locator1 = "UserGroupsEditUserGroup#PUBLIC_PAGES_SELECT",
				value1 = ${siteTemplateName});

			if (${checkEnablePropagationCheckbox} == "true") {
				AssertChecked(locator1 = "UserGroupsEditUserGroup#ENABLE_PROPAGATION_PUBLIC_PAGE_CHECKBOX");
			}
			else {
				Uncheck(locator1 = "UserGroupsEditUserGroup#ENABLE_PROPAGATION_PUBLIC_PAGE_CHECKBOX");
			}
		}

		PortletEntry.save();
	}

	@summary = "Default summary"
	macro addSuborganizationCP(orgName = null, suborgName = null) {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Add Organization",
			rowEntry = ${orgName});

		Type(
			locator1 = "TextInput#NAME",
			value1 = ${suborgName});

		PortletEntry.save();

		AssertTextEquals(
			locator1 = "TextInput#NAME",
			value1 = ${suborgName});
	}

	@summary = "Default summary"
	macro addUser(userJobTitle = null, userComment = null, organizationName = null, userTag = null, userLastName = null, userScreenName = null, userMiddleName = null, userEmailAddress = null, userGender = null, userFirstName = null, userBirthday = null) {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Add User",
			rowEntry = ${organizationName});

		User._editUserInformation(
			userBirthday = ${userBirthday},
			userComment = ${userComment},
			userEmailAddress = ${userEmailAddress},
			userFirstName = ${userFirstName},
			userGender = ${userGender},
			userJobTitle = ${userJobTitle},
			userLastName = ${userLastName},
			userMiddleName = ${userMiddleName},
			userScreenName = ${userScreenName},
			userTag = ${userTag});

		var browserType = PropsUtil.get("browser.type");

		if (${browserType} == "safari") {
			Button.clickSave();
		}
		else {
			AssertClick(
				locator1 = "Button#SUBMIT",
				value1 = "Save");
		}

		WaitForLiferayEvent.initializeLiferayEventLog();
	}

	@summary = "Default summary"
	macro addWithLocalizedCountryCP(navItem = null, localizedOption = null, orgName = null, countryName = null, saveButton = null) {
		Navigator.gotoNavItem(navItem = ${navItem});

		Button.click(button = ${newButton});

		if (IsElementPresent(key_menuItem = ${localizedOption}, locator1 = "MenuItem#ANY_MENU_ITEM")) {
			MenuItem.click(menuItem = ${localizedOption});
		}

		Type(
			locator1 = "TextInput#NAME",
			value1 = ${orgName});

		Select(
			key_countryName = ${countryName},
			locator1 = "UsersAndOrganizations#ORGANIZATION_COUNTRY_NAME",
			value1 = ${countryName});

		AssertClick(
			key_saveButton = ${saveButton},
			locator1 = "Button#SAVE_LOCALIZED",
			value1 = ${saveButton});

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro assignOrgRole(orgName = null, roleTitle = null, userLastName = null, userScreenName = null, userFirstName = null) {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Assign Organization Roles",
			rowEntry = ${orgName});

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		LexiconTable.clickTableEntryTitle(tableEntry = ${roleTitle});

		Navigator.gotoNavItem(navItem = "Available");

		Check(
			key_userScreenName = ${userScreenName},
			locator1 = "UserGroupsAssignUsers#USER_TABLE_CHECKBOX");

		AssertClick(
			locator1 = "Button#UPDATE_ASSOCIATIONS",
			value1 = "Update Associations");

		Alert.viewSuccessMessage();

		Navigator.gotoNavItem(navItem = "Current");

		LexiconEntry.viewEntryName(rowEntry = "${userFirstName} ${userLastName}");

		LexiconEntry.viewEntryName(rowEntry = ${userScreenName});

		IFrame.selectTopFrame();

		Click(locator1 = "Icon#CLOSE");
	}

	@summary = "Default summary"
	macro assignUser(orgName = null, userScreenName = null) {
		Organization.gotoCP(orgName = ${orgName});

		LexiconEntry.gotoAdd();

		MenuItem.click(menuItem = "Assign Users");

		AssertElementPresent.assertVisible(locator1 = "Button#DONE");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		AssertTextEquals(
			key_userScreenName = ${userScreenName},
			locator1 = "UserGroupsAssignUsers#USER_TABLE_SCREEN_NAME",
			value1 = ${userScreenName});

		Check(
			key_userScreenName = ${userScreenName},
			locator1 = "UserGroupsAssignUsers#USER_TABLE_CHECKBOX");

		Pause(value1 = 3000);

		SelectFrameTop();

		AssertClick.assertTextClickAtWaitForLastScript(
			locator1 = "Button#DONE",
			value1 = "Done");

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro delete(orgName = null) {
		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Delete",
			rowEntry = ${orgName});

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro edit(orgName = null, orgNameEdit = null) {
		Organization.gotoEdit(orgName = ${orgName});

		Type(
			locator1 = "TextInput#NAME",
			value1 = ${orgNameEdit});

		PortletEntry.save();
	}

	@summary = "Default summary"
	macro gotoCP(orgName = null) {
		LexiconEntry.gotoEntry(rowEntry = ${orgName});
	}

	@summary = "Default summary"
	macro gotoEdit(orgName = null) {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = ${orgName});
	}

	@summary = "Default summary"
	macro openOrganizationsAdmin(baseURL = null) {
		Navigator.openWithAppendToBaseURL(
			baseURL = ${baseURL},
			urlAppend = "group/control_panel/manage?p_p_id=com_liferay_users_admin_web_portlet_UsersAdminPortlet&p_p_lifecycle=0&p_p_state=maximized&p_p_mode=view&_com_liferay_users_admin_web_portlet_UsersAdminPortlet_screenNavigationCategoryKey=organizations&_com_liferay_users_admin_web_portlet_UsersAdminPortlet_usersListView=flat-organizations");
	}

	@summary = "Default summary"
	macro removeAssignee(userFullName = null) {
		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Remove",
			rowEntry = ${userFullName});

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro removeSuborganizationCP(orgName = null, subOrgName = null) {
		var key_orgName = ${orgName};

		AssertClick(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME",
			value1 = ${orgName});

		LexiconEntry.openEntryMenu(rowEntry = ${subOrgName});

		MenuItem.click(menuItem = "Remove");
	}

	@summary = "Default summary"
	macro searchCP(searchTerm = null, orgName = null) {
		Search.searchCP(searchTerm = ${searchTerm});

		var key_orgName = ${orgName};

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME",
			value1 = ${orgName});
	}

	@summary = "Default summary"
	macro tearDownCP(orgName = null) {
		User.openUsersAdmin();

		AssertClick(
			locator1 = "NavBar#ORGANIZATIONS",
			value1 = "Organizations");

		if (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			if (isSet(orgName)) {
				Search.searchCP(searchTerm = ${orgName});

				PortletEntry.selectAll();

				ClickNoError(
					key_text = "times-circle",
					locator1 = "ManagementBar#ANY_ICON");

				AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

				Alert.viewSuccessMessage();
			}
			else {
				while (IsElementPresent(locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME_GENERIC")) {
					Click(
						locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME_GENERIC",
						value1 = ${orgName});

					AssertClick(
						locator1 = "UsersAndOrganizationsOrganization#MENU_ASSIGN_USERS",
						value1 = "Assign Users");

					Navigator.gotoNavTab(navTab = "Current");

					if ((IsElementPresent(locator1 = "Checkbox#SELECT_ALL")) && (IsVisible(locator1 = "Checkbox#SELECT_ALL"))) {
						Uncheck(locator1 = "Checkbox#SELECT_ALL");

						AssertClick(
							locator1 = "Button#UPDATE_ASSOCIATIONS",
							value1 = "Update Associations");

						AssertTextEquals(
							locator1 = "Message#INFO",
							value1 = "No users were found.");
					}

					Navigator.gotoBack();

					AssertClickNoError(
						locator1 = "UsersAndOrganizationsOrganization#MENU_DELETE",
						value1 = "Delete");

					AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");
				}
			}
		}
	}

	@summary = "Default summary"
	macro viewCP(orgType = null, orgName = null) {
		AssertTextEquals(
			key_columnName = "name",
			key_tableEntry = ${orgName},
			locator1 = "LexiconTable#TABLE_ENTRY_INFO",
			value1 = ${orgName});

		AssertTextEquals(
			key_columnName = "type",
			key_tableEntry = ${orgType},
			locator1 = "LexiconTable#TABLE_ENTRY_INFO",
			value1 = ${orgType});
	}

	@summary = "Default summary"
	macro viewLocalizedCountryCP(editLocalized = null, navItem = null, orgName = null, countryName = null) {
		AssertClick(
			key_navItem = ${navItem},
			locator1 = "NavBar#NAV_ITEM_LINK",
			value1 = ${navItem});

		LexiconEntry.openEntryMenu(rowEntry = ${orgName});

		MenuItem.click(menuItem = ${editLocalized});

		AssertTextEquals.assertPartialText(
			key_countryName = ${countryName},
			locator1 = "UsersAndOrganizations#ORGANIZATION_COUNTRY_NAME",
			value1 = ${countryName});
	}

	@summary = "Default summary"
	macro viewParentOrganizationCP(orgName = null, subOrgName = null, parentOrgName = null) {
		Organization.gotoEdit(orgName = ${orgName});

		var key_orgName = ${subOrgName};

		Click(locator1 = "UsersAndOrganizationsEditOrganization#PARENT_ORGANIZATION_SELECT_BUTTON");

		SelectFrame(
			key_title = "Select Parent Organization",
			locator1 = "IFrame#MODAL_ANY");

		AssertTextEquals(
			key_columnName = "name",
			key_rowEntry = ${subOrgName},
			locator1 = "ContentRow#ENTRY_CONTENT_ROW_NUMBER_TD",
			value1 = ${subOrgName});

		AssertTextEquals(
			key_columnName = "path",
			key_rowEntry = ${subOrgName},
			locator1 = "ContentRow#ENTRY_CONTENT_ROW_NUMBER_TD",
			value1 = ${parentOrgName});

		IFrame.closeFrame();
	}

	@summary = "Default summary"
	macro viewSuborganizationCP(orgType = null, orgName = null, suborgType = null, suborgName = null) {
		LexiconEntry.openEntryMenu(rowEntry = ${suborgName});

		MenuItem.click(menuItem = "Edit");

		var key_orgType = ${orgType};

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Edit ${suborgName}");

		AssertTextEquals(
			locator1 = "TextInput#NAME",
			value1 = ${suborgName});

		AssertTextEquals.assertValue(
			locator1 = "UsersAndOrganizationsEditOrganization#DETAILS_TYPE",
			value1 = ${suborgType});

		AssertTextEquals(
			locator1 = "UsersAndOrganizationsEditOrganization#PARENT_ORGANIZATION_NAME",
			value1 = ${orgName});
	}

	@summary = "Default summary"
	macro viewUser(userName = null) {
		LexiconEntry.viewEntryName(rowEntry = ${userName});
	}

}