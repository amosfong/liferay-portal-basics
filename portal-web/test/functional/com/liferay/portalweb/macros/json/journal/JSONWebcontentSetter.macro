definition {

	@summary = "Default summary"
	macro setArticleId(groupName = null, site = null, title = null) {
		Variables.assertDefined(parameterList = "${groupName},${title}");

		var groupId = JSONWebcontentSetter.setGroupId(
			groupName = ${groupName},
			site = ${site});

		if (isSet(urlTitle)) {
			var urlTitle = ${urlTitle};
		}
		else {
			var urlTitle = StringUtil.replace(${title}, " ", "-");

			var urlTitle = StringUtil.lowerCase(${urlTitle});
		}

		var articleId = JSONWebcontentAPI._getArticleIdByURLTitle(
			groupName = ${groupName},
			site = ${site},
			urlTitle = ${urlTitle});

		echo("## * Article Id: ${articleId}");

		return ${articleId};
	}

	@summary = "Default summary"
	macro setContentXML(contentMap = null, source = null, content = null) {
		JSONUtil2._exclusiveOrCheck(
			a = ${content},
			b = ${contentMap},
			failMessage = "Please only set 'content' or only 'contentMap' but not both or none.");

		if (isSet(contentMap)) {
			var contentXml = JSONWebcontentUtil._localizedConvertToXML(contentMap = ${contentMap});
		}
		else {
			var contentXml = JSONWebcontentUtil._convertToXML(
				content = ${content},
				source = ${source});
		}

		return ${contentXml};
	}

	@summary = "Default summary"
	macro setDescriptionMap(description = null) {
		if (isSet(description) && isSet(descriptionMap)) {
			fail("Please only set 'description' or 'descriptionMap' but not both.");
		}

		if (isSet(descriptionMap)) {
			var descriptionMap = JSONUtil2._convertLocalizedListToMap(listMap = ${descriptionMap});
		}
		else if (isSet(description)) {
			var descriptionMap = '''{"en_US":"%3Cp%3E${description}%3C%2Fp%3E"}''';
		}
		else {
			var descriptionMap = "{}";
		}

		return ${descriptionMap};
	}

	@summary = "Default summary"
	macro setFolderId(groupId = null, folderName = null) {
		if (isSet(folderName)) {
			Variables.assertDefined(parameterList = ${groupId});

			var folderId = JSONWebcontentAPI._getFolderIdByFolderName(
				folderName = ${folderName},
				groupId = ${groupId});
		}
		else {
			var folderId = 0;
		}

		return ${folderId};
	}

	@summary = "Default summary"
	macro setGroupId(groupName = null) {
		Variables.assertDefined(parameterList = ${groupName});

		if (!(isSet(site))) {
			var site = "true";
		}

		var groupId = JSONGroupAPI._getGroupIdByName(
			groupName = ${groupName},
			site = ${site});

		return ${groupId};
	}

	@summary = "Default summary"
	macro setServiceContext(assetCategoryIds = null, assetTagNames = null, workflowAction = null) {
		if (!(isSet(serviceContext))) {
			var serviceContext = JSONServiceContextUtil.setServiceContext(
				addGroupPermissions = "true",
				addGuestPermissions = "true",
				assetCategoryIds = ${assetCategoryIds},
				assetTagNames = ${assetTagNames},
				workflowAction = ${workflowAction});
		}

		return ${serviceContext};
	}

	@summary = "Default summary"
	macro setStatus(statusKey = null) {
		Variables.assertDefined(parameterList = ${statusKey});

		if (${statusKey} == "APPROVED") {
			var status = 0;
		}

		if (${statusKey} == "DRAFT") {
			var status = 2;
		}

		if (${statusKey} == "EXPIRE") {
			var status = 3;
		}

		return ${status};
	}

	@summary = "Default summary"
	macro setTitleMap(title = null) {
		JSONUtil2._exclusiveOrCheck(
			a = ${title},
			b = ${titleMap},
			failMessage = "Please only set 'title' or only 'titleMap' but not both or none.");

		if (isSet(titleMap)) {
			var titleMap = JSONUtil2._convertLocalizedListToMap(listMap = ${titleMap});
		}
		else {
			var titleMap = '''{"en_US":"${title}"}''';
		}

		return ${titleMap};
	}

}