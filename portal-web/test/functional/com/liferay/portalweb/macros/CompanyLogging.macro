definition {

	@summary = "Assert ${logContent} for company ${portalInstanceName} contains the unique group ID logging"
	macro assertCompanyLogContent(logContent = null, portalInstanceName = null) {
		var groupId = JSONCompany.getCompanyGroupIdByGroupKey(
			groupKey = "Guest",
			portalInstanceName = ${portalInstanceName});

		if (!(contains(${logContent}, "for group ${groupId}"))) {
			fail("The ${portalInstanceName} company log file content does not contain the group ID.");
		}

		AssertTextPresent(
			locator1 = "CompanyLogging#COMPANY_LOG_CONTENT",
			value1 = "for group ${groupId}");
	}

	@summary = "Assert the company log is isolated for a given company by asserting unique text contained in other company logs is not present"
	macro assertCompanyLogIsolated(portalInstanceName = null) {
		var logContent = CompanyLogging.downloadCompanyLog(portalInstanceName = ${portalInstanceName});

		CompanyLogging.assertCompanyLogContent(
			logContent = ${logContent},
			portalInstanceName = ${portalInstanceName});

		var portalInstanceNames = JSONCompany.getAllCompanyNames();

		var portalInstanceNames = ListUtil.newListFromString(${portalInstanceNames});

		ListUtil.remove(${portalInstanceNames}, ${portalInstanceName});

		for (var additionalPortalInstanceName : list ${portalInstanceNames}) {
			if (contains(${logContent}, "Begin initializing company with web ID ${additionalPortalInstanceName}")) {
				fail("The ${portalInstanceName} company log file contains logs from the ${additionalPortalInstanceName} company");
			}
		}
	}

	@summary = "Assert company log ${logName} was not downloaded"
	macro assertNoLogDownloaded(logName = null) {
		DownloadTempFile();

		DMDocument.assertNoFileNameFromTempFolder(fileName = ${logName});
	}

	@summary = "Download the company log ${logName}"
	macro downloadCompanyLog(portalInstanceName = null) {
		var logName = CompanyLogging.getCompanyLogName(portalInstanceName = ${portalInstanceName});

		Click(
			key_logName = ${logName},
			locator1 = "CompanyLogging#COMPANY_LOG_NAME");

		Click(
			ariaLabel = "Download",
			locator1 = "Button#ANY_WITH_ARIA_LABEL");

		DownloadTempFile();

		DMDocument.assertFileNameFromTempFolder(fileName = ${logName});

		var logContent = TestCase.getTempFileContent(fileName = ${logName});

		return ${logContent};
	}

	@summary = "Get the company log path for company ${portalInstanceName}"
	macro getCompanyLogDirPath(portalInstanceName = null) {
		var companyId = JSONCompany.getCompanyId(portalInstanceName = ${portalInstanceName});
		var homeDir = PropsUtil.get("liferay.home.dir.name");

		var logsDir = "${homeDir}/logs/companies/${companyId}";
		var pattern = StringUtil.split("**/*.log");

		var filePath = FileUtil.getIncludedResourceURLs(${pattern}, ${logsDir});

		var filePath = StringUtil.valueOf(${filePath});

		var filePath = RegexUtil.replace(${filePath}, "\[(.*)\]", 1);

		var filePath = StringUtil.replace(${filePath}, "file:/", "");

		return ${filePath};
	}

	@summary = "Get the company log name for company ${portalInstanceName}"
	macro getCompanyLogName(portalInstanceName = null) {
		var companyLogDirPath = CompanyLogging.getCompanyLogDirPath(portalInstanceName = ${portalInstanceName});

		var logName = FileUtil.getFileName(${companyLogDirPath});

		return ${logName};
	}

	@summary = "Open the server log admin via URL"
	macro openServerLogsAdmin(baseURL = null) {
		Navigator.openWithAppendToBaseURL(
			baseURL = ${baseURL},
			urlAppend = "group/guest/~/control_panel/manage?p_p_id=com_liferay_portal_company_log_web_internal_portlet_PortalCompanyLogPortlet");
	}

}