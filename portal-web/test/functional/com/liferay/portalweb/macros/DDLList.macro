definition {

	var ddlDataDefinitionDescription = "Data Definition Description";
	var ddlDataDefinitionName = "Data Definition Name";
	var ddlListName = "List Name";

	@summary = "Default summary"
	macro addCP(ddlListName = null, workflowDefinition = null, ddlListDescription = null) {
		LexiconEntry.gotoAdd();

		Type.clickAtType(
			locator1 = "DDL#NAME_INPUT",
			value1 = ${ddlListName});

		if (isSet(ddlListDescription)) {
			Type.clickAtType(
				locator1 = "TextArea#DESCRIPTION",
				value1 = ${ddlListDescription});
		}

		if (isSet(workflowDefinition)) {
			Select(
				locator1 = "DDLEditList#WORKFLOW_SELECT",
				value1 = ${workflowDefinition});
		}

		AssertClick(
			locator1 = "DDLEditList#DATA_DEFINITION_SELECT",
			value1 = "Select");
	}

	@summary = "Default summary"
	macro addViaDDLDisplayPG(ddlListName = null, workflowDefinition = null, ddlListDescription = null) {
		Click(
			locator1 = "DDL#PORTLET_ACTIONS_ADD_LIST",
			value1 = "Add List");

		AssertTextEquals(
			locator1 = "Portlet#MODAL_TITLE",
			value1 = "Add List");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		PortletEntry.inputName(name = ${ddlListName});

		if (isSet(ddlListDescription)) {
			PortletEntry.inputDescription(description = ${ddlListDescription});
		}

		if (isSet(workflowDefinition)) {
			Select(
				locator1 = "DDLEditList#WORKFLOW_SELECT",
				value1 = ${workflowDefinition});
		}

		AssertClick(
			locator1 = "DDLEditList#DATA_DEFINITION_SELECT",
			value1 = "Select");

		SelectFrameTop();
	}

	@summary = "Default summary"
	macro assertCannotDeleteRecordViaDDLDisplayPG() {
		Click(locator1 = "DDL#RECORD_TABLE_ELLIPSIS_1");

		MenuItem.viewNotPresent(menuItem = "Delete");

		MenuItem.viewPresent(menuItem = "View");
	}

	@summary = "Default summary"
	macro deleteCP(ddlListName = null) {
		LexiconEntry.viewEntryName(rowEntry = ${ddlListName});

		ClickNoError(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS");

		MenuItem.clickNoError(menuItem = "Delete");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();

		LexiconEntry.viewNoEntry(rowEntry = ${ddlListName});
	}

	@summary = "Default summary"
	macro editCP(ddlListName = null, ddlListDescriptionEdit = null, ddlListNameEdit = null) {
		LexiconEntry.viewEntryName(rowEntry = ${ddlListName});

		LexiconEntry.gotoEllipsisMenuItem(menuItem = "Edit");

		Type.clickAtType(
			locator1 = "TextInput#NAME",
			value1 = ${ddlListNameEdit});

		if (isSet(ddlListDescriptionEdit)) {
			Type.clickAtType(
				locator1 = "TextArea#DESCRIPTION",
				value1 = ${ddlListDescriptionEdit});
		}

		Click(locator1 = "Button#SAVE");

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro editPermissions(ddlListName = null) {
		Search.searchCP(searchTerm = ${ddlListName});

		LexiconEntry.gotoEllipsisMenuItem(menuItem = "Permissions");
	}

	@summary = "Default summary"
	macro editUserViewPermissionCP(ddlListName = null, addOrRemoveViewPermission = null, roleList = null) {
		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Dynamic Data Lists");

		DDLList.editPermissions(ddlListName = ${ddlListName});

		if (IsElementPresent.isElementPresentAfterWait(locator1 = "IFrame#MODAL_BODY")) {
			SelectFrame(locator1 = "IFrame#MODAL_BODY");
		}

		PermissionsInline.addOrRemoveViewPermissionFromAllRoles(
			addOrRemoveViewPermission = ${addOrRemoveViewPermission},
			roleList = ${roleList});
	}

	@summary = "Default summary"
	macro editViaDDLDisplayPG(ddlListDescriptionEdit = null, ddlListNameEdit = null) {
		Click(
			locator1 = "DDL#PORTLET_ACTIONS_EDIT_LIST",
			value1 = "Edit List");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		PortletEntry.inputName(name = ${ddlListNameEdit});

		if (isSet(ddlListDescription)) {
			PortletEntry.inputDescription(description = ${ddlListDescriptionEdit});
		}

		AssertClick(
			locator1 = "Button#SAVE",
			value1 = "Save");
	}

	@summary = "Default summary"
	macro expandCollapsedIcon(classNameList = null) {
		if (isSet(classNameList)) {
			for (var className : list ${classNameList}) {
				var key_sectionName = ${className};

				if (IsElementPresent(key_tab = ${className}, locator1 = "StyleBookEditor#STYLE_BOOK_EDITOR_SIDEBAR_EXPANDED")) {
					AssertElementPresent(locator1 = "StyleBookEditor#STYLE_BOOK_EDITOR_SIDEBAR_EXPANDED");
				}
				else {
					Click(locator1 = "StyleBookEditor#STYLE_BOOK_EDITOR_SIDEBAR_COLLAPSED");
				}
			}
		}
		else {
			var key_sectionName = ${className};

			if (IsElementPresent(key_tab = ${className}, locator1 = "StyleBookEditor#STYLE_BOOK_EDITOR_SIDEBAR_EXPANDED")) {
				AssertElementPresent(locator1 = "StyleBookEditor#STYLE_BOOK_EDITOR_SIDEBAR_EXPANDED");
			}
			else {
				Click(locator1 = "StyleBookEditor#STYLE_BOOK_EDITOR_SIDEBAR_COLLAPSED");
			}
		}
	}

	@summary = "Default summary"
	macro openDynamicDataListsAdmin(siteURLKey = null) {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_dynamic_data_lists_web_portlet_DDLPortlet");
	}

	@summary = "Default summary"
	macro saveViaDDLDisplayPG() {
		DDLDataDefinition.save();
	}

	@summary = "Default summary"
	macro selectViaDDLDisplayPG(ddlListName = null) {
		Click(
			locator1 = "DDL#PORTLET_ACTIONS_SELECT_LIST",
			value1 = "Select List");

		SelectFrame(locator1 = "DDLConfiguration#CONFIGURATION_IFRAME");

		var key_ddlListName = ${ddlListName};

		AssertClick(
			locator1 = "DDLConfiguration#LIST_TABLE_NAME",
			value1 = ${ddlListName});

		AssertTextEquals(
			locator1 = "Message#INFO",
			value1 = "Displaying List: ${ddlListName} (Modified)");

		AssertClick(
			locator1 = "Button#SAVE",
			value1 = "Save");

		Alert.viewSuccessMessage();

		SelectFrameTop();
	}

	@summary = "Default summary"
	macro tearDownCP() {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Dynamic Data Lists");

		while (IsElementPresent(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS")) {
			LexiconEntry.gotoEllipsisMenuItemNoError(menuItem = "Delete");

			AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

			Alert.viewSuccessMessage();
		}
	}

	@summary = "Default summary"
	macro viewCP(ddlListName = null, ddlListDescription = null) {
		LexiconEntry.viewEntryName(rowEntry = ${ddlListName});

		if (isSet(ddlListDescription)) {
			var key_ddlListDescription = ${ddlListDescription};

			AssertTextEquals(
				locator1 = "DDL#LIST_TABLE_DESCRIPTION",
				value1 = ${ddlListDescription});
		}

		LexiconEntry.gotoEntry(rowEntry = ${ddlListName});

		SiteAdmin.viewPortletTitle(portletTitle = ${ddlListName});
	}

	@summary = "Default summary"
	macro viewDeleteRecordOptionViaDDLDisplayPG() {
		Click(locator1 = "DDL#RECORD_TABLE_ELLIPSIS_1");

		MenuItem.viewPresent(menuItem = "Delete");
	}

	@summary = "Default summary"
	macro viewViaDDLDisplayPG(ddlListName = null) {
		AssertTextEquals(
			locator1 = "Portlet#H2_HEADER",
			value1 = ${ddlListName});
	}

}