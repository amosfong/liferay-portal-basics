definition {

	@summary = "Default summary"
	macro addStyleBooks(styleBookName = null) {
		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = ${styleBookName});

		Button.clickSave();
	}

	@summary = "Default summary"
	macro deleteStyleBook(styleBookName = null) {
		StyleBooksAdmin.gotoStyleBookEllipsisItem(
			item = "Delete",
			styleBookName = ${styleBookName});

		AssertTextEquals(
			locator1 = "Modal#BODY",
			value1 = "Deleting a style book is an action impossible to revert. All Style Book tokens and values will be removed and it will not be possible to recover it. Watch out for critical impact on the site's look and feel.");

		Click(
			key_text = "Delete",
			locator1 = "Modal#MODAL_FOOTER_BUTTON");
	}

	@summary = "Default summary"
	macro gotoStyleBook(styleBookName = null) {
		LexiconCard.clickCardTitle(card = ${styleBookName});
	}

	@summary = "Default summary"
	macro gotoStyleBookEllipsisItem(item = null, styleBookName = null) {
		LexiconCard.clickEllipsisItem(
			card = ${styleBookName},
			item = ${item});
	}

	@summary = "Default summary"
	macro importStyleBookFromTempFolder() {
		Click.waitForMenuToggleJSClick(locator1 = "Icon#HEADER_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "Import");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		var stylebookZipFileName = TestCase.getDownloadedTempFileName(fileNamePattern = "style-book-*.zip");

		UploadTempFile(
			locator1 = "TextInput#FILE",
			value1 = ${stylebookZipFileName});

		Button.clickImport();

		Alert.viewSuccessMessage();

		IFrame.closeFrame();
	}

	@summary = "Default summary"
	macro markStyleBookAsDefault(confirmMessage = null, styleBookName = null) {
		StyleBooksAdmin.gotoStyleBookEllipsisItem(
			item = "Mark as Default",
			styleBookName = ${styleBookName});

		AssertConfirm(value1 = ${confirmMessage});

		LexiconCard.viewCardSticker(
			card = ${styleBookName},
			icon = "check-circle");
	}

	@summary = "Default summary"
	macro openStyleBooksAdmin(siteURLKey = null) {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_style_book_web_internal_portlet_StyleBookPortlet");
	}

	@summary = "Default summary"
	macro viewStyleBooksPresent(styleBookName = null) {
		LexiconCard.viewCardPresent(card = ${styleBookName});
	}

}