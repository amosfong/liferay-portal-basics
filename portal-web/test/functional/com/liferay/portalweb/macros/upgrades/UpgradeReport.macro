definition {

	@summary = "Retrieves and returns the content of 'upgrade_report.txt' when using auto.upgrade"
	macro getAutoUpgradeReportFileContent(directory = null) {
		var liferayHome = PropsUtil.get("liferay.home.dir.name");

		if (isSet(directory)) {
			var fileContentReport = FileUtil.read("${directory}/upgrade_report.txt");
		}
		else {
			var fileContentReport = FileUtil.read("${liferayHome}/reports/upgrade_report.txt");
		}

		return ${fileContentReport};
	}

	@summary = "Retrieves and returns the total Upgrade execution time available in the 'upgrade_report.txt' file"
	macro getUpgradeExecutionTime(reportFileContent = null) {
		var upgradeRunTimeReport = RegexUtil.replace(${reportFileContent}, "Execution time: (.*[0-9])", 1);

		return ${upgradeRunTimeReport};
	}

	@summary = "Retrieves and returns the content of 'upgrade_report.txt' when using DB Upgrade Tool"
	macro getUpgradeReportFileContent(directory = null) {
		var liferayHome = PropsUtil.get("liferay.home.dir.name");

		if (isSet(directory)) {
			var fileContentReport = FileUtil.read("${directory}/upgrade_report.txt");
		}
		else {
			var fileContentReport = FileUtil.read("${liferayHome}/tools/portal-tools-db-upgrade-client/reports/upgrade_report.txt");
		}

		return ${fileContentReport};
	}

	@summary = "Prints the Upgrade execution time in the console"
	macro printUpgradeExecutionTime(upgradeType = null) {
		if (${upgradeType} == "auto") {
			var reportFileContent = UpgradeReport.getAutoUpgradeReportFileContent();
		}
		else if (${upgradeType} == "client") {
			var reportFileContent = UpgradeReport.getUpgradeReportFileContent();
		}

		var upgradeRunTimeReport = UpgradeReport.getUpgradeExecutionTime(reportFileContent = ${reportFileContent});

		echo("Upgrade execution time is ${upgradeRunTimeReport} seconds");
	}

}