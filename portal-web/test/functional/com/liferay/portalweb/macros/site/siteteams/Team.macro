definition {

	@summary = "Default summary"
	macro _editCP(teamName = null, teamDescription = null) {
		PortletEntry.inputName(name = ${teamName});

		PortletEntry.inputDescription(description = ${teamDescription});
	}

	@summary = "Default summary"
	macro addCP(teamName = null, teamDescription = null) {
		LexiconEntry.gotoAdd();

		Team._editCP(
			teamDescription = ${teamDescription},
			teamName = ${teamName});

		PortletEntry.save();
	}

	@summary = "Default summary"
	macro assignMembersCP(teamName = null, userScreenName = null, userNameIsNotFound = null) {
		var key_teamName = ${teamName};
		var key_userScreenName = ${userScreenName};

		Search.searchCP(searchTerm = ${teamName});

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		AssertTextEquals(
			locator1 = "SiteTeams#TEAM_TABLE_NAME",
			value1 = ${teamName});

		AssertClick(
			locator1 = "SiteTeams#TEAM_TABLE_NAME",
			value1 = ${teamName});

		Navigator.gotoNavItem(navItem = "Users");

		LexiconEntry.gotoAdd();

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		WaitForPageLoad();

		Search.searchCP(searchTerm = ${userScreenName});

		if (${userNameIsNotFound} == "true") {
			AssertTextEquals.assertPartialText(
				locator1 = "Message#EMPTY_INFO",
				value1 = "No users were found.");
		}
		else {
			if (IsElementNotPresent(key_card = ${userScreenName}, locator1 = "Card#CARD")) {
				LexiconEntry.changeDisplayStyle(displayStyle = "cards");
			}

			LexiconCard.checkCardCheckbox(card = ${userScreenName});

			SelectFrameTop();

			WaitForElementPresent(
				key_text = "Add",
				locator1 = "Button#ANY_ENABLED");

			Button.clickAdd();

			Alert.viewSuccessMessage();

			LexiconCard.viewCardPresent(card = ${userScreenName});
		}

		SelectFrameTop();
	}

	@summary = "Default summary"
	macro assignUserGroups(teamName = null, userGroupsNameIsNotAvailable = null, userGroupName = null) {
		var key_teamName = ${teamName};

		Search.searchCP(searchTerm = ${teamName});

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		AssertTextEquals(
			locator1 = "SiteTeams#TEAM_TABLE_NAME",
			value1 = ${teamName});

		AssertClick(
			locator1 = "SiteTeams#TEAM_TABLE_NAME",
			value1 = ${teamName});

		Navigator.gotoNavItem(navItem = "User Groups");

		LexiconEntry.gotoAdd();

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		var key_cardText = ${userGroupName};

		Search.searchCP(searchTerm = ${userGroupName});

		if (${userGroupsNameIsNotAvailable} == "true") {
			AssertTextNotPresent(
				locator1 = "SiteTeamsAssignMembers#USER_GROUPS_TABLE_NAME",
				value1 = ${userGroupName});
		}
		else {
			LexiconCard.checkCardCheckbox(card = ${userGroupName});

			SelectFrameTop();

			WaitForElementPresent(
				key_text = "Add",
				locator1 = "Button#ANY_ENABLED");

			Button.clickAdd();

			Alert.viewSuccessMessage();
		}

		SelectFrameTop();
	}

	@summary = "Default summary"
	macro definePermissionPG(teamName = null, permissionDefinitionKey = null) {
		Check.checkNotVisible(locator1 = "//tr[contains(.,'${teamName}')]/td//input[contains(@id,'${permissionDefinitionKey}')]");

		PortletEntry.save();

		AssertChecked.assertCheckedNotVisible(locator1 = "//tr[contains(.,'${teamName}')]/td//input[contains(@id,'${permissionDefinitionKey}')]");
	}

	@summary = "Default summary"
	macro deleteCP(teamName = null) {
		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		LexiconEntry.viewEntryName(rowEntry = ${teamName});

		LexiconEntry.openEntryMenu(rowEntry = ${teamName});

		MenuItem.clickNoError(menuItem = "Delete");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro editCP(teamName = null, teamDescriptionEdit = null, teamNameEdit = null) {
		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = ${teamName});

		Team._editCP(
			teamDescription = ${teamDescriptionEdit},
			teamName = ${teamNameEdit});

		PortletEntry.save();
	}

	@summary = "Default summary"
	macro removeMemberCP(teamName = null, membershipType = null, userLastName = null, userGroupName = null, userFirstName = null) {
		AssertClick(
			key_rowEntry = ${teamName},
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME_LINK",
			value1 = ${teamName});

		Navigator.gotoNavItem(navItem = "${membershipType}s");

		if (${membershipType} == "User") {
			LexiconCard.checkCardCheckbox(card = "${userFirstName} ${userLastName}");
		}
		else if (${membershipType} == "User Group") {
			Check(
				key_rowEntry = ${userGroupName},
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_CHECKBOX");
		}

		ClickNoError(
			key_text = "times-circle",
			locator1 = "ManagementBar#ANY_ICON");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro removePermissionPG(teamName = null, permissionDefinitionKey = null) {
		Uncheck.uncheckNotVisible(locator1 = "//tr[contains(.,'${teamName}')]/td//input[contains(@id,'${permissionDefinitionKey}')]");

		PortletEntry.save();

		AssertNotChecked.assertNotCheckedNotVisible(locator1 = "//tr[contains(.,'${teamName}')]/td//input[contains(@id,'${permissionDefinitionKey}')]");
	}

	@summary = "Default summary"
	macro searchCP(membershipType = null, searchTerm = null, isEmpty = null) {
		Navigator.gotoNavItem(navItem = "${membershipType}s");

		Search.searchCP(searchTerm = ${searchTerm});

		if (${isEmpty} == "true") {
			AssertElementPresent(locator1 = "Message#EMPTY_INFO");
		}
		else {
			var name = StringUtil.unquote(${searchTerm});

			AssertTextEquals.assertPartialText(
				locator1 = "ContentRow#ENTRY_CONTENT_ROW_1",
				value1 = ${name});
		}
	}

	@summary = "Default summary"
	macro tearDownCP() {
		var siteName = TestCase.getSiteName(siteName = ${siteName});

		Navigator.openSiteURL(siteName = ${siteName});

		ProductMenu.gotoPortlet(
			category = "People",
			portlet = "Teams");

		if (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			PortletEntry.selectAll();

			ClickNoError(
				key_text = "trash",
				locator1 = "ManagementBar#ANY_ICON");

			AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

			Alert.viewSuccessMessage();
		}
	}

	@summary = "Default summary"
	macro viewCP(teamName = null, teamDescription = null) {
		var key_teamName = ${teamName};

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		AssertTextEquals(
			locator1 = "SiteTeams#TEAM_TABLE_NAME",
			value1 = ${teamName});

		AssertTextEquals(
			locator1 = "SiteTeams#TEAM_TABLE_DESCRIPTION",
			value1 = ${teamDescription});

		AssertElementPresent(locator1 = "SiteTeams#TEAM_TABLE_ACTIONS");
	}

	@summary = "Default summary"
	macro viewDefaultCP(teamName = null) {
		var key_teamName = ${teamName};

		PortletEntry.viewEmptyMessage(entity = "teams");

		AssertTextNotPresent(
			locator1 = "SiteTeams#TEAM_TABLE_NAME",
			value1 = ${teamName});
	}

	@summary = "Default summary"
	macro viewMemberCP(teamName = null, membershipType = null, userLastName = null, userGroupName = null, userFirstName = null) {
		AssertClick(
			key_rowEntry = ${teamName},
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME_LINK",
			value1 = ${teamName});

		Navigator.gotoNavItem(navItem = "${membershipType}s");

		if (${membershipType} == "User") {
			AssertTextEquals.assertPartialText(
				key_rowEntry = "${userFirstName} ${userLastName}",
				locator1 = "ContentRow#ENTRY_CONTENT_ROW_1",
				value1 = "${userFirstName} ${userLastName}");
		}
		else if (${membershipType} == "User Group") {
			AssertTextEquals.assertPartialText(
				key_rowEntry = ${userGroupName},
				locator1 = "ContentRow#ENTRY_CONTENT_ROW_1",
				value1 = ${userGroupName});
		}
	}

	@summary = "Default summary"
	macro viewNoMembersCP(teamName = null) {
		AssertClick(
			key_rowEntry = ${teamName},
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME_LINK",
			value1 = ${teamName});

		for (var navItem : list "Users,User Groups") {
			Navigator.gotoNavItem(navItem = ${navItem});

			var lowercaseNavItem = StringUtil.lowerCase(${navItem});

			AssertTextEquals.assertPartialText(
				locator1 = "Message#EMPTY_INFO",
				value1 = "No ${lowercaseNavItem} were found.");
		}
	}

}