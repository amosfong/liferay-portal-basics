definition {

	@summary = "Default summary"
	macro _addCP(subcategoryName = null, grandSubcategoryName = null, vocabularyName = null, categoryName = null, subcategoryDescription = null, grandSubcategoryDescription = null) {
		Navigator.gotoNavNested(navNested = ${vocabularyName});

		if (isSet(grandSubcategoryName)) {
			Click(
				key_categoryName = ${categoryName},
				locator1 = "Categories#CATEGORY_ENTRY_LINK");

			LexiconEntry.gotoEntryMenuItem(
				menuItem = "Add Subcategory",
				rowEntry = ${subcategoryName});

			PortletEntry.inputTitle(title = ${grandSubcategoryName});

			if (isSet(grandSubcategoryDescription)) {
				CKEditor.addContentInWebContent(
					content = ${grandSubcategoryDescription},
					fieldLabel = "Description");
			}
		}
		else {
			LexiconEntry.gotoEntryMenuItem(
				menuItem = "Add Subcategory",
				rowEntry = ${categoryName});

			PortletEntry.inputTitle(title = ${subcategoryName});

			if (isSet(subcategoryDescription)) {
				CKEditor.addContentInWebContent(
					content = ${subcategoryDescription},
					fieldLabel = "Description");
			}
		}
	}

	@summary = "Default summary"
	macro addCP(displayPageTemplateType = null, subcategoryName = null, grandSubcategoryName = null, displayPageName = null, vocabularyName = null, categoryName = null, subcategoryDescription = null, grandSubcategoryDescription = null) {
		Subcategory._addCP(
			categoryName = ${categoryName},
			grandSubcategoryDescription = ${grandSubcategoryDescription},
			grandSubcategoryName = ${grandSubcategoryName},
			subcategoryDescription = ${subcategoryDescription},
			subcategoryName = ${subcategoryName},
			vocabularyName = ${vocabularyName});

		if (isSet(displayPageTemplateType)) {
			Panel.expandPanel(panel = "Display Page");

			if (${displayPageTemplateType} == "Inherited") {
				AssertSelectedLabel(
					locator1 = "Select#SELECT_ID",
					selectFieldId = "displayPageType",
					value1 = "Inherited");
			}
			else {
				Select(
					locator1 = "Select#SELECT_ID",
					selectFieldId = "displayPageType",
					value1 = ${displayPageTemplateType});
			}

			AssertSelectedLabel(
				locator1 = "Select#SELECT_ID",
				selectFieldId = "displayPageType",
				value1 = ${displayPageTemplateType});

			if (${displayPageTemplateType} != "None") {
				if (${displayPageTemplateType} == "Default") {
					AssertTextEquals(
						inputFieldId = "defaultDisplayPageNameInput",
						locator1 = "TextInput#INPUT_ID",
						value1 = ${displayPageName});
				}

				if (${displayPageTemplateType} == "Specific") {
					AssertTextEquals(
						inputFieldId = "specificDisplayPageNameInput",
						locator1 = "TextInput#INPUT_ID",
						value1 = "No Display Page Template Selected");

					WaitForLiferayEvent.initializeLiferayEventLog();

					Click(
						buttonId = "chooseSpecificDisplayPage",
						locator1 = "Button#BUTTON_ID");

					SelectFrame(locator1 = "IFrame#MODAL_BODY");

					LexiconCard.clickCard(card = ${displayPageName});

					SelectFrameTop();

					AssertTextEquals(
						inputFieldId = "specificDisplayPageNameInput",
						locator1 = "TextInput#INPUT_ID",
						value1 = ${displayPageName});
				}
			}
		}

		PortletEntry.save();
	}

	@summary = "Default summary"
	macro addDuplicateCP(subcategoryName = null, vocabularyName = null, categoryName = null) {
		Navigator.gotoNavNested(navNested = ${vocabularyName});

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Add Subcategory",
			rowEntry = ${categoryName});

		PortletEntry.inputTitle(title = ${subcategoryName});

		Button.clickSave();

		Alert.viewRequestFailedToComplete();

		AssertTextEquals.assertPartialText(
			locator1 = "Message#ERROR_1",
			value1 = "Please enter a unique name.");
	}

	@summary = "Default summary"
	macro addPropertyCP(subcategoryName = null, propertiesValueField = null, propertiesKeyField = null) {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = ${subcategoryName});

		Navigator.gotoNavUnderline(navUnderline = "Properties");

		Type(
			locator1 = "CategoriesEditCategory#PROPERTIES_KEY_FIELD_1",
			value1 = ${propertiesKeyField});

		Type(
			locator1 = "CategoriesEditCategory#PROPERTIES_VALUE_FIELD_1",
			value1 = ${propertiesValueField});

		PortletEntry.save();
	}

	@summary = "Default summary"
	macro deleteViaIconCP(subcategoryName = null, vocabularyName = null, categoryName = null) {
		Navigator.gotoNavNested(navNested = ${vocabularyName});

		LexiconEntry.gotoEntry(rowEntry = ${categoryName});

		Check(
			key_listEntry = ${subcategoryName},
			locator1 = "LexiconList#LIST_ENTRY_CHECKBOX");

		Click(
			key_text = "trash",
			locator1 = "ManagementBar#ANY_ICON");

		AssertTextEquals(
			locator1 = "Modal#BODY",
			value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Click(
			key_text = "Delete",
			locator1 = "Modal#MODAL_FOOTER_BUTTON");

		Alert.viewSuccessMessage();

		AssertTextNotPresent(value1 = ${subcategoryName});
	}

	@summary = "Default summary"
	macro deleteViaMenuCP(subcategoryName = null, vocabularyName = null, categoryName = null) {
		Navigator.gotoNavNested(navNested = ${vocabularyName});

		LexiconEntry.gotoEntry(rowEntry = ${categoryName});

		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Delete",
			rowEntry = ${subcategoryName});

		AssertTextEquals(
			locator1 = "Modal#BODY",
			value1 = "This category might be being used in some contents. If you delete it, this category and its subcategories will also be deleted and removed from those contents. Friendly URLs may also be affected. Do you want to continue deleting this category?");

		Click(
			key_text = "Delete",
			locator1 = "Modal#MODAL_FOOTER_BUTTON");

		Alert.viewSuccessMessage();

		AssertTextNotPresent(value1 = ${subcategoryName});
	}

	@summary = "Default summary"
	macro editCP(subcategoryNameEdit = null, subcategoryName = null, vocabularyName = null, categoryName = null) {
		Navigator.gotoNavNested(navNested = ${vocabularyName});

		LexiconEntry.gotoEntry(rowEntry = ${categoryName});

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = ${subcategoryName});

		PortletEntry.inputTitle(title = ${subcategoryNameEdit});

		PortletEntry.save();
	}

	@summary = "Default summary"
	macro moveCP(subcategoryName = null, categoryNameMove = null, vocabularyName = null, categoryName = null) {
		Navigator.gotoNavNested(navNested = ${vocabularyName});

		AssertClick(
			key_rowEntry = ${categoryName},
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME_LINK",
			value1 = ${categoryName});

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Move",
			rowEntry = ${subcategoryName});

		AssetCategorization.selectCategory(categoryName = ${categoryNameMove});
	}

	@summary = "Default summary"
	macro searchCP(subcategorySearchEntry = null, vocabularyName = null, categoryName = null) {
		Navigator.gotoNavNested(navNested = ${vocabularyName});

		LexiconEntry.gotoEntry(rowEntry = ${categoryName});

		Search.searchCP(searchTerm = ${subcategorySearchEntry});
	}

	@summary = "Default summary"
	macro viewCategoryPropertyCP(subcategoryName = null, propertiesValueField = null, propertiesKeyField = null, vocabularyName = null, categoryName = null) {
		Navigator.gotoNavNested(navNested = ${vocabularyName});

		var key_categoryName = ${categoryName};

		Click(locator1 = "Categories#CATEGORY_ENTRY_LINK");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = ${subcategoryName});

		Navigator.gotoNavUnderline(navUnderline = "Properties");

		AssertTextEquals(
			locator1 = "CategoriesEditCategory#PROPERTIES_KEY_FIELD_1",
			value1 = ${propertiesKeyField});

		AssertTextEquals(
			locator1 = "CategoriesEditCategory#PROPERTIES_VALUE_FIELD_1",
			value1 = ${propertiesValueField});
	}

	@summary = "Default summary"
	macro viewCP(subcategoryName = null, vocabularyName = null, categoryName = null) {
		var key_categoryName = ${categoryName};

		Navigator.gotoNavNested(navNested = ${vocabularyName});

		AssertClick(
			locator1 = "Categories#CATEGORY_ENTRY_LINK",
			value1 = ${categoryName});

		var key_subcategoryName = ${subcategoryName};

		AssertTextEquals(
			locator1 = "Categories#SUBCATEGORY_ENTRY",
			value1 = ${subcategoryName});
	}

	@summary = "Default summary"
	macro viewSearchResultsCP(subcategoryName = null, nullResult = null, subcategoryDescriptionList = null, subCategoryNameList = null, subcategoryDescription = null) {
		if (isSet(subCategoryNameList)) {
			for (var subCategoryName : list ${subCategoryNameList}) {
				var key_subCategoryName = ${subCategoryName};

				AssertTextEquals(
					locator1 = "Categories#SUBCATEGORY_ENTRY",
					value1 = ${subcategoryName});
			}
		}

		if (isSet(subcategoryDescriptionList)) {
			for (var categoryDescription : list ${subcategoryDescriptionList}) {
				var key_subcategoryDescription = ${subcategoryDescription};

				AssertTextEquals(
					locator1 = "Categories#SUBCATEGORY_ENTRY_DESCRIPTION",
					value1 = ${subcategoryDescription});
			}
		}

		if (${nullResult} == "true") {
			PortletEntry.viewEmptyMessage(entity = "Categories");
		}
	}

}