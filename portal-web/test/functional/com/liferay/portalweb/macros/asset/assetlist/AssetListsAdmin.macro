definition {

	@summary = "Default summary"
	macro addCategoriesFilter(queryContains = null, queryAndOperator = null, categoryNameList = null) {
		Panel.expandPanel(panel = "Filter");

		if (isSet(queryAndOperator)) {
			Select(
				locator1 = "APConfiguration#FILTER_QUERY_AND_OPERATOR",
				value1 = ${queryAndOperator});
		}

		if (isSet(queryContains)) {
			Select(
				locator1 = "APConfiguration#FILTER_QUERY_CONTAINS",
				value1 = ${queryContains});
		}

		Select(
			locator1 = "APConfiguration#FILTER_CATEGORIZATION_SELECT",
			value1 = "Categories");

		Click(locator1 = "Button#SELECT_CATEGORIES");

		AssetCategorization.gotoSelectCategory();

		for (var categoryName : list ${categoryNameList}) {
			Check.checkNotVisible(
				key_nodeName = ${categoryName},
				locator1 = "Treeview#NODE_CHECKBOX");
		}

		SelectFrameTop();

		Click(locator1 = "Button#DONE");

		PortletEntry.save();
	}

	@summary = "Default summary"
	macro addDynamicSelection(assetListTitle = null) {
		if (isSet(depotName)) {
			LexiconEntry.gotoAddMenuItem(menuItem = "Dynamic Collection");
		}
		else if (IsVisible(locator1 = "Message#EMPTY_INFO_TAGLIB")) {
			Click.waitForMenuToggleJSClick(locator1 = "Dropdown#EMPTY_MESSAGE_DROPDOWN");

			Click(
				key_menuItem = "Dynamic Collection",
				locator1 = "MenuItem#DROPDOWN_MENU_ITEM");
		}
		else {
			LexiconEntry.gotoAddMenuItem(menuItem = "Dynamic Collection");
		}

		PortletEntry.inputText(
			fieldLabel = "Title",
			text = ${assetListTitle});

		PortletEntry.save();
	}

	@summary = "Default summary"
	macro addKeywordsFilter(keyWords = null, queryContains = null, queryAndOperator = null) {
		Panel.expandPanel(panel = "Source");

		if (!(IsSelectedLabel(locator1 = "APConfiguration#SOURCE_SCOPE_ASSET_TYPE_SELECT", value1 = "All Types"))) {
			Select(
				locator1 = "APConfiguration#SOURCE_SCOPE_ASSET_TYPE_SELECT",
				value1 = "All Types");
		}

		Panel.expandPanel(panel = "Filter");

		Select(
			locator1 = "APConfiguration#FILTER_CATEGORIZATION_SELECT",
			value1 = "Keywords");

		Select(
			locator1 = "APConfiguration#FILTER_QUERY_CONTAINS",
			value1 = ${queryContains});

		Select(
			locator1 = "APConfiguration#FILTER_QUERY_AND_OPERATOR",
			value1 = ${queryAndOperator});

		Type(
			key_text = "Keywords",
			locator1 = "TextInput#ANY",
			value1 = ${keyWords});

		PortletEntry.save();

		AssertTextEquals(
			key_text = "Keywords",
			locator1 = "TextInput#ANY",
			value1 = ${keyWords});
	}

	@summary = "Default summary"
	macro addManualSelection(assetListTitle = null) {
		if (isSet(depotName)) {
			LexiconEntry.gotoAddMenuItem(menuItem = "Manual Collection");
		}
		else if (IsVisible(locator1 = "Message#EMPTY_INFO_TAGLIB")) {
			Click.waitForMenuToggleJSClick(locator1 = "Dropdown#EMPTY_MESSAGE_DROPDOWN");

			Click(
				key_menuItem = "Manual Collection",
				locator1 = "MenuItem#DROPDOWN_MENU_ITEM");
		}
		else {
			LexiconEntry.gotoAddMenuItem(menuItem = "Manual Collection");
		}

		PortletEntry.inputText(
			fieldLabel = "Title",
			text = ${assetListTitle});

		PortletEntry.save();
	}

	@summary = "Default summary"
	macro addPersonalizedVariation(segmentName = null) {
		if (IsElementPresent(locator1 = "AssetLists#ADD_VARIATION_ICON")) {
			Click(locator1 = "AssetLists#ADD_VARIATION_ICON");
		}
		else if (IsElementPresent(key_text = "New Personalized Variation", locator1 = "Button#ANY")) {
			Button.click(button = "New Personalized Variation");
		}
		else {
			Button.click(button = "Add Personalized Variation");
		}

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		Click(
			key_tableEntry = ${segmentName},
			locator1 = "LexiconTable#TABLE_ENTRY_TITLE");

		SelectFrameTop();

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro addSelection(menuItem = null, collectionName = null, selectedAsset = null) {
		Click(locator1 = "Button#PLUS");

		MenuItem.click(menuItem = ${menuItem});

		PortletEntry.inputText(
			fieldLabel = "Title",
			text = ${collectionName});

		Button.clickSave();

		Select(
			locator1 = "APConfiguration#SOURCE_SCOPE_ASSET_TYPE_SELECT",
			value1 = ${selectedAsset});

		WaitForElementNotPresent(
			key_text = "Save",
			locator1 = "Button#ANY_DISABLED");

		PortletEntry.save();
	}

	@summary = "Default summary"
	macro addTagsFilter(queryContains = null, queryAndOperator = null, tagNameList = null) {
		Panel.expandPanel(panel = "Filter");

		if (isSet(queryAndOperator)) {
			Select(
				locator1 = "APConfiguration#FILTER_QUERY_AND_OPERATOR",
				value1 = ${queryAndOperator});
		}

		if (isSet(queryContains)) {
			Select(
				locator1 = "APConfiguration#FILTER_QUERY_CONTAINS",
				value1 = ${queryContains});
		}

		Select(
			locator1 = "APConfiguration#FILTER_CATEGORIZATION_SELECT",
			value1 = "Tags");

		Click(
			key_fieldLabel = "Tags",
			key_text = "Select",
			locator1 = "Button#ANY_WITH_LABEL");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		for (var tagName : list ${tagNameList}) {
			Check(
				key_rowEntry = ${tagName},
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_CHECKBOX");
		}

		SelectFrameTop();

		AssertClick(
			locator1 = "Button#DONE",
			value1 = "Done");

		PortletEntry.save();
	}

	@summary = "Default summary"
	macro clickDeleteDisabled(variationTitle = null) {
		Click(
			key_variation = ${variationTitle},
			locator1 = "AssetLists#VARIATION_ELLIPSIS");
	}

	@summary = "Default summary"
	macro clickDepriotirizeDisabled(variationTitle = null) {
		Click(
			key_variation = ${variationTitle},
			locator1 = "AssetLists#VARIATION_ELLIPSIS");
	}

	@summary = "Default summary"
	macro clickPriotirizeDisabled(variationTitle = null) {
		Click(
			key_variation = ${variationTitle},
			locator1 = "AssetLists#VARIATION_ELLIPSIS");
	}

	@summary = "Default summary"
	macro configureFilterByField(fieldName = null) {
		Check.checkToggleSwitch(locator1 = "APConfiguration#SOURCE_SCOPE_FILTER_BY_FIELD_CHECKBOX");

		WaitForElementPresent(locator1 = "APConfiguration#SOURCE_SCOPE_FILTER_BY_FIELD_SELECT_BUTTON_ENABLED");

		Click(locator1 = "APConfiguration#SOURCE_SCOPE_FILTER_BY_FIELD_SELECT_BUTTON");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		Select(
			key_fieldLabel = "Select",
			locator1 = "Select#GENERIC_SELECT_FIELD",
			value1 = ${fieldName});
	}

	@summary = "Default summary"
	macro configureItemTypeInDynamicCollection(itemType = null, itemSubtype = null) {
		WaitForPageLoad();

		Panel.expandPanel(panel = "Source");

		if (isSet(itemType)) {
			Select(
				locator1 = "APConfiguration#SOURCE_SCOPE_ASSET_TYPE_SELECT",
				value1 = ${itemType});

			if (isSet(moveToAvailableItemTypeList)) {
				for (var moveToAvailableItemType : list ${moveToAvailableItemTypeList}) {
					Click(
						key_contentName = ${moveToAvailableItemType},
						locator1 = "APConfiguration#SELECT_MORE_THAN_ONE_ASSET_TYPE_CONTENT");

					Click(locator1 = "APConfiguration#SELECT_MORE_THAN_ONE_ASSET_TYPE_MOVE_AVAILABLE");
				}
			}
		}

		if (isSet(itemSubtype)) {
			Select(
				locator1 = "APConfiguration#SOURCE_SCOPE_ASSET_SUBTYPE_SELECT",
				value1 = ${itemSubtype});
		}

		WaitForElementNotPresent(
			key_text = "Save",
			locator1 = "Button#ANY_DISABLED");

		PortletEntry.save();
	}

	@summary = "Default summary"
	macro configureItemTypeInManualCollection(itemType = null, variationName = null, itemSubtype = null) {
		WaitForPageLoad();

		if (isSet(variationName)) {
			Navigator.gotoNavTab(navTab = ${variationName});
		}

		if (isSet(itemType)) {
			Select(
				locator1 = "APConfiguration#SOURCE_SCOPE_ASSET_TYPE_SELECT",
				value1 = ${itemType});

			if (isSet(moveToAvailableItemTypeList)) {
				for (var moveToAvailableItemType : list ${moveToAvailableItemTypeList}) {
					Click(
						key_contentName = ${moveToAvailableItemType},
						locator1 = "APConfiguration#SELECT_MORE_THAN_ONE_ASSET_TYPE_CONTENT");

					Click(locator1 = "APConfiguration#SELECT_MORE_THAN_ONE_ASSET_TYPE_MOVE_AVAILABLE");
				}
			}
		}

		if (isSet(itemSubtype)) {
			Select(
				locator1 = "APConfiguration#SOURCE_SCOPE_ASSET_SUBTYPE_SELECT",
				value1 = ${itemSubtype});
		}

		WaitForElementNotPresent(
			key_text = "Save",
			locator1 = "Button#ANY_DISABLED");

		PortletEntry.save();
	}

	@summary = "Default summary"
	macro configureScope(otherSite = null, assetListTitle = null, defaultSite = null, selectedAsset = null) {
		Click(
			key_title = ${assetListTitle},
			locator1 = "AssetLists#ASSET_LIST_ENTRY_TITLE");

		if (isSet(selectedAsset)) {
			Panel.expandPanel(panel = "Source");

			Select(
				locator1 = "APConfiguration#SOURCE_SCOPE_ASSET_TYPE_SELECT",
				value1 = ${selectedAsset});
		}

		Panel.expandPanel(panel = "Scope");

		if (isSet(otherSite)) {
			AssertClick(
				locator1 = "Configuration#SCOPE_SELECT",
				value1 = "Select");

			MenuItem.click(menuItem = "Other Site or Asset Library...");

			SelectFrame(value1 = "relative=top");

			SelectFrame(locator1 = "IFrame#SELECT_SITES");

			Navigator.gotoNavTab(navTab = "My Sites");

			LexiconEntry.changeDisplayStyle(displayStyle = "table");

			LexiconEntry.gotoEntry(rowEntry = ${otherSite});
		}

		if (isSet(defaultSite)) {
			AssertClick(
				locator1 = "Configuration#SCOPE_SELECT",
				value1 = "Select");

			AssertClick(
				key_menuItem = ${defaultSite},
				locator1 = "AssetLists#ASSET_LIST_SCOPE_ITEM",
				value1 = ${defaultSite});
		}

		SelectFrameTop();

		PortletEntry.save();
	}

	@summary = "Default summary"
	macro deleteEntry(assetListTitle = null) {
		LexiconList.clickEllipsisItem(
			item = "Delete",
			listEntry = ${assetListTitle});

		AssertTextEquals(
			locator1 = "Modal#BODY",
			value1 = "Deleting a collection is an action impossible to revert.");

		Click(
			key_text = "Delete",
			locator1 = "Modal#MODAL_FOOTER_BUTTON");

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro deleteManualSelectionAssets(assetsList = null) {
		for (var assetName : list ${assetsList}) {
			Click(
				key_entryName = ${assetName},
				locator1 = "LexiconTable#TABLE_ENTRY_DELETE");
		}
	}

	@summary = "Default summary"
	macro deleteVariation(variationTitle = null) {
		Click(
			key_variation = ${variationTitle},
			locator1 = "AssetLists#VARIATION_ELLIPSIS");

		ClickNoError(
			key_menuItem = "Delete",
			locator1 = "MenuItem#ANY_MENU_ITEM");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro deprioritizeVariation(variationTitle = null) {
		if (IsElementNotPresent(key_menuItem = "Deprioritize", locator1 = "MenuItem#ANY_MENU_ITEM")) {
			Click(
				key_variation = ${variationTitle},
				locator1 = "AssetLists#VARIATION_ELLIPSIS");
		}

		Click(
			key_menuItem = "Deprioritize",
			locator1 = "MenuItem#ANY_MENU_ITEM");
	}

	@summary = "Default summary"
	macro editAssetList(assetListTitle = null) {
		AssetListsAdmin.gotoEntryMenuItem(
			assetListTitle = ${assetListTitle},
			item = "Edit");

		WaitForPageLoad();
	}

	@summary = "Default summary"
	macro enableCollectionsToCombineAssetsFromSegments(collectionType = null) {
		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "System Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "Assets",
			configurationName = "Collections",
			configurationScope = "System Scope");

		Click.javaScriptClick(
			checkboxName = ${collectionType},
			locator1 = "Checkbox#ANY_CHECKBOX");

		if (IsElementPresent(locator1 = "Button#SAVE")) {
			Click(locator1 = "Button#SAVE");
		}
		else if (IsElementPresent(locator1 = "Button#UPDATE")) {
			Click(locator1 = "Button#UPDATE");
		}

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro gotoEllipsisMenuItemInItemSelector() {
		Click(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "View Items");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");
	}

	@summary = "Default summary"
	macro gotoEntry(assetListName = null) {
		Click(
			key_title = ${assetListName},
			locator1 = "AssetLists#ASSET_LIST_ENTRY_TITLE");
	}

	@summary = "Default summary"
	macro gotoEntryMenuItem(item = null, assetListTitle = null) {
		LexiconList.clickEllipsisItem(
			item = ${item},
			listEntry = ${assetListTitle});
	}

	@summary = "Default summary"
	macro openAssetListsAdmin(siteURLKey = null) {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_asset_list_web_portlet_AssetListPortlet");
	}

	@summary = "Default summary"
	macro openPersonalizedVariation() {
		if (IsElementPresent(key_text = "New Personalized Variation", locator1 = "Button#ANY")) {
			Button.click(button = "New Personalized Variation");
		}
		else {
			Button.click(button = "Add Personalized Variation");
		}

		SelectFrame(locator1 = "IFrame#MODAL_BODY");
	}

	@summary = "Default summary"
	macro openViewItems(personalizedVariation = null, assetListTitle = null) {
		if (isSet(assetListTitle)) {
			LexiconEntry.gotoEntry(rowEntry = ${assetListTitle});
		}

		if (isSet(personalizedVariation)) {
			Navigator.gotoNavItem(navItem = ${personalizedVariation});
		}

		WaitForLiferayEvent.initializeLiferayEventLog();

		LexiconEntry.gotoVerticalEllipsisMenuItemNoError(menuItem = "View Items");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");
	}

	@summary = "Default summary"
	macro prioritizeVariation(variationTitle = null) {
		if (IsElementNotPresent(key_menuItem = "Prioritize", locator1 = "MenuItem#ANY_MENU_ITEM")) {
			Click(
				key_variation = ${variationTitle},
				locator1 = "AssetLists#VARIATION_ELLIPSIS");
		}

		Click(
			key_menuItem = "Prioritize",
			locator1 = "MenuItem#ANY_MENU_ITEM");
	}

	@summary = "Default summary"
	macro removeScope(scopeName = null, assetListTitle = null) {
		Click(
			key_title = ${assetListTitle},
			locator1 = "AssetLists#ASSET_LIST_ENTRY_TITLE");

		Panel.expandPanel(panel = "Scope");

		Click(
			key_scopeName = ${scopeName},
			locator1 = "AssetLists#ASSET_LIST_REMOVE_SCOPE");

		PortletEntry.save();

		AssertTextNotPresent(
			key_scopeName = ${scopeName},
			locator1 = "AssetLists#ASSET_LIST_SCOPE_NAME",
			value1 = ${scopeName});

		SelectFrameTop();
	}

	@summary = "Default summary"
	macro renameAssetList(newAssetListTitle = null, oldAssetListTitle = null) {
		AssetListsAdmin.gotoEntryMenuItem(
			assetListTitle = ${oldAssetListTitle},
			item = "Rename");

		PortletEntry.inputText(
			fieldLabel = "Title",
			text = ${newAssetListTitle});

		Button.clickSave();
	}

	@summary = "Default summary"
	macro resetCollectionsToCombineAssetsFromSegments() {
		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "System Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "Assets",
			configurationName = "Collections",
			configurationScope = "System Scope");

		if (IsElementPresent(locator1 = "Button#UPDATE")) {
			Click(locator1 = "Icon#HISTORY_VERTICAL_ELLIPSIS");

			MenuItem.clickNoError(menuItem = "Reset Default Values");

			Alert.viewSuccessMessage();
		}
	}

	@summary = "Default summary"
	macro selectAsset(itemType = null, depotName = null, entryList = null, itemSubtype = null, assetListName = null, assetType = null) {
		if (isSet(assetListName)) {
			Click(
				key_title = ${assetListName},
				locator1 = "AssetLists#ASSET_LIST_ENTRY_TITLE");

			WaitForPageLoad();
		}

		if (IsElementPresent(locator1 = "APConfiguration#SOURCE_SCOPE_ASSET_TYPE_SELECT")) {
			AssetListsAdmin.configureItemTypeInManualCollection(
				itemSubtype = ${itemSubtype},
				itemType = ${itemType});
		}

		Button.clickSelect();

		MenuItem.click(menuItem = ${assetType});

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		if (isSet(depotName)) {
			AssertClick(
				key_breadcrumbName = "Sites and Libraries",
				locator1 = "Breadcrumb#BREADCRUMB_ENTRY",
				value1 = "Sites and Libraries");

			AssertClick(
				key_groupTab = "Asset Library",
				locator1 = "ItemSelector#NAVIGATION_GROUP_TAB",
				value1 = "Asset Library");

			LexiconCard.clickCard(card = ${depotName});
		}

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		if (isSet(entryList)) {
			for (var assetName : list ${entryList}) {
				LexiconEntry.selectEntry(rowEntry = ${assetName});
			}
		}
		else {
			LexiconEntry.selectEntry(rowEntry = ${assetName});
		}

		SelectFrameTop();

		Button.click(button = "Add");

		SelectFrameTop();

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro viewAssetList(assetListTitle = null, type = null) {
		var key_title = ${assetListTitle};
		var key_type = ${type};

		AssertTextEquals.assertPartialText(
			locator1 = "AssetLists#ASSET_LIST_ENTRY_TITLE",
			value1 = ${assetListTitle});

		AssertTextEquals.assertPartialText(
			key_assetTitle = ${assetListTitle},
			locator1 = "AssetLists#ASSET_TYPE_ROW",
			value1 = ${type});
	}

	@summary = "Default summary"
	macro viewAssetTypeList(itemType = null, assetTypeList = null, itemSubtype = null, assetListName = null) {
		var key_title = ${assetListName};

		Click(locator1 = "AssetLists#ASSET_LIST_ENTRY_TITLE");

		if (isSet(manualSelection)) {
			if (isSet(itemType)) {
				Select(
					locator1 = "APConfiguration#SOURCE_SCOPE_ASSET_TYPE_SELECT",
					value1 = ${itemType});

				if (isSet(itemSubtype)) {
					Select(
						locator1 = "APConfiguration#SOURCE_SCOPE_ASSET_SUBTYPE_SELECT",
						value1 = ${itemSubtype});
				}

				WaitForElementNotPresent(
					key_text = "Save",
					locator1 = "Button#ANY_DISABLED");

				PortletEntry.save();
			}

			Button.clickSelect();

			for (var assetType : list ${assetTypeList}) {
				MenuItem.viewPresent(menuItem = ${assetType});
			}
		}

		if (isSet(dynamicSelection)) {
			Panel.expandPanel(panel = "Source");

			Click(locator1 = "APConfiguration#SOURCE_SCOPE_ASSET_TYPE_SELECT");

			for (var assetType : list ${assetTypeList}) {
				AssertElementPresent(
					key_assetType = ${assetType},
					locator1 = "APConfiguration#SOURCE_SCOPE_ASSET_TYPE_OPTION");
			}
		}
	}

	@summary = "Default summary"
	macro viewContent(assetTitleList = null, personalizedVariation = null, assetListTitle = null, assetType = null) {
		AssetListsAdmin.openViewItems(
			assetListTitle = ${assetListTitle},
			personalizedVariation = ${personalizedVariation});

		if (isSet(assetTitleList)) {
			for (var assetTitle : list ${assetTitleList}) {
				AssertTextEquals(
					key_assetTitle = ${assetTitle},
					locator1 = "AssetLists#ASSET_TITLE_ROW",
					value1 = ${assetTitle});
			}
		}
		else {
			AssertTextEquals(
				key_assetTitle = ${assetTitle},
				locator1 = "AssetLists#ASSET_TITLE_ROW",
				value1 = ${assetTitle});

			AssertTextEquals(
				key_assetTitle = ${assetTitle},
				locator1 = "AssetLists#ASSET_TYPE_ROW",
				value1 = ${assetType});
		}

		IFrame.closeFrame();
	}

	@summary = "Default summary"
	macro viewNoAssetType(assetListName = null) {
		var key_title = ${assetListName};

		Click(locator1 = "AssetLists#ASSET_LIST_ENTRY_TITLE");

		if (isSet(manualSelection)) {
			if (isSet(allAssetsDisabled)) {
				AssertElementNotPresent(
					key_text = "Select",
					locator1 = "Button#ANY");
			}
			else {
				Button.clickSelect();

				for (var assetType : list ${assetType}) {
					AssertElementNotPresent(
						key_assetType = ${assetType},
						locator1 = "APConfiguration#SOURCE_SCOPE_ASSET_TYPE_OPTION");
				}
			}
		}

		if (isSet(dynamicSelection)) {
			Panel.expandPanel(panel = "Source");

			Click(locator1 = "APConfiguration#SOURCE_SCOPE_ASSET_TYPE_SELECT");

			for (var assetType : list ${assetType}) {
				AssertElementNotPresent(
					key_assetType = ${assetType},
					locator1 = "APConfiguration#SOURCE_SCOPE_ASSET_TYPE_OPTION");
			}
		}
	}

	@summary = "Default summary"
	macro viewNoContent(personalizedVariation = null, assetTitle = null, assetListTitle = null) {
		AssetListsAdmin.openViewItems(
			assetListTitle = ${assetListTitle},
			personalizedVariation = ${personalizedVariation});

		var key_assetTitle = ${assetTitle};

		AssertElementNotPresent(locator1 = "AssetLists#ASSET_TITLE_ROW");

		AssertElementNotPresent(locator1 = "AssetLists#ASSET_TYPE_ROW");

		IFrame.closeFrame();
	}

}