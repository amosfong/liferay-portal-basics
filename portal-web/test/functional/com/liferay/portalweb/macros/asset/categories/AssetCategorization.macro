definition {

	@summary = "Default summary"
	macro _addTag(tagName = null) {
		Type(
			locator1 = "AssetCategorization#TAGS_FIELD",
			value1 = ${tagName});

		ScrollWebElementIntoView(locator1 = "AssetCategorization#TAGS_FIELD");

		if (IsElementPresent(key_tagName = ${tagName}, locator1 = "AssetCategorization#TAG_AUTOCOMPLETE_SPECIFIC")) {
			Click.mouseDownMouseUp(
				key_tagName = ${tagName},
				locator1 = "AssetCategorization#TAG_AUTOCOMPLETE_SPECIFIC");
		}
		else {
			KeyPress(
				locator1 = "AssetCategorization#TAGS_FIELD",
				value1 = ",");
		}
	}

	@summary = "Default summary"
	macro addCategory(vocabularyName = null, categoryName = null) {
		var key_categoryName = ${categoryName};

		Type(
			key_vocabularyName = ${vocabularyName},
			locator1 = "AssetCategorization#CATEGORIES_FIELD",
			value1 = ${categoryName});

		Click.mouseDownMouseUp(locator1 = "AssetCategorization#CATEGORY_AUTOCOMPLETE_SPECIFIC");

		AssertTextEquals(
			locator1 = "AssetCategorization#CATEGORY",
			value1 = ${categoryName});
	}

	@summary = "Default summary"
	macro addCategoryViaSelect(vocabularyName = null, categoryName = null) {
		Panel.expandPanel(panel = "Categorization");

		Click(
			key_vocabularyName = ${vocabularyName},
			locator1 = "AssetCategorization#CATEGORY_SELECT");

		IFrame.selectCategoryFrame();

		LexiconEntry.gotoAdd();

		IFrame.selectCategoryFrame();

		PortletEntry.inputName(name = ${categoryName});

		IFrame.selectTopFrame();

		Click(locator1 = "Modal#SAVE_BUTTON");

		IFrame.closeFrame();
	}

	@summary = "Default summary"
	macro addDuplicateTag(tagName = null) {
		AssetCategorization._addTag(tagName = ${tagName});

		AssertTextEquals(
			locator1 = "//div[contains(@class,'form-feedback-item')]",
			value1 = "Duplicate Tag: ${tagName}");
	}

	@summary = "Default summary"
	macro addMultipleCategories(vocabularyName = null, categoryNameList = null) {
		for (var categoryName : list ${categoryNameList}) {
			AssetCategorization.addCategory(
				categoryName = ${categoryName},
				vocabularyName = ${vocabularyName});
		}
	}

	@summary = "Default summary"
	macro addTag(tagName = null) {
		AssetCategorization._addTag(tagName = ${tagName});

		AssertTextEquals(
			key_tagName = ${tagName},
			locator1 = "AssetCategorization#TAG",
			value1 = ${tagName});
	}

	@summary = "Default summary"
	macro gotoSelectCategory() {
		IFrame.selectCategoryFrame();

		Portlet.expandTree();
	}

	@summary = "Default summary"
	macro openSelectCategory(vocabularyName = null) {
		Click(
			key_vocabularyName = ${vocabularyName},
			locator1 = "AssetCategorization#CATEGORY_SELECT");
	}

	@summary = "Default summary"
	macro removeCategories(categoryNameList = null) {
		for (var categoryName : list ${categoryNameList}) {
			var key_categoryName = ${categoryName};

			AssertTextEquals(
				locator1 = "AssetCategorization#CATEGORY",
				value1 = ${categoryName});

			Click(
				key_itemName = ${categoryName},
				locator1 = "AssetCategorization#ITEM_REMOVE");

			AssertElementNotPresent(locator1 = "AssetCategorization#CATEGORY");
		}
	}

	@summary = "Default summary"
	macro removeTag(tagName = null) {
		var key_tagName = ${tagName};

		AssertTextEquals(
			locator1 = "AssetCategorization#TAG",
			value1 = ${tagName});

		Click(
			key_itemName = ${tagName},
			locator1 = "AssetCategorization#ITEM_REMOVE");

		AssertElementNotPresent(locator1 = "AssetCategorization#TAG");
	}

	@summary = "Default summary"
	macro selectCategories(modalTitle = null, categoryNameList = null) {
		AssetCategorization.gotoSelectCategory();

		Portlet.expandTree();

		for (var categoryName : list ${categoryNameList}) {
			Check.checkNotVisible(
				key_nodeName = ${categoryName},
				locator1 = "Treeview#NODE_CHECKBOX");
		}

		SelectFrameTop();

		if (isSet(modalTitle)) {
			SelectFrame(
				key_title = ${modalTitle},
				locator1 = "IFrame#MODAL_ANY");
		}

		if (IsElementPresent(key_text = "Add", locator1 = "Modal#MODAL_FOOTER_BUTTON")) {
			Click(
				key_text = "Add",
				locator1 = "Modal#MODAL_FOOTER_BUTTON");
		}
		else if (IsElementPresent(key_text = "Done", locator1 = "Modal#MODAL_FOOTER_BUTTON")) {
			Click(
				key_text = "Done",
				locator1 = "Modal#MODAL_FOOTER_BUTTON");
		}
		else if (IsElementPresent(key_text = "Select", locator1 = "Modal#MODAL_FOOTER_BUTTON")) {
			Click(
				key_text = "Select",
				locator1 = "Modal#MODAL_FOOTER_BUTTON");
		}
	}

	@summary = "Default summary"
	macro selectCategory(categoryName = null) {
		AssetCategorization.gotoSelectCategory();

		AssertVisible(
			key_nodeName = ${categoryName},
			locator1 = "Treeview#NODE_ITEM");

		Portlet.selectTreeNode(nodeName = ${categoryName});

		SelectFrameTop();

		if (IsElementPresent(key_text = "Add", locator1 = "Modal#MODAL_FOOTER_BUTTON")) {
			Click(
				key_text = "Add",
				locator1 = "Modal#MODAL_FOOTER_BUTTON");
		}
		else if (IsElementPresent(key_text = "Done", locator1 = "Modal#MODAL_FOOTER_BUTTON")) {
			Click(
				key_text = "Done",
				locator1 = "Modal#MODAL_FOOTER_BUTTON");
		}
	}

	@summary = "Default summary"
	macro selectTag(tagRow = null, tagNameList = null) {
		if (isSet(tagRow)) {
			Click(
				key_tagRow = ${tagRow},
				locator1 = "AssetCategorization#TAGS_FRAGMENT_BY_ROW");
		}
		else {
			Click(
				key_fieldLabel = "Tags",
				key_text = "Select",
				locator1 = "Button#ANY_WITH_LABEL");
		}

		IFrame.selectTagFrame();

		for (var tagName : list ${tagNameList}) {
			Check(
				key_rowEntry = ${tagName},
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_CHECKBOX");
		}

		SelectFrameTop();

		if (!(isSet(skipSelectFrame))) {
			SelectFrame(
				key_title = "Asset Publisher",
				locator1 = "IFrame#MODAL_ANY");
		}

		AssertClick(
			locator1 = "Button#DONE",
			value1 = "Done");

		AssertTextEquals(
			key_tagName = ${tagName},
			locator1 = "AssetCategorization#TAG",
			value1 = ${tagName});
	}

	@summary = "Select tags"
	macro selectTags(tagNameList = null) {
		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		for (var tagName : list ${tagNameList}) {
			LexiconTable.checkTableCheckbox(tableEntry = ${tagName});
		}

		SelectFrame(value1 = "relative=top");

		Click(
			key_text = "Select",
			locator1 = "Modal#MODAL_FOOTER_BUTTON");
	}

	@summary = "Default summary"
	macro viewCategory(categoryName = null) {
		AssertTextEquals(
			key_categoryName = ${categoryName},
			locator1 = "AssetCategorization#CATEGORY",
			value1 = ${categoryName});
	}

	@summary = "Default summary"
	macro viewNoAddPermissions(vocabularyName = null) {
		Panel.expandPanel(panel = "Categorization");

		Click(
			key_vocabularyName = ${vocabularyName},
			locator1 = "AssetCategorization#CATEGORY_SELECT");

		IFrame.selectCategoryFrame();

		AssertElementNotPresent(locator1 = "Button#PLUS");
	}

	@summary = "Default summary"
	macro viewNoTag(tagName = null) {
		AssertElementNotPresent(
			key_tagName = ${tagName},
			locator1 = "AssetCategorization#TAG",
			value1 = ${tagName});
	}

	@summary = "Default summary"
	macro viewNoVocabularyField(vocabularyName = null) {
		Panel.expandPanel(panel = "Categorization");

		AssertElementNotPresent(
			key_vocabularyName = ${vocabularyName},
			locator1 = "AssetCategorization#CATEGORIES_FIELD");
	}

	@summary = "Default summary"
	macro viewTag(tagName = null) {
		AssertTextEquals(
			key_tagName = ${tagName},
			locator1 = "AssetCategorization#TAG",
			value1 = ${tagName});
	}

	@summary = "Default summary"
	macro viewVocabulary(vocabularyName = null) {
		AssertTextEquals(
			key_vocabularyName = ${vocabularyName},
			locator1 = "UsersAndOrganizationsEditUser#VOCABULARY_LABEL",
			value1 = ${vocabularyName});
	}

}