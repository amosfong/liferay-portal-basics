definition {

	var ddlDataDefinitionDescription = "Data Definition Description";
	var ddlDataDefinitionName = "Data Definition Name";
	var ddlListName = "List Name";

	@summary = "Default summary"
	macro _add(ddlDataDefinitionName = null, ddlDataDefinitionDescription = null) {
		if (IsTextNotEqual(locator1 = "Portlet#HEADER", value1 = "Data Definitions")) {
			AssertTextEquals(
				locator1 = "DataEngineModal#MODAL_TITLE",
				value1 = "Data Definitions");

			SelectFrame(
				key_title = "Data Definitions",
				locator1 = "IFrame#MODAL_ANY");
		}

		LexiconEntry.gotoAdd();

		if ((IsTextNotEqual(locator1 = "Portlet#HEADER", value1 = "New Data Definition")) && (IsTextNotEqual(locator1 = "Portlet#HEADER", value1 = "Data Definitions"))) {
			AssertTextEquals(
				locator1 = "DataEngineModal#MODAL_TITLE",
				value1 = "Data Definitions");

			SelectFrame(
				key_title = "Data Definitions",
				locator1 = "IFrame#MODAL_ANY");
		}

		DDLDataDefinition.editName(ddlDataDefinitionName = ${ddlDataDefinitionName});

		if (isSet(ddlDataDefinitionDescription)) {
			DDLDataDefinition._showDetails();

			Type.clickAtType(
				locator1 = "TextInput#DESCRIPTION",
				value1 = ${ddlDataDefinitionDescription});

			DDLDataDefinition._closeDetails();
		}
	}

	@summary = "Default summary"
	macro _closeDetails() {
		if (IsElementPresent.isVisible(locator1 = "TextInput#DESCRIPTION")) {
			Click(locator1 = "DDMEditStructure#DETAILS_TOGGLE");
		}

		AssertNotVisible(locator1 = "TextInput#DESCRIPTION");
	}

	@summary = "Default summary"
	macro _select(ddlDataDefinitionName = null) {
		SelectFrame(
			key_title = "Data Definitions",
			locator1 = "IFrame#MODAL_ANY");

		var key_ddlDataDefinitionName = ${ddlDataDefinitionName};

		AssertClick(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME_LINK",
			value1 = ${ddlDataDefinitionName});
	}

	@summary = "Default summary"
	macro _showDetails() {
		if (IsElementNotPresent.isNotVisible(locator1 = "TextInput#DESCRIPTION")) {
			Click(locator1 = "DDMEditStructure#DETAILS_TOGGLE");

			if (IsElementNotPresent.isNotVisible(locator1 = "TextInput#DESCRIPTION")) {
				Click(locator1 = "DDMEditStructure#DETAILS_TOGGLE");
			}
		}

		AssertVisible(locator1 = "TextInput#DESCRIPTION");
	}

	@summary = "Default summary"
	macro addCP(ddlDataDefinitionName = null, ddlDataDefinitionDescription = null) {
		DDLNavigator.gotoManageDataDefinitions();

		DDLDataDefinition._add(
			ddlDataDefinitionDescription = ${ddlDataDefinitionDescription},
			ddlDataDefinitionName = ${ddlDataDefinitionName});
	}

	@summary = "Default summary"
	macro addViaDDLDisplayPG(ddlDataDefinitionName = null, ddlDataDefinitionDescription = null) {
		DDLDataDefinition._add(
			ddlDataDefinitionDescription = ${ddlDataDefinitionDescription},
			ddlDataDefinitionName = ${ddlDataDefinitionName});

		SelectFrame(value1 = "relative=top");

		DynamicDataMapping.selectDynamicDataMappingFrame();
	}

	@summary = "Default summary"
	macro clickSave() {
		AssertClick(
			locator1 = "DDMEditStructure#SAVE_BUTTON",
			value1 = "Save");
	}

	@summary = "Default summary"
	macro copyCP(ddlDataDefinitionName = null, ddlDataDefinitionCopyName = null, copyDisplayTemplates = null, copyFormTemplates = null) {
		DDLNavigator.gotoManageDataDefinitions();

		var key_ddlDataDefinitionName = ${ddlDataDefinitionName};

		Click.waitForMenuToggleJSClick(locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS");

		MenuItem.click(menuItem = "Make a Copy");

		PortletEntry.inputName(name = ${ddlDataDefinitionCopyName});

		if (${copyFormTemplates} == "true") {
			Check(locator1 = "DDMSelectTemplate#COPY_FORM_TEMPLATES_CHECKBOX");
		}

		if (${copyDisplayTemplates} == "true") {
			Check(locator1 = "DDMSelectTemplate#COPY_DISPLAY_TEMPLATES_CHECKBOX");
		}

		AssertClick(
			locator1 = "Button#COPY",
			value1 = "Make a Copy");

		VerifyElementPresent(locator1 = "Message#SUCCESS");

		DDLDataDefinition.viewName(ddlDataDefinitionName = ${ddlDataDefinitionCopyName});
	}

	@summary = "Default summary"
	macro deleteCP(ddlDataDefinitionName = null) {
		DDLNavigator.gotoManageDataDefinitions();

		var key_ddlDataDefinitionName = ${ddlDataDefinitionName};

		Click.waitForMenuToggleJSClick(locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS");

		MenuItem.clickNoError(menuItem = "Delete");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Dynamic Data Lists");

		DDLNavigator.gotoManageDataDefinitions();

		AssertElementNotPresent(
			key_ddlDataDefinitionName = ${ddlDataDefinitionName},
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS");
	}

	@summary = "Default summary"
	macro deleteNoCP(ddlDataDefinitionName = null) {
		DDLNavigator.gotoManageDataDefinitions();

		Search.searchCP(searchTerm = ${ddlDataDefinitionName});

		Click.waitForMenuToggleJSClick(locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS_1");

		MenuItem.clickNoError(menuItem = "Delete");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		AssertTextEquals.assertPartialText(
			locator1 = "Message#ERROR",
			value1 = "The structure cannot be deleted because it is required by one or more structure links.");
	}

	@summary = "Default summary"
	macro editCP(ddlDataDefinitionName = null, ddlDataDefinitionNameEdit = null, ddlDataDefinitionDescriptionEdit = null) {
		DDLNavigator.gotoManageDataDefinitions();

		Search.searchCP(searchTerm = ${ddlDataDefinitionName});

		Click.waitForMenuToggleJSClick(locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS_1");

		MenuItem.click(menuItem = "Edit");

		Type.clickAtType(
			locator1 = "TextInput#NAME",
			value1 = ${ddlDataDefinitionNameEdit});

		DDLDataDefinition._showDetails();

		Type.clickAtType(
			locator1 = "TextInput#DESCRIPTION",
			value1 = ${ddlDataDefinitionDescriptionEdit});
	}

	@summary = "Default summary"
	macro editName(ddlDataDefinitionName = null) {
		Type.clickAtType(
			locator1 = "DDMEditStructure#NAME_FIELD",
			value1 = ${ddlDataDefinitionName});
	}

	@summary = "Default summary"
	macro editPermissionsCP(addOrRemoveViewPermission = null, roleList = null) {
		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		PermissionsInline.addOrRemoveViewPermissionFromAllRoles(
			addOrRemoveViewPermission = ${addOrRemoveViewPermission},
			roleList = ${roleList});
	}

	@summary = "Default summary"
	macro save() {
		Click.mouseDownMouseUp(locator1 = "//span[contains(@class,'btn')][normalize-space(text())='Save']/..");

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro saveViaDDLDisplayPG() {
		DDLDataDefinition.clickSave();

		Alert.viewSuccessMessage();

		SelectFrame(value1 = "relative=top");
	}

	@summary = "Default summary"
	macro selectCP(ddlDataDefinitionName = null) {
		DDLDataDefinition._select(ddlDataDefinitionName = ${ddlDataDefinitionName});

		SelectFrameTop();

		AssertTextEquals(
			locator1 = "TextInput#DATA_DEFINITION",
			value1 = ${ddlDataDefinitionName});
	}

	@summary = "Default summary"
	macro selectViaDDLDisplayPG(ddlDataDefinitionName = null) {
		DDLDataDefinition._select(ddlDataDefinitionName = ${ddlDataDefinitionName});

		SelectFrameTop();

		while (IsElementNotPresent(locator1 = "IFrame#MODAL_BODY")) {
			WaitForElementPresent(locator1 = "IFrame#MODAL_BODY");
		}

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		AssertTextEquals.assertValue(
			locator1 = "TextInput#DATA_DEFINITION",
			value1 = ${ddlDataDefinitionName});
	}

	@summary = "Default summary"
	macro setInputFieldLocalization(languageValue = null) {
		var key_languageValue = ${languageValue};

		PortletEntry.changeLocale(
			fieldName = "Name",
			locale = ${languageValue});
	}

	@summary = "Default summary"
	macro tearDownCP(kfProcessExists = null) {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Dynamic Data Lists");

		DDLNavigator.gotoManageDataDefinitions();

		var ddlDataDefinitionName = "Data Definition";

		Search.searchCP(searchTerm = ${ddlDataDefinitionName});

		if (IsElementNotPresent(locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME_1")) {
			AssertTextEquals.assertPartialText(
				locator1 = "Message#EMPTY_INFO",
				value1 = "No Data Definitions were found.");
		}
		else {
			AssertTextNotEquals(
				locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME_1",
				value1 = "Contacts");

			var key_ddlDataDefinitionName = ${ddlDataDefinitionName};

			if (IsElementPresent(locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS_1")) {
				PortletEntry.selectAll();

				ClickNoError(locator1 = "Icon#TIMES");

				AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

				if (${kfProcessExists} == "true") {
					Alert.viewRequestFailedToComplete();

					AssertTextEquals.assertPartialText(
						locator1 = "Message#ERROR_2",
						value1 = "The structure cannot be deleted because it is required by one or more structure links.");
				}
				else {
					Alert.viewSuccessMessage();
				}
			}
		}
	}

	@summary = "Default summary"
	macro viewCP(ddlDataDefinitionName = null, fieldFieldLabel = null, ddlDataDefinitionDescription = null) {
		DDLNavigator.gotoManageDataDefinitions();

		var key_ddlDataDefinitionName = ${ddlDataDefinitionName};

		Click(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME_LINK",
			value1 = ${ddlDataDefinitionName});

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Edit ${ddlDataDefinitionName}");

		AssertTextEquals(
			locator1 = "TextInput#NAME",
			value1 = ${ddlDataDefinitionName});

		if (isSet(ddlDataDefinitionDescription)) {
			DDLDataDefinition._showDetails();

			AssertTextEquals(
				locator1 = "TextInput#DESCRIPTION",
				value1 = ${ddlDataDefinitionDescription});
		}

		if (isSet(fieldFieldLabel)) {
			AssertElementPresent(
				key_fieldFieldLabel = ${fieldFieldLabel},
				locator1 = "DDMEditStructure#FORM_FIELD");
		}
	}

	@summary = "Default summary"
	macro viewName(ddlDataDefinitionName = null) {
		var key_ddlDataDefinitionName = ${ddlDataDefinitionName};

		AssertTextEquals(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME",
			value1 = ${ddlDataDefinitionName});
	}

	@summary = "Default summary"
	macro viewParentStructureHeader(assetTitle = null) {
		Click(locator1 = "DDMEditStructure#DETAILS_TOGGLE");

		AssertClick(
			locator1 = "Button#SELECT",
			value1 = "Select");

		var key_iframeTitle = ${assetTitle};

		if (IsElementNotPresent(locator1 = "IFrame#IFRAME_TITLE")) {
			AssertTextEquals(
				locator1 = "Modal#HEADER",
				value1 = ${assetTitle});
		}
		else {
			AssertTextEquals(
				locator1 = "IFrame#IFRAME_TITLE",
				value1 = ${assetTitle});
		}
	}

}