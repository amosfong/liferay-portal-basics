definition {

	@summary = "Default summary"
	macro _manageCollaborators() {
		DMDocument.expandInfo();

		var key_text = "Manage Collaborators";

		AssertClick(
			locator1 = "Button#ANY",
			value1 = "Manage Collaborators");
	}

	@summary = "Default summary"
	macro changePermission(dmDocumentTitle = null, dmDocumentDescription = null, userName = null, selectPermission = null) {
		if (isSet(depotName)) {
			DMDocument.viewCP(dmDocumentTitle = ${dmDocumentTitle});
		}
		else {
			DMDocument.viewPG(
				dmDocumentDescription = ${dmDocumentDescription},
				dmDocumentTitle = ${dmDocumentTitle});
		}

		DMDocumentSharing._manageCollaborators();

		SelectFrame(locator1 = "IFrame#MODAL_IFRAME");

		var key_userName = ${userName};

		AssertTextEquals(
			locator1 = "Sharing#MANAGE_COLLABORATORS_NAME",
			value1 = ${userName});

		Select(
			locator1 = "Sharing#MANAGE_COLLABORATORS_ACCESS_DROPDOWN",
			value1 = ${selectPermission});

		AssertSelectedLabel(
			locator1 = "Sharing#MANAGE_COLLABORATORS_ACCESS_DROPDOWN",
			value1 = ${selectPermission});

		Button.clickSave();

		SelectFrame(value1 = "relative=top");

		Alert.viewSuccessMessageText(successMessage = "Permissions Changed");
	}

	@summary = "Default summary"
	macro editViaSharedwithMe(dmDocumentTitle = null, entryTitleEdit = null, entryTitle = null) {
		Asset.openTosharedContent(
			navTab = "Shared with Me",
			siteURLKey = "guest");

		if (isSet(dmDocumentTitle)) {
			DMDocument.editCP(
				dmDocumentTitle = ${dmDocumentTitle},
				dmDocumentTitleEdit = ${entryTitleEdit});
		}
		else {
			LexiconEntry.gotoEntryMenuItem(
				menuItem = "Edit",
				rowEntry = ${entryTitle});

			BlogsEntry.editTitle(
				entryTitle = ${entryTitle},
				entryTitleEdit = ${entryTitleEdit});

			Button.clickPublish();
		}

		LexiconEntry.viewEntryName(rowEntry = ${entryTitleEdit});
	}

	@summary = "Default summary"
	macro removeCollaborator(collaboratorList = null) {
		if (!(isSet(sharedByMe))) {
			DMDocumentSharing._manageCollaborators();
		}

		SelectFrame(locator1 = "IFrame#MODAL_IFRAME");

		for (var collaborator : list ${collaboratorList}) {
			var key_userName = ${collaborator};

			Click(locator1 = "Sharing#MANAGE_COLLABORATORS_REMOVE_BUTTON");
		}

		if (isSet(emptyStateMessage)) {
			AssertElementPresent(locator1 = "Sharing#MANAGE_COLLABORATORS_EMPTY_STATE_MESSAGE");
		}

		Button.clickSave();

		SelectFrame(value1 = "relative=top");

		Alert.viewSuccessMessageText(successMessage = "Permissions Changed");
	}

	@summary = "Default summary"
	macro viewCollaborators(collaboratorList = null) {
		DMDocument.expandInfo();

		for (var collaborator : list ${collaboratorList}) {
			var key_collaborator = ${collaborator};

			AssertElementPresent(
				locator1 = "DocumentsAndMediaDocument#DOCUMENT_INFO_COLLABORATOR_TOOLTIP",
				value1 = ${collaborator});
		}
	}

	@summary = "Default summary"
	macro viewDocumentViaSharedContent(dmDocumentTitle = null, contentSharingPermission = null) {
		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Shared Content");

		if (${contentSharingPermission} == "View") {
			AssertElementNotPresent(locator1 = "CKEditor#BODY_FIELD_IFRAME");

			Click.waitForMenuToggleJSClick(locator1 = "Icon#VERTICAL_ELLIPSIS_BUTTON");

			MenuItem.viewNotPresent(menuItem = "Edit");
		}
		else if (${contentSharingPermission} == "Comment") {
			SelectFrame.selectFrameNoLoading(locator1 = "CKEditor#BODY_FIELD_IFRAME");

			AssertElementPresent(locator1 = "CKEditor#BODY");

			SelectFrame(value1 = "relative=top");

			Click.waitForMenuToggleJSClick(locator1 = "Icon#VERTICAL_ELLIPSIS_BUTTON");

			MenuItem.viewNotPresent(menuItem = "Edit");
		}
		else if (${contentSharingPermission} == "Update") {
			SelectFrame.selectFrameNoLoading(locator1 = "CKEditor#BODY_FIELD_IFRAME");

			AssertElementPresent(locator1 = "CKEditor#BODY");

			SelectFrame(value1 = "relative=top");

			Click.waitForMenuToggleJSClick(locator1 = "Icon#VERTICAL_ELLIPSIS_BUTTON");

			MenuItem.viewPresent(menuItem = "Edit");
		}

		var key_dmDocumentTitle = ${dmDocumentTitle};

		AssertTextEquals(
			locator1 = "DocumentsAndMediaDocument#DOCUMENT_INFO_TITLE",
			value1 = ${dmDocumentTitle});
	}

	@summary = "Default summary"
	macro viewNoDuplicateDocument(dmDocumentTitle = null) {
		UserBar.gotoDropdownItem(dropdownItem = "Shared Content");

		AssertTextEquals(
			key_contentTitle = ${dmDocumentTitle},
			locator1 = "Sharing#SHARED_CONTENT_CONTENT_TITLE",
			value1 = ${dmDocumentTitle});

		AssertElementNotPresent(
			key_contentTitle = ${dmDocumentTitle},
			locator1 = "Sharing#SHARED_CONTENT_CONTENT_TITLE_2");
	}

	@summary = "Default summary"
	macro viewSharedFlag(displayStyle = null, dmDocumentTitle = null) {
		if (isSet(detailsView)) {
			if (${displayStyle} == "table") {
				AssertVisible(
					key_dmDocumentTitle = ${dmDocumentTitle},
					locator1 = "DocumentsAndMedia#TABLE_DOCUMENT_SHARED");
			}
			else {
				AssertVisible(
					key_dmDocumentTitle = ${dmDocumentTitle},
					locator1 = "DocumentsAndMedia#ICON_DOCUMENT_SHARED");
			}
		}
		else {
			AssertElementNotPresent(
				key_dmDocumentTitle = ${dmDocumentTitle},
				locator1 = "DocumentsAndMedia#ICON_DOCUMENT_SHARED");
		}
	}

	@summary = "Default summary"
	macro viewSharingOption(dmDocumentTitle = null, sharing = null) {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		Click(
			key_dmDocumentTitle = ${dmDocumentTitle},
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_VERTICAL_ELLIPSIS");

		if (${sharing} == "true") {
			MenuItem.viewPresent(menuItem = "Share");
		}
		else {
			MenuItem.viewNotPresent(menuItem = "Share");
		}
	}

}