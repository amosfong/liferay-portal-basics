@component-name = "portal-site-management"
definition {

	property custom.properties = "virtual.hosts.valid.hosts=localhost,127.0.0.1,www.able.com";
	property database.types = "mysql, postgresql";
	property liferay.online.properties = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Liferay Online";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		HeadlessPortalInstanceAPI.addPortalInstanceWithVirtualInstanceInitializer(
			domain = "www.able.com",
			portalInstanceId = "www.able.com",
			siteInitializerKey = "com.liferay.site.initializer.masterclass",
			virtualHost = "www.able.com");
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "LPS-142122: Clicking on Masterclass logo opens a page with an error"
	@priority = 3
	test ClickMasterclassLogoGoesHomePage {
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		task ("Assert the Virtual Instance created by Json is present") {
			PortalInstances.openVirtualInstancesAdmin();

			PortalInstances.viewCP(virtualHost = "www.able.com");

			Navigator.openSpecificURL(url = "http://www.able.com:8080");

			AssertElementPresent(
				key_title = "Welcome to Masterclass",
				locator1 = "Masterclass#FRAGMENT_HERO_PRE");
		}

		task ("Go to Sign In Page") {
			Click(locator1 = "Masterclass#NAV_TAB");
		}

		task ("Click Mastercard logo") {
			Click(locator1 = "Masterclass#MASTERCLASS_LOGO");
		}

		task ("Assert the default Masterclass Moderns is applied to Home page") {
			AssertCssValue(
				key_content = "Read More",
				key_element = "a",
				key_id = "hero-button",
				key_type = "link",
				locator1 = "StyleBookEditor#CONTRIBUTED_FRAGMENT_INLINE_CONTENT",
				locator2 = "color",
				value1 = "rgba(255, 255, 255, 1)");
		}
	}

	@description = "Verify if a friendly error appears when the user is not able to create the virtual host"
	@priority = 3
	test FriendlyErrorAppearsWhenTheUserCantCreateVirtualHost {
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		task ("Go to Virtual Instances") {
			PortalInstances.openVirtualInstancesAdmin();
		}

		task ("Create a new Virtual instance using localhost") {
			LexiconEntry.gotoAdd();

			SelectFrame(locator1 = "IFrame#MODAL_BODY");

			Type(
				locator1 = "TextInput#WEB_ID",
				value1 = "www.baker.com");

			Type(
				locator1 = "TextInput#VIRTUAL_HOST",
				value1 = "localhost");

			Type(
				locator1 = "TextInput#MAIL_DOMAIN",
				value1 = "www.baker.com");

			SelectFrameTop();

			Button.clickAdd();
		}

		task ("Assert the error message") {
			SelectFrame(locator1 = "IFrame#MODAL_BODY");

			Alert.viewErrorMessage(errorMessage = "Please enter a valid virtual host.");
		}
	}

}