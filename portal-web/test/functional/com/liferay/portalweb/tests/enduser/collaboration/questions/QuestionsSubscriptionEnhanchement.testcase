@component-name = "portal-questions"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Questions";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Questions Page");

		JSONLayout.addWidgetToPublicLayout(
			column = 2,
			groupName = "Guest",
			layoutName = "Questions Page",
			widgetName = "Questions");

		JSONMBMessage.addCategory(
			categoryDescription = "Description",
			categoryName = "Questions Test Topic",
			userEmailAddress = "test@liferay.com");
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			ProductMenu.gotoPortlet(
				category = "Content & Data",
				portlet = "Message Boards");

			if (IsElementPresent(locator1 = "Questions#QUESTIONS_LIST")) {
				Questions.tearDown();
			}

			PagesAdmin.tearDownCP();
		}
	}

	@description = "This is a test for LPS-159147. Verify if the user is notified when a followed question has a new answer."
	@priority = 3
	test CanBeNotifiedByAnswer {
		task ("Given there are a question created") {
			JSONQuestions.addQuestion(
				bodyField = "Content AddQuestion",
				categoryName = "Questions Test Topic",
				groupName = "Guest",
				titleField = "Question1");
		}

		task ("And the user Subcribes on a Topic") {
			Questions.openToQuestionsTopic(
				layoutName = "questions-page",
				siteURLKey = "guest",
				topicName = "questions-test-topic");

			Questions.subscribeTopic();
		}

		task ("When another user with admin role is logged") {
			JSONUser.addUser(
				userEmailAddress = "test1@liferay.com",
				userFirstName = "test1",
				userLastName = "test",
				userScreenName = "test1");

			JSONUser.setFirstPassword(
				agreeToTermsAndAnswerReminderQuery = "true",
				requireReset = "false",
				userEmailAddress = "test1@liferay.com");

			JSONRole.assignRoleToUser(
				roleTitle = "Administrator",
				userEmailAddress = "test1@liferay.com");

			User.logoutAndLoginPG(
				userLoginEmailAddress = "test1@liferay.com",
				userLoginFullName = "test1 test");
		}

		task ("And Answer the Question") {
			Questions.openToQuestionsTopic(
				layoutName = "questions-page",
				siteURLKey = "guest",
				topicName = "questions-test-topic");

			Questions.gotoDetails(questionTitle = "Question1");

			Questions.answerQuestion(contentField = "This is my answer");
		}

		task ("Then the user that was subscribed on the topic can see the notifications") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "test@liferay.com",
				userLoginFullName = "test test");

			Notifications.viewBadgeCount(notificationCount = 1);
		}
	}

	@description = "This is a test for LPS-159156. Verify if the user is notified when a followed topic has a new comment."
	@priority = 3
	test CanBeNotifiedByComment {
		task ("Given there are a question created") {
			JSONQuestions.addQuestion(
				bodyField = "Content AddQuestion",
				categoryName = "Questions Test Topic",
				groupName = "Guest",
				titleField = "Question1");
		}

		task ("And a user answer a question on a followed topic") {
			Questions.openToQuestionsTopic(
				layoutName = "questions-page",
				siteURLKey = "guest",
				topicName = "questions-test-topic");

			Questions.subscribeTopic();

			Questions.gotoDetails(questionTitle = "Question1");

			Questions.answerQuestion(contentField = "This is my answer text.");
		}

		task ("When another user with admin role is logged") {
			JSONUser.addUser(
				userEmailAddress = "test1@liferay.com",
				userFirstName = "test1",
				userLastName = "test",
				userScreenName = "test1");

			JSONUser.setFirstPassword(
				agreeToTermsAndAnswerReminderQuery = "true",
				requireReset = "false",
				userEmailAddress = "test1@liferay.com");

			JSONRole.assignRoleToUser(
				roleTitle = "Administrator",
				userEmailAddress = "test1@liferay.com");

			User.logoutAndLoginPG(
				userLoginEmailAddress = "test1@liferay.com",
				userLoginFullName = "test1 test");
		}

		task ("And Add a comment in an Answer ") {
			Questions.openToQuestionsTopic(
				layoutName = "questions-page",
				siteURLKey = "guest",
				topicName = "questions-test-topic");

			Questions.gotoDetails(questionTitle = "Question1");

			Questions.replyToComment(
				commentReply = "This is my reply text.",
				repliedLabel = "true");
		}

		task ("Then the user that was subscribed on the topic can see the notifications") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "test@liferay.com",
				userLoginFullName = "test test");

			Notifications.viewBadgeCount(notificationCount = 1);
		}
	}

	@description = "This is a test for LPS-159158. Verify if the user is notified when a followed topic has a new question."
	@priority = 3
	test CanBeNotifiedByQuestion {
		task ("Create a new User with an Admin Role") {
			JSONUser.addUser(
				userEmailAddress = "user@liferay.com",
				userFirstName = "user",
				userLastName = "user",
				userScreenName = "useruser");

			JSONUser.setFirstPassword(
				agreeToTermsAndAnswerReminderQuery = "true",
				requireReset = "false",
				userEmailAddress = "user@liferay.com");

			JSONRole.assignRoleToUser(
				roleTitle = "Administrator",
				userEmailAddress = "user@liferay.com");
		}

		task ("Subcribe on a Topic") {
			Navigator.gotoPage(pageName = "Questions Page");

			Questions.gotoCategory(categoryName = "Questions Test Topic");

			Questions.subscribeTopic();
		}

		task ("Login with a new User") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "user@liferay.com",
				userLoginFullName = "user user");
		}

		task ("Enter on the Topic") {
			Navigator.gotoPage(pageName = "Questions Page");

			Questions.gotoCategory(categoryName = "Questions Test Topic");
		}

		task ("Create a Question") {
			Questions.askQuestionFillingTheMandatoryFields(
				contentField = "Content AddQuestion",
				titleField = "Question1",
				topicName = "questions-test-topic");
		}

		task ("Login with Test test") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "test@liferay.com",
				userLoginFullName = "test test");
		}

		task ("Validate the notifications") {
			Notifications.viewBadgeCount(notificationCount = 1);
		}
	}

	@description = "This is a test for LPS-159138. Verify if the user is able to subscribe a tag if they want to."
	@priority = 5
	test CanSubscribeAQuestion {
		task ("Create a Question") {
			Navigator.gotoPage(pageName = "Questions Page");

			Questions.gotoCategory(categoryName = "Questions Test Topic");

			Questions.askQuestionFillingTheMandatoryFields(
				contentField = "Content AddQuestion",
				titleField = "Question1",
				topicName = "questions-test-topic");
		}

		task ("Go to Subscriptions") {
			Questions.gotoDetails(questionTitle = "Question1");

			Questions.gotoTab(questionTabField = "Subscriptions");
		}

		task ("Validate the subcribed question") {
			Questions.viewSubscribedQuestion(subscribedQuestion = "Question1");
		}
	}

	@description = "This is a test for LPS-159144. Verify if the user is able to subscribe a tag if they want to."
	@priority = 5
	test CanSubscribeATag {
		task ("Given there is a Created Question with a tag") {
			JSONQuestions.addQuestionWithTag(
				bodyField = "Content1",
				categoryName = "Questions Test Topic",
				groupName = "Guest",
				tagField = "new",
				titleField = "Question2");
		}

		task ("When the user Subscribe to Tag") {
			Questions.openToTagsTab(
				layoutName = "questions-page",
				siteURLKey = "guest");

			Questions.subscribeFromTag();
		}

		task ("Then he can validate that he is subscribed tag") {
			Questions.openToQuestionsTopic(
				layoutName = "questions-page",
				siteURLKey = "guest",
				topicName = "questions-test-topic");

			Questions.gotoTab(questionTabField = "Subscriptions");

			Questions.viewSubscribedTag(subscribedTag = "new");
		}
	}

	@description = "This is a test for LPS-159160. Verify if the user is able to subscribe a topic if they want to."
	@priority = 5
	test CanSubscribeATopic {
		task ("Given there is a Created subtopic") {
			Questions.openToQuestionsTopic(
				layoutName = "questions-page",
				siteURLKey = "guest",
				topicName = "questions-test-topic");

			Questions.addSubTopic(
				subTopicDescription = "Topic Description",
				subTopicName = "Questions Test Topic");
		}

		task ("When the user subcribes on a topic") {
			Questions.subscribeTopic();
		}

		task ("Then the can see that he is subscribed in a topic on the subscriptions page") {
			Questions.gotoTab(questionTabField = "Subscriptions");

			Questions.viewSubscribedTopic(subscribedTopic = "Questions Test Topic");
		}
	}

	@description = "This is a test for LPS-159138. Verify if the user is able to unsubscribe a question if they want to."
	@priority = 5
	test CanUnsubscribeAQuestion {
		task ("Given there are a Topic and a question created") {
			Questions.openToQuestionsTopic(
				layoutName = "questions-page",
				siteURLKey = "guest",
				topicName = "questions-test-topic");

			Questions.askQuestionFillingTheMandatoryFields(
				contentField = "Body AddQuestion",
				titleField = "Question1");
		}

		task ("And he can see that the question is subscribed by default") {
			Questions.openToQuestionsTopic(
				layoutName = "questions-page",
				siteURLKey = "guest",
				topicName = "questions-test-topic");

			Questions.gotoTab(questionTabField = "Subscriptions");

			Questions.viewSubscribedQuestion(subscribedQuestion = "Question1");
		}

		task ("When the user goes to Unsubsribes to the Question") {
			Questions.openToQuestionsTopic(
				layoutName = "questions-page",
				siteURLKey = "guest",
				topicName = "questions-test-topic");

			Questions.gotoDetails(
				questionTabField = "Questions",
				questionTitle = "Question1");

			Questions.unsubscribeFromQuestion(questionTitle = "Question1");
		}

		task ("Then the user can see that the question Unsubsribes to the Question") {
			Questions.gotoTab(questionTabField = "Subscriptions");

			Questions.viewSubscribedQuestion(noSubscribedQuestion = "true");
		}
	}

	@description = "This is a test for LPS-159144. Verify if the user is able to unsubscribe a tag if they want to."
	@priority = 5
	test CanUnsubscribeATag {
		task ("Given there are a category and a question with a tag created") {
			JSONQuestions.addQuestionWithTag(
				bodyField = "Content1",
				categoryName = "Questions Test Topic",
				groupName = "Guest",
				tagField = "new",
				titleField = "Question1");
		}

		task ("And the user is Subscribed on a Tag") {
			Questions.openToTagsTab(
				layoutName = "questions-page",
				siteURLKey = "guest");

			Questions.subscribeFromTag();

			Questions.gotoTab(questionTabField = "Subscriptions");

			Questions.viewSubscribedTag(subscribedTag = "new");
		}

		task ("When the user goes to Unsubscribe to Tag") {
			Questions.openToTagsTab(
				layoutName = "questions-page",
				siteURLKey = "guest");

			Questions.unsubscribeFromTag();
		}

		task ("Then he can se that is possible unsubscribe on a tag") {
			Questions.gotoTab(questionTabField = "Subscriptions");

			Questions.viewSubscribedTag(noSubscribedTag = "true");
		}
	}

	@description = "This is a test for LPS-159160. Verify if the user is able to unsubscribe a topic if they want to"
	@priority = 5
	test CanUnsubscribeATopic {
		task ("Given there are a Topic and a subtopic created") {
			JSONMBMessage.addCategory(
				categoryDescription = "Description",
				categoryName = "Questions Test Topic",
				userEmailAddress = "test@liferay.com");

			Questions.openToQuestionsTopic(
				layoutName = "questions-page",
				siteURLKey = "guest",
				topicName = "questions-test-topic");

			Questions.addSubTopic(
				subTopicDescription = "Topic Description",
				subTopicName = "Questions Test Topic");
		}

		task ("And the user is subiscribed on this topic") {
			Questions.subscribeTopic();

			Questions.gotoTab(questionTabField = "Subscriptions");

			Questions.viewSubscribedTopic(subscribedTopic = "Questions Test Topic");
		}

		task ("When the user goes to Unsubcribe to the Topic") {
			Questions.unsubscribeFromTopic(topicName = "Questions Test Topic");
		}

		task ("Then he can see that he was unsubcribed to the topic") {
			Questions.viewSubscribedTopic(noSubscribedTopic = "true");
		}
	}

}