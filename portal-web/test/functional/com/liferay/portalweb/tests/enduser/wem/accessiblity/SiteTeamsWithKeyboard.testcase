@component-name = "portal-commerce"
definition {

	property ci.retries.disabled = "true";
	property portal.accessibility = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Site Teams";

	setUp {
		task ("Set up virtual instance") {
			TestCase.setUpPortalInstance();
		}

		task ("Sign in") {
			User.firstLoginPG();
		}

		task ("Add a site") {
			HeadlessSite.addSite(siteName = "Test Site Name");
		}
	}

	@description = "This is a test for LPS-177717. Using Keyboard Navigation it is possible to access the back button of a Team view or Team edit mode."
	@priority = 3
	test ViewBackButtonTooltipForTeamsAdminViaKeyboard {
		task ("Given a site administrator adds a team") {
			Site.openSiteTeamsAdmin(siteURLKey = "test-site-name");

			LexiconEntry.gotoAdd();

			WaitForElementPresent(locator1 = "TextInput#NAME");
		}

		task ("When the site administrator hovers over the back button via keyboard") {
			KeyPress(
				locator1 = "ProductMenu#TOGGLE",
				value1 = "\TAB");

			AssertElementFocused(locator1 = "Icon#BACK");
		}

		task ("Then the site administrator could see the tooltip message") {
			AssertVisible(
				key_content = "Go to Teams",
				locator1 = "Tooltip#FLOATING_TOOLTIP_CONTENT");
		}

		task ("Given a site administrator edits the team") {
			JSONTeam.addTeam(
				siteName = "Test Site Name",
				teamName = "Test Team Name");

			Navigator.gotoBack();

			LexiconEntry.gotoEntryMenuItem(
				menuItem = "Edit",
				rowEntry = "Test Team Name");

			WaitForElementPresent(locator1 = "TextInput#NAME");
		}

		task ("When the site administrator hovers over the back button via keyboard") {
			KeyPress(
				locator1 = "ProductMenu#TOGGLE",
				value1 = "\TAB");

			AssertElementFocused(locator1 = "Icon#BACK");
		}

		task ("Then the site administrator could see the tooltip message") {
			AssertVisible(
				key_content = "Go to Teams",
				locator1 = "Tooltip#FLOATING_TOOLTIP_CONTENT");
		}
	}

}