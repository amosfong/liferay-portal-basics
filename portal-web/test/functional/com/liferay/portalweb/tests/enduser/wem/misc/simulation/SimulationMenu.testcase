@component-name = "portal-page-management"
definition {

	property ci.retries.disabled = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Page Administration";

	static var randomSiteName = StringUtil.randomString(8);

	var siteURLKey = StringUtil.toLowerCase(${randomSiteName});

	setUp {
		task ("Set up virtual instance") {
			TestCase.setUpPortalInstance();
		}

		task ("Sign in") {
			User.firstLoginPG();
		}

		task ("Add a site") {
			HeadlessSite.addSite(siteName = ${randomSiteName});
		}
	}

	@description = "This is a test for LPS-128810 and LPS-186155. View collection items on content page when select segment."
	@priority = 4
	test PreviewContentOnContentPageBySegment {
		task ("Add a segment with a Regular Role field") {
			var roleId = JSONPermissionSetter.setRoleId(roleTitle = "Owner");

			JSONSegmentsentry.addSegment(
				fieldName = "Regular Role",
				groupName = ${randomSiteName},
				operator = "equals",
				segmentName = "New Segment",
				text = ${roleId});
		}

		task ("Add a web content without tag") {
			JSONWebcontent.addWebContent(
				content = "Web Content Content",
				groupName = ${randomSiteName},
				title = "Web Content Title 1");
		}

		task ("Add a web content with tag") {
			JSONWebcontent.addWebContent(
				assetTagNames = "tag name",
				content = "Web Content Content",
				groupName = ${randomSiteName},
				title = "Web Content Title 2");
		}

		task ("Add a dynamic collection for Web Content Article and Basic Web Content") {
			AssetListsAdmin.openAssetListsAdmin(siteURLKey = ${siteURLKey});

			AssetListsAdmin.addDynamicSelection(assetListTitle = "Dynamic Collection");

			AssetListsAdmin.configureItemTypeInDynamicCollection(
				itemSubtype = "Basic Web Content",
				itemType = "Web Content Article");
		}

		task ("Add a new personalized variation with new segment") {
			AssetListsAdmin.addPersonalizedVariation(segmentName = "New Segment");
		}

		task ("Select the Web Content Article item type and Basic Web Content item subtype") {
			AssetListsAdmin.configureItemTypeInDynamicCollection(
				itemSubtype = "Basic Web Content",
				itemType = "Web Content Article");
		}

		task ("Add a tag filter") {
			AssetListsAdmin.addTagsFilter(tagNameList = "tag name");
		}

		task ("View both web contents shown in collection items when select Anyone personalized variation") {
			AssetListsAdmin.viewContent(
				assetTitleList = "Web Content Title 1,Web Content Title 2",
				personalizedVariation = "Anyone");
		}

		task ("View only the web content with tag is shown in collection items when select New Segment personalized variation") {
			AssetListsAdmin.viewContent(
				assetTitle = "Web Content Title 2",
				assetType = "Web Content Article",
				personalizedVariation = "New Segment");

			AssetListsAdmin.viewNoContent(
				assetTitle = "Web Content Title 1",
				assetType = "Web Content Article",
				personalizedVariation = "New Segment");

			AssetListsAdmin.prioritizeVariation(variationTitle = "New Segment");
		}

		task ("Add a Collection Display fragment to content page") {
			JSONLayout.addPublicLayout(
				groupName = ${randomSiteName},
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = ${randomSiteName});

			PageEditor.addFragment(
				collectionName = "Content Display",
				fragmentName = "Collection Display");
		}

		task ("Select the dynamic collection in Collection Display") {
			PageEditor.editCollectionDisplay(
				assetListName = "Dynamic Collection",
				fragmentName = "Collection Display");
		}

		task ("Add a Heading fragment into Collection Display") {
			PageEditor.addFragmentToCollectionDisplay(
				collectionName = "Basic Components",
				entryTitle = "Web Content Title 2",
				fragmentName = "Heading");
		}

		task ("Map the Title field to editable field of Heading") {
			PageEditorMapping.mapEditableFieldToCollectionItems(
				field = "Title",
				fragmentName = "Heading",
				id = "element-text");

			PageEditor.publish();
		}

		task ("View both web contents shown in simulation modal by default") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Page Name",
				siteName = ${randomSiteName});

			MobileDevice.previewPG();

			var n = 1;

			for (var title : list "Web Content Title 2,Web Content Title 1") {
				ContentPages.viewFragmentTextInCollectionDisplay(
					row = ${n},
					text = ${title});

				var n = ${n} + 1;
			}

			SelectFrameTop();
		}

		task ("Select the segment on Simulation panel") {
			MobileDevice.previewBy(segment = "New Segment");
		}

		task ("View only the web content with tag shown in simulation modal when select segment") {
			SelectFrame.selectFrameNoLoading(locator1 = "ControlMenuPreviewPanel#PREVIEW_DEVICE_GENERAL_IFRAME");

			ContentPages.viewFragmentTextInCollectionDisplay(text = "Web Content Title 2");

			AssertElementNotPresent(
				column = 1,
				locator1 = "Fragment#FRAGMENT_TEXT_IN_COLLECTION_DISPLAY",
				row = 2);
		}
	}

	@description = "This is a test for LPS-186155. View content in segment under Simulation panel in widget page."
	@priority = 4
	test PreviewContentOnWidgetPageBySegment {
		property asset.publisher.selection.style = "asset-list";

		task ("Enable dynamic and manual selection") {
			AssetPublisherPortlet.enableDynamicAndManualSelection();
		}

		task ("Given a site administrator displays manual collection with variation on a widget page") {
			var roleId = JSONPermissionSetter.setRoleId(roleTitle = "Owner");

			JSONSegmentsentry.addSegment(
				fieldName = "Regular Role",
				groupName = ${randomSiteName},
				operator = "equals",
				segmentName = "New Segment",
				text = ${roleId});

			JSONWebcontent.addWebContent(
				content = "Web Content Content",
				groupName = ${randomSiteName},
				title = "Web Content Title 1");

			JSONWebcontent.addWebContent(
				content = "Web Content Content",
				groupName = ${randomSiteName},
				title = "Web Content Title 2");

			AssetListsAdmin.openAssetListsAdmin(siteURLKey = ${siteURLKey});

			AssetListsAdmin.addManualSelection(assetListTitle = "Manual Collection");

			AssetListsAdmin.configureItemTypeInManualCollection(itemType = "All Types");

			AssetListsAdmin.selectAsset(
				assetType = "Basic Web Content",
				entryList = "Web Content Title 1,Web Content Title 2");

			AssetListsAdmin.addPersonalizedVariation(segmentName = "New Segment");

			AssetListsAdmin.selectAsset(
				assetName = "Web Content Title 2",
				assetType = "Basic Web Content");

			AssetListsAdmin.prioritizeVariation(variationTitle = "New Segment");

			JSONLayout.addPublicLayout(
				groupName = ${randomSiteName},
				layoutName = "Test Page Name");

			JSONLayout.addWidgetToPublicLayout(
				groupName = ${randomSiteName},
				layoutName = "Test Page Name",
				widgetName = "Asset Publisher");

			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = ${randomSiteName});

			AssetPublisherPortlet.gotoSelectCollection();

			AssetPublisherPortlet.selectAssetList(assetListName = "Manual Collection");

			IFrame.closeFrame();
		}

		task ("When the site administrator accesses to the simulation menu") {
			Click.waitForMenuToggleJSClick(locator1 = "ControlMenu#SIMULATION");
		}

		task ("Then the site administrator could preview content by segment") {
			SelectFrame.selectFrameNoLoading(locator1 = "ControlMenuPreviewPanel#PREVIEW_DEVICE_GENERAL_IFRAME");

			for (var title : list "Web Content Title 2,Web Content Title 1") {
				AssetPublisherPortlet.viewAssetPG(assetTitle = ${title});
			}

			SelectFrameTop();

			MobileDevice.previewBy(segment = "New Segment");

			SelectFrame.selectFrameNoLoading(locator1 = "ControlMenuPreviewPanel#PREVIEW_DEVICE_GENERAL_IFRAME");

			AssetPublisherPortlet.viewAssetPG(assetTitle = "Web Content Title 2");

			AssertElementNotPresent(
				key_assetTitle = "Web Content Title 1",
				locator1 = "AP#ASSET_ABSTRACT_TITLE");
		}
	}

}