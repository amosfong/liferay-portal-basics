@component-name = "portal-content-management"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Document Management";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Site Name");
		}
	}

	@description = "This test asserts that users can disable AI image creation from instance settings."
	@priority = 4
	test CanDisableAIImageCreationfromInstanceSettings {
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		DMNavigator.openToConfigureOpenAI();

		FormFields.disableCheckbox(fieldName = "Enable DALL-E to Create Images");

		PortletEntry.save();

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		Click(locator1 = "Button#PLUS");

		MenuItem.viewNotPresent(menuItem = "Create AI Image");
	}

	@description = "This test asserts that users can disable AI image creation from site settings."
	@priority = 4
	test CanDisableAIImageCreationfromSiteSettings {
		HeadlessSite.addSite(siteName = "Site Name");

		DMNavigator.openToConfigureOpenAI(
			configurationScope = "Site Settings",
			siteURLKey = "site-name");

		FormFields.disableCheckbox(fieldName = "Enable DALL-E to Create Images");

		PortletEntry.save();

		BlogsNavigator.openToAddEntry(siteURLKey = "site-name");

		ItemSelector.gotoItemSelectorViaAlloyEditor();

		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		Navigator.gotoNavTab(navTab = "Documents and Media");

		Click(locator1 = "Button#PLUS");

		MenuItem.viewNotPresent(menuItem = "Create AI Image");
	}

	@description = "This test asserts that users can not create AI image without API key."
	@priority = 4
	test CannotCreateAIImageWithoutAPIKey {
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		DMNavigator.openToConfigureOpenAI();

		FormFields.enableCheckbox(fieldName = "Enable DALL-E to Create Images");

		PortletEntry.save();

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMDocument.createAIImage(createAIImageWithoutKey = "true");

		BlogsNavigator.openToAddEntry(siteURLKey = "guest");

		ItemSelector.gotoItemSelectorViaAlloyEditor();

		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		Navigator.gotoNavTab(navTab = "Documents and Media");

		DMDocument.createAIImage(createAIImageWithoutKey = "true");
	}

}