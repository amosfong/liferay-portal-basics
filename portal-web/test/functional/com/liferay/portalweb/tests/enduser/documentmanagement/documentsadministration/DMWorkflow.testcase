@component-name = "portal-content-management"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Document Management";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			DMDocument.tearDownCP();

			DMFolder.tearDownCP();

			User.logoutPG();
		}
	}

	@description = "This ensures that a document can be copied to a workflow-enabled folder."
	@priority = 4
	test CanApproveCopiedDocument {
		JSONFolder.addFolder(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name",
			groupName = "Guest");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMFolder.configureWorkflowCP(
			dmFolderName = "DM Folder Name",
			workflowDefinition = "Single Approver",
			workflowRestriction = "Default Workflow");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			groupName = "Guest",
			mimeType = "image/jpeg",
			sourceFileName = "Document_1.jpg");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMDocument.copy(
			dmDocumentTitle = "DM Document Title",
			targetFolderName = "DM Folder Name");

		DMNavigator.openToFolderInAdmin(
			dmFolderName = "DM Folder Name",
			groupName = "Guest",
			siteURLKey = "guest");

		DMDocument.viewStatus(
			dmDocumentStatus = "Pending",
			dmDocumentTitle = "DM Document Title");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "DM Document Title",
			workflowAssetType = "Document",
			workflowTask = "Review");

		Workflow.approveTaskByActions(
			workflowAssetTitle = "DM Document Title",
			workflowAssetType = "Document",
			workflowTask = "Review");

		DMNavigator.openToFolderInAdmin(
			dmFolderName = "DM Folder Name",
			groupName = "Guest",
			siteURLKey = "guest");

		DMDocument.viewStatus(
			dmDocumentStatus = "Approved",
			dmDocumentTitle = "DM Document Title");
	}

	@description = "This is a test for LPS-136855. It checks that a pending file cannot be added to another asset."
	@priority = 4
	test CannotAddPendingFileToAsset {
		property portal.acceptance = "true";

		HeadlessSite.addSite(siteName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Test Page");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Site Name",
			layoutName = "Test Page",
			widgetName = "Web Content Display");

		JSONFolder.addFolder(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name",
			groupName = "Site Name");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "site-name");

		DMFolder.configureWorkflowCP(
			dmFolderName = "DM Folder Name",
			workflowDefinition = "Single Approver",
			workflowRestriction = "Default Workflow");

		DMNavigator.openToFolderInAdmin(
			dmFolderName = "DM Folder Name",
			groupName = "Site Name",
			siteURLKey = "site-name");

		DMDocument.addWithWorkflowCP(
			dmDocumentFile = "Document_1.png",
			dmDocumentTitle = "Document_1.png");

		Navigator.gotoSitePage(
			pageName = "Test Page",
			siteName = "Site Name");

		WebContentDisplayPortletNavigator.gotoAdd();

		WebContent.addCP(
			embedImage = "true",
			folderName = "DM Folder Name",
			imageFileName = "Document_1.png",
			pendingEntry = "true",
			siteName = "Site Name");
	}

	@description = "This is a test for LPS-84593. It ensures that a user cannot delete the dismissed notification after deleting the pending file."
	@priority = 4
	test CannotDeleteDismissedNotificationAfterDeletingPendingFile {
		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMFolder.configureWorkflowCP(
			dmFolderName = "homeFolder",
			homeFolder = "true",
			workflowDefinition = "Single Approver",
			workflowRestriction = "Default Workflow");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMDocument.addWithWorkflowCP(
			dmDocumentFile = "Document_1.png",
			dmDocumentTitle = "Document_1.png");

		JSONDocument.moveToRecycleBin(
			dmDocumentTitle = "Document_1.png",
			groupName = "Guest");

		Navigator.openURL();

		Notifications.viewBadgeCount(notificationCount = 1);

		Notifications.gotoNotifications();

		Notifications.viewDeletedContent(
			contentBody = "Notification for My Workflow Tasks was deleted.",
			noOptions = "true");
	}

	@description = "This is a test for LPS-136854. It checks that the single approver workflow can be removed from a folder."
	@priority = 3
	test CanRemoveWorkflowFromFolder {
		HeadlessSite.addSite(siteName = "Site Name");

		JSONFolder.addFolder(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name",
			groupName = "Site Name");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "site-name");

		DMFolder.configureWorkflowCP(
			dmFolderName = "DM Folder Name",
			workflowDefinition = "Single Approver",
			workflowRestriction = "Default Workflow");

		DMNavigator.openToFolderInAdmin(
			dmFolderName = "DM Folder Name",
			groupName = "Site Name",
			siteURLKey = "site-name");

		DMDocument.addWithWorkflowCP(
			dmDocumentFile = "Document_1.jpg",
			dmDocumentTitle = "Document_1.jpg");

		DMDocument.viewStatus(
			dmDocumentStatus = "Pending",
			dmDocumentTitle = "Document_1.jpg");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "site-name");

		DMFolder.configureWorkflowCP(
			dmFolderName = "DM Folder Name",
			workflowDefinition = "No Workflow",
			workflowRestriction = "Default Workflow");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			folderName = "DM Folder Name",
			groupName = "Site Name",
			mimeType = "image/jpeg",
			sourceFileName = "Document_2.jpeg");

		DMNavigator.openToFolderInAdmin(
			dmFolderName = "DM Folder Name",
			groupName = "Site Name",
			siteURLKey = "site-name");

		DMDocument.viewStatus(
			dmDocumentStatus = "Approved",
			dmDocumentTitle = "DM Document Title");
	}

	@description = "This is a test for LPS-136853. It checks that a file can be resubmitted after it has been rejected."
	@priority = 4
	test CanResubmitRejectedDocument {
		property portal.acceptance = "true";

		JSONFolder.addFolder(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name",
			groupName = "Guest");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMFolder.configureWorkflowCP(
			dmFolderName = "DM Folder Name",
			workflowDefinition = "Single Approver",
			workflowRestriction = "Default Workflow");

		DMNavigator.openToFolderInAdmin(
			dmFolderName = "DM Folder Name",
			groupName = "Guest",
			siteURLKey = "guest");

		DMDocument.addWithWorkflowCP(
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "Document_1.doc");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Document_1.doc",
			workflowAssetType = "Document",
			workflowTask = "Review");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.rejectTaskByActions(
			workflowAssetTitle = "Document_1.doc",
			workflowAssetType = "Document",
			workflowTask = "Review");

		Notifications.gotoNotifications();

		AssertClick(
			locator1 = "Notifications#NOTIFICATIONS_WORKFLOW_TITLE",
			value1 = "Your submission was rejected by Test Test, please modify and resubmit.");

		Workflow.gotoPreviewEdit();

		Button.clickSubmitForWorkflow();

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.resubmitTaskByActions(
			workflowAssetTitle = "Document_1.doc",
			workflowAssetType = "Document",
			workflowTask = "Update");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Document_1.doc",
			workflowAssetType = "Document",
			workflowTask = "Review");

		Workflow.approveTaskByActions(
			workflowAssetTitle = "Document_1.doc",
			workflowAssetType = "Document",
			workflowTask = "Review");

		DMNavigator.openToFolderInAdmin(
			dmFolderName = "DM Folder Name",
			groupName = "Guest",
			siteURLKey = "guest");

		DMDocument.viewStatus(
			dmDocumentStatus = "Approved",
			dmDocumentTitle = "Document_1.doc");
	}

	@description = "This ensures that a folder can be copied to a workflow-enabled folder."
	@priority = 4
	test CopiedDocumentRespectsGrandparentFolderWorkflow {
		HeadlessSite.addSite(siteName = "Site Name");

		JSONFolder.addFolder(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Destination Folder Name",
			groupName = "Site Name");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "site-name");

		DMFolder.configureWorkflowCP(
			dmFolderName = "DM Destination Folder Name",
			workflowDefinition = "Single Approver",
			workflowRestriction = "Default Workflow");

		JSONFolder.addFolder(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name",
			groupName = "Guest");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			folderName = "DM Folder Name",
			groupName = "Guest",
			mimeType = "application/msword",
			sourceFileName = "Document_1.doc");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMFolder.copy(
			dmFolderName = "DM Folder Name",
			siteName = "Site Name",
			targetFolderName = "DM Destination Folder Name");

		DMNavigator.openToFolderInAdmin(
			dmFolderName = "DM Destination Folder Name",
			groupName = "Site Name",
			siteURLKey = "site-name");

		DMNavigator.gotoFolder(dmFolderName = "DM Folder Name");

		DMDocument.viewStatus(
			dmDocumentStatus = "Pending",
			dmDocumentTitle = "DM Document Title");
	}

}