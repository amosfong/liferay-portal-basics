@component-name = "portal-content-management"
definition {

	property ci.retries.disabled = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Content Management Accessibility";
	property testray.main.component.name = "Tags";

	setUp {
		task ("Set up virtual instance") {
			TestCase.setUpPortalInstance();
		}

		task ("Sign in") {
			User.firstLoginPG();
		}

		task ("Add a site") {
			HeadlessSite.addSite(siteName = "Test Site Name");
		}
	}

	@description = "This is a test for LPS-177675. Accessing via keyboard navigation the tooltip of the back button is Go to Tags when adding or merging Tags."
	@priority = 2
	test ViewBackButtonTooltipForTagsAdminViaKeyboard {
		task ("Given a site administrator adds a tag") {
			Tag.openTagsAdmin(siteURLKey = "test-site-name");

			LexiconEntry.gotoAdd();

			WaitForElementPresent(locator1 = "TextInput#NAME");
		}

		task ("When the site administrator hovers over the back button via keyboard") {
			KeyPress(
				locator1 = "ProductMenu#TOGGLE",
				value1 = "\TAB");

			AssertElementFocused(locator1 = "Icon#BACK");
		}

		task ("Then the site administrator could see the tooltip message") {
			AssertVisible(
				key_content = "Go to Tags",
				locator1 = "Tooltip#FLOATING_TOOLTIP_CONTENT");
		}

		task ("Given a site administrator merges tags") {
			for (var i : list "1,2") {
				JSONAssettag.addTag(
					groupName = "Test Site Name",
					tagName = "tagname${i}");
			}

			Navigator.gotoBack();

			PortletEntry.selectAll();

			ManagementBar.gotoManagementBarEllipsisMenuItem(menuItem = "Merge");

			WaitForElementPresent(
				locator1 = "Select#SELECT_ID",
				selectFieldId = "targetTagName");
		}

		task ("When the site administrator hovers over the back button via keyboard") {
			KeyPress(
				locator1 = "ProductMenu#TOGGLE",
				value1 = "\TAB");

			AssertElementFocused(locator1 = "Icon#BACK");
		}

		task ("Then the site administrator could see the tooltip message") {
			AssertVisible(
				key_content = "Go to Tags",
				locator1 = "Tooltip#FLOATING_TOOLTIP_CONTENT");
		}
	}

}