@component-name = "portal-content-management"
definition {

	property ci.retries.disabled = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Content Management Accessibility";
	property testray.main.component.name = "Web Content Administration";

	setUp {
		task ("Set up virtual instance") {
			TestCase.setUpPortalInstance();
		}

		task ("Sign in") {
			User.firstLoginPG();
		}

		task ("Add a site") {
			HeadlessSite.addSite(siteName = "Test Site Name");
		}
	}

	@description = "This is a test for LPS-197163. When displaying the search results, the file path can be accessed via keyboard."
	@priority = 4
	test NavigateByBreadcrumbInSearchResultsViaKeyboard {
		task ("Given a web content and a folder with a web content") {
			JSONWebcontent.addWebContent(
				content = "Web Content",
				groupName = "Test Site Name",
				title = "Over the Years");

			JSONWebcontent.addFolder(
				folderName = "WC Folder Name",
				groupName = "Test Site Name");

			JSONWebcontent.addWebContent(
				content = "After many years I understood",
				folderName = "WC Folder Name",
				groupName = "Test Site Name",
				title = "Web Content Title");
		}

		task ("When searching for a term on the web content admin page") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "Test Site Name");

			Search.searchCP(searchTerm = "Years");
		}

		task ("Then it is possible to access to the Home folder using the keyboard") {
			KeyPress(
				key_listEntry = "Over the Years",
				locator1 = "LexiconList#LIST_ENTRY_TITLE_LINK",
				value1 = "\TAB");

			KeyPress(
				key_breadcrumbName = "Home",
				key_listEntry = "Over the Years",
				locator1 = "LexiconList#LIST_ENTRY_BREADCRUMB_ITEM",
				value1 = "\ENTER");

			Breadcrumb.viewActiveEntry(breadcrumbName = "Home");

			WebContentAdmin.viewEntryTitle(entry = "WC Folder Name");
		}

		task ("When searching for a term on the web content admin page") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "Test Site Name");

			Search.searchCP(searchTerm = "Years");
		}

		task ("Then it is possible to access to the new folder using the keyboard") {
			KeyPress(
				key_breadcrumbName = "Home",
				key_listEntry = "Web Content Title",
				locator1 = "LexiconList#LIST_ENTRY_BREADCRUMB_ITEM",
				value1 = "\TAB");

			KeyPress(
				key_breadcrumbName = "WC Folder Name",
				key_listEntry = "Web Content Title",
				locator1 = "LexiconList#LIST_ENTRY_BREADCRUMB_ITEM",
				value1 = "\ENTER");

			Breadcrumb.viewActiveEntry(breadcrumbName = "WC Folder Name");

			WebContentAdmin.viewEntryTitle(entry = "Web Content Title");
		}
	}

	@description = "This is a test for LPS-196842. The user can select the Reverse Sort Direction button via the keyboard."
	@priority = 4
	test RevertSortDirectionOfSearchResultsViaKeyboard {
		task ("Given two web contents with different names") {
			JSONWebcontent.addWebContent(
				content = "EMEA Guild",
				groupName = "Test Site Name",
				title = "EMEA Guild");

			JSONWebcontent.addWebContent(
				content = "APAC Guild",
				groupName = "Test Site Name",
				title = "APAC Guild");
		}

		task ("When searching for a term on the web content admin page") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			Search.searchCP(searchTerm = "Guild");

			ManagementBar.setFilterAndOrder(orderBy = "Title");

			AssertTextEquals.assertPartialText(
				key_position = 1,
				locator1 = "LexiconList#LIST_ENTRY_X",
				value1 = "APAC Guild");

			AssertTextEquals.assertPartialText(
				key_position = 2,
				locator1 = "LexiconList#LIST_ENTRY_X",
				value1 = "EMEA Guild");
		}

		task ("The user can reverse sort direction via the keyboard") {
			KeyPress(
				locator1 = "ManagementBar#FILTER",
				value1 = "\TAB");

			KeyPress(
				locator1 = "ManagementBar#ORDER",
				value1 = "\ENTER");

			KeyPress(
				key_menuItem = "Ascending",
				locator1 = "MenuItem#ANY_MENU_ITEM",
				value1 = "\DOWN");

			KeyPress(
				key_menuItem = "Descending",
				locator1 = "MenuItem#ANY_MENU_ITEM",
				value1 = "\ENTER");

			AssertTextEquals.assertPartialText(
				key_position = 1,
				locator1 = "LexiconList#LIST_ENTRY_X",
				value1 = "EMEA Guild");

			AssertTextEquals.assertPartialText(
				key_position = 2,
				locator1 = "LexiconList#LIST_ENTRY_X",
				value1 = "APAC Guild");

			KeyPress(
				locator1 = "ManagementBar#FILTER",
				value1 = "\TAB");

			KeyPress(
				locator1 = "ManagementBar#ORDER",
				value1 = "\ENTER");

			KeyPress(
				key_menuItem = "Descending",
				locator1 = "MenuItem#ANY_MENU_ITEM",
				value1 = "\UP");

			KeyPress(
				key_menuItem = "Ascending",
				locator1 = "MenuItem#ANY_MENU_ITEM",
				value1 = "\ENTER");

			AssertTextEquals.assertPartialText(
				key_position = 1,
				locator1 = "LexiconList#LIST_ENTRY_X",
				value1 = "APAC Guild");

			AssertTextEquals.assertPartialText(
				key_position = 2,
				locator1 = "LexiconList#LIST_ENTRY_X",
				value1 = "EMEA Guild");
		}
	}

}