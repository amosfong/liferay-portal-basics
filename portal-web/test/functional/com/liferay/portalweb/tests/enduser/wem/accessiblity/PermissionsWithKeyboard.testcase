@component-name = "portal-site-management"
definition {

	property ci.retries.disabled = "true";
	property portal.accessibility = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Sites Administration";

	setUp {
		task ("Set up virtual instance") {
			TestCase.setUpPortalInstance();
		}

		task ("Sign in") {
			User.firstLoginPG();
		}
	}

	@description = "This is a test for LPS-195134. Using keyboard navigation it is possible to select or unselect the checkbox Page Rule Builder."
	@priority = 3
	test DefinePageRuleBuilderPermissionViaKeyboard {
		property custom.properties = "feature.flag.LPS-169837=true";

		task ("Given an administrator accesses to the Roles admin") {
			Role.openRolesAdmin();
		}

		task ("When the administrator defines Pages permissions for the Power User role") {
			LexiconEntry.gotoEntry(rowEntry = "Power User");

			Navigator.gotoNavItem(navItem = "Define Permissions");

			Type(
				locator1 = "RolesPermissionsNavigation#SEARCH_FIELD",
				value1 = "Pages");

			Click(
				key_permissionNavHeader = "Site Builder",
				key_permissionNavItem = "Pages",
				locator1 = "RolesPermissionsNavigation#PERMISSIONS_NAVIGATION");

			AssertTextEquals(
				locator1 = "RolesPermissions#PERMISSION_ITEM_HEADER",
				permissionNavItem = "Pages",
				value1 = "Pages");
		}

		task ("Then the administrator could define the Page Rule Builder permission via keyboard") {
			KeyPress(
				key_permissionSheetSubtitle = "Page",
				key_permissionValue = "Delete Discussion",
				locator1 = "RolesPermissions#PERMISSION_CHANGE_SCOPE",
				value1 = "\TAB");

			AssertElementFocused(
				key_permissionSheetSubtitle = "Page",
				key_permissionValue = "Page Rule Builder",
				locator1 = "RolesPermissions#PERMISSION_CHECKBOX");

			KeyPress(
				key_permissionSheetSubtitle = "Page",
				key_permissionValue = "Page Rule Builder",
				locator1 = "RolesPermissions#PERMISSION_CHECKBOX",
				value1 = "\SPACE");

			AssertChecked(
				key_permissionSheetSubtitle = "Page",
				key_permissionValue = "Page Rule Builder",
				locator1 = "RolesPermissions#PERMISSION_CHECKBOX");

			KeyPress(
				key_permissionSheetSubtitle = "Page",
				key_permissionValue = "Page Rule Builder",
				locator1 = "RolesPermissions#PERMISSION_CHECKBOX",
				value1 = "\SPACE");

			AssertNotChecked(
				key_permissionSheetSubtitle = "Page",
				key_permissionValue = "Page Rule Builder",
				locator1 = "RolesPermissions#PERMISSION_CHECKBOX");
		}
	}

}