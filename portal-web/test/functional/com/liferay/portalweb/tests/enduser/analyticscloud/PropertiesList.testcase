@component-name = "portal-analytics-cloud"
definition {

	property analytics.cloud.release = "true";
	property analytics.cloud.upstream = "true";
	property portal.release = "false";
	property portal.upstream = "true";
	property property.group = "analytics.cloud.profile";
	property testray.main.component.name = "Analytics Cloud";

	setUp {
		ACUtils.launchAC();
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		ACUtils.tearDownDXP();

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Site Name");
		}

		ACUtils.tearDownAC();
	}

	@description = "Bug: LRAC-11360 | Automation ID: LRAC-11361 | Test Summary: Paginate property list"
	@priority = 4
	test CanPaginatePropertyList {
		var propertyList = "Property Name 1,Property Name 2,Property Name 3,Property Name 4,Property Name 5,Property Name 6";

		task ("go to property and add 6 properties") {
			ACNavigation.goToSettings();

			for (var n : list ${propertyList}) {
				ACSettings.goToProperties();

				ACProperties.addNewProperty(propertyName = ${n});
			}
		}

		task ("Change the pagination to 4") {
			ACSettings.goToProperties();

			ACUtils.changePagination(itemsPerPage = 4);
		}

		task ("Check first 4 properties appear on first page") {
			var propertyList1 = "Property Name 6,Property Name 5,Property Name 4,Property Name 3";

			ACUtils.assertItemsName(itemList = ${propertyList1});
		}

		task ("Click next page") {
			ACUtils.checkPreviousNextButton(nextButton = "true");
		}

		task ("Check last property appear on second page") {
			var propertyList2 = "Property Name 2,Property Name 1";

			ACUtils.assertItemsName(itemList = ${propertyList2});
		}
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-9119 | Test Summary: Property list loading a lot of values"
	@ignore = "true"
	@priority = 2
	test CanPropertyListLoadingALotOfValues {

		// TODO LRAC-9119 CanPropertyListLoadingALotOfValues pending implementation

	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-9121 | Test Summary: Settings page properties list is searchable"
	@priority = 3
	test CanSearchForPagePropertiesList {
		var propertyList = "Property CanSearchForPagePropertiesList 1,Property CanSearchForPagePropertiesList 2,Property CanSearchForPagePropertiesList 3,Property CanSearchForPagePropertiesList 4";

		task ("go to property and add new properties") {
			ACNavigation.goToSettings();

			for (var n : list ${propertyList}) {
				ACSettings.goToProperties();

				ACProperties.addNewProperty(propertyName = ${n});
			}
		}

		task ("search the properties") {
			ACSettings.goToProperties();

			for (var n : list ${propertyList}) {
				ACUtils.searchBar(searchItem = ${n});

				ACSettings.viewPropertyEntryName(propertyName = ${n});
			}
		}
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-9122 | Test Summary: Search for a property"
	@priority = 5
	test CanSearchForProperty {
		var propertyList = "Property CanSearchForProperty 1,Property CanSearchForProperty 2,Property CanSearchForProperty 3,Property CanSearchForProperty 4";

		task ("go to property and add new properties") {
			ACNavigation.goToSettings();

			for (var n : list ${propertyList}) {
				ACSettings.goToProperties();

				ACProperties.addNewProperty(propertyName = ${n});
			}
		}

		task ("search for a property") {
			ACNavigation.exitSettings();

			for (var n : list ${propertyList}) {
				ACProperties.switchProperty(propertyName = ${n});
			}
		}
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-9117 | Test Summary: User selecting a property from the drop down by 1st time"
	@priority = 3
	test CanUserSelectPropertyFromTheDropDownByFirstTime {
		var propertyName = "Property CanUserSelectPropertyFromTheDropDownByFirstTime 1";

		task ("go to properties and add a new property") {
			ACNavigation.goToSettings();

			ACSettings.goToProperties();

			ACProperties.addNewProperty(propertyName = ${propertyName});
		}

		task ("select new property") {
			ACNavigation.exitSettings();

			ACProperties.switchProperty(propertyName = ${propertyName});
		}
	}

	@description = "Feature ID: LRAC-14082 | Automation ID: LRAC-14648 | Test Summary: Check that a message appears if you don't put text or the expected text in the delete modal"
	@priority = 2
	test CheckMessageAppearsIfNotPutTextOrExpectedTextOnDeleteModal {
		property analytics.cloud.release = "false";
		property portal.upstream = "false";

		var propertyName = "Property CheckMessageAppearsIfNotPutTextOrExpectedTextOnDeleteModal";

		task ("Go to properties page and add new property") {
			ACNavigation.goToSettings();

			ACSettings.goToProperties();

			ACProperties.addNewProperty(propertyName = ${propertyName});
		}

		task ("Check that the created property is present in the list") {
			ACSettings.goToProperties();

			ACUtils.assertItemsName(itemList = ${propertyName});
		}

		task ("Open the property deletion modal") {
			ACUtils.searchBar(searchItem = ${propertyName});

			ACUtils.clickKebab();

			MenuItem.clickNoError(menuItem = "Delete");
		}

		task ("Click on the text field to write the deletion confirmation and then click on another part of the modal to remove the focus of the field") {
			Click(locator1 = "ACSettings#DELETE_CONFIRMATION_INPUT");

			Click(locator1 = "ACSettings#DELETE_CONFIRMATION_MESSAGE");
		}

		task ("Check that a UI feedback appears because the field is empty") {
			AssertTextEquals(
				locator1 = "Message#WARNING_FEEDBACK",
				value1 = "String does not match.");
		}

		task ("Write a different value than expected in the deletion confirmation field") {
			Type(
				locator1 = "ACSettings#DELETE_CONFIRMATION_INPUT",
				value1 = ${propertyName});

			Click(locator1 = "ACSettings#DELETE_CONFIRMATION_MESSAGE");
		}

		task ("Check that a UI feedback appears if the field value is not equal to what was expected") {
			AssertTextEquals(
				locator1 = "Message#WARNING_FEEDBACK",
				value1 = "String does not match.");
		}

		task ("Write the expected value in the delete confirmation field") {
			Type(
				locator1 = "ACSettings#DELETE_CONFIRMATION_INPUT",
				value1 = "Delete ${propertyName}");
		}

		task ("Check that a UI feedback does not appear") {
			AssertElementNotPresent(locator1 = "Message#WARNING_FEEDBACK");
		}
	}

	@description = "Feature ID: LRAC-14082 | Automation ID: LRAC-14649 | Test Summary: Check that a successful alert appears if the property is deleted correctly"
	@priority = 2
	test CheckSuccessfulAlertAppearsIfPropertyDeleted {
		property analytics.cloud.release = "false";
		property portal.upstream = "false";

		var propertyName = "Property CheckSuccessfulAlertAppearsIfPropertyDeleted";

		task ("Go to properties page and add new property") {
			ACNavigation.goToSettings();

			ACSettings.goToProperties();

			ACProperties.addNewProperty(propertyName = ${propertyName});
		}

		task ("Check that the created property is present in the list") {
			ACSettings.goToProperties();

			ACUtils.assertItemsName(itemList = ${propertyName});
		}

		task ("Delete the property use ellipse") {
			ACSettings.deletePropertyByEllipse(propertyName = ${propertyName});
		}

		task ("Check that the success alert with the correct message appears after deletion") {
			Alert.viewSuccessMessageText(successMessage = "Success:1 property has been deleted");
		}
	}

	@description = "Bug: LRAC-10602 | Automation ID: LRAC-11055 | Test Summary: Deleted properties don't appear as an option to sync sites"
	@priority = 2
	test DeletedPropertiesNotAppearAsOptionInSyncSites {
		task ("Go to properties page and add 2 new properties") {
			var propertyList = "DeletedProperty 1,DeletedProperty 2";

			ACNavigation.goToSettings();

			for (var propertyName : list ${propertyList}) {
				ACSettings.goToProperties();

				ACProperties.addNewProperty(propertyName = ${propertyName});
			}
		}

		task ("Delete a property") {
			ACSettings.goToProperties();

			ACSettings.deletePropertyByCheckbox(propertyName = "DeletedProperty 2");
		}

		task ("Copy the token from AC") {
			ACSettings.takeTokenFromAnalyticsCloud();
		}

		task ("Set up instance and sign in DXP") {
			TestCase.setUpPortalInstance();

			ACUtils.launchDXP();
		}

		task ("Connect the DXP to AC") {
			ACDXPSettings.goToInstanceSettingsAC();

			ACDXPSettings.connectAnalyticsCloud();
		}

		task ("Check that the existing properties in the AC appear in the list") {
			ACDXPSettings.viewGenericListItem(itemValueList = "DeletedProperty 1");
		}

		task ("Check that the deleted AC properties do not appear in the list") {
			ACDXPSettings.viewGenericListItemNotPresent(itemValueList = "DeletedProperty 2");
		}

		task ("Disconnect the DXP to AC") {
			ACDXPSettings.disconnectDXPFromAnalyticsCloud();
		}
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-9118 | Test Summary: The setting info (from AC configuration menu) are global and don't depend of property"
	@priority = 3
	test ViewSettingInfoAreGlobalAndDontDependOfProperty {
		task ("go to properties and add 2 new properties") {
			ACNavigation.goToSettings();

			var propertyList = "Property ViewSettingInfoAreGlobalAndDontDependOfProperty 1,Property ViewSettingInfoAreGlobalAndDontDependOfProperty 2";

			for (var n : list ${propertyList}) {
				ACSettings.goToProperties();

				ACProperties.addNewProperty(propertyName = ${n});
			}
		}

		task ("switch between properties and assert the workspace settings is the same for those properties") {
			for (var n : list ${propertyList}) {
				ACNavigation.exitSettings();

				ACProperties.switchProperty(propertyName = ${n});

				ACNavigation.goToSettings();

				ACSettings.goToWorkspace();

				ACWorkspace.viewWorkspaceSettings(
					timezone = "UTC",
					timezoneId = "UTC Coordinated Universal Time (UTC)",
					workspaceName = "FARO-DEV-liferay");
			}
		}
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-9116 | Test Summary: Dropdown transiction between no properties or no site and one or more properties available"
	@priority = 3
	test ViewTheDropdownTransition {
		task ("go to properties page and add 2 new properties") {
			var propertyList = "Property ViewTheDropdownTransition 1,Property ViewTheDropdownTransition 2";

			ACNavigation.goToSettings();

			for (var n : list ${propertyList}) {
				ACSettings.goToProperties();

				ACProperties.addNewProperty(propertyName = ${n});
			}
		}

		task ("assert transition when switching between properties") {
			ACNavigation.exitSettings();

			for (var n : list ${propertyList}) {
				ACProperties.switchProperty(propertyName = ${n});

				ACProperties.assertPropertyHeader(propertyName = ${n});

				ACProperties.assertSitesOverviewPropertyHeader(propertyName = ${n});

				ACProperties.assertCurrentActiveProperty(propertyName = ${n});
			}
		}
	}

}