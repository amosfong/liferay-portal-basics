@component-name = "portal-content-management"
definition {

	property custom.properties = "default.layout.template.id=1_column";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Knowledge Base";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Knowledge Base Display Page");

		JSONLayout.addWidgetToPublicLayout(
			column = 1,
			groupName = "Guest",
			layoutName = "Knowledge Base Display Page",
			widgetName = "Knowledge Base Display");
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Workflow.tearDownSiteWorkflow();

			KBFolder.tearDownCP();

			KBArticle.tearDownCP();

			JSONUser.tearDownNonAdminUsers();

			PagesAdmin.tearDownCP();
		}
	}

	@description = "This case covers LPS-175896. When removing guest's view permission of a kb child article, they can not view the child article on knowledge base display."
	@priority = 4
	test CanNotViewChildArticleAsGuestAfterRemovePermission {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBArticle.addChildArticleViaKBAdmin(
			kbArticleTitle = "Knowledge Base Article Title",
			kbChildArticleContent = "Knowledge Base Article Content Child",
			kbChildArticleTitle = "Knowledge Base Article Title Child 1");

		LexiconEntry.openEntryMenu(rowEntry = "Knowledge Base Article Title");

		MenuItem.click(menuItem = "View Child Articles");

		KBArticle.goToEntryPermissions(kbArticleTitle = "Knowledge Base Article Title Child 1");

		PermissionsInline.addOrRemoveAnyPermissionsFromSpecificRole(
			addOrRemoveViewPermission = "Remove",
			permissionsKeyList = "INLINE_PERMISSIONS_VIEW_CHECKBOX",
			roleTitle = "guest");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBDisplayWidget.viewChildArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Title");

		User.logoutPG();

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBDisplayWidget.viewParentArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Title");

		KBArticle.viewNoPGViaKBDisplay(kbArticleTitle = "Knowledge Base Article Title");
	}

	@description = "This test ensures that users can edit the kb entry permissions by clicking the ellipsis on the kb entry."
	@priority = 5
	test CanViewArticleAsGuest {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBArticle.goToEntryPermissions(kbArticleTitle = "Knowledge Base Article Title");

		PermissionsInline.addOrRemoveAnyPermissionsFromSpecificRole(
			addOrRemoveViewPermission = "remove",
			permissionsKeyList = "INLINE_PERMISSIONS_VIEW_CHECKBOX",
			roleTitle = "guest");

		User.logoutPG();

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewNoPGViaKBDisplay(kbArticleTitle = "Knowledge Base Article Title");
	}

	@priority = 4
	@refactorneeded
	test ViewSuggestionsWithPermissions {
		property custom.properties = "jsonws.web.service.paths.excludes=";

		JSONUser.addUser(
			userEmailAddress = "user1@liferay.com",
			userFirstName = "userfn1",
			userLastName = "userln1",
			userScreenName = "usersn1");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "user1@liferay.com");

		JSONUser.addUser(
			userEmailAddress = "user2@liferay.com",
			userFirstName = "userfn2",
			userLastName = "userln2",
			userScreenName = "usersn2");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "user2@liferay.com");

		JSONUser.addUser(
			userEmailAddress = "poweruser@liferay.com",
			userFirstName = "Power",
			userLastName = "User",
			userScreenName = "poweruser");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "poweruser@liferay.com");

		User.openUsersAdmin();

		User.assignRegularRoleCP(
			roleTitle = "Power User",
			userScreenName = "poweruser");

		User.openUsersAdmin();

		User.assignRegularRoleCP(
			roleTitle = "Administrator",
			userScreenName = "poweruser");

		JSONKnowledgeBase.addkBArticle(
			addGuestPermissions = "true",
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBArticle.openPageTreePanel(section = "Suggestions");

		KBArticle.viewDefaultCP(suggestions = "true");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewPG(
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		KBArticleSuggestion.addPG(
			kbArticleTitle = "Knowledge Base Article Title",
			kbSuggestionBody = "Test Test Knowledge Base Article Suggestion");

		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBArticle.openPageTreePanel(section = "Suggestions");

		KBArticleSuggestion.gotoSuggestionStatusNew(suggestionCount = 1);

		KBArticleSuggestion.viewPG(
			kbSuggestionBody = "Test Test Knowledge Base Article Suggestion",
			kbSuggestionUserName = "Test Test",
			status = "New");

		User.logoutPG();

		User.loginUserPG(
			password = PropsUtil.get("default.admin.password"),
			userEmailAddress = "user1@liferay.com");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewPG(
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		KBArticleSuggestion.addPG(
			kbArticleTitle = "Knowledge Base Article Title",
			kbSuggestionBody = "User 1 Knowledge Base Article Suggestion 1");

		KBArticleSuggestion.addPG(
			kbArticleTitle = "Knowledge Base Article Title",
			kbSuggestionBody = "User 1 Knowledge Base Article Suggestion 2");

		KBArticleSuggestion.viewSubmittedSuggestionCountNoPermissionPG(kbSuggestionCount = 2);

		User.logoutPG();

		User.loginUserPG(
			password = PropsUtil.get("default.admin.password"),
			userEmailAddress = "user2@liferay.com");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewPG(
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		KBArticleSuggestion.addPG(
			kbArticleTitle = "Knowledge Base Article Title",
			kbSuggestionBody = "User 2 Knowledge Base Article Suggestion 1");

		KBArticleSuggestion.viewSubmittedSuggestionCountNoPermissionPG(kbSuggestionCount = 1);

		User.logoutPG();

		User.loginUserPG(
			password = PropsUtil.get("default.admin.password"),
			userEmailAddress = "poweruser@liferay.com");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewPG(
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		AssertTextEquals.assertPartialText(
			locator1 = "KnowledgeBase#LINK_THERE_ARE_SUGGESTIONS",
			value1 = "There are 4 suggestions.");

		KBArticleSuggestion.viewPG(
			kbSuggestionBody = "User 2 Knowledge Base Article Suggestion 1",
			kbSuggestionUserName = "userfn2 userln2",
			status = "New");

		KBArticleSuggestion.viewPG(
			kbSuggestionBody = "User 1 Knowledge Base Article Suggestion 2",
			kbSuggestionUserName = "userfn1 userln1",
			status = "New");

		KBArticleSuggestion.viewPG(
			kbSuggestionBody = "User 1 Knowledge Base Article Suggestion 1",
			kbSuggestionUserName = "userfn1 userln1",
			status = "New");

		KBArticleSuggestion.viewPG(
			kbSuggestionBody = "Test Test Knowledge Base Article Suggestion",
			kbSuggestionUserName = "Test Test",
			status = "New");
	}

}