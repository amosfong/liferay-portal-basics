@component-name = "portal-analytics-cloud"
definition {

	property analytics.cloud.release = "true";
	property analytics.cloud.upstream = "true";
	property custom.properties = "jsonws.web.service.paths.excludes=";
	property portal.release = "false";
	property portal.upstream = "true";
	property property.group = "analytics.cloud.profile";
	property testray.main.component.name = "Analytics Cloud";

	setUp {
		task ("Copy the token from AC") {
			ACUtils.launchAC();

			ACSettings.takeTokenFromAnalyticsCloud();
		}

		task ("Set up instance and sign in DXP") {
			TestCase.setUpPortalInstance();

			ACUtils.launchDXP();
		}

		task ("Add a new site and add a web content in this site") {
			ACUtils.addSiteAndPage();

			ACUtils.createWCAndAddToPage();
		}
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		ACUtils.tearDownDXP();

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Site Name");
		}

		ACUtils.tearDownAC();
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-8456 | Test Summary: Web content appears on card shows the pages that the web content appears on"
	@priority = 5
	test AssertAppearsOnListShowsPagesWebContentExists {
		ACDXPSettings.connectDXPtoAnalyticsCloud(siteName = "Site Name");

		task ("Go to the site page") {
			ACUtils.navigateToSitePage(
				actionType = "View WC",
				pageName = "AC Page",
				siteName = "Site Name");
		}

		task ("Close sessions") {
			ACUtils.closeAllSessionsAndWait();
		}

		task ("Switch to new property in AC and go to WC tab") {
			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = ${assignedPropertyName});

			ACNavigation.goToAssets();

			ACNavigation.switchTab(tabName = "Web Content");
		}

		task ("Go to the WC overview") {
			ACTimeFilter.clickTimeFilterButton();

			ACTimeFilter.setLast24Hours();

			ACNavigation.openItem(itemName = "Web Content AC Title");
		}

		task ("Assert appears on metric") {
			var portalURL = PropsUtil.get("portal.url");
			var dataSourceName = StringUtil.extractFirst(${assignedPropertyName}, " ");

			ACAssets.viewAssetAppearsOnMetric(
				canonicalURL = "${portalURL}/web/site-name",
				cardTitle = "Asset Appears On",
				metricTitle = "Views",
				pageName = "AC Page - Site Name - ${dataSourceName}");
		}
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-8446 | Test Summary: Assert 180 day time filter in Assets"
	@ignore = "true"
	@priority = 3
	test AssertAssetsPageTimeFilter180Days {

		// AC Refactor: the test needs data from a past date
		// AC Refactor ticket: LRAC-8860

		task ("Connect DXP to Analytics Cloud and get the property name") {
			ACDXPSettings.connectDXPtoAnalyticsCloud(siteName = "Site Name");
		}

		task ("Go to Site Page") {
			ACUtils.navigateToSitePage(
				pageName = "AC Page",
				siteName = "Site Name");
		}

		task ("Navigate to AC workspace") {
			ACUtils.closeAllSessionsAndWait();

			ACUtils.launchAC();
		}

		task ("Navigate to Web Content tab in Assets") {
			ACProperties.switchProperty(propertyName = ${assignedPropertyName});

			ACNavigation.goToAssets();

			ACNavigation.switchTab(tabName = "Web Content");
		}

		task ("Set time filter to 24hs and Assert WC presence") {
			ACTimeFilter.clickTimeFilterButton();

			ACTimeFilter.setLast24Hours();

			ACUtils.viewNameList(nameList = "Web Content AC Title");
		}

		task ("Set time filter use 180Days") {
			ACTimeFilter.clickTimeFilterButton();

			ACTimeFilter.setLast180Days();
		}

		// Task ("Assert not present the Web Content when time filter use 180 Days")
		// It is not possible to perform the assert because this test needs data from the past

		task ("Assert result returns") {
			ACTimeFilter.assertCurrentTimeFilterRange(timeFilterRange = "Last 180 days");
		}
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-8448 | Test Summary: Web content audience card shows expected amount of known and anonymous individuals"
	@priority = 5
	test AudienceCardShowsExpectedAmountKnowAndAnonymousIndividualsInWC {
		var usernameList = "ac,liferay";
		var emailList = "ac@liferay.com,liferay@liferay.com";

		for (var name : list ${usernameList}) {
			JSONUser.addUser(
				userEmailAddress = "${name}@liferay.com",
				userFirstName = ${name},
				userLastName = ${name},
				userScreenName = ${name});
		}

		ACDXPSettings.connectDXPtoAnalyticsCloud(siteName = "Site Name");

		ACUtils.navigateToSitePageWithUser(
			actionType = "View WC",
			documentTitleList = "1,2,3,4",
			emailList = ${emailList},
			pageName = "AC Page",
			siteName = "Site Name");

		ACUtils.closeAllSessionsAndWait();

		User.logoutPG();

		ACUtils.clearLocalStorage();

		ACUtils.navigateToSitePage(
			actionType = "View WC",
			documentTitleList = "1,2,3,4",
			pageName = "AC Page",
			siteName = "Site Name");

		ACUtils.closeAllSessionsAndWait();

		ACUtils.launchAC();

		ACProperties.switchProperty(propertyName = ${assignedPropertyName});

		ACNavigation.goToAssets();

		ACNavigation.switchTab(tabName = "Web Content");

		ACTimeFilter.clickTimeFilterButton();

		ACTimeFilter.setLast24Hours();

		ACNavigation.openItem(itemName = "Web Content AC Title");

		ACIndividualsDashboard.goToOverviewTab();

		ACNavigation.viewActiveSwitchTab(tabName = "Overview");

		ACUtils.assertAudienceCard(
			index = 1,
			percentageList = "66.67%,33.33%");
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-8449 | Test Summary: Web content audience card shows expected amount of segmented and unsegmented individuals"
	@priority = 5
	test AudienceCardShowsExpectedAmountSegmentedAndUnsegmentedInWC {
		property test.run.type = "single";

		var usernameList = "ac,liferay,dxp";
		var emailList = "ac@liferay.com,liferay@liferay.com,dxp@liferay.com";

		task ("Create users and sync") {
			for (var name : list ${usernameList}) {
				JSONUser.addUser(
					userEmailAddress = "${name}@liferay.com",
					userFirstName = ${name},
					userLastName = ${name},
					userScreenName = ${name});
			}

			ACDXPSettings.connectDXPtoAnalyticsCloud(siteName = "Site Name");
		}

		task ("Go to page and get data for unsegmented individuals") {
			ACUtils.navigateToSitePageWithUser(
				actionType = "View WC",
				documentTitleList = "1,2,3,4",
				emailList = ${emailList},
				pageName = "AC Page",
				siteName = "Site Name");
		}

		task ("Close all sessions") {
			ACUtils.closeAllSessionsAndWait();
		}

		task ("Go to AC and create segment for the user") {
			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = ${assignedPropertyName});

			ACNavigation.goToSegments();

			ACSegments.createStaticSegment();

			ACUtils.setItemName(itemName = "Static Segment Test");

			ACSegments.addStaticMember(tableMember = "ac ac");

			ACSegments.saveSegment();
		}

		task ("Go to Assets > Go to Web Content") {
			ACNavigation.goToAssets();

			ACNavigation.switchTab(tabName = "Web Content");

			ACTimeFilter.clickTimeFilterButton();

			ACTimeFilter.setLast24Hours();

			ACNavigation.openItem(itemName = "Web Content AC Title");
		}

		task ("Assert views number of segmented and unsegmented users") {
			ACUtils.assertAudienceCard(
				index = 2,
				percentageList = "66.67%,33.33%");

			ACUtils.assertViewerSegmentsCard(segmentNameList = "Static Segment Test");
		}
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-8444 | Test Summary: Custom Range time filter in Web Content assets page"
	@ignore = "true"
	@priority = 4
	test CanAssertWebContentTimeFilterCustom {

		// AC Refactor: It is not possible to perform the assert because this test needs data from the past.
		// AC Refactor ticket: LRAC-8817

		task ("Connect DXP to Analytics Cloud and get the property name") {
			ACDXPSettings.connectDXPtoAnalyticsCloud(siteName = "Site Name");
		}

		task ("Go to Site Page") {
			ACUtils.navigateToSitePage(
				pageName = "AC Page",
				siteName = "Site Name");
		}

		task ("Navigate to AC workspace") {
			ACUtils.closeAllSessionsAndWait();

			ACUtils.launchAC();
		}

		task ("Navigate to Web Content tab in Assets") {
			ACProperties.switchProperty(propertyName = ${assignedPropertyName});

			ACNavigation.goToAssets();

			ACNavigation.switchTab(tabName = "Web Content");
		}

		task ("Set time filter to 24hs and Assert WC presence") {
			ACTimeFilter.clickTimeFilterButton();

			ACTimeFilter.setLast24Hours();

			ACUtils.viewNameList(nameList = "Web Content AC Title");
		}

		task ("Set time filter use Custom Range") {
			ACTimeFilter.setCustomRange(
				endDay = 13,
				endMonth = "July",
				endYear = 2021,
				startDay = 1,
				startMonth = "July",
				startYear = 2021);
		}

		task ("Assert result returns") {
			ACTimeFilter.assertCurrentTimeFilterRange(timeFilterRange = "Jul 1, 2021 - Jul 13, 2021");
		}

		task ("Assert result returns the Custom Range") {

			// Use a macro to assert time filter return the right data
			// Create a locator that can be used later

			ACUtils.viewNameList(nameList = "Web Content AC Title Custom Range");
		}
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-8130 | Test Summary: Click a web content in the web content list navigates to the web content overview page"
	@priority = 5
	test CanNavigateToWebContentOverviewPage {
		property test.run.type = "single";

		ACDXPSettings.connectDXPtoAnalyticsCloud(siteName = "Site Name");

		JSONUser.addUser(
			userEmailAddress = "ac@liferay.com",
			userFirstName = "ac",
			userLastName = "ac",
			userScreenName = "ac");

		ACDXPSettings.syncNewContactData();

		User.logoutAndLoginPG(
			userLoginEmailAddress = "ac@liferay.com",
			userLoginFullName = "ac ac");

		ACUtils.navigateToSitePage(
			actionType = "View WC",
			pageName = "AC Page",
			siteName = "Site Name");

		ACUtils.closeAllSessionsAndWait();

		ACUtils.launchAC();

		ACProperties.switchProperty(propertyName = ${assignedPropertyName});

		ACNavigation.goToAssets();

		ACNavigation.switchTab(tabName = "Web Content");

		ACTimeFilter.clickTimeFilterButton();

		ACTimeFilter.setLast24Hours();

		ACNavigation.openItem(itemName = "Web Content AC Title");

		ACIndividualsDashboard.goToOverviewTab();

		ACNavigation.viewActiveSwitchTab(tabName = "Overview");

		for (var cardTitle : list "Visitors Behavior,Audience,Views by Location,Views by Technology,Asset Appears On") {
			ACCards.viewCards(cardTitle = ${cardTitle});
		}

		ACAssets.viewVisitorsBehaviorMetric(
			metricTitle = "Views",
			metricValue = 1);
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-8441 | Test Summary: Web content list is searchable"
	@priority = 5
	test CanSearchWebContentList {
		property test.run.type = "single";

		var assetNameList = "Web Content AC Title,Web Content AC Title 2,Web Content AC Title 3";

		task ("Add pages and add web content to earch of the page") {
			for (var layoutName : list "AC Page 2,AC Page 3") {
				ACUtils.addPage(layoutName = ${layoutName});
			}

			for (var n : list "2,3") {
				ACUtils.createWCAndAddToPage(
					layoutName = "AC Page ${n}",
					webContentTitle = "Web Content AC Title ${n}");
			}
		}

		task ("Get site assigned property name") {
			ACDXPSettings.connectDXPtoAnalyticsCloud(siteName = "Site Name");
		}

		task ("Add a new user in DXP") {
			JSONUser.addUser(
				userEmailAddress = "ac@liferay.com",
				userFirstName = "ac",
				userLastName = "ac",
				userScreenName = "ac");
		}

		task ("Sync the Contact Data") {
			ACDXPSettings.syncNewContactData();
		}

		task ("Sign in with the new user to visit the site pages") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "ac@liferay.com",
				userLoginFullName = "ac ac");

			for (var pageName : list "AC Page,AC Page 2,AC Page 3") {
				ACUtils.navigateToSitePage(
					actionType = "View WC",
					pageName = ${pageName},
					siteName = "Site Name");
			}
		}

		task ("Switch to new property in AC") {
			ACUtils.closeAllSessionsAndWait();

			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = ${assignedPropertyName});
		}

		task ("Go to Web Content assets") {
			ACNavigation.goToAssets();

			ACNavigation.switchTab(tabName = "Web Content");
		}

		task ("Choose Last 24 Hours") {
			ACTimeFilter.clickTimeFilterButton();

			ACTimeFilter.setLast24Hours();
		}

		task ("Search the Web Contents") {
			ACUtils.searchBar(searchItem = "Web Content AC Title");

			ACUtils.viewNameList(nameList = ${assetNameList});

			ACUtils.searchBar(searchItem = 2);

			ACUtils.viewNameList(nameList = "Web Content AC Title 2");

			ACUtils.searchBar(searchItem = 3);

			ACUtils.viewNameList(nameList = "Web Content AC Title 3");
		}
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-8131 | Test Summary: Web content known individuals shows which individuals interact with the web content"
	@priority = 5
	test KnownIndividualsShowsWhichIndividualsInteractWithWebContent {
		property test.run.type = "single";

		var nameList = "ac ac,liferay liferay,dxp dxp";
		var usernameList = "ac,liferay,dxp";
		var emailList = "ac@liferay.com,liferay@liferay.com,dxp@liferay.com";

		ACDXPSettings.connectDXPtoAnalyticsCloud(siteName = "Site Name");

		for (var name : list ${usernameList}) {
			JSONUser.addUser(
				userEmailAddress = "${name}@liferay.com",
				userFirstName = ${name},
				userLastName = ${name},
				userScreenName = ${name});
		}

		ACDXPSettings.syncNewContactData();

		ACUtils.navigateToSitePageWithUser(
			emailList = ${emailList},
			pageName = "AC Page",
			siteName = "Site Name");

		JSONUser.addUser(
			userEmailAddress = "userac@liferay.com",
			userFirstName = "userac",
			userLastName = "userac",
			userScreenName = "userac");

		ACUtils.navigateToSitePageWithUser(
			emailList = "userac@liferay.com",
			pageName = "AC Page",
			siteName = "Site Name");

		ACUtils.closeAllSessionsAndWait();

		ACUtils.launchAC();

		ACProperties.switchProperty(propertyName = ${assignedPropertyName});

		ACNavigation.goToAssets();

		ACNavigation.switchTab(tabName = "Web Content");

		ACTimeFilter.clickTimeFilterButton();

		ACTimeFilter.setLast24Hours();

		ACNavigation.openItem(itemName = "Web Content AC Title");

		ACIndividualsDashboard.goToKnownIndividualsTab();

		ACNavigation.viewActiveSwitchTab(tabName = "Known Individuals");

		ACTimeFilter.clickTimeFilterButton();

		ACTimeFilter.setLast24Hours();

		ACUtils.viewNameList(nameList = ${nameList});

		AssertElementNotPresent(
			key_userName = "userac userac",
			locator1 = "ACSegments#SEGMENT_MEMBERS");
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-8125 | Test Summary: Web content list shows all web content in a list"
	@priority = 5
	test ViewAllWebContentShownInAssetList {
		property test.run.type = "single";

		var assetNameList = "Web Content AC Title,Web Content AC Title 2,Web Content AC Title 3";

		for (var layoutName : list "AC Page 2,AC Page 3") {
			ACUtils.addPage(layoutName = ${layoutName});
		}

		for (var n : list "2,3") {
			ACUtils.createWCAndAddToPage(
				layoutName = "AC Page ${n}",
				webContentTitle = "Web Content AC Title ${n}");
		}

		ACDXPSettings.connectDXPtoAnalyticsCloud(siteName = "Site Name");

		JSONUser.addUser(
			userEmailAddress = "ac@liferay.com",
			userFirstName = "ac",
			userLastName = "ac",
			userScreenName = "ac");

		ACDXPSettings.syncNewContactData();

		User.logoutAndLoginPG(
			userLoginEmailAddress = "ac@liferay.com",
			userLoginFullName = "ac ac");

		for (var pageName : list "AC Page,AC Page 2,AC Page 3") {
			ACUtils.navigateToSitePage(
				actionType = "View WC",
				documentTitleList = "1,2,3,4",
				pageName = ${pageName},
				siteName = "Site Name");
		}

		ACUtils.closeAllSessionsAndWait();

		ACUtils.launchAC();

		ACProperties.switchProperty(propertyName = ${assignedPropertyName});

		ACNavigation.goToAssets();

		ACNavigation.switchTab(tabName = "Web Content");

		ACTimeFilter.clickTimeFilterButton();

		ACTimeFilter.setLast24Hours();

		ACUtils.viewNameList(nameList = ${assetNameList});
	}

	@description = "Web content visitor behavior card shows expected amount of views"
	@priority = 5
	test VisitorBehaviorCardShowsExpectedAmountOfViewsInWC {
		property test.run.type = "single";

		task ("Connect DXP to Analytics Cloud and get the property name") {
			ACDXPSettings.connectDXPtoAnalyticsCloud(siteName = "Site Name");
		}

		task ("Visit page 3 times on DXP") {
			ACUtils.navigateToSitePage(
				actionType = "View WC",
				documentTitleList = "1,2,3",
				pageName = "AC Page",
				siteName = "Site Name");

			ACUtils.closeAllSessionsAndWait();
		}

		task ("Navigate to AC property") {
			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = ${assignedPropertyName});
		}

		task ("Navigate to Web Content tab in Assets") {
			ACNavigation.goToAssets();

			ACNavigation.switchTab(tabName = "Web Content");
		}

		task ("Navigate into Web Content entry") {
			ACTimeFilter.clickTimeFilterButton();

			ACTimeFilter.setLast24Hours();

			ACNavigation.openItem(itemName = "Web Content AC Title");
		}

		task ("Confirm that web content events appear in AC") {
			ACCards.viewIfVisitorsBehaviorMetricIsBetweenCorrectRange(
				maxMetricValue = 4,
				metricTitle = "Views",
				minMetricValue = 1);
		}
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-8454 | Test Summary: Web content submissions by technology card shows views by expected browser"
	@priority = 5
	test WebContentTechnologyCardShowsViewsByExpectedBrowser {
		property test.run.type = "single";

		ACDXPSettings.connectDXPtoAnalyticsCloud(siteName = "Site Name");

		JSONUser.addUser(
			userEmailAddress = "ac@liferay.com",
			userFirstName = "ac",
			userLastName = "ac",
			userScreenName = "ac");

		ACDXPSettings.syncNewContactData();

		User.logoutAndLoginPG(
			userLoginEmailAddress = "ac@liferay.com",
			userLoginFullName = "ac ac");

		ACUtils.navigateToSitePage(
			actionType = "View WC",
			pageName = "AC Page",
			siteName = "Site Name");

		ACUtils.closeAllSessionsAndWait();

		ACUtils.launchAC();

		ACProperties.switchProperty(propertyName = ${assignedPropertyName});

		ACNavigation.goToAssets();

		ACNavigation.switchTab(tabName = "Web Content");

		ACTimeFilter.clickTimeFilterButton();

		ACTimeFilter.setLast24Hours();

		ACNavigation.openItem(itemName = "Web Content AC Title");

		ACIndividualsDashboard.goToOverviewTab();

		ACNavigation.viewActiveSwitchTab(tabName = "Overview");

		ACNavigation.switchTabInCard(tabName = "Browsers");

		ACAssets.viewViewsByTechnologyTabActive(
			cardTitle = "Views by Technology",
			tabName = "Browsers");

		ACAssets.viewTechnologyBrowsersMetric(
			browserMetricValue = "Chrome",
			cardTitle = "Views by Technology",
			cssValue = "rgb(75, 155, 255)",
			percentageMetricValue = "100%");
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-8453 | Test Summary: Web content submissions by technology card shows views by expected device"
	@priority = 5
	test WebContentTechnologyCardShowsViewsByExpectedDevice {
		ACDXPSettings.connectDXPtoAnalyticsCloud(siteName = "Site Name");

		JSONUser.addUser(
			userEmailAddress = "ac@liferay.com",
			userFirstName = "ac",
			userLastName = "ac",
			userScreenName = "ac");

		ACDXPSettings.syncNewContactData();

		User.logoutAndLoginPG(
			userLoginEmailAddress = "ac@liferay.com",
			userLoginFullName = "ac ac");

		ACUtils.navigateToSitePage(
			actionType = "View WC",
			documentTitleList = "1,2",
			pageName = "AC Page",
			siteName = "Site Name");

		ACUtils.closeAllSessionsAndWait();

		ACUtils.launchAC();

		ACProperties.switchProperty(propertyName = ${assignedPropertyName});

		ACNavigation.goToAssets();

		ACNavigation.switchTab(tabName = "Web Content");

		ACTimeFilter.clickTimeFilterButton();

		ACTimeFilter.setLast24Hours();

		ACNavigation.openItem(itemName = "Web Content AC Title");

		ACIndividualsDashboard.goToOverviewTab();

		ACNavigation.viewActiveSwitchTab(tabName = "Overview");

		ACAssets.viewTechnologyDeviceViewsAndPercentage(
			actionValue = "2 Views",
			cardTitle = "Views by Technology",
			deviceName = "Desktop",
			percentageValue = "100%");
	}

}