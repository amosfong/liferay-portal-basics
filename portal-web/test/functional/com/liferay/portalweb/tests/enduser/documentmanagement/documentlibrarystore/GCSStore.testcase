@component-name = "portal-core-infra"
definition {

	property gcs.store.enabled = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property test.assert.warning.exceptions = "true";
	property testray.main.component.name = "Document Library Stores";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		HeadlessSite.addSite(siteName = "Test Site Name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if (${testPortalInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");

			User.logoutPG();
		}
	}

	@description = "This is a use case for LPS-129271."
	@priority = 4
	test AssertNoErrorMessageAfterExportAndImportSiteLAR {
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		var bucketId = PropsUtil.get("gcs.bucket.id");
		var companyId = JSONCompany.getCompanyId();
		var groupId = JSONGroupAPI._getGroupIdByName(
			groupName = "Test Site Name",
			site = "true");

		AntCommands.runCommand("build-test-gcs-store.xml", "assert-no-document-in-bucket -Dcompany.id=${companyId} -Dgcs.bucket.id=${bucketId} -Dgroup.id=${groupId}");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			groupName = "Test Site Name",
			mimeType = "image/jpeg",
			sourceFileName = "Document_1.jpg");

		AntCommands.runCommand("build-test-gcs-store.xml", "assert-document-in-bucket -Dcompany.id=${companyId} -Dgcs.bucket.id=${bucketId} -Dgroup.id=${groupId}");

		LAR.exportSiteCP(siteScopeName = "Test Site Name");

		var larFileName = LAR.getLarFileName();

		LAR.downloadLar();

		HeadlessSite.addSite(siteName = "Test Site Name 1");

		for (var layoutUtilityPageName : list "404 Error,500 Error") {
			JSONLayoututilitypage.deleteLayoutUtilityPage(
				groupName = "Test Site Name 1",
				layoutUtilityPageName = ${layoutUtilityPageName});
		}

		var groupId1 = JSONGroupAPI._getGroupIdByName(
			groupName = "Test Site Name 1",
			site = "true");

		AntCommands.runCommand("build-test-gcs-store.xml", "assert-no-document-in-bucket -Dcompany.id=${companyId} -Dgcs.bucket.id=${bucketId} -Dgroup.id=${groupId1}");

		LAR.importSiteCP(
			larFileName = ${larFileName},
			siteName = "Test Site Name 1");

		AntCommands.runCommand("build-test-gcs-store.xml", "assert-document-in-bucket -Dcompany.id=${companyId} -Dgcs.bucket.id=${bucketId} -Dgroup.id=${groupId1}");

		AssertConsoleTextNotPresent(value1 = "java.io.IOException");
	}

	@description = "Verify a data migration can be executed from the default store to Google Cloud store."
	@priority = 3
	test GCSStoreDataMigration {
		property store.migration.test = "true";
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		task ("Given a document is added to the default store") {
			var bucketId = PropsUtil.get("gcs.bucket.id");
			var companyId = JSONCompany.getCompanyId();
			var groupId = JSONGroupAPI._getGroupIdByName(
				groupName = "Test Site Name",
				site = "true");

			JSONDocument.addFileWithUploadedFile(
				dmDocumentDescription = "DM Document Description",
				dmDocumentTitle = "DM Document Title",
				groupName = "Test Site Name",
				mimeType = "text/plain",
				sourceFileName = "Document_1.txt");
		}

		task ("And the user cannot migrate data to GCS store without setting up a connection configuration") {
			ServerAdministration.openServerAdmin();

			Navigator.gotoNavItem(navItem = "Data Migration");

			AssertElementNotPresent(
				key_fieldLabel = "dl.store.impl",
				key_value = "com.liferay.portal.store.gcs.GCSStore",
				locator1 = "Select#GENERIC_SELECT_VALUE");
		}

		task ("When the user adds the configuration") {
			AntCommands.runCommand("build-test.xml", "prepare-gcs-store-configuration -Dgcs.bucket.id=${bucketId}");

			while ((IsElementNotPresent(locator1 = "Select#SELECT_VALUE_ID", option = "com.liferay.portal.store.gcs.GCSStore", selectFieldId = "dl-store-impl")) && (maxIterations = "2")) {
				OSGiConfig.waitForOSGiConfig();

				Refresh();
			}
		}

		task ("Then the user can now migrate data over to the GCS bucket") {
			SelectField.select(
				selectFieldLabel = "dl.store.impl",
				selectFieldValue = "com.liferay.portal.store.gcs.GCSStore");
		}

		task ("When the user executes the data migration") {
			Button.click(button = "Execute");

			AssertTextPresent(
				locator1 = "//body",
				value1 = "Executing com.liferay.document.library.internal.convert.document.library.DocumentLibraryConvertProcess");

			WaitForElementNotPresent(
				locator1 = "//body",
				value1 = "Migrating images in 16 adaptive media image entries");

			WaitForConsoleTextPresent(value1 = "Please set dl.store.impl in your portal-ext.properties to use com.liferay.portal.store.gcs.GCSStore");

			WaitForConsoleTextPresent(value1 = "Finished conversion for com.liferay.document.library.internal.convert.document.library.DocumentLibraryConvertProcess");
		}

		task ("Then the document is migrated over from the default store to the GCS bucket") {
			AntCommands.runCommand("build-test-gcs-store.xml", "assert-document-in-bucket -Dcompany.id=${companyId} -Dgcs.bucket.id=${bucketId} -Dgroup.id=${groupId}");
		}

		task ("And the user can configure the portal to use GCS store without issue") {
			Portlet.shutdownServer();

			var newProperty = "dl.store.impl=com.liferay.portal.store.gcs.GCSStore";

			AntCommands.runCommand("build-test.xml", "portal-ext-properties-update -Dadd.new.properties=true -Dupdate.properties=${newProperty}");

			Portlet.startServer(deleteLiferayHome = "false");

			User.firstLoginPG();

			Smoke.viewWelcomeContentPage();
		}
	}

	@priority = 5
	test GCSStoreSmoke {
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		var bucketId = PropsUtil.get("gcs.bucket.id");
		var companyId = JSONCompany.getCompanyId();
		var groupId = JSONGroupAPI._getGroupIdByName(
			groupName = "Test Site Name",
			site = "true");

		AntCommands.runCommand("build-test-gcs-store.xml", "assert-no-document-in-bucket -Dcompany.id=${companyId} -Dgcs.bucket.id=${bucketId} -Dgroup.id=${groupId}");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "test-site-name");

		DMDocument.addCP(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.txt",
			dmDocumentTitle = "DM Document Title");

		AntCommands.runCommand("build-test-gcs-store.xml", "assert-document-in-bucket -Dcompany.id=${companyId} -Dgcs.bucket.id=${bucketId} -Dgroup.id=${groupId}");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "test-site-name");

		DMDocument.deleteCP(dmDocumentTitle = "DM Document Title");

		RecycleBin.openRecycleBinAdmin(siteURLKey = "test-site-name");

		RecycleBin.emptyCP();

		AntCommands.runCommand("build-test-gcs-store.xml", "assert-no-document-in-bucket -Dcompany.id=${companyId} -Dgcs.bucket.id=${bucketId} -Dgroup.id=${groupId}");
	}

}