@component-name = "portal-commerce"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Users and Organizations";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONUser.tearDownNonAdminUsers();

			Site.tearDownCP();

			Organization.tearDownCP(orgName = "Suborganization*");

			Organization.tearDownCP();
		}
	}

	@description = "This test adds five suborganizations to an organization"
	@priority = 4
	test AddMultipleSuborganizations {
		property testray.component.names = "Training";

		JSONOrganization.addOrganization(organizationName = "Organization Name 1");

		var count = 1;

		while (${count} != 5) {
			JSONOrganization.addOrganization(
				organizationName = "Suborganization Name ${count}",
				parentOrganizationName = "Organization Name 1");

			User.openUsersAdmin();

			UsersAndOrganizationsNavigator.gotoOrganizations();

			Organization.gotoCP(orgName = "Organization Name 1");

			Organization.viewParentOrganizationCP(
				orgName = "Suborganization Name ${count}",
				parentOrgName = "Organization Name 1",
				subOrgName = "Suborganization Name ${count}");

			var count = ${count} + 1;
		}

		Organization.openOrganizationsAdmin();

		Organization.searchCP(
			orgName = "Suborganization Name 1",
			searchTerm = "Suborganization");

		Organization.searchCP(
			orgName = "Suborganization Name 2",
			searchTerm = "Suborganization");
	}

	@priority = 4
	test AddOrganization {
		property testray.component.names = "Training";

		JSONOrganization.addOrganization(organizationName = "Organization Name");

		Organization.openOrganizationsAdmin();

		Organization.searchCP(
			orgName = "Organization Name",
			searchTerm = "Organization Name");

		Organization.viewCP(
			orgName = "Organization Name",
			orgType = "Organization");

		LexiconEntry.openEntryMenu(rowEntry = "Organization Name");

		MenuItem.click(menuItem = "Edit");

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Edit Organization Name");

		AssertTextEquals(
			locator1 = "TextInput#NAME",
			value1 = "Organization Name");

		AssertTextEquals.assertValue(
			key_text = "Type",
			locator1 = "TextInput#ANY",
			value1 = "Organization");

		LexiconEntry.viewNoEntry(rowEntry = "Organization Name");

		ProductMenu.viewNoSite(site = "Organization Name");

		Site.viewNoMySitesViaMySitesPG(siteName = "Organization Name");
	}

	@priority = 4
	test AddOrganizationComment {
		Organization.openOrganizationsAdmin();

		Organization.addCP(orgName = "Organization Name");

		Organization.addComment(orgComment = "This is a test comment!");

		AssertTextEquals(
			locator1 = "TextArea#COMMENTS",
			value1 = "This is a test comment!");
	}

	@priority = 4
	test AddOrganizationName50Characters {
		Organization.openOrganizationsAdmin();

		Organization.addCP(orgName = "Organization Staging Organization Web Content Display");

		Organization.openOrganizationsAdmin();

		Organization.searchCP(
			orgName = "Organization Staging Organization Web Content Display",
			searchTerm = "Organization Staging Organization Web Content Display");

		Organization.viewCP(
			orgName = "Organization Staging Organization Web Content Display",
			orgType = "Organization");
	}

	@priority = 4
	test AddOrganizationNameDuplicate {
		JSONOrganization.addOrganization(organizationName = "Organization Name");

		Organization.openOrganizationsAdmin();

		Organization.searchCP(
			orgName = "Organization Name",
			searchTerm = "Organization Name");

		Organization.viewCP(
			orgName = "Organization Name",
			orgType = "Organization");

		Organization.openOrganizationsAdmin();

		LexiconEntry.gotoAdd();

		Type(
			locator1 = "TextInput#NAME",
			value1 = "Organization Name");

		Button.clickSave();

		Alert.viewRequestFailedToComplete();

		AssertTextEquals.assertPartialText(
			locator1 = "Message#ERROR_1",
			value1 = "The organization name is already taken.");
	}

	@priority = 4
	test AddOrganizations {
		Organization.openOrganizationsAdmin();

		Organization.addCP(orgName = "Organization Name 1");

		Organization.openOrganizationsAdmin();

		Organization.searchCP(
			orgName = "Organization Name 1",
			searchTerm = "Organization Name 1");

		Organization.viewCP(
			orgName = "Organization Name 1",
			orgType = "Organization");

		Organization.openOrganizationsAdmin();

		Organization.addCP(orgName = "Organization Name 2");

		Organization.openOrganizationsAdmin();

		Organization.searchCP(
			orgName = "Organization Name 2",
			searchTerm = "Organization Name 2");

		Organization.viewCP(
			orgName = "Organization Name 2",
			orgType = "Organization");

		Organization.openOrganizationsAdmin();

		Organization.addCP(orgName = "Organization Name 3");

		Organization.openOrganizationsAdmin();

		Organization.searchCP(
			orgName = "Organization Name 3",
			searchTerm = "Organization Name 3");

		Organization.viewCP(
			orgName = "Organization Name 3",
			orgType = "Organization");
	}

	@priority = 4
	test AddOrganizationSitePrivatePage {
		property testray.component.names = "Training";

		task ("Enable private pages") {
			PagesAdmin.enablePrivatePages();
		}

		var portalURL = PropsUtil.get("portal.url");

		JSONOrganization.addOrganization(organizationName = "Organization Name");

		Organization.openOrganizationsAdmin();

		Organization.addSiteCP(orgName = "Organization Name");

		Site.openSitesAdmin();

		Site.viewCP(
			siteMember = "Organization",
			siteName = "Organization Name",
			siteType = "Private");

		PagesAdmin.openPagesAdmin(siteURLKey = "Organization Name");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		PagesAdmin.addPrivatePage(pageName = "Private Page");

		Navigator.gotoSpecificURLPage(
			pageName = "Private Page",
			url = "${portalURL}/group/organization-name/private-page");

		PagesAdmin.viewPageItemPresent(
			pageName = "Private Page",
			privacy = "Private Pages");
	}

	@priority = 4
	test AddOrganizationSitePublicPage {
		var portalURL = PropsUtil.get("portal.url");

		JSONOrganization.addOrganization(organizationName = "Organization Name");

		Organization.openOrganizationsAdmin();

		Organization.addSiteCP(orgName = "Organization Name");

		PagesAdmin.openPagesAdmin(siteURLKey = "Organization Name");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		PagesAdmin.addPage(pageName = "Public Page");

		Navigator.gotoSpecificURLPage(
			pageName = "Public Page",
			url = "${portalURL}/web/organization-name/public-page");

		PagesAdmin.viewPageItemPresent(pageName = "Public Page");
	}

	@priority = 4
	test AddSuborganization {
		property portal.acceptance = "true";
		property testray.component.names = "Training";

		JSONOrganization.addOrganization(organizationName = "Organization Name");

		Organization.openOrganizationsAdmin();

		Organization.searchCP(
			orgName = "Organization Name",
			searchTerm = "Organization Name");

		Organization.viewCP(
			orgName = "Organization Name",
			orgType = "Organization");

		Organization.openOrganizationsAdmin();

		Organization.addSuborganizationCP(
			orgName = "Organization Name",
			suborgName = "Suborganization Name");

		Organization.openOrganizationsAdmin();

		Organization.gotoCP(orgName = "Organization Name");

		Breadcrumb.viewActiveEntry(breadcrumbName = "Organization Name");

		Organization.viewSuborganizationCP(
			orgName = "Organization Name",
			orgType = "Organization",
			suborgName = "Suborganization Name",
			suborgType = "Organization");
	}

	@priority = 4
	test AssignImportedUserGroupToSuborganizationSites {
		property liferay.training.dependency.jars = "01-liferay-fundamentals/modules/02-manage-user-accounts/exercise-src/com.liferay.training.contentloader.jar";
		property portal.release = "false";
		property portal.upstream = "false";
		property testray.component.names = "Training";

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		PagesAdmin.addPage(pageName = "Mock LDAP Page");

		Navigator.gotoPage(pageName = "Mock LDAP Page");

		Portlet.addPG(
			pageName = "Mock LDAP Page",
			portletName = "Mock LDAP");

		Navigator.gotoPage(pageName = "Mock LDAP Page");

		MockLDAP.selectJsonFile(
			fileName = "space-groups.json",
			tabName = "User Groups");

		UserGroup.openUserGroupsAdmin();

		Pagination.changePagination(itemsPerPage = 20);

		Pagination.viewResults(results = "Showing 1 to 11 of 11 entries.");

		Organization.openOrganizationsAdmin();

		LAR.importPortlet(
			larFileName = "organizations-7.0.lar",
			uploadFrom = "dependencies");

		for (var suborgName : list "Accounting,Business Administration,College of Agricultural Science,College of Business,College of Engineering") {
			Organization.openOrganizationsAdmin();

			Organization.addSiteCP(
				orgName = ${suborgName},
				orgType = "Organization");

			var suborgName = StringUtil.replace(${suborgName}, " ", "-");

			var suborgName = StringUtil.lowerCase(${suborgName});

			Site.openSiteSettingsAdmin(siteURLKey = ${suborgName});

			Site.manageMembershipOptions(allowManualMembershipManagement = "true");
		}

		Navigator.openSiteURL(siteName = "College of Agricultural Science");

		ProductMenu.gotoPortlet(
			category = "People",
			portlet = "Memberships");

		Site.assignUserGroupAsMemberCP(userGroupName = "Agricultural Science");

		Navigator.openSiteURL(siteName = "College of Business");

		ProductMenu.gotoPortlet(
			category = "People",
			portlet = "Memberships");

		for (var userGroupName : list "Business: Accounting,Business: Business Administration") {
			Site.assignUserGroupAsMemberCP(userGroupName = ${userGroupName});
		}

		for (var siteName : list "Accounting,Business Administration") {
			Navigator.openSiteURL(siteName = ${siteName});

			ProductMenu.gotoPortlet(
				category = "People",
				portlet = "Memberships");

			Site.assignUserGroupAsMemberCP(userGroupName = "Business: ${siteName}");
		}
	}

	@description = "Assign multiple members to an organization and searches for them under assignees"
	@priority = 4
	test AssignMultipleUsersToOrganization {
		property testray.component.names = "Training";

		var count = 1;

		while (${count} != 6) {
			JSONUser.addUser(
				userEmailAddress = "userea${count}@liferay.com",
				userFirstName = "userfn${count}",
				userLastName = "userln${count}",
				userScreenName = "usersn${count}");

			var count = ${count} + 1;
		}

		User.openUsersAdmin();

		for (var userScreenName : list "usersn1,usersn2,usersn3,usersn4,usersn5") {
			var key_userScreenName = ${userScreenName};

			AssertTextEquals(
				locator1 = "UsersAndOrganizations#USER_TABLE_SCREEN_NAME",
				value1 = ${userScreenName});
		}

		JSONOrganization.addOrganization(organizationName = "Organization Name 1");

		Organization.openOrganizationsAdmin();

		Organization.assignUser(
			orgName = "Organization Name 1",
			userFirstName = "userfn1",
			userLastName = "userln1",
			userName = "userfn1 userln1",
			userScreenName = "usersn1");

		JSONOrganization.assignUserToOrganization(
			organizationName = "Organization Name 1",
			userEmailAddress = "userea3@liferay.com");

		JSONOrganization.assignUserToOrganization(
			organizationName = "Organization Name 1",
			userEmailAddress = "userea5@liferay.com");

		Organization.openOrganizationsAdmin();

		Organization.gotoCP(orgName = "Organization Name 1");

		for (var userName : list "userfn1 userln1,userfn3 userln3,userfn5 userln5") {
			AssertTextEquals(
				key_userScreenName = ${userName},
				locator1 = "UsersAndOrganizations#USER_TABLE_NAME",
				value1 = ${userName});
		}

		for (var userName : list "userfn2 userln2,userfn4 userln4") {
			AssertElementNotPresent(
				key_userScreenName = ${userName},
				locator1 = "UsersAndOrganizations#USER_TABLE_NAME");
		}

		Organization.openOrganizationsAdmin();

		Organization.gotoCP(orgName = "Organization Name 1");

		Organization.viewUser(userName = "userfn1 userln1");

		Organization.viewUser(userName = "userfn3 userln3");

		Organization.viewUser(userName = "userfn5 userln5");
	}

	@description = "Add a site to an organization, assign user to that organization and assert it displays in My Dashboard"
	@priority = 4
	test AssignUserToOrganizationWithSite {
		User.openUsersAdmin();

		JSONOrganization.addOrganization(
			organizationName = "Organization Name",
			organizationSite = "true");

		Site.openSitesAdmin();

		LexiconEntry.viewEntryName(rowEntry = "Organization Name");

		ProductMenu.viewNoSite(site = "Organization Name");

		UserBar.gotoDropdownItem(dropdownItem = "My Dashboard");

		Site.viewNoMySitesViaMySitesPG(siteName = "Organization Name");

		JSONOrganization.assignUserToOrganization(
			organizationName = "Organization Name",
			userEmailAddress = "test@liferay.com");

		UserBar.gotoDropdownItem(dropdownItem = "My Dashboard");

		LexiconEntry.viewEntryName(rowEntry = "Organization Name");

		Organization.openOrganizationsAdmin();

		Organization.searchCP(
			orgName = "Organization Name",
			searchTerm = "Organization");
	}

	@description = "This adds a global category to an organization."
	@priority = 4
	test CanAddGlobalCategoryToOrganization {
		JSONCategory.addVocabulary(
			groupName = "Global",
			title = "Vocabulary Name 1");

		JSONCategory.addCategory(
			groupName = "Global",
			title = "Category Name 1",
			vocabularyName = "Vocabulary Name 1");

		JSONOrganization.addOrganization(organizationName = "Organization Name");

		Organization.openOrganizationsAdmin();

		Organization.gotoEdit(orgName = "Organization Name");

		Click(
			key_vocabularyName = "Vocabulary Name 1",
			locator1 = "AssetCategorization#CATEGORY_SELECT");

		AssetCategorization.selectCategories(categoryNameList = "Category Name 1");

		PortletEntry.save();

		Organization.openOrganizationsAdmin();

		Organization.gotoEdit(orgName = "Organization Name");

		AssetCategorization.viewCategory(categoryName = "Category Name 1");

		AssetCategorization.viewVocabulary(vocabularyName = "Vocabulary Name 1");
	}

	@description = "Resolved bug automation for LPS-140712. This test asserts that a user can be added to an organization through the UI and displays a success message"
	@priority = 4
	test CanAddOrganizationUser {
		task ("Given an organization") {
			JSONOrganization.addOrganization(
				organizationName = "Organization Name",
				organizationSite = "true");
		}

		task ("When a user is added to that organization through the UI") {
			Organization.openOrganizationsAdmin();

			Organization.addUser(
				organizationName = "Organization Name",
				userEmailAddress = "userea@liferay.com",
				userFirstName = "userfn",
				userLastName = "userln",
				userScreenName = "usersn");
		}

		task ("Then a success message should display and the user should be listed under the organization") {
			Alert.viewSuccessMessageText(successMessage = "The user was created successfully. You can now add additional information about the user.");

			User.viewUserInformation(
				userEmailAddress = "userea@liferay.com",
				userFirstName = "userfn",
				userLastName = "userln",
				userScreenName = "usersn");

			Organization.openOrganizationsAdmin();

			Organization.gotoCP(orgName = "Organization Name");

			LexiconEntry.viewEntryContent(
				rowAny = "userfn userln",
				rowEntry = "User");
		}
	}

	@description = "This test automates LPS-141217 and asserts that a user can still be added to an organization after initially using an invalid email"
	@priority = 4
	test CanAddOrganizationUserAfterFirstUsingInvalidEmail {
		task ("Given the creation of a user under an organization is attempted using an invalid email") {
			JSONOrganization.addOrganization(organizationName = "Organization Name");

			Organization.openOrganizationsAdmin();

			Organization.addUser(
				organizationName = "Organization Name",
				userEmailAddress = "postmaster@liferay.com",
				userFirstName = "userfn",
				userLastName = "userln",
				userScreenName = "usersn");

			Alert.viewErrorMessage(errorMessage = "Error:Please enter a valid email address.");
		}

		task ("When the email is changed to a valid email address and the user is saved") {
			Type(
				locator1 = "UsersAndOrganizationsEditUser#DETAILS_EMAIL_ADDRESS_FIELD",
				value1 = "postmaster1@liferay.com");

			Button.clickSave();
		}

		task ("Then the user should be created and associated under the organization") {
			Organization.openOrganizationsAdmin();

			Organization.gotoCP(orgName = "Organization Name");

			LexiconEntry.viewEntryContent(
				rowAny = "userfn userln",
				rowEntry = "User");
		}
	}

	@description = "This test asserts that an organization cannot be deleted if there is a suborganization associated with it."
	@priority = 4
	test CannotDeleteAnOrganizationWithSuborganization {
		JSONOrganization.addOrganization(organizationName = "Organization Name");

		JSONOrganization.addOrganization(
			organizationName = "Suborganization Name 1",
			parentOrganizationName = "Organization Name");

		Organization.openOrganizationsAdmin();

		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Delete",
			rowEntry = "Organization Name");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		AssertTextEquals.assertPartialText(
			locator1 = "Message#ERROR_1",
			value1 = "You cannot delete organizations that have suborganizations or users.");

		Organization.openOrganizationsAdmin();

		Organization.removeSuborganizationCP(
			orgName = "Organization Name",
			subOrgName = "Suborganization Name 1");

		Organization.openOrganizationsAdmin();

		Organization.delete(orgName = "Organization Name");
	}

	@description = "This sets Order By Name and asserts descending and ascending order"
	@priority = 4
	test CanOrderOrganizationsByName {
		for (var organizationNumber : list "1,2,3,4,5,6,7,8") {
			JSONOrganization.addOrganization(organizationName = "Organization ${organizationNumber}");
		}

		Organization.openOrganizationsAdmin();

		Pagination.changePagination(itemsPerPage = 4);

		ManagementBar.setFilterAndOrder(orderBy = "Name");

		ManagementBar.sortAssets(sortDirection = "Descending");

		for (var organizationNumber : list "5,6,7,8") {
			LexiconEntry.viewEntryName(rowEntry = "Organization ${organizationNumber}");
		}

		for (var organizationNumber : list "1,2,3,4") {
			LexiconEntry.viewNoEntry(rowEntry = "Organization ${organizationNumber}");
		}

		ManagementBar.sortAssets(sortDirection = "Ascending");

		for (var organizationNumber : list "1,2,3,4") {
			LexiconEntry.viewEntryName(rowEntry = "Organization ${organizationNumber}");
		}

		for (var organizationNumber : list "5,6,7,8") {
			LexiconEntry.viewNoEntry(rowEntry = "Organization ${organizationNumber}");
		}
	}

	@description = "Use case for LPS-198960.Organization users and sub organizations are not mixed across pages using the interactor button."
	@priority = 4
	test CanViewListedUsersAndSubOrganizationsNotMixedAcrossPages {
		task ("Given an Organization with two Users") {
			JSONOrganization.addOrganization(organizationName = "Organization for Users");

			for (var user : list "A,B") {
				JSONUser.addUser(
					userEmailAddress = "${user}@liferay.com",
					userFirstName = "${user}_fn",
					userLastName = "${user}_sn",
					userScreenName = "${user}_sn");

				JSONOrganization.assignUserToOrganization(
					organizationName = "Organization for Users",
					userEmailAddress = "${user}@liferay.com");
			}
		}

		task ("And 4 Sub Organizations within the same organization") {
			for (var count : list "A,B,C,D") {
				JSONOrganization.addOrganization(
					organizationName = "${count} Suborganization",
					parentOrganizationName = "Organization for Users");
			}
		}

		task ("And set the page interator button to show 4 Entries per Page") {
			ApplicationsMenu.gotoPortlet(
				category = "Users",
				panel = "Control Panel",
				portlet = "Users and Organizations");

			UsersAndOrganizationsNavigator.gotoOrganizations();

			Organization.gotoCP(orgName = "Organization for Users");

			Pagination.changePagination(itemsPerPage = 4);
		}

		task ("Then Users and Sub Organizations are not mixed across pages") {
			AssertElementPresent(
				key_userName = "Organizations",
				locator1 = "Account#USER_TABLE_NAME");

			AssertElementNotPresent(
				key_userName = "Users",
				locator1 = "Account#USER_TABLE_NAME");

			Click(locator1 = "Pagination#NEXT_LINK");

			AssertElementPresent(
				key_userName = "Users",
				locator1 = "Account#USER_TABLE_NAME");

			AssertElementNotPresent(
				key_userName = "Organizations",
				locator1 = "Account#USER_TABLE_NAME");
		}
	}

	@description = "This is a use case for LPS-123120."
	@priority = 4
	test ClearSearchResults {
		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONOrganization.addOrganization(organizationName = "Organization Name");

		User.openUsersAdmin();

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Permissions",
			rowEntry = "usersn");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		Search.searchCP(searchTerm = "User");

		Search.clearSearchResult();

		Organization.openOrganizationsAdmin();

		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Assign Organization Roles",
			rowEntry = "Organization Name");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		Search.searchCP(searchTerm = "Organization Administrator");

		Search.clearSearchResult();

		LexiconTable.clickTableEntryTitle(tableEntry = "Organization Administrator");

		Navigator.gotoNavItem(navItem = "Available");

		Search.searchCP(searchTerm = "test");

		Search.clearSearchResult();

		Organization.openOrganizationsAdmin();

		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Assign Users",
			rowEntry = "Organization Name");

		AssertElementPresent.assertVisible(locator1 = "Button#DONE");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		Search.searchCP(searchTerm = "test");

		Search.clearSearchResult();
	}

	@priority = 4
	test DeleteOrganization {
		property portal.acceptance = "true";

		User.openUsersAdmin();

		UsersAndOrganizationsNavigator.gotoOrganizations();

		Organization.addCP(orgName = "Organization Name");

		Organization.openOrganizationsAdmin();

		Organization.delete(orgName = "Organization Name");

		Organization.openOrganizationsAdmin();

		AssertTextEquals.assertPartialText(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No organizations were found.");
	}

	@priority = 4
	test DeleteOrganizationSite {
		JSONOrganization.addOrganization(organizationName = "Organization Name");

		Organization.openOrganizationsAdmin();

		Organization.addSiteCP(orgName = "Organization Name");

		Site.openSitesAdmin();

		Site.viewCP(
			siteMember = "Organization",
			siteName = "Organization Name",
			siteType = "Private");

		Site.openSitesAdmin();

		Site.deleteCP(siteName = "Organization Name");
	}

	@priority = 4
	test EditOrganizationSite {
		property portal.acceptance = "true";
		property testray.component.names = "Training";

		JSONOrganization.addOrganization(organizationName = "Organization Name");

		Organization.openOrganizationsAdmin();

		Organization.addSiteCP(orgName = "Organization Name");

		Site.openSitesAdmin();

		Site.viewCP(
			siteMember = "Organization",
			siteName = "Organization Name",
			siteType = "Private");

		Organization.openOrganizationsAdmin();

		Organization.edit(
			orgName = "Organization Name",
			orgNameEdit = "Organization Name Edit");

		AssertTextEquals(
			locator1 = "TextInput#NAME",
			value1 = "Organization Name Edit");

		Organization.openOrganizationsAdmin();

		Organization.searchCP(
			orgName = "Organization Name Edit",
			searchTerm = "Organization Name Edit");

		Organization.viewCP(
			orgName = "Organization Name Edit",
			orgType = "Organization");
	}

	@priority = 4
	test EditOrganizationSiteTeam {
		User.openUsersAdmin();

		User.addCP(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONOrganization.addOrganization(organizationName = "Organization Name");

		Organization.openOrganizationsAdmin();

		Organization.addSiteCP(orgName = "Organization Name");

		Site.openSiteSettingsAdmin(siteURLKey = "organization-name");

		Site.manageMembershipOptions(allowManualMembershipManagement = "true");

		Organization.openOrganizationsAdmin();

		Organization.assignUser(
			orgName = "Organization Name",
			userFirstName = "userfn",
			userLastName = "userln",
			userName = "userfn userln",
			userScreenName = "usersn");

		PagesAdmin.openPagesAdmin(siteURLKey = "Organization Name");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		PagesAdmin.addPage(pageName = "Public Page");

		PagesAdmin.openPagesAdmin(siteURLKey = "Organization Name");

		ProductMenu.gotoPortlet(
			category = "People",
			portlet = "Teams");

		Team.addCP(
			siteName = "Organization Name",
			teamDescription = "Site Team Description",
			teamName = "Site Team Name");

		Team.editCP(
			siteName = "Organization Name",
			teamDescriptionEdit = "Site Team Description Edit",
			teamName = "Site Team Name",
			teamNameEdit = "Site Team Name Edit");

		Team.viewCP(
			siteName = "Organization Name",
			teamDescription = "Site Team Description Edit",
			teamName = "Site Team Name Edit");
	}

	@description = "This is a use case for LPS-75084."
	@priority = 4
	test NoPermissionUserCannotAddOrganizationViaURL {
		var portalURL = PropsUtil.get("portal.url");

		JSONOrganization.addOrganization(organizationName = "Organization for Test");

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		User.openUsersAdmin();

		User.editPasswordCP(
			userEmailAddress = "userea@liferay.com",
			userScreenName = "usersn");

		User.logoutAndLoginPG(
			userLoginEmailAddress = "userea@liferay.com",
			userLoginFullName = "userfn userln");

		Navigator.openSpecificURL(url = "${portalURL}/group/control_panel/manage?p_p_id=com_liferay_users_admin_web_portlet_MyOrganizationsPortlet&p_p_lifecycle=0&p_p_state=maximized&p_p_mode=view&_com_liferay_users_admin_web_portlet_MyOrganizationsPortlet_mvcRenderCommandName=%2Fusers_admin%2Fedit_organization&_com_liferay_users_admin_web_portlet_MyOrganizationsPortlet_type=organization");

		Alert.viewErrorMessage(errorMessage = "You do not have the required permissions.");

		Navigator.openSpecificURL(url = "${portalURL}/group/control_panel/manage?p_p_id=com_liferay_users_admin_web_portlet_MyOrganizationsPortlet&p_p_lifecycle=0&p_p_state=maximized&p_p_mode=view&_com_liferay_users_admin_web_portlet_MyOrganizationsPortlet_mvcPath=%2Fedit_organization.jsp&_com_liferay_users_admin_web_portlet_MyOrganizationsPortlet_type=organization");

		Alert.viewErrorMessage(errorMessage = "You do not have the required permissions.");

		Navigator.openSpecificURL(url = "${portalURL}/group/control_panel/manage?p_p_id=com_liferay_users_admin_web_portlet_MyOrganizationsPortlet&p_p_lifecycle=0&p_p_state=maximized&p_p_mode=view&_com_liferay_users_admin_web_portlet_UsersAdminPortlet_screenNavigationCategoryKey=organizations&_com_liferay_users_admin_web_portlet_UsersAdminPortlet_usersListView=flat-organizations");

		AssertTextEquals.assertPartialText(
			locator1 = "Message#INFO",
			value1 = "You do not belong to an organization and are not allowed to view other organizations.");
	}

	@priority = 4
	test OrganizationAdministratorActivate {
		property testray.component.names = "Training";

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "VIEW_CONTROL_PANEL",
			resourceName = 90,
			roleTitle = "User");

		JSONOrganization.addOrganization(organizationName = "Organization Name");

		JSONUser.addUser(
			userEmailAddress = "userea1@liferay.com",
			userFirstName = "userfn1",
			userLastName = "userln1",
			userScreenName = "usersn1");

		User.openUsersAdmin();

		User.editPasswordCP(
			userEmailAddress = "userea1@liferay.com",
			userScreenName = "usersn1");

		Organization.openOrganizationsAdmin();

		Organization.assignUser(
			orgName = "Organization Name",
			userFirstName = "userfn1",
			userLastName = "userln1",
			userName = "userfn1 userln1",
			userScreenName = "usersn1");

		Organization.openOrganizationsAdmin();

		Organization.assignOrgRole(
			orgName = "Organization Name",
			roleTitle = "Organization Administrator",
			userFirstName = "userfn1",
			userLastName = "userln1",
			userScreenName = "usersn1");

		Navigator.openURL();

		JSONUser.addUser(
			userEmailAddress = "userea2@liferay.com",
			userFirstName = "userfn2",
			userLastName = "userln2",
			userScreenName = "usersn2");

		Organization.openOrganizationsAdmin();

		Organization.assignUser(
			orgName = "Organization Name",
			userFirstName = "userfn2",
			userLastName = "userln2",
			userName = "userfn2 userln2",
			userScreenName = "usersn2");

		User.logoutPG();

		User.loginUserPG(userEmailAddress = "userea1@liferay.com");

		Organization.openOrganizationsAdmin();

		Organization.gotoCP(orgName = "Organization Name");

		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Deactivate",
			rowEntry = "userfn2");

		AssertConfirm(value1 = "Are you sure you want to deactivate this?");

		Alert.viewSuccessMessage();

		AssertTextEquals(
			key_columnName = "status",
			key_tableEntry = "userfn2",
			locator1 = "LexiconTable#TABLE_ENTRY_INFO",
			value1 = "Inactive");

		Organization.openOrganizationsAdmin();

		Organization.gotoCP(orgName = "Organization Name");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Activate",
			rowEntry = "userfn2");

		Alert.viewSuccessMessage();

		AssertTextEquals(
			key_columnName = "status",
			key_tableEntry = "userfn2",
			locator1 = "LexiconTable#TABLE_ENTRY_INFO",
			value1 = "Active");

		User.logoutPG();

		User.loginPG(
			password = PropsUtil.get("default.admin.password"),
			userEmailAddress = "test@liferay.com");
	}

	@priority = 4
	test OrganizationDropDownItems {
		for (var siteName : list "Site Name A,Site Name B,Site Name C,Site Name D,Site Name E,Site Name F,Site Name G,Site Name H") {
			HeadlessSite.addSite(siteName = ${siteName});
		}

		Site.openSitesAdmin();

		for (var siteName : list "Site Name A,Site Name B,Site Name C,Site Name D,Site Name E,Site Name F,Site Name G,Site Name H") {
			var key_siteName = ${siteName};

			AssertTextEquals.assertPartialText(
				locator1 = "Sites#SITE_TABLE_NAME",
				value1 = ${siteName});
		}

		ApplicationsMenu.gotoSiteViaViewAll(site = "Site Name H");

		JSONOrganization.addOrganization(organizationName = "Site Name M");

		JSONOrganization.assignUserToOrganization(
			organizationName = "Site Name M",
			userEmailAddress = "test@liferay.com");

		UserBar.gotoDropdownItem(dropdownItem = "Shared Content");

		ProductMenuHelper.openProductMenu();

		AssertTextPresent(
			locator1 = "ProductMenu#PRODUCT_MENU_PANEL_SITE_ADMINISTRATION_SITE_NAME",
			value1 = "Site Name H");
	}

	@priority = 4
	test OrganizationSiteDefaultUserAssociations {
		Site.openSitesAdmin();

		Site.addCP(
			siteName = "Site Name Alpha",
			siteType = "Blank");

		JSONOrganization.addOrganization(organizationName = "Organization Site Bravo");

		Organization.openOrganizationsAdmin();

		Organization.addSiteCP(orgName = "Organization Site Bravo");

		PortalSettings.editConfigurationUsersDefaultUserAssociationsCP(siteName = "Site Name Alpha");

		PortalSettings.editConfigurationUsersDefaultUserAssociationsCP(orgName = "Organization Site Bravo");

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		User.openUsersAdmin();

		UserNavigator.gotoUser(userScreenName = "usersn");

		User.viewMemberships(
			assetType = "Sites",
			assigneeName = "Site Name Alpha");

		User.openUsersAdmin();

		UserNavigator.gotoUser(userScreenName = "usersn");

		User.viewMemberships(
			assetType = "Sites",
			assigneeName = "Organization Site Bravo");
	}

	@priority = 4
	test OrganizationUserManagement {
		property portal.acceptance = "true";
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		SearchAdministration.executeReindex();

		JSONOrganization.addOrganization(organizationName = "Organization Name");

		Organization.openOrganizationsAdmin();

		Organization.gotoEdit(orgName = "Organization Name");

		Navigator.gotoNavTab(navTab = "Contact");

		Organization.addContactInformation(
			addressType = "Billing",
			city = "Diamond Bar",
			contactType = "add-address-link",
			country = "United States",
			postalCode = 91765,
			region = "California",
			street1 = "1400 Montefino Ave");

		Navigator.gotoNavNested(navNested = "Contact Information");

		Organization.addContactInformation(
			contactType = "add-phone-number-link",
			number = "1-877-543-3729");

		Organization.addContactInformation(
			address = "testuser@liferay.com",
			contactType = "add-email-address-link");

		Organization.addContactInformation(
			contactType = "add-website-link",
			url = "http://www.liferay.com");

		Navigator.gotoNavNested(navNested = "Opening Hours");

		Organization.addContactInformation(
			contactType = "add-opening-hours-link",
			mondayClose = "18:00",
			mondayOpen = "08:00");

		Organization.openOrganizationsAdmin();

		Organization.gotoEdit(orgName = "Organization Name");

		Organization.addComment(orgComment = "This is a test organization.");

		AssertTextEquals(
			locator1 = "TextArea#COMMENTS",
			value1 = "This is a test organization.");

		Navigator.gotoNavNested(navNested = "Security Questions");

		Organization.addSecurityQuestionsCP(
			localizedLanguage = "ja-JP",
			localizedSecurityQuestions = "Nihongo",
			orgName = "Organization Name",
			securityQuestions = "This is just a test");
	}

	@priority = 4
	test SearchOrganization {
		JSONOrganization.addOrganization(organizationName = "Organization Name");

		Organization.openOrganizationsAdmin();

		Organization.searchCP(
			orgName = "Organization Name",
			searchTerm = "Organization Name");

		Search.clearSearchResult();
	}

	@description = "This test adds tags to organizations and asserts proper organization displays when tag is searched"
	@priority = 4
	test SearchTags {
		JSONOrganization.addOrganization(organizationName = "Organization Name 1");

		Organization.openOrganizationsAdmin();

		Organization.gotoEdit(orgName = "Organization Name 1");

		AssetCategorization.addTag(tagName = "alpha");

		PortletEntry.save();

		AssertTextEquals(
			key_tagName = "alpha",
			locator1 = "AssetCategorization#TAG",
			value1 = "alpha");

		JSONOrganization.addOrganization(organizationName = "Organization Name 2");

		Organization.openOrganizationsAdmin();

		Organization.gotoEdit(orgName = "Organization Name 2");

		AssetCategorization.addTag(tagName = "bravo");

		PortletEntry.save();

		AssertTextEquals(
			key_tagName = "bravo",
			locator1 = "AssetCategorization#TAG",
			value1 = "bravo");

		Organization.openOrganizationsAdmin();

		Organization.searchCP(
			orgName = "Organization Name 1",
			searchTerm = "alpha");

		Organization.searchCP(
			orgName = "Organization Name 2",
			searchTerm = "bravo");

		Organization.searchCP(
			orgName = "Organization Name 1",
			searchTerm = 1);

		Organization.searchCP(
			orgName = "Organization Name 2",
			searchTerm = 2);
	}

	@priority = 4
	test SearchUsersInOrganization {
		User.openUsersAdmin();

		User.addCP(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONOrganization.addOrganization(organizationName = "Organization Name");

		Organization.openOrganizationsAdmin();

		Organization.assignUser(
			orgName = "Organization Name",
			userFirstName = "userfn",
			userLastName = "userln",
			userName = "userfn userln",
			userScreenName = "usersn");

		Organization.openOrganizationsAdmin();

		Organization.gotoCP(orgName = "Organization Name");

		User.searchCP(searchTerm = "usersn");

		Organization.viewUser(userName = "userfn userln");

		User.searchCP(searchTerm = "test");

		AssertTextEquals.assertPartialText(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No results were found.");
	}

	@priority = 4
	test ViewBack {
		Organization.openOrganizationsAdmin();

		Organization.addCP(orgName = "Organization Name");

		AssertElementPresent(locator1 = "Icon#BACK");

		takeScreenshot();
	}

	@priority = 4
	test ViewContentInCustomOrganizationTypeSite {
		property custom.properties = "jsonws.web.service.paths.excludes=";
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		var portalURL = PropsUtil.get("portal.url");
		var siteName = TestCase.getSiteName(siteName = ${siteName});

		JSONRole.addOrganizationRole(roleTitle = "Canada Orgrole");

		JSONRole.addOrganizationRole(roleTitle = "United States Orgrole");

		for (var userNum : list "1,2") {
			JSONUser.addUser(
				userEmailAddress = "userea${userNum}@liferay.com",
				userFirstName = "userfn${userNum}",
				userLastName = "userln${userNum}",
				userScreenName = "usersn${userNum}");

			JSONUser.setFirstPassword(
				agreeToTermsAndAnswerReminderQuery = "true",
				requireReset = "false",
				userEmailAddress = "userea${userNum}@liferay.com");
		}

		SiteTemplates.addCP(siteTemplateName = "Site Template Name");

		SiteTemplates.addSitePageViaSiteTemplateCP(
			pageName = "MB Page",
			siteTemplateName = "Site Template Name");

		SiteTemplatesNavigator.gotoSiteTemplatesPage(
			pageName = "MB Page",
			siteTemplateName = "Site Template Name");

		Portlet.addPG(
			defaultURL = "false",
			portletName = "Message Boards");

		JSONOrganization.addOrganization(organizationName = "North America");

		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "System Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "Users",
			configurationName = "Organization Type",
			configurationScope = "System Scope");

		Button.clickAdd();

		for (var settingName : list "Name,Children Types") {
			SystemSettings.editTextAreaSetting(
				settingName = ${settingName},
				settingValue = "Location");
		}

		for (var fieldName : list "countryEnabled,rootable") {
			FormFields.enableCheckbox(fieldName = ${fieldName});
		}

		SystemSettings.saveConfiguration();

		JSONOrganization.addOrganization(
			organizationName = "United States",
			organizationType = "Location");

		JSONOrganization.addOrganization(
			organizationName = "Canada",
			organizationType = "Location");

		Organization.openOrganizationsAdmin();

		Organization.addSiteCP(
			orgName = "United States",
			siteTemplateName = "Site Template Name");

		Organization.openOrganizationsAdmin();

		Organization.addSiteCP(
			orgName = "Canada",
			siteTemplateName = "Site Template Name");

		JSONOrganization.assignUserToOrganization(
			organizationName = "Canada",
			userEmailAddress = "userea1@liferay.com");

		JSONOrganization.assignUserToOrganization(
			organizationName = "United States",
			userEmailAddress = "userea2@liferay.com");

		User.openUsersAdmin();

		User.assignOrgRoleCP(
			roleTitle = "Canada Orgrole",
			userScreenName = "usersn1");

		Navigator.gotoBack();

		User.assignOrgRoleCP(
			roleTitle = "United States Orgrole",
			userScreenName = "usersn2");

		Navigator.openSpecificURL(url = "${portalURL}/web/canada/mb-page");

		MessageboardsPortlet.gotoInlinePermissionsPG();

		PermissionsInline.addOrRemoveViewPermissionFromAllRoles(
			addOrRemoveViewPermission = "Remove",
			roleList = "Guest,Site Member");

		Navigator.openSpecificURL(url = "${portalURL}/web/canada/mb-page");

		MessageboardsPortlet.gotoInlinePermissionsPG();

		PermissionsInline.addOrRemoveViewPermissionFromAllRoles(
			addOrRemoveViewPermission = "Add",
			roleList = "Canada Orgrole");

		Navigator.openSpecificURL(url = "${portalURL}/web/canada/mb-page");

		MessageboardsThread.addPG(
			threadBody = "Canada Message Body",
			threadSubject = "Canada Message Subject");

		Navigator.openSpecificURL(url = "${portalURL}/web/united-states/mb-page");

		MessageboardsPortlet.gotoInlinePermissionsPG();

		PermissionsInline.addOrRemoveViewPermissionFromAllRoles(
			addOrRemoveViewPermission = "Remove",
			roleList = "Guest,Site Member");

		Navigator.openSpecificURL(url = "${portalURL}/web/united-states/mb-page");

		MessageboardsPortlet.gotoInlinePermissionsPG();

		PermissionsInline.addOrRemoveViewPermissionFromAllRoles(
			addOrRemoveViewPermission = "Add",
			roleList = "United States Orgrole");

		Navigator.openSpecificURL(url = "${portalURL}/web/united-states/mb-page");

		MessageboardsThread.addPG(
			threadBody = "United States Message Body",
			threadSubject = "United States Message Subject");

		User.logoutPG();

		User.loginPG(
			password = PropsUtil.get("default.admin.password"),
			userEmailAddress = "userea1@liferay.com");

		Navigator.openSpecificURL(url = "${portalURL}/web/united-states/mb-page");

		MessageboardsThread.viewDoNotHaveRequiredPermissionsPG(threadSubject = "United States Message Subject");

		User.logoutPG();

		User.loginPG(
			password = PropsUtil.get("default.admin.password"),
			userEmailAddress = "userea2@liferay.com");

		Navigator.openSpecificURL(url = "${portalURL}/web/canada/mb-page");

		MessageboardsThread.viewDoNotHaveRequiredPermissionsPG(threadSubject = "Canada Message Subject");
	}

	@priority = 4
	test ViewOrganizationSiteViaSitesDirectory {
		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		PagesAdmin.addPage(pageName = "Sites Directory Page");

		Navigator.gotoPage(pageName = "Sites Directory Page");

		Portlet.addPG(portletName = "Sites Directory");

		JSONOrganization.addOrganization(organizationName = "Organization Name 1");

		Organization.openOrganizationsAdmin();

		Organization.addSiteCP(orgName = "Organization Name 1");

		PagesAdmin.openPagesAdmin(siteURLKey = "Organization Name 1");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		PagesAdmin.addPage(pageName = "Test Page");

		JSONOrganization.addOrganization(organizationName = "Organization Name 2");

		Navigator.gotoPage(pageName = "Sites Directory Page");

		var siteName = TestCase.getSiteName(siteName = ${siteName});

		var key_siteName = ${siteName};
		var key_orgName = "Organization Name 1";

		AssertTextEquals.assertPartialText(
			locator1 = "SitesDirectory#SITE_ENTRY",
			value1 = ${siteName});

		AssertTextEquals(
			locator1 = "SitesDirectory#ORG_SITE_ENTRY",
			value1 = "Organization Name 1");

		AssertElementNotPresent(
			key_orgName = "Organization Name 2",
			locator1 = "SitesDirectory#ORG_SITE_ENTRY");

		AssertClick(
			locator1 = "SitesDirectory#ORG_SITE_ENTRY",
			value1 = "Organization Name 1");

		Portlet.addPG(portletName = "Breadcrumb");

		Navigator.gotoPage(pageName = "Sites Directory Page");

		var key_orgName = "Organization Name 1";

		AssertClick(
			locator1 = "SitesDirectory#ORG_SITE_ENTRY",
			value1 = "Organization Name 1");

		Breadcrumb.viewPG(
			breadcrumbListNotVisible = ${siteName},
			breadcrumbListVisible = "Organization Name 1",
			pathLocator = "CURRENT_LOCATION");

		Breadcrumb.viewPG(
			breadcrumbListVisible = "Test Page",
			pathLocator = "PAGE");
	}

}