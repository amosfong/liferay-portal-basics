@component-name = "portal-upgrades"
definition {

	property app.server.types = "tomcat";
	property ci.retries.disabled = "true";
	property database.types = "mysql";
	property portal.release = "true";
	property portal.upstream = "true";
	property test.liferay.virtual.instance = "false";
	property test.run.type = "single";
	property testray.main.component.name = "Database Upgrade Framework";

	@priority = 4
	test CompareLatest7413PartitionedUpgradedAndFreshDBSchemas {
		property database.bare.enabled = "true";
		property database.partition.enabled = "true";
		property portal.acceptance = "true";
		property portal.smoke.upgrades = "false";
		property skip.start.app.server = "true";
		property testcase.url = "file:///opt/dev/projects/github/liferay-portal/";

		task ("Given the user is using the latest 7413 portal version") {
			Upgrade.startReleasedBundle();
		}

		task ("And a partitioned is present") {
			HeadlessPortalInstanceAPI.addPortalInstance(
				domain = "www.able.com",
				portalInstanceId = "www.able.com",
				virtualHost = "www.able.com");

			var upgradedCompanyId = JSONCompany.getCompanyId(portalInstanceName = "www.able.com");
		}

		task ("And the server is upgraded to a new version") {
			Upgrade.restoreOriginalBundleAndUpgrade();

			User.firstLoginPG(
				password = PropsUtil.get("default.admin.password"),
				userEmailAddress = "test@www.able.com",
				virtualHostsURL = "http://www.able.com:8080");

			Portlet.shutdownServer();
		}

		task ("And the database is captured") {
			AntCommands.runCommand("build-test.xml", "export-database-schema -Ddatabase.file.prefix=upgraded -Ddatabase.schema.name=lportal");

			AntCommands.runCommand("build-test.xml", "export-database-schema -Ddatabase.file.prefix=upgraded -Ddatabase.schema.name=lpartition_${upgradedCompanyId}");
		}

		task ("When the server is started again with a fresh database") {
			AntCommands.runCommand("build-test.xml", "rebuild-database -Ddatabase.type=mysql -Dsql.dir=sql/create-bare/ -Dsql.file=create-bare-mysql.sql");

			TestCase.updatePortalProperties(
				newProperty = "upgrade.database.auto.run=false",
				oldProperty = "upgrade.database.auto.run=true");

			SQL.executeSQLStatement(sqlStatement = "DROP DATABASE lpartition_${upgradedCompanyId};");

			Portlet.startServer();
		}

		task ("And a new partition is created to emulate the same enviroment as before") {
			HeadlessPortalInstanceAPI.addPortalInstance(
				domain = "www.baker.com",
				portalInstanceId = "www.baker.com",
				virtualHost = "www.baker.com");

			User.firstLoginPG(
				password = PropsUtil.get("default.admin.password"),
				userEmailAddress = "test@www.baker.com",
				virtualHostsURL = "http://www.baker.com:8080");
		}

		task ("And the database is captured again") {
			var freshCompanyId = JSONCompany.getCompanyId(portalInstanceName = "www.baker.com");

			Portlet.shutdownServer();

			AntCommands.runCommand("build-test.xml", "export-database-schema -Ddatabase.file.prefix=fresh -Ddatabase.schema.name=lportal");

			AntCommands.runCommand("build-test.xml", "export-database-schema -Ddatabase.file.prefix=fresh -Ddatabase.schema.name=lpartition_${freshCompanyId}");
		}

		task ("Then the schemas structure is the same between upgraded and fresh databases") {
			UpgradeSchema.validateSchemas(
				freshDBSchemaName = "lpartition_${freshCompanyId}",
				upgradedDBSchemaName = "lpartition_${upgradedCompanyId}");

			UpgradeSchema.validateSchemas(
				freshDBSchemaName = "lportal",
				upgradedDBSchemaName = "lportal");
		}
	}

	@priority = 4
	test ComparePartitionedUpgradedAndFreshDBSchemas7413 {
		property data.archive.type = "data-archive-portal-partition";
		property liferay.online.properties = "true";
		property portal.version = "7.4.13.u33";

		task ("Given the user have a partitioned upgraded base And export the schemas") {
			Portlet.shutdownServer();

			AntCommands.runCommand("build-test.xml", "export-database-schema -Ddatabase.file.prefix=upgraded -Ddatabase.schema.name=lportal");

			AntCommands.runCommand("build-test.xml", "export-database-schema -Ddatabase.file.prefix=upgraded -Ddatabase.schema.name=lpartition_44913");

			AntCommands.runCommand("build-test.xml", "export-database-schema -Ddatabase.file.prefix=upgraded -Ddatabase.schema.name=lpartition_42638");
		}

		task ("When the user rebuild the database And clean the missing partitions") {
			AntCommands.runCommand("build-test.xml", "rebuild-database -Ddatabase.type=mysql -Dsql.dir=sql/create-bare/ -Dsql.file=create-bare-mysql.sql");

			SQL.executeSQLStatement(sqlStatement = "DROP DATABASE lpartition_42638;");

			SQL.executeSQLStatement(sqlStatement = "DROP DATABASE lpartition_44913;");
		}

		task ("And the user start the server again to have a fresh and clean base") {
			Portlet.startServer();

			User.firstLoginPG(
				password = PropsUtil.get("default.admin.password"),
				userEmailAddress = "test@liferay.com",
				virtualHostsURL = "http://localhost:8080");
		}

		task ("And the user add two new partitions to emulate the same enviroment as before") {
			HeadlessPortalInstanceAPI.addPortalInstance(
				domain = "www.able.com",
				portalInstanceId = "www.able.com",
				virtualHost = "www.able.com");

			HeadlessPortalInstanceAPI.addPortalInstance(
				domain = "www.baker.com",
				portalInstanceId = "www.baker.com",
				virtualHost = "www.baker.com");

			User.firstLoginPG(
				password = PropsUtil.get("default.admin.password"),
				userEmailAddress = "test@www.able.com",
				virtualHostsURL = "http://www.able.com:8080");
		}

		task ("Then he export the fresh schemas") {
			var companyId1 = JSONCompany.getCompanyId(portalInstanceName = "www.able.com");
			var companyId2 = JSONCompany.getCompanyId(portalInstanceName = "www.baker.com");

			Portlet.shutdownServer();

			AntCommands.runCommand("build-test.xml", "export-database-schema -Ddatabase.file.prefix=fresh -Ddatabase.schema.name=lportal");

			AntCommands.runCommand("build-test.xml", "export-database-schema -Ddatabase.file.prefix=fresh -Ddatabase.schema.name=lpartition_${companyId1}");

			AntCommands.runCommand("build-test.xml", "export-database-schema -Ddatabase.file.prefix=fresh -Ddatabase.schema.name=lpartition_${companyId2}");
		}

		task ("And validate that the schemas structure is the same between upgraded and fresh bases") {
			UpgradeSchema.validateSchemas(
				freshDBSchemaName = "lpartition_${companyId1}",
				upgradedDBSchemaName = "lpartition_44913");

			UpgradeSchema.validateSchemas(
				freshDBSchemaName = "lpartition_${companyId2}",
				upgradedDBSchemaName = "lpartition_42638");

			UpgradeSchema.validateSchemas(
				freshDBSchemaName = "lportal",
				upgradedDBSchemaName = "lportal");
		}
	}

}