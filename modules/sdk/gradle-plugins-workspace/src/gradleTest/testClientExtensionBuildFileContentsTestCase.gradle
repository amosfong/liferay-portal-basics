task runGradleTest

assert ext.properties.containsKey("expectedFilePath") : "No ext.expectedFilePath extension property set"
assert ext.properties.containsKey("taskPath") : "No ext.taskPath extension property set"

String expectedFilePath = ext.expectedFilePath
String taskPath = ext.taskPath

String clientExtensionBuildDir = ext.properties['clientExtensionBuildDir'] ?: 'liferay-client-extension-build'
String[] expectedContentStrings = ext.properties['expectedContentStrings'] ?: []
String[] unexpectedContentStrings = ext.properties['unexpectedContentStrings'] ?: []

Task task = tasks.getByPath(taskPath)

runGradleTest {
	dependsOn task

	doLast {
		Project taskProject = task.project

		File cxBuildDir = taskProject.file("${taskProject.buildDir}/${clientExtensionBuildDir}")

		File file = taskProject.file("${cxBuildDir}/${expectedFilePath}")

		assert file.exists()

		expectedContentStrings.each {
			assert _contains(file, it)
		}

		unexpectedContentStrings.each {
			assert !_contains(file, it)
		}
	}
}

private boolean _contains(File file, String s) {
	if (file.text.contains(s)) {
		return true
	}

	if (file.text.contains(s.replaceAll('"(\\S+)": ', '"\$1" : '))) {
		return true
	}

	return false
}