import com.liferay.gradle.util.GradleUtil

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

String gradleVersion = "8.5"

if (Boolean.getBoolean("gradle.test.enabled")) {
	apply from: "test.gradle"
}

tasks.eclipse {
	ext.gradleVersion = gradleVersion
}

dependencies {
	api group: "com.github.jk1", name: "gradle-license-report", version: "2.0"
	api group: "com.gradle.publish", name: "plugin-publish-plugin", version: "0.11.0"
	api group: "com.liferay", name: "com.liferay.gradle.plugins", version: "16.0.0"
	api group: "com.liferay", name: "com.liferay.gradle.plugins.app.javadoc.builder", version: "1.2.6"
	api group: "com.liferay", name: "com.liferay.gradle.plugins.baseline", version: "7.0.3"
	api group: "com.liferay", name: "com.liferay.gradle.plugins.cache", version: "1.0.19"
	api group: "com.liferay", name: "com.liferay.gradle.plugins.change.log.builder", version: "1.1.7"
	api group: "com.liferay", name: "com.liferay.gradle.plugins.dependency.checker", version: "1.0.6"
	api group: "com.liferay", name: "com.liferay.gradle.plugins.go", version: "2.0.1"
	api group: "com.liferay", name: "com.liferay.gradle.plugins.jsdoc", version: "2.0.79"
	api group: "com.liferay", name: "com.liferay.gradle.plugins.patcher", version: "1.0.23"
	api group: "com.liferay", name: "com.liferay.gradle.plugins.poshi.runner", version: "4.0.20"
	api group: "com.liferay", name: "com.liferay.gradle.plugins.testray", version: "1.0.22"
	api group: "com.liferay", name: "com.liferay.gradle.plugins.whip", version: "1.0.10"
	api group: "gradle.plugin.com.github.maiflai", name: "gradle-scalatest", version: "0.25"
	api group: "gradle.plugin.com.github.spotbugs.snom", name: "spotbugs-gradle-plugin", version: "4.0.0"
	api group: "org.apache.aries.jmx", name: "org.apache.aries.jmx.api", version: "1.1.0"
	api group: "org.springframework.boot", name: "spring-boot-gradle-plugin", version: "2.7.18"

	compileOnly fileTree(builtBy: [rootProject.tasks.getByName("extractGradleApi" + gradleVersion.replace(".", ""))], dir: new File(rootProject.buildDir, "gradle-${gradleVersion}"))

	implementation group: "jakarta.xml.bind", name: "jakarta.xml.bind-api", version: "2.3.2"
}

processResources {
	into("com/liferay/gradle/plugins/defaults/dependencies") {
		from "../../../tools/sdk/dependencies/net.sourceforge.pmd/rulesets/java/standard-rules.xml"
	}
}

updateFileVersions {
	match(/classpath group: "com\.liferay", name: "com\.liferay\.gradle\.plugins\.defaults", version: "(\d.+)"/, "../../../portal-web/build-test.gradle")

	FileTree workspacesFileTree = fileTree(dir: new File(GradleUtil.getRootDir(project.getRootProject(), "portal-impl"), "workspaces"), include: "**/build.gradle")

	match(/classpath group: "com\.liferay", name: "com\.liferay\.gradle\.plugins\.defaults", version: "(\d.+)"/, workspacesFileTree.files)
}