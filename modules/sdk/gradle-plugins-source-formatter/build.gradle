import com.liferay.gradle.plugins.defaults.task.ReplaceRegexTask

apply plugin: "com.gradle.plugin-publish"

task updateReadme(type: ReplaceRegexTask)

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

String gradleVersion = "8.5"

if (Boolean.getBoolean("gradle.test.enabled")) {
	apply from: "test.gradle"
}

tasks.eclipse {
	ext.gradleVersion = gradleVersion
}

dependencies {
	api group: "com.liferay", name: "com.liferay.gradle.util", version: "1.0.49"

	compileOnly fileTree(builtBy: [rootProject.tasks.getByName("extractGradleApi" + gradleVersion.replace(".", ""))], dir: new File(rootProject.buildDir, "gradle-${gradleVersion}"))
	compileOnly group: "com.liferay", name: "com.liferay.source.formatter", version: "1.0.1460"
}

pluginBundle {
	tags = ["java", "style"]
}

publish {
	dependsOn updateReadme
}

updateReadme {
	match(/group: "com\.liferay", name: "com\.liferay\.gradle\.plugins\.source\.formatter", version: "(\d.+)"/, "README.markdown")

	onlyIf {
		!project.version.endsWith("-SNAPSHOT")
	}

	replacement = {
		project.version
	}
}