@component-name = "portal-headless"
definition {

	property custom.properties = "feature.flag.COMMERCE-8087=true${line.separator}feature.flag.LPS-186620=true";
	property portal.acceptance = "quarantine";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Data Migration Center";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		ObjectAdmin.deleteAllCustomObjectsViaAPI();

		BatchPlanner.batchPlannerTearDown();

		Picklist.deletePicklistViaAPI(picklistName = "customPicklist");

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@priority = 3
	test DownloadSampleFileOfTheCustomObject {
		task ("Given a custom object is published") {
			ObjectDefinitionAPI.createAndPublishObjectDefinition(
				en_US_label = "student",
				en_US_plural_label = "students",
				name = "Student",
				requiredStringFieldName = "name");
		}

		task ("And Given I create an import file task with custom object as the entity type") {
			ImportExport.openImportExportAdmin();

			ImportExport.gotoImport();

			ImportExport.selectEntity(entityType = "Student (v1.0 - Liferay Object REST)");
		}

		task ("When I download a sample file of this entity") {
			Click.clickAt(locator1 = "ImportExport#SAMPLE_FILE_DOWNLOAD_BUTTON");
		}

		task ("Then the sample file download successfully") {
			DownloadTempFile();

			var sampleFileName = TestCase.getDownloadedTempFileName(fileNamePattern = "c_student-*.csv");

			ImportExport.assertFileContent(
				comparisonFileName = "objectEntry_import_sample.csv",
				fileName = ${sampleFileName});
		}
	}

	@priority = 4
	test ExportCustomObjectEntryWithManyToManyRelationship {
		task ("Given two custom objects published") {
			ObjectDefinitionAPI.createAndPublishObjectDefinition(
				en_US_label = "university",
				en_US_plural_label = "universities",
				name = "University",
				requiredStringFieldName = "name");

			ObjectDefinitionAPI.createAndPublishObjectDefinition(
				en_US_label = "subject",
				en_US_plural_label = "subjects",
				name = "Subject",
				requiredStringFieldName = "name");
		}

		task ("And Given manyToMany releationship has been set between them") {
			ObjectDefinitionAPI.createRelationshipWithObjectDefinitionNames(
				childObjectName = "Subject",
				deletionType = "cascade",
				name = "universitiesSubjects",
				parentObjectName = "University",
				type = "manyToMany");
		}

		task ("And Given one entry related to antoher one") {
			ObjectDefinitionAPI.createObjectEntryWithName(
				en_US_plural_label = "universities",
				name = "Oxford");

			ObjectDefinitionAPI.createObjectEntryWithName(
				en_US_plural_label = "subjects",
				name = "Math");

			ObjectDefinitionAPI.relateObjectEntriesByName(
				child_plural_label = "subjects",
				childEntryName = "Math",
				parent_plural_label = "universities",
				parentEntryName = "Oxford",
				relationshipName = "universitiesSubjects");
		}

		task ("When user export custom object as entity type") {
			ImportExport.exportAndUnzipFile(
				entityType = "Subject (v1.0 - Liferay Object REST)",
				exportFields = "Attribute Code",
				exportFileFormat = "JSON");
		}

		task ("Then zip file contains related file type export successfully") {
			BatchEngine.assertExportedFileContainsCorrectObject(
				expectedValue = "Math",
				fileName = "export.json",
				jsonObject = "$..[0]name");
		}
	}

	@priority = 4
	test ExportCustomObjectEntryWithOneToManyRelationship {
		task ("Given two custom objects published") {
			ObjectDefinitionAPI.createAndPublishObjectDefinition(
				en_US_label = "university",
				en_US_plural_label = "universities",
				name = "University",
				requiredStringFieldName = "name");

			ObjectDefinitionAPI.createAndPublishObjectDefinition(
				en_US_label = "student",
				en_US_plural_label = "students",
				name = "Student",
				requiredStringFieldName = "name");
		}

		task ("And Given oneToMany releationship has been set between them") {
			ObjectDefinitionAPI.createRelationshipWithObjectDefinitionNames(
				childObjectName = "Student",
				deletionType = "cascade",
				name = "universityStudents",
				parentObjectName = "University",
				type = "oneToMany");
		}

		task ("And Given one entry related to antoher one") {
			var universityEntryId = ObjectDefinitionAPI.createObjectEntryWithName(
				en_US_plural_label = "universities",
				name = "Oxford");

			ObjectDefinitionAPI.createObjectEntryWithName(
				en_US_plural_label = "students",
				name = "Pearl");

			ObjectDefinitionAPI.relateObjectEntriesByName(
				child_plural_label = "students",
				childEntryName = "Pearl",
				parent_plural_label = "universities",
				parentEntryName = "Oxford",
				relationshipName = "universityStudents");
		}

		task ("When user export custom object as entity type") {
			ImportExport.exportAndUnzipFile(
				entityType = "Student (v1.0 - Liferay Object REST)",
				exportFields = "Attribute Code",
				exportFileFormat = "JSON");
		}

		task ("Then zip file contains related file type export successfully") {
			BatchEngine.assertExportedFileContainsCorrectObject(
				expectedValue = "Pearl",
				fileName = "export.json",
				jsonObject = "$..[0]name");
		}
	}

	@priority = 4
	test ExportCustomObjectWithObjectEntry {
		task ("Given a custom object is published") {
			ObjectDefinitionAPI.createAndPublishObjectDefinition(
				en_US_label = "student",
				en_US_plural_label = "students",
				name = "Student",
				requiredStringFieldName = "name");
		}

		task ("And Given object entry is created") {
			ObjectDefinitionAPI.createObjectEntryWithName(
				en_US_plural_label = "students",
				name = "Jessica");
		}

		task ("When user export custom object as entity type and map attribute codes") {
			ImportExport.exportAndUnzipFile(
				entityType = "Student (v1.0 - Liferay Object REST)",
				exportFields = "Attribute Code",
				exportFileFormat = "JSON");
		}

		task ("Then the zip file contains a related file type export successfully") {
			BatchEngine.assertExportedFileContainsCorrectObject(
				expectedValue = "Jessica",
				fileName = "export.json",
				jsonObject = "$..[0]name");
		}
	}

	@priority = 3
	test ExportCustomObjectWithoutObjectEntry {
		task ("Given a custom object is published") {
			ObjectDefinitionAPI.createAndPublishObjectDefinition(
				en_US_label = "student",
				en_US_plural_label = "students",
				name = "Student",
				requiredStringFieldName = "name");
		}

		task ("When user export custom object with all attribute codes selected") {
			ImportExport.exportAndUnzipFile(
				entityType = "Student (v1.0 - Liferay Object REST)",
				exportFields = "Attribute Code",
				exportFileFormat = "JSON");
		}

		task ("Then the zip file contains a related file type export successfully") {
			ImportExport.assertDownloadedFileName(downloadedFile = "export.json");
		}

		task ("And Then no error in server console") {
			AssertConsoleTextNotPresent(value1 = "HTTP 404");
		}
	}

	@priority = 3
	test SaveCustomObjectEntityTypeAsTemplate {
		task ("Given a custom object is published") {
			ObjectDefinitionAPI.createAndPublishObjectDefinition(
				en_US_label = "student",
				en_US_plural_label = "students",
				name = "Student",
				requiredStringFieldName = "name");
		}

		task ("And Given several attribute codes are selected in export wizard") {
			ImportExport.openImportExportAdmin();

			ImportExport.gotoExport();
		}

		task ("When save as template with a template name") {
			ImportExport.configureExport(
				entityType = "Student (v1.0 - Liferay Object REST)",
				exportFields = "Attribute Code",
				exportFileFormat = "JSON");

			ImportExport.saveAsTemplate(templateName = "Template Name");
		}

		task ("Then the template saved successfully") {
			ImportExport.openImportExportAdmin();

			Navigator.gotoNavTab(navTab = "Templates");

			LexiconEntry.viewEntryName(rowEntry = "Template Name");
		}
	}

	@priority = 3
	test SaveImportCustomObjectAsTemplate {
		task ("And Given I create an import task with custom objects") {
			ObjectDefinitionAPI.createAndPublishObjectDefinition(
				en_US_label = "student",
				en_US_plural_label = "students",
				name = "Student",
				requiredStringFieldName = "name");

			ImportExport.importFile(
				entityType = "Student (v1.0 - Liferay Object REST)",
				fieldMappings = "name:name,externalReferenceCode:externalReferenceCode",
				fileName = "csv_student_import.csv",
				noImport = "true");
		}

		task ("When save as template with a template name") {
			ImportExport.saveAsTemplate(templateName = "Template Name");
		}

		task ("Then the template saved successfully") {
			ImportExport.openImportExportAdmin();

			Navigator.gotoNavTab(navTab = "Templates");

			LexiconEntry.viewEntryName(rowEntry = "Template Name");
		}
	}

	@priority = 4
	test SelectCustomObjectAsEntityType {
		task ("Given a custom object is published") {
			ObjectDefinitionAPI.createAndPublishObjectDefinition(
				en_US_label = "student",
				en_US_plural_label = "students",
				name = "Student",
				requiredStringFieldName = "name");
		}

		task ("When select the custom object as the entity type to export") {
			ImportExport.openImportExportAdmin();

			ImportExport.gotoExport();

			ImportExport.selectEntity(entityType = "Student (v1.0 - Liferay Object REST)");
		}

		task ("Then no error in server console") {
			AssertConsoleTextNotPresent(value1 = "HTTP 404");
		}
	}

}