definition {

	@summary = "Default summary"
	macro addCustomFieldEntryViaUI(invalidEntry = null, entry = null, customField = null) {
		LexiconEntry.gotoAdd();

		Type.type(
			key_customField = ${customField},
			locator1 = "ObjectPortlet#ENTRY_FIELD_GENERIC",
			value1 = ${entry});

		if (${invalidEntry} == "true") {
			Button.clickSave();
		}
		else {
			PortletEntry.save();
		}
	}

	@summary = "Default summary"
	macro addSingleFieldEntryViaUI(invalidEntry = null, entry = null) {
		LexiconEntry.gotoAdd();

		Type.type(
			locator1 = "ObjectPortlet#ADD_OBJECT_ENTRY",
			value1 = ${entry});

		if (${invalidEntry} == "true") {
			Button.clickSave();
		}
		else {
			PortletEntry.save();
		}
	}

	@summary = "Default summary"
	macro assertAuthorColumnDisplaysUserName(authorName = null, column = null) {
		AssertElementPresent(
			key_column = ${column},
			locator1 = "ObjectPortlet#ENTRY_COLUMN");

		AssertElementPresent(
			key_entry = ${authorName},
			locator1 = "ObjectPortlet#ENTRY_VALUE");
	}

	@summary = "Default summary"
	macro assertColumnLabelEntries(columnLabelName = null) {
		AssertElementPresent(
			key_sortFieldName = ${columnLabelName},
			locator1 = "ObjectPortlet#SORT_FIELD",
			value1 = ${columnLabelName});
	}

	@summary = "Default summary"
	macro assertDeleteEntryButtonIsPresent(entry = null) {
		if (IsElementPresent(key_entry = "", locator1 = "ObjectPortlet#ENTRY_KEBAB_MENU")) {
			Click(
				key_entry = ${entry},
				locator1 = "ObjectPortlet#ENTRY_KEBAB_MENU");
		}
		else {
			AssertElementPresent(
				key_entry = ${entry},
				locator1 = "ObjectPortlet#ENTRY_KEBAB_MENU");
		}

		AssertElementPresent(locator1 = "ObjectPortlet#DELETE_ENTRY_BUTTON");
	}

	@summary = "Default summary"
	macro assertDeleteEntryButtonNotPresent(entry = null) {
		if (IsElementPresent(key_entry = "", locator1 = "ObjectPortlet#ENTRY_KEBAB_MENU")) {
			Click(
				key_entry = ${entry},
				locator1 = "ObjectPortlet#ENTRY_KEBAB_MENU");
		}
		else {
			AssertElementNotPresent(
				key_entry = ${entry},
				locator1 = "ObjectPortlet#ENTRY_KEBAB_MENU");
		}

		AssertElementNotPresent(locator1 = "ObjectPortlet#DELETE_ENTRY_BUTTON");
	}

	@summary = "Default summary"
	macro assertEntryDetailsCheckedPresent() {
		AssertChecked.assertCheckedNotVisible(locator1 = "//input[@type='checkbox']");
	}

	@summary = "Default summary"
	macro assertEntryDetailsNotPresent(entryNotSelected = null) {
		SelectFrameTop();

		AssertElementNotPresent(
			key_entrySelected = ${entryNotSelected},
			locator1 = "ObjectPortlet#OBJECT_FIELD_SELECTED");
	}

	@summary = "Default summary"
	macro assertEntryDetailsValueSelected(entrySelected = null) {
		SelectFrameTop();

		AssertElementPresent(
			key_entrySelected = ${entrySelected},
			locator1 = "ObjectPortlet#OBJECT_FIELD_SELECTED");
	}

	@summary = "Default summary"
	macro assertEntryNotPresent(entry = null) {
		AssertElementNotPresent(
			key_entry = ${entry},
			locator1 = "ObjectPortlet#ENTRY_VALUE");
	}

	@summary = "Default summary"
	macro assertFieldLabelName(key_fieldLabelName = null, fieldLabelName = null) {
		AssertTextEquals(
			key_fieldLabelName = ${fieldLabelName},
			locator1 = "ObjectPortlet#FIELD_LABEL_NAME",
			value1 = ${key_fieldLabelName});
	}

	@summary = "Default summary"
	macro assertFieldLabelNameNotPresent(key_fieldLabelName = null, fieldLabelName = null) {
		AssertTextNotEquals(
			key_fieldLabelName = ${fieldLabelName},
			locator1 = "ObjectPortlet#FIELD_LABEL_NAME",
			value1 = ${key_fieldLabelName});
	}

	@summary = "Default summary"
	macro assertFilterResultsSpecificOrder() {
		var resultOrder = 0;

		for (var entry : list ${entry}) {
			var resultOrder = ${resultOrder} + 1;

			AssertVisible(
				key_entry = ${entry},
				locator1 = "ObjectPortlet#ENTRY_VALUE_SPECIFIC_ORDER",
				resultOrder = ${resultOrder});
		}
	}

	@summary = "Default summary"
	macro assertIsDisplayedOnControlPanel(itemMenu = null) {
		ApplicationsMenuHelper.openApplicationsMenu();

		ApplicationsMenu.gotoPanel(panel = "Control Panel");

		AssertElementPresent(
			key_category = "",
			key_portlet = ${itemMenu},
			locator1 = "ApplicationsMenu#PORTLET");
	}

	@summary = "Default summary"
	macro assertUserCanNotUpdateEntryWithoutPermission() {
		Click(locator1 = "ObjectPortlet#VIEW_ENTRY_EYE_BUTTON");

		AssertNotEditable(locator1 = "ObjectPortlet#ADD_OBJECT_ENTRY");

		Button.clickSave();

		AssertTextEquals.assertPartialText(
			locator1 = "Message#ERROR_1",
			value1 = "must have UPDATE permission for com.liferay.object.model.ObjectDefinition#");
	}

	@summary = "Default summary"
	macro changePagination(itemsPerPage = null) {
		Click(locator1 = "ObjectPortlet#PAGINATION_ITEMS_PER_PAGE_SELECT");

		MenuItem.click(menuItem = ${itemsPerPage});
	}

	@summary = "Default summary"
	macro checkAnyPage(pageNumber = null) {
		var key_pageNumber = ${pageNumber};

		Click(locator1 = "ObjectPortlet#ANY_PAGE");
	}

	@summary = "Default summary"
	macro checkUncheckColumnsDisplayed(dropdownItem = null) {
		Click(locator1 = "ObjectPortlet#ARROW_DOWN_ICON");

		Click(
			key_dropdownItem = ${dropdownItem},
			locator1 = "ObjectPortlet#CHECK_UNCHECK_DROPDOWN_ITEM");
	}

	@summary = "Default summary"
	macro chooseEntryOnRelationshipField(entryOption = null) {
		Click(locator1 = "ObjectPortlet#SEARCH_RELATIONSHIP_ENTRY");

		KeyPress(
			locator1 = "ObjectPortlet#SEARCH_RELATIONSHIP_ENTRY",
			value1 = "\BACK_SPACE");

		Click(
			key_picklistOption = ${entryOption},
			locator1 = "ObjectAdmin#ENTRY_PICKLIST_OPTION");

		if (IsElementPresent(key_feedbackMessage = "The field value is invalid.", locator1 = "Form#FEEDBACK_MESSAGE")) {
			WaitForElementNotPresent(
				key_feedbackMessage = "The field value is invalid.",
				locator1 = "Form#FEEDBACK_MESSAGE");
		}
	}

	@summary = "Default summary"
	macro chooseEntryOnRelationshipFieldSpecific(entryOption = null, relationshipFieldLabel = null) {
		Click(
			key_relationshipFieldLabel = ${relationshipFieldLabel},
			locator1 = "ObjectPortlet#SEARCH_ENTRY_ON_RELATIONSHIP_FIELD_LABEL_SPECIFIC");

		KeyPress(
			key_relationshipFieldLabel = ${relationshipFieldLabel},
			locator1 = "ObjectPortlet#SEARCH_ENTRY_ON_RELATIONSHIP_FIELD_LABEL_SPECIFIC",
			value1 = "\BACK_SPACE");

		Click(
			key_picklistOption = ${entryOption},
			locator1 = "ObjectAdmin#ENTRY_PICKLIST_OPTION");

		if (IsElementPresent(key_feedbackMessage = "The field value is invalid.", locator1 = "Form#FEEDBACK_MESSAGE")) {
			WaitForElementNotPresent(
				key_feedbackMessage = "The field value is invalid.",
				locator1 = "Form#FEEDBACK_MESSAGE");
		}

		PortletEntry.save();

		Navigator.gotoBack();
	}

	@summary = "Default summary"
	macro deleteEntry(entry = null) {
		Click(
			key_entry = ${entry},
			locator1 = "ObjectPortlet#ENTRY_KEBAB_MENU");

		ClickNoError(locator1 = "ObjectPortlet#DELETE_ENTRY_BUTTON");

		Button.click(button = "Delete");

		WaitForElementNotPresent(
			key_entry = ${entry},
			locator1 = "ObjectPortlet#ENTRY_VALUE");
	}

	@summary = "Default summary"
	macro gotoRelationshipTab() {
		Click(
			key_tabName = "Relationship Tab",
			locator1 = "ObjectAdmin#ENTRY_RELATIONSHIP_TAB");
	}

	@summary = "Default summary"
	macro searchColumn(columnName = null) {
		Click(locator1 = "ObjectPortlet#ARROW_DOWN_ICON");

		AssertElementPresent(locator1 = "ObjectPortlet#COLUMN_SEARCH_FIELD");

		Type(
			locator1 = "ObjectPortlet#COLUMN_SEARCH_FIELD",
			value1 = ${columnName});

		Click(locator1 = "ObjectPortlet#COLUMN_ICON_SEARCH");
	}

	@summary = "Default summary"
	macro searchEntry(entryName = null) {
		AssertElementPresent(locator1 = "ObjectPortlet#SEARCH_FIELD");

		Type(
			locator1 = "ObjectPortlet#SEARCH_FIELD",
			value1 = ${entryName});

		KeyPress(
			locator1 = "ObjectPortlet#SEARCH_FIELD",
			value1 = "\RETURN");
	}

	@summary = "Default summary"
	macro searchEntryRelationshipField(entryValue = null) {
		Type.type(
			locator1 = "ObjectPortlet#SEARCH_RELATIONSHIP_ENTRY",
			value1 = ${entryValue});
	}

	@summary = "Default summary"
	macro selectCustomObject(label = null) {
		SelectFrame(locator1 = "ObjectPortlet#SELECT_FRAME_OBJECT");

		WaitForVisible(
			key_label = ${label},
			locator1 = "ObjectPortlet#SELECT_CUSTOM_OBJECT");

		Click(
			key_label = ${label},
			locator1 = "ObjectPortlet#SELECT_CUSTOM_OBJECT");
	}

	@summary = "Default summary"
	macro selectObject(label = null) {
		Click(
			key_label = ${label},
			locator1 = "ObjectPortlet#SELECT_OBJECT");
	}

	@summary = "Default summary"
	macro selectRelationshipFieldEntry(entry = null, newEntry = null) {
		Click(
			key_entry = ${entry},
			locator1 = "ObjectPortlet#SEARCH_RELATIONSHIP_ENTRY");

		Click(
			key_entry = ${newEntry},
			locator1 = "CreateObject#ENTRY_RELATIONSHIP_FIELD_OPTION");

		PortletEntry.save();
	}

	@summary = "Default summary"
	macro selectSystemObject(label = null) {
		Click(
			key_label = ${label},
			locator1 = "ObjectPortlet#SELECT_SYSTEM_OBJECT");
	}

	@summary = "Default summary"
	macro sortField(sortFieldName = null) {
		Click(
			key_sortFieldName = ${sortFieldName},
			locator1 = "ObjectPortlet#SORT_FIELD",
			value1 = ${sortFieldName});
	}

	@summary = "Default summary"
	macro typeCustomField(entry = null, customField = null) {
		Type.type(
			key_customField = ${customField},
			locator1 = "ObjectPortlet#ENTRY_FIELD_GENERIC",
			value1 = ${entry});
	}

	@summary = "Default summary"
	macro updateBooleanEntry(entry = null) {
		ObjectPortlet.viewEntryDetails(entry = ${entry});

		Check.checkNotVisible(locator1 = "//input[@type='checkbox']");

		PortletEntry.save();
	}

	@summary = "Default summary"
	macro updateEntry(invalidEntry = null, entry = null, updateEntry = null) {
		ObjectPortlet.viewEntryDetails(entry = ${entry});

		Type.type(
			locator1 = "ObjectPortlet#ADD_OBJECT_ENTRY",
			value1 = ${updateEntry});

		if (${invalidEntry} == "true") {
			Button.clickSave();
		}
		else {
			PortletEntry.save();
		}
	}

	@summary = "Default summary"
	macro viewClobEntryDetails(clobEntry = null) {
		var key_entry = ${clobEntry};

		if (IsElementPresent(locator1 = "ObjectPortlet#ENTRY_CLOB_KEBAB_MENU")) {
			Click(locator1 = "ObjectPortlet#ENTRY_CLOB_KEBAB_MENU");

			Click(locator1 = "ObjectPortlet#VIEW_ENTRY_BUTTON");
		}
		else {
			Click(locator1 = "ObjectPortlet#VIEW_ENTRY_EYE_BUTTON");
		}

		WaitForPageLoad();
	}

	@summary = "Default summary"
	macro viewEmptyLabelInDateEntry() {
		AssertElementPresent(locator1 = "ObjectPortlet#VIEW_EMPTY_LABEL_IN_DATE_ENTRY");
	}

	@summary = "Default summary"
	macro viewEntry(entry = null) {
		WaitForSPARefresh();

		SelectFrameTop();

		AssertElementPresent(
			key_entry = ${entry},
			locator1 = "ObjectPortlet#ENTRY_VALUE");
	}

	@summary = "Default summary"
	macro viewEntryDetails(entry = null) {
		var key_entry = ${entry};

		Refresh();

		WaitForVisible(locator1 = "ObjectPortlet#ENTRY_VALUE");

		if (IsElementPresent(locator1 = "ObjectPortlet#ENTRY_KEBAB_MENU")) {
			Click(locator1 = "ObjectPortlet#ENTRY_KEBAB_MENU");

			Click(locator1 = "ObjectPortlet#VIEW_ENTRY_BUTTON");
		}
		else {
			Click(locator1 = "Icon#EYE_PREVIEW");
		}

		WaitForPageLoad();
	}

	@summary = "Default summary"
	macro viewWarningMessage(warningMessage = null) {
		AssertElementPresent(
			locator1 = "Message#ERROR_3_ALT",
			value1 = ${warningMessage});
	}

	@summary = "Default summary"
	macro viewWidgetButton() {
		AssertElementPresent(locator1 = "ObjectPortlet#VIEW_WIDGET_IN_PAGE_BUILDER_TOGGLE");
	}

}