@component-name = "portal-bpm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Object";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		JSONObjectFolder.tearDown();

		ObjectAdmin.deleteAllCustomObjectsViaAPI();

		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "LPS-185681 - Verify Object definitions can be moved to the current folder when the user is in the model buider view"
	@priority = 5
	test CanMoveObjectDefinitionToCurrentFolder {
		task ("Given two object folders titled Folder A and Folder B") {
			var folderERC = JSONObjectFolder.addFolder(
				folderLabel = "Folder A",
				folderName = "FolderA");

			JSONObjectFolder.addFolder(
				folderLabel = "Folder B",
				folderName = "FolderB");
		}

		task ("and Given an object definition in Folder A") {
			ObjectAdmin.addObjectViaAPI(
				labelName = "Custom Object 185681",
				objectName = "CustomObject185681",
				pluralLabelName = "Custom Objects 185681");

			JSONObject.updateObject(
				configKey = "objectFolderExternalReferenceCode",
				configValue = ${folderERC},
				objectName = "CustomObject185681");
		}

		task ("When the user navigates to Folder B and moves the object definition to the current folder") {
			ObjectModelBuilder.openObjectFolderInModelBuilder(objectFolderName = "FolderA");

			Click.javaScriptClick(
				key_folderLabel = "Folder B",
				locator1 = "ObjectAdmin#GO_TO_FOLDER");

			Click.javaScriptClick(
				key_objectLabel = "Custom Object 185681",
				locator1 = "ObjectModelBuilder#OBJECT_IN_LEFT_SIDEBAR_KEBAB_MENU");

			Click(
				key_item = "Move to Current Folder",
				locator1 = "ManagementBar#ACTIVE_DROPDOWN_ITEM");
		}

		task ("Then the object definition is moved from Folder A to the current folder, Folder B") {
			AssertElementPresent(
				key_folderLabel = "Folder B",
				key_objectLabel = "Custom Object 185681",
				locator1 = "ObjectModelBuilder#SELECT_OBJECT_IN_LEFT_SIDEBAR");
		}
	}

}