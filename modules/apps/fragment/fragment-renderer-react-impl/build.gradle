import groovy.json.JsonBuilder
import groovy.json.JsonSlurper

task addReactAMDDependency

addReactAMDDependency {
	dependsOn packageRunBuild

	doLast {
		File packageJSONFile = file("build/node/packageRunBuild/resources/package.json")
		Map<String, Object> packageJSONMap = [:]

		if (packageJSONFile.exists()) {
			JsonSlurper jsonSlurper = new JsonSlurper()

			packageJSONMap = jsonSlurper.parse(packageJSONFile, "UTF-8")
		}

		if (!packageJSONMap.containsKey("dependencies")) {
			packageJSONMap.dependencies = [:]
		}

		packageJSONMap.dependencies.put("liferay!frontend-js-react-web\$react", "*")

		JsonBuilder jsonBuilder = new JsonBuilder(packageJSONMap)

		packageJSONFile.text = jsonBuilder.toPrettyString()
	}
}

classes {
	dependsOn addReactAMDDependency
}

dependencies {
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "default"
	compileOnly group: "org.apache.felix", name: "org.apache.felix.http.servlet-api", version: "1.1.2"
	compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.4.0"
	compileOnly project(":apps:fragment:fragment-api")
	compileOnly project(":apps:frontend-js:frontend-js-loader-modules-extender-api")
	compileOnly project(":apps:portal-template:portal-template-react-renderer-api")
	compileOnly project(":core:petra:petra-string")
}