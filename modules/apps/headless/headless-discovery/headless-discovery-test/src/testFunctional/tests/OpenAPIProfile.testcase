@component-name = "portal-headless"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Headless Discovery Application";

	setUp {
		TestCase.setUpPortalInstance();

		SignIn.signInTestSetup();
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@priority = 4
	test CanLoadAPIExplorerWithProxy {
		property portal.acceptance = "true";
		property portal.proxy.path = "portal";
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		task ("When navigate to /o/api") {
			APIExplorer.navigateToOpenAPI();
		}

		task ("Then page should load successfully") {
			var portalURL = PropsUtil.get("portal.url");

			AssertTextEquals(
				locator1 = "OpenAPI#API_URL",
				value1 = "${portalURL}/o/headless-delivery/v1.0/openapi.json");

			AssertConsoleTextNotPresent(value1 = "Failed to load API definition.");
		}
	}

	@priority = 4
	test CanLoadOpenAPIProfileWithoutErrors {
		property portal.acceptance = "true";

		task ("When navigate to /o/openapi/openapi.json endpoint") {
			APIExplorer.navigateToOpenAPI(
				api = "openapi",
				version = "");
		}

		task ("Then global rest api loaded without errors") {
			var portalURL = PropsUtil.get("portal.url");

			AssertTextEquals(
				locator1 = "Select#HEADLESS_SERVERS",
				value1 = "${portalURL}/o");

			AssertConsoleTextNotPresent(value1 = "Failed to load Global REST API");
		}
	}

	@priority = 4
	test CanRetrieveHeadlessAdminWorkflowGetOpenAPI {
		property portal.acceptance = "true";

		task ("Given navigate to /o/openapi/openapi.json endpoint") {
			APIExplorer.navigateToOpenAPI(
				api = "openapi",
				version = "");
		}

		task ("When executing HeadlessAdminWorkflow.v1.0.getOpenAPI") {
			APIExplorer.executeAPIMethod(
				method = "HeadlessAdminWorkflow\.v1\.0\.getOpenAPI",
				parameter = "type",
				service = "default",
				value = "json");
		}

		task ("Then the shown response code is 200") {
			AssertTextEquals(
				locator1 = "OpenAPI#RESPONSE_CODE",
				method = "HeadlessAdminWorkflow\.v1\.0\.getOpenAPI",
				value1 = 200);
		}
	}

	@priority = 4
	test CanReturnDetailsOfAPIsInTheSystem {
		property portal.acceptance = "true";

		task ("When with get request to retrieve /o/openapi/openapi.json") {
			var response = OpenAPIProfile.getOpenApiProfile();
		}

		task ("Then I can see details of all the APIs in the system") {
			var attributesList = "application/json,application/xml,#/components/schemas/";

			OpenAPIProfile.assertEndpointsInResponse(
				attributesList = ${attributesList},
				responseToParse = ${response});
		}
	}

}