definition {

	@summary = "Default summary"
	macro _curlAPIEndpoint(virtualHost = null) {
		if (isSet(virtualHost)) {
			if (!(isSet(port))) {
				var port = 8080;
			}

			var portalURL = "http://${virtualHost}:${port}";
		}
		else {
			var portalURL = JSONCompany.getPortalURL();
		}

		var api = "headless-builder/endpoints";
		var userPassword = PropsUtil.get("default.admin.password");

		var curl = '''
			${portalURL}/o/${api} \
				-u test@liferay.com:${userPassword} \
				-H Content-Type: application/json
		''';

		return ${curl};
	}

	@summary = "Default summary"
	macro createAPIEndpoint(requestSchemaId = null, path = null, responseSchemaId = null, name = null, virtualHost = null, apiApplicationERC = null) {
		Variables.assertDefined(parameterList = "${apiApplicationERC},${name},${path}");

		var curl = EndpointAPI._curlAPIEndpoint(virtualHost = ${virtualHost});

		if (!(isSet(httpMethod))) {
			var httpMethod = "get";
		}

		var body = '''
			"httpMethod": "${httpMethod}",
			"name": "${name}",
			"path": "${path}",
			"r_apiApplicationToAPIEndpoints_l_apiApplicationERC": "${apiApplicationERC}"
		''';

		if (isSet(requestSchemaId)) {
			var body = StringUtil.add(${body}, ", \"r_requestAPISchemaToAPIEndpoints_l_apiSchemaId\": ${requestSchemaId}", "");
		}

		if (isSet(responseSchemaId)) {
			var body = StringUtil.add(${body}, ", \"r_responseAPISchemaToAPIEndpoints_l_apiSchemaId\": ${responseSchemaId}", "");
		}

		var curl = StringUtil.add(${curl}, " -d { ${body} }", "");

		var response = JSONCurlUtil.post(${curl});

		return ${response};
	}

	@summary = "Default summary"
	macro createNAPIEndpoints(path = null, numberOfEndpoints = null, name = null, apiApplicationERC = null) {
		Variables.assertDefined(parameterList = "${apiApplicationERC},${name},${numberOfEndpoints},${path}");

		var i = 0;

		while (${i} != ${numberOfEndpoints}) {
			var i = ${i} + 1;

			EndpointAPI.createAPIEndpoint(
				apiApplicationERC = ${apiApplicationERC},
				name = "${name}${i}",
				path = "${path}${i}");
		}
	}

	@summary = "Default summary"
	macro getAPIEndpoints() {
		var curl = EndpointAPI._curlAPIEndpoint();

		var response = JSONCurlUtil.get(${curl});

		return ${response};
	}

}