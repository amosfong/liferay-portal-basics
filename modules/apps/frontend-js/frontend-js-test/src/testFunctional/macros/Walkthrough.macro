definition {

	@summary = "Default summary"
	macro _editWalkthroughJSON(fileName = null) {
		var jsonSnippet = Walkthrough.getWalkthroughSteps(fileName = ${fileName});

		SystemSettings.editTextAreaSetting(
			settingName = "Steps",
			settingValue = ${jsonSnippet});
	}

	@summary = "Default summary"
	macro _enableWalkthrough() {
		SystemSettings.configureSystemSetting(
			enableSetting = "true",
			settingFieldName = "Enable Walkthrough");
	}

	@summary = "Default summary"
	macro addWalkthrough(fileName = null) {
		Walkthrough.gotoSiteConfigWalkthrough();

		Walkthrough._enableWalkthrough();

		Walkthrough._editWalkthroughJSON(fileName = ${fileName});

		SystemSettings.saveConfiguration();
	}

	@summary = "Default summary"
	macro assertHighlightedSpecificObject(key_stepId = null) {
		AssertAttributeValue.assertAttributeNotPresent(
			key_stepId = ${key_stepId},
			locator1 = "Walkthrough#STEP_ID",
			value1 = "fill-opacity",
			value2 = "0.8");

		AssertAttributeValue(
			locator1 = "Walkthrough#POPOVER_OVERLAY",
			value1 = "fill",
			value2 = "#393a4a");
	}

	@summary = "Default summary"
	macro enablePopoverMode() {
		if (IsElementNotPresent(locator1 = "Walkthrough#POPOVER")) {
			if (IsElementPresent(key_portletName = "JS Walkthrough Sample", locator1 = "Portlet#TITLE")) {
				Click(locator1 = "Walkthrough#HOTSPOT");
			}
			else {
				Click(locator1 = "Walkthrough#SAMPLE_PORTLET_HOTSPOT");
			}
		}
	}

	@summary = "Default summary"
	macro getWalkthroughSteps(fileName = null) {
		var dependencyFilePath = TestCase.getDependenciesDirPath(fileName = ${fileName});

		var fileContent = FileUtil.read(${dependencyFilePath});

		return ${fileContent};
	}

	@summary = "Default summary"
	macro goToNextStep(key_step = null) {
		Click(locator1 = "Button#OK");

		VerifyElementPresent(
			key_title = "Step ${key_step}",
			locator1 = "Walkthrough#POPOVER_TITLE");
	}

	@summary = "Default summary"
	macro goToPreviousStep(key_step = null) {
		Click(locator1 = "Button#PREVIOUS");

		VerifyElementPresent(
			key_title = "Step ${key_step}",
			locator1 = "Walkthrough#POPOVER_TITLE");
	}

	@summary = "Default summary"
	macro gotoSiteConfigWalkthrough() {
		ProductMenu.gotoPortlet(
			category = "Configuration",
			portlet = "Site Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "Walkthrough",
			configurationName = "Walkthrough",
			configurationScope = "Site Scope");
	}

	@summary = "Default summary"
	macro goToSpecificStep(key_step = null) {
		while (IsElementNotPresent(key_title = "Step ${key_step}", locator1 = "Walkthrough#POPOVER_TITLE")) {
			Click(locator1 = "Button#OK");
		}
	}

	@summary = "Default summary"
	macro setupWalkthroughPage(filename = null, siteName = null) {
		task ("Given there is a site-level JSON defined for current site") {
			Walkthrough.addWalkthrough(fileName = ${filename});

			JSONLayout.addPublicLayout(
				groupName = ${siteName},
				layoutName = "Walkthrough Page 1");

			JSONLayout.addWidgetToPublicLayout(
				groupName = ${siteName},
				layoutName = "Walkthrough Page 1",
				widgetName = "JS Walkthrough Sample");

			if (${filename} == "walkthrough_configuration_multiple_page.json") {
				JSONLayout.addPublicLayout(
					groupName = ${siteName},
					layoutName = "Walkthrough Page 2");

				JSONLayout.addWidgetToPublicLayout(
					groupName = ${siteName},
					layoutName = "Walkthrough Page 2",
					widgetName = "JS Walkthrough Sample");
			}
		}

		task ("When instantiate the walkthrough") {
			Navigator.openSitePage(
				pageName = "Walkthrough Page 1",
				siteName = ${siteName});

			Walkthrough.enablePopoverMode();
		}
	}

}