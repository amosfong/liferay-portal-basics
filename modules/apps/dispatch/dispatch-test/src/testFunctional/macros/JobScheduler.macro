definition {

	@summary = "Activate a Job Scheduler Trigger"
	macro activateScheduleTrigger(cronExpression = null, trigger = null) {
		LexiconEntry.gotoEntry(rowEntry = ${trigger});

		Navigator.gotoNavTab(navTab = "Job Scheduler Trigger");

		Check.checkToggleSwitch(locator1 = "CommerceEntry#ACTIVE_CHECKBOX");

		Type(
			key_text = "Cron Expression",
			locator1 = "TextInput#ANY",
			value1 = ${cronExpression});

		Button.clickSave();

		Alert.viewSuccessMessage();

		Button.clickCancel();
	}

	@summary = "Add a Job Scheduler Trigger"
	macro addScheduleTrigger(triggerName = null, triggerProperties = null, taskExecutorType = null) {
		Click(locator1 = "Button#PLUS");

		MenuItem.click(menuItem = ${taskExecutorType});

		Type(
			locator1 = "TextInput#NAME",
			value1 = ${triggerName});

		if (isSet(triggerProperties)) {
			Type.typeAceEditor(
				locator1 = "TextArea#ACE_EDITOR",
				value1 = ${triggerProperties});
		}

		Button.clickSave();

		Alert.viewSuccessMessage();
	}

	@summary = "Delete a Job Scheduler item"
	macro deleteJobItem(jobItemName = null) {
		Variables.assertDefined(parameterList = ${jobItemName});

		Click(
			key_name = ${jobItemName},
			locator1 = "JobScheduler#JOB_ITEM_KEBAB_MENU");

		ClickNoError(
			key_option = "Delete",
			locator1 = "JobScheduler#KEBAB_MENU_OPTION");

		AssertConfirm.waitForConfirmation(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();

		AssertElementNotPresent(
			key_name = ${jobItemName},
			locator1 = "JobScheduler#JOB_ITEM_NAME");
	}

	@summary = "Edit a Job Scheduler Item"
	macro editJobItem(jobItemName = null) {
		Variables.assertDefined(parameterList = ${jobItemName});

		Click(
			key_name = ${jobItemName},
			locator1 = "JobScheduler#JOB_ITEM_KEBAB_MENU");

		ClickNoError(
			key_option = "Edit",
			locator1 = "JobScheduler#KEBAB_MENU_OPTION");
	}

	@summary = "Open the Job Scheduler Admin page"
	macro openJobSchedulerAdmin(baseURL = null) {
		Navigator.openWithAppendToBaseURL(
			baseURL = ${baseURL},
			urlAppend = "group/control_panel/manage?p_p_id=com_liferay_dispatch_web_internal_portlet_DispatchPortlet");
	}

	@summary = "Set a Job scheduler Trigger"
	macro setJobSchedulerTrigger(key_name = null, selectFieldValue = null, selectFieldLabel = null) {
		Variables.assertDefined(parameterList = "${key_name},${selectFieldLabel},${selectFieldValue}");

		Click(
			key_name = ${key_name},
			locator1 = "JobScheduler#JOB_ITEM_NAME");

		Click(
			key_tab = "Job Scheduler Trigger",
			locator1 = "NavTab#TAB_LINK");

		SelectField.select(
			selectFieldLabel = ${selectFieldLabel},
			selectFieldValue = ${selectFieldValue});

		Button.clickSave();

		Alert.viewSuccessMessage();
	}

}