@component-name = "portal-commerce"
definition {

	property ci.retries.disabled = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Product Info Management";

	setUp {
		CommerceConfiguration.commerceSetUp();

		task ("Enable private pages") {
			PagesAdmin.enablePrivatePages();
		}
	}

	tearDown {
		CommerceConfiguration.commerceTearDown();
	}

	@priority = 5
	test CreateNewSpecificationOptions {
		property environment.acceptance = "true";
		property operating.system.types = "alpine,amazonlinux,centos,debian,fedora,orcllinux,osx,redhat,rockylinux,solaris,suse,ubuntu,windows";
		property portal.acceptance = "true";

		CommerceNavigator.gotoPortlet(
			category = "Product Management",
			portlet = "Specifications");

		CommerceEntry.createNewSpecificationLabels(
			specificationDescription = "Specification-1 Description",
			specificationTitle = "specification-1");

		Navigator.gotoBack();

		CommerceEntry.viewSpecificationLabels(
			specificationDescription = "Specification-1 Description",
			specificationTitle = "specification-1");

		CommerceNavigator.gotoPortlet(
			category = "Product Management",
			portlet = "Specifications");

		CommerceNavigator.gotoCommercePortletNavItem(navItem = "Specification Groups");

		CommerceEntry.createNewSpecificationGroups(
			specificationDescription = "Specification-1 Description",
			specificationTitle = "specification-1");

		Navigator.gotoBack();

		CommerceEntry.viewSpecificationGroups(
			specificationDescription = "Specification-1 Description",
			specificationTitle = "specification-1");
	}

	@priority = 3
	test PublishSimpleProduct {
		property environment.acceptance = "true";
		property operating.system.types = "alpine,amazonlinux,centos,debian,fedora,orcllinux,osx,redhat,rockylinux,solaris,suse,ubuntu,windows";
		property portal.acceptance = "false";

		CommerceNavigator.gotoPortlet(
			category = "Product Management",
			portlet = "Products");

		CPCommerceCatalog.newProductsEntry(
			productName = "Simple T-Shirt",
			productType = "Simple");

		CommerceProducts.openProductsAdmin();

		CommerceNavigator.gotoEntry(entryName = "Simple T-Shirt");

		CommerceEntry.viewProductsEntry(
			productFullDescription = "Simple T-Shirt Full Description",
			productName = "Simple T-Shirt",
			productShortDescription = "Simple T-Shirt Short Description");

		Click(locator1 = "Icon#BACK");

		CommerceNavigator.searchEntry(entryName = "Simple T-Shirt");

		Click(
			key_entryName = "Simple T-Shirt",
			locator1 = "CommerceEntry#ENTRY_MENU_DROPDOWN_BUTTON");

		Click(
			filterOption = "Delete",
			locator1 = "ContentDashboard#FILTER_BY_OPTION");

		AssertElementPresent(locator1 = "Message#SUCCESS");

		AssertElementNotPresent(
			key_entryName = "Simple T-Shirt",
			locator1 = "CommerceEntry#VALUE_PRODUCT_RELATIONS");
	}

}