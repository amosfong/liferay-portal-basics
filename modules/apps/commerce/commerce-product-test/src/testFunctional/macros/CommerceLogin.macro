definition {

	@summary = "Default summary"
	macro createAccount(userLastName = null, userScreenName = null, userEmailAddress = null, newPassword = null, userFirstName = null) {
		Type(
			locator1 = "TextInput#FIRST_NAME",
			value1 = ${userFirstName});

		Type(
			locator1 = "TextInput#LAST_NAME",
			value1 = ${userLastName});

		Type(
			locator1 = "TextInput#SCREEN_NAME",
			value1 = ${userScreenName});

		Type(
			locator1 = "TextInput#EMAIL_ADDRESS",
			value1 = ${userEmailAddress});

		Type(
			locator1 = "TextInput#PASSWORD",
			value1 = ${newPassword});

		Type(
			key_text = "Reenter",
			locator1 = "TextInput#ANY",
			value1 = ${newPassword});

		Button.clickSave();
	}

	@summary = "Default summary"
	macro editPasswordCP(userScreenName = null, requirePasswordReset = null) {
		UserNavigator.gotoUser(userScreenName = ${userScreenName});

		if (!(isSet(newPassword))) {
			var newPassword = "password";
		}

		UserNavigator.gotoPassword();

		Type(
			locator1 = "UsersAndOrganizationsEditUser#PASSWORD_NEW_PASSWORD_FIELD",
			value1 = ${newPassword});

		Type(
			locator1 = "UsersAndOrganizationsEditUser#PASSWORD_ENTER_AGAIN_FIELD",
			value1 = ${newPassword});

		if (${requirePasswordReset} == "true") {
			Check(
				checkboxName = "Require Password Reset",
				locator1 = "Checkbox#ANY_CHECKBOX");
		}

		PortletEntry.save();
	}

	@summary = "Default summary"
	macro loginPG(localization = null, password = null, authenticationMethod = null, userScreenName = null, userEmailAddress = null, rememberMeChecked = null, newPassword = null, samlEnabled = null) {
		if (${samlEnabled} != "true") {
			User._clickSignInLink(localization = ${localization});
		}

		if (!(isSet(userEmailAddress))) {
			var userEmailAddress = JSONUtil2.getDefaultUserEmail();
		}

		if (!(isSet(password))) {
			var password = JSONUtil2.getDefaultUserPassword();
		}

		if (isSet(authenticationMethod) && (${authenticationMethod} == "By Screen Name")) {
			Type(
				locator1 = "TextInput#SCREEN_NAME",
				value1 = ${userScreenName});
		}
		else {
			Type(
				locator1 = "TextInput#EMAIL_ADDRESS",
				value1 = ${userEmailAddress});
		}

		Type(
			locator1 = "TextInput#PASSWORD",
			value1 = ${password});

		if (isSet(rememberMeChecked) && (${rememberMeChecked} == "true")) {
			Check(locator1 = "Checkbox#REMEMBER_ME");
		}

		User._clickSignInButton(localization = ${localization});

		if (IsElementPresent(locator1 = "Message#ERROR_FORM_FIELD_REQUIRED")) {
			echo("Sign in failed due to empty required field. Retrying...");

			if (isSet(authenticationMethod) && (${authenticationMethod} == "By Screen Name")) {
				Type(
					locator1 = "TextInput#SCREEN_NAME",
					value1 = ${userScreenName});
			}
			else {
				Type(
					locator1 = "TextInput#EMAIL_ADDRESS",
					value1 = ${userEmailAddress});
			}

			Type(
				locator1 = "TextInput#PASSWORD",
				value1 = ${password});

			Click.mouseDownMouseUp(
				key_text = "Sign In",
				locator1 = "Button#ANY");
		}

		Button.viewNotPresent(button = "Sign In");

		User.acceptEndUserLicenseAgreement();

		User.resetPassword(newPassword = ${newPassword});

		User.answerPasswordReminder();
	}

	@summary = "Default summary"
	macro miniumLogin(password = null, userEmailAddress = null, urlAppend = null) {
		var baseURL = ${baseURL};

		if (!(isSet(baseURL))) {
			var baseURL = JSONCompany.getPortalURL();
		}

		var specificURL = "${baseURL}/${urlAppend}";

		Navigator.openSpecificURL(url = ${specificURL});

		CommerceLogin.loginPG(
			password = ${password},
			samlEnabled = "true",
			specificURL = "${baseURL}/${urlAppend}",
			userEmailAddress = ${userEmailAddress});

		WaitForPageLoad();
	}

	@summary = "Default summary"
	macro miniumLogoutPG() {
		ScrollBy(value1 = "0, -1000");

		Click(locator1 = "CommerceAccelerators#MINIUM_SIDEBAR_USER_AVATAR");

		Click(
			key_menuItem = "Sign Out",
			locator1 = "CommerceAccelerators#MINIUM_SIDEBAR_MENU_ITEM");
	}

	@summary = "Default summary"
	macro speedwellLogin(password = null, userEmailAddress = null, urlAppend = null) {
		var baseURL = ${baseURL};

		if (!(isSet(baseURL))) {
			var baseURL = JSONCompany.getPortalURL();
		}

		var specificURL = "${baseURL}/${urlAppend}";

		Navigator.openSpecificURL(url = ${specificURL});

		CommerceNavigator.gotoSpeedwellAccountDropdownItem(dropdownItem = "Sign In");

		CommerceLogin.loginPG(
			password = ${password},
			samlEnabled = "true",
			userEmailAddress = ${userEmailAddress});
	}

	@summary = "Default summary"
	macro speedwellLogout() {
		Click(locator1 = "CommerceAccelerators#SPEEDWELL_ACCOUNT_DROPDOWN");

		Click(
			key_dropdownItem = "Sign Out",
			locator1 = "CommerceAccelerators#SPEEDWELL_ACCOUNT_DROPDOWN_ITEM");
	}

}