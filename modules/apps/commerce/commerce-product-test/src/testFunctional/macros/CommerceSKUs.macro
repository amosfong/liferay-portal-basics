definition {

	@summary = "Default summary"
	macro addPriceToSKU(price = null) {
		Type(
			locator1 = "CommerceEntry#PRICE_FIELD",
			value1 = ${price});
	}

	@summary = "Default summary"
	macro addSKU(noOption = null, key_menuItem = null, optionValuesList = null) {
		for (var optionValues : list ${optionValuesList}) {
			var optionValue = StringUtil.extractFirst(${optionValues}, ":");
			var unitPrice = StringUtil.extractLast(${optionValues}, ":");
			var optionValueUppercase = StringUtil.upperCase(${optionValue});

			Refresh();

			Click(locator1 = "Button#PLUS");

			if (IsElementPresent(key_menuItem = "Add SKU", locator1 = "MenuItem#DROPDOWN_MENU_ITEM")) {
				AssertClick(
					key_menuItem = "Add SKU",
					locator1 = "MenuItem#DROPDOWN_MENU_ITEM",
					value1 = ${key_menuItem});
			}

			SelectFrame.selectFrameNoLoading(locator1 = "CommerceEntry#IFRAME_SIDE_PANEL");

			CommerceEntry.addSKUsEntry(
				noOption = ${noOption},
				optionValue = ${optionValue},
				productSku = "${optionValueUppercase}SKU");

			WaitForElementNotPresent(locator1 = "TextInput#REQUIRED_ALERT");

			Check.checkToggleSwitch(
				key_toggleSwitchLabel = "Purchasable",
				locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

			Button.clickPublish();

			CommerceEntry.gotoMenuTab(menuTab = "Price");

			CommerceSKUs.addSKUPriceEntry(
				priceList = ${priceList},
				unitPrice = ${unitPrice});

			Click(locator1 = "Icon#CLOSE");
		}

		Refresh();
	}

	@summary = "This macro can be used to add a product SKU with/without UOM to a Price List"
	macro addSKUPriceEntry(key_index = null, priceList = null, unitPrice = null, uomKey = null) {
		Click(locator1 = "Button#PLUS");

		SelectFrameTop();

		SelectFrame.selectFrameNoLoading(locator1 = "IFrame#MODAL_BODY");

		if (isSet(priceList)) {
			Select(
				key_fieldLabel = "Price List",
				locator1 = "CommerceEntry#LABEL_GENERIC_SELECT",
				value1 = ${priceList});
		}

		if (isSet(uomKey)) {
			Select(
				key_fieldLabel = "Unit of Measure",
				locator1 = "CommerceEntry#LABEL_GENERIC_SELECT",
				value1 = ${uomKey});
		}

		if (isSet(unitPrice)) {
			Type(
				key_fieldLabel = "Unit Price",
				locator1 = "TextInput#GENERIC_NUMBER_INPUT",
				value1 = ${unitPrice});
		}

		if (isSet(addNewEntry)) {
			Button.click(button = "Add Entry");

			if (isSet(newEntryPriceList)) {
				Select(
					key_fieldLabel = "Price List",
					key_index = ${key_index},
					locator1 = "CommerceEntry#LABEL_SELECT_BY_INDEX",
					value1 = ${newEntryPriceList});
			}

			if (isSet(newEntryUomKey)) {
				Select(
					key_fieldLabel = "Unit of Measure",
					key_index = ${key_index},
					locator1 = "CommerceEntry#LABEL_SELECT_BY_INDEX",
					value1 = ${newEntryUomKey});
			}

			if (isSet(newEntryUnitPrice)) {
				Type(
					key_fieldLabel = "Unit Price",
					key_indexNumber = ${key_index},
					locator1 = "TextInput#GENERIC_NUMBER_INPUT_N",
					value1 = ${newEntryUnitPrice});
			}
		}

		SelectFrame.selectFrameTop();

		Click(
			key_text = "Add",
			locator1 = "Button#ANY");

		SelectFrame.selectFrameNoLoading(locator1 = "CommerceEntry#IFRAME_SIDE_PANEL");
	}

	@summary = "Default summary"
	macro editSkuERC(skuERC = null) {
		SelectFrame.selectFrameNoLoading(locator1 = "CommerceEntry#IFRAME_SIDE_PANEL");

		Type(
			locator1 = "CommerceEntry#SKU_ERC_FIELD",
			value1 = ${skuERC});

		if (isSet(publish)) {
			Button.clickPublish();
		}

		if (isSet(closeFrame)) {
			Click(locator1 = "Icon#CLOSE");

			SelectFrame.selectFrameTop();
		}
	}

	@summary = "This macro edits the SKU Name"
	macro editSkuName(productSku = null) {
		SelectFrame.selectFrameNoLoading(locator1 = "CommerceEntry#IFRAME_SIDE_PANEL");

		Type(
			locator1 = "CommerceEntry#SKU_FIELD",
			value1 = ${productSku});

		if (isSet(publish)) {
			Button.clickPublish();
		}

		if (isSet(closeFrame)) {
			Click(locator1 = "Icon#CLOSE");

			SelectFrame.selectFrameTop();
		}
	}

	@summary = "Default summary"
	macro editSkuPricing(priceOnApplication = null, promoPrice = null, cost = null, basePrice = null) {
		SelectFrame.selectFrameNoLoading(locator1 = "CommerceEntry#IFRAME_SIDE_PANEL");

		CommerceSKUs.setSkuPricing(
			basePrice = ${basePrice},
			cost = ${cost},
			priceOnApplication = ${priceOnApplication},
			promoPrice = ${promoPrice});

		if (IsElementPresent(key_text = "Publish", locator1 = "Button#ANY")) {
			Button.clickPublish();
		}
		else if (IsElementPresent(key_text = "Save", locator1 = "Button#ANY")) {
			Button.clickSave();
		}

		Click(locator1 = "Icon#CLOSE");

		SelectFrame.selectFrameTop();
	}

	@summary = "Default summary"
	macro generateAllSkuCombinations() {
		Click(locator1 = "Button#PLUS");

		Click(
			key_fieldName = "Generate All SKU Combinations",
			locator1 = "FormViewBuilder#OBJECT_FIELD_BUTTON_DROPDOWN");
	}

	@summary = "Default summary"
	macro setSkuInventory(northeastQuantity = null, italyQuantity = null, southwestQuantity = null) {
		if (isSet(italyQuantity)) {
			Type(
				key_value1 = 0,
				locator1 = "CommerceEntry#WAREHOUSE_INVENTORY_ITEM_QUANTITY",
				value1 = ${italyQuantity});

			Click(
				key_value1 = 0,
				locator1 = "CommerceEntry#WAREHOUSE_INVENTORY_SAVE_BUTTON");
		}

		if (isSet(northeastQuantity)) {
			Type(
				key_value1 = 1,
				locator1 = "CommerceEntry#WAREHOUSE_INVENTORY_ITEM_QUANTITY",
				value1 = ${northeastQuantity});

			Click(
				key_value1 = 1,
				locator1 = "CommerceEntry#WAREHOUSE_INVENTORY_SAVE_BUTTON");
		}

		if (isSet(southwestQuantity)) {
			Type(
				key_value1 = 2,
				locator1 = "CommerceEntry#WAREHOUSE_INVENTORY_ITEM_QUANTITY",
				value1 = ${southwestQuantity});

			Click(
				key_value1 = 2,
				locator1 = "CommerceEntry#WAREHOUSE_INVENTORY_SAVE_BUTTON");
		}
	}

	@summary = "Default summary"
	macro setSkuPricing(priceOnApplication = null, promoPrice = null, basePrice = null) {
		if (isSet(priceOnApplication)) {
			if (${priceOnApplication} == "true") {
				Check.checkToggleSwitch(
					key_toggleSwitchLabel = "Price on Application",
					locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
			}
			else {
				Uncheck.uncheckToggleSwitch(
					key_toggleSwitchLabel = "Price on Application",
					locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
			}
		}

		if (isSet(basePrice)) {
			Type(
				key_text = "Price List Price",
				locator1 = "TextInput#ANY",
				value1 = ${basePrice});
		}

		if (isSet(promoPrice)) {
			Type(
				key_text = "Promotion Price",
				locator1 = "TextInput#ANY",
				value1 = ${promoPrice});
		}
	}

	@summary = "Default summary"
	macro setSkuUomInventory(uomQuantityLists = null) {
		for (var uomQuantityList : list ${uomQuantityLists}) {
			var warehouseItemList = StringUtil.extractFirst(${uomQuantityList}, ":");
			var warehouseItemQuantity = StringUtil.extractLast(${uomQuantityList}, ":");

			Type(
				key_value1 = ${warehouseItemList},
				locator1 = "CommerceEntry#WAREHOUSE_INVENTORY_ITEM_QUANTITY",
				value1 = ${warehouseItemQuantity});

			Click(
				key_value1 = ${warehouseItemList},
				locator1 = "CommerceEntry#WAREHOUSE_INVENTORY_SAVE_BUTTON");
		}
	}

	@summary = "Default summary"
	macro viewSkuUomInventory(uomQuantityLists = null) {
		for (var uomQuantityList : list ${uomQuantityLists}) {
			var warehouseItemList = StringUtil.extractFirst(${uomQuantityList}, ":");
			var warehouseItemQuantity = StringUtil.extractLast(${uomQuantityList}, ":");

			AssertTextEquals(
				key_value1 = ${warehouseItemList},
				locator1 = "CommerceEntry#WAREHOUSE_INVENTORY_ITEM_QUANTITY",
				value1 = ${warehouseItemQuantity});
		}
	}

	@summary = "Default summary"
	macro viewSkuUomWarehouse(uomKey = null, uomLists = null) {
		for (var uomList : list ${uomLists}) {
			var index = StringUtil.extractFirst(${uomList}, ":");
			var warehouseName = StringUtil.extractLast(${uomList}, ":");

			AssertTextEquals(
				key_index = ${index},
				key_warehouseName = ${warehouseName},
				locator1 = "CommerceEntry#WAREHOUSE_INVENTORY_ITEM_UNIT_OF_MEASURE",
				value1 = ${uomKey});
		}
	}

}