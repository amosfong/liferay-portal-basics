definition {

	@summary = "Default summary"
	macro addAssignee(accountGroupName = null, assigneeNames = null) {
		LexiconEntry.gotoAdd();

		SelectFrame(
			key_title = "Assign Accounts to ${accountGroupName}",
			locator1 = "IFrame#MODAL_ANY");

		if (isSet(assigneeAll)) {
			PortletEntry.selectAll();
		}
		else {
			for (var assigneeName : list ${assigneeNames}) {
				Check(
					key_tableEntry = ${assigneeName},
					locator1 = "LexiconTable#TABLE_ENTRY_CHECKBOX");
			}
		}

		SelectFrameTop();

		Button.click(button = "Assign");

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro addCP(accountGroupDescription = null, accountGroupName = null) {
		LexiconEntry.gotoAdd();

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Add Account Group");

		PortletEntry.inputName(name = ${accountGroupName});

		if (isSet(accountGroupDescription)) {
			PortletEntry.inputDescription(description = ${accountGroupDescription});
		}

		Button.clickSave();

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro editCP(accountGroupName = null, accountGroupDescriptionEdit = null, accountGroupNameEdit = null) {
		AccountGroups.openAccountGroupsAdmin();

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = ${accountGroupName});

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Edit ${accountGroupName}");

		PortletEntry.inputName(name = ${accountGroupNameEdit});

		if (isSet(accountGroupDescriptionEdit)) {
			PortletEntry.inputDescription(description = ${accountGroupDescriptionEdit});
		}

		Button.clickSave();

		Alert.viewSuccessMessage();

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Edit ${accountGroupNameEdit}");
	}

	@summary = "Default summary"
	macro openAccountGroupsAdmin() {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/control_panel/manage?p_p_id=com_liferay_account_admin_web_internal_portlet_AccountGroupsAdminPortlet");
	}

	@summary = "Default summary"
	macro removeAccount(removeType = null, accountGroupName = null) {
		if (${removeType} == "Toolbar Remove Icon") {
			LexiconEntry.selectEntry(key_rowEntry = ${accountGroupName});

			ClickNoError(
				key_text = "times-circle",
				locator1 = "ManagementBar#ANY_ICON");

			AssertConfirm(value1 = "Are you sure you want to remove the selected accounts?");

			Alert.viewSuccessMessage();
		}
		else if (${removeType} == "Management Toolbar") {
			LexiconEntry.selectEntry(key_rowEntry = ${accountGroupName});

			Click(locator1 = "ManagementBar#ELLIPSIS");

			MenuItem.clickNoError(menuItem = "Remove");

			AssertConfirm(value1 = "Are you sure you want to remove the selected accounts?");

			Alert.viewSuccessMessage();
		}
		else {
			ClickNoError(
				key_entryName = ${accountGroupName},
				locator1 = "LexiconTable#TABLE_ENTRY_DELETE");

			AssertConfirm(value1 = "Are you sure you want to remove this account?");

			Alert.viewSuccessMessage();
		}

		LexiconEntry.viewNoEntry(rowEntry = ${accountGroupName});
	}

	@summary = "Default summary"
	macro viewAssignee(assigneeName = null) {
		AssertElementPresent(
			key_tab = "Accounts",
			locator1 = "NavTab#ACTIVE_TAB_LINK");

		AssertTextEquals.assertPartialText(
			key_tableEntry = ${assigneeName},
			locator1 = "LexiconTable#TABLE_ENTRY",
			value1 = ${assigneeName});
	}

	@summary = "Default summary"
	macro viewDefault() {
		if (IsElementPresent(key_tab = ${tabName}, locator1 = "NavTab#ACTIVE_TAB_LINK")) {
			AssertElementPresent(
				key_tab = ${tabName},
				locator1 = "NavTab#ACTIVE_TAB_LINK");
		}

		var tabName = StringUtil.toLowerCase(${tabName});

		AssertTextEquals.assertPartialText(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No ${tabName} were found.");
	}

	@summary = "Default summary"
	macro viewEntry(accountGroupDescription = null, accountGroupName = null, accountGroupAccount = null) {
		LexiconEntry.viewEntryName(rowEntry = ${accountGroupName});

		if (isSet(accountGroupDescription)) {
			AssertTextEquals(
				key_rowEntry = ${accountGroupName},
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_DESCRIPTION_TD3",
				value1 = ${accountGroupDescription});
		}

		if (isSet(accountGroupAccount)) {
			AssertTextEquals(
				key_columnName = "lfr-accounts-column",
				key_rowEntry = ${accountGroupName},
				locator1 = "ContentRow#ENTRY_CONTENT_ROW_NUMBER_TD",
				value1 = ${accountGroupAccount});
		}
	}

}