definition {

	@summary = "Default summary"
	macro addChildArticle(kbChildArticleContent = null, submitForWorkflow = null, saveAsDraft = null, kbChildArticleTitle = null, kbArticleTitle = null) {
		var key_rowEntry = ${kbArticleTitle};

		if (IsElementPresent(locator1 = "Icon#ROW_VERTICAL_ELLIPSIS")) {
			Click(locator1 = "Icon#ROW_VERTICAL_ELLIPSIS");
		}
		else {
			Click(locator1 = "Icon#HEADER_VERTICAL_ELLIPSIS");
		}

		MenuItem.click(menuItem = "Add Child Article");

		AssertElementPresent(locator1 = "TextInput#TITLE");

		Type(
			locator1 = "TextInput#TITLE",
			value1 = ${kbChildArticleTitle});

		CKEditor.addContent(content = ${kbChildArticleContent});

		if (${submitForWorkflow} == "true") {
			AssertClick(
				locator1 = "Button#SUBMIT_FOR_WORKFLOW",
				value1 = "Submit for Workflow");

			AssertClick(
				locator1 = "WorkflowKnowledgeBaseArticle#ARTICLE_PUBLISH_BUTTON",
				value1 = "Publish");
		}
		else if (${saveAsDraft} == "true") {
			PortletEntry.saveAsDraft();
		}
		else {
			PortletEntry.publish();
		}
	}

	@summary = "Default summary"
	macro editKBArticle(cancel = null, kbArticleTitleEdit = null, kbArticleContentEdit = null, submitForWorkflow = null, saveAsDraft = null, kbArticleAttachment = null, customFieldTextEdit = null, kbArticleTitle = null) {
		Click(
			key_rowEntry = ${kbArticleTitle},
			locator1 = "Icon#ROW_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "Edit");

		AssertTextEquals(
			locator1 = "Message#INFO",
			value1 = "A new version is created automatically if this content is modified.");

		AssertElementPresent(locator1 = "TextInput#TITLE");

		Type(
			locator1 = "TextInput#TITLE",
			value1 = ${kbArticleTitleEdit});

		if (isSet(kbArticleContentEdit)) {
			CKEditor.addContent(kbArticleContentEdit = ${kbArticleContentEdit});
		}

		if (isSet(kbArticleAttachment)) {
			KBArticle.selectAttachmentCP(kbArticleAttachment = ${kbArticleAttachment});
		}

		if (isSet(customFieldTextEdit)) {
			Panel.expandPanel(panel = "Custom Fields");

			Type(
				locator1 = "TextInput#CUSTOM_FIELD",
				value1 = ${customFieldTextEdit});
		}

		if (${cancel} == "true") {
			Button.clickCancel();
		}
		else if (${submitForWorkflow} == "true") {
			AssertClick(
				locator1 = "Button#SUBMIT_FOR_WORKFLOW",
				value1 = "Submit for Workflow");

			AssertClick(
				locator1 = "WorkflowKnowledgeBaseArticle#ARTICLE_PUBLISH_BUTTON",
				value1 = "Publish");
		}
		else if (${saveAsDraft} == "true") {
			PortletEntry.saveAsDraft();
		}
		else {
			PortletEntry.publish();
		}
	}

}