apply plugin: "com.liferay.maven.plugin.builder"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

buildPluginDescriptor {
	ext {
		autoClean = false
	}

	finalizedBy formatSource

	goalPrefix = "bundle-support"
	useSetterComments = false
}

configurations {
	mavenDistribution
}

copyCSS {
	mustRunAfter buildPluginDescriptor
}

dependencies {
	compileInclude group: "com.beust", name: "jcommander", version: "1.72"
	compileInclude group: "com.liferay", name: "org.apache.commons.logging", version: "1.2.LIFERAY-PATCHED-2"
	compileInclude group: "commons-codec", name: "commons-codec", version: "1.15"
	compileInclude group: "commons-io", name: "commons-io", version: "2.15.1"
	compileInclude group: "org.apache.commons", name: "commons-compress", version: "1.26.0"
	compileInclude group: "org.apache.httpcomponents", name: "httpclient", version: "4.5.13"
	compileInclude group: "org.apache.httpcomponents", name: "httpcore", version: "4.4.14"
	compileInclude group: "org.tukaani", name: "xz", version: "1.9"

	compileOnly group: "javax.annotation", name: "javax.annotation-api", version: "1.3.2"
	compileOnly group: "org.apache.ant", name: "ant", transitive: false, version: "1.10.14"
	compileOnly group: "org.apache.maven", name: "maven-core", transitive: false, version: "3.8.1"
	compileOnly group: "org.apache.maven", name: "maven-model", transitive: false, version: "3.8.1"
	compileOnly group: "org.apache.maven", name: "maven-plugin-api", transitive: false, version: "3.8.1"
	compileOnly group: "org.apache.maven", name: "maven-settings", transitive: false, version: "3.8.1"
	compileOnly group: "org.apache.maven.plugin-tools", name: "maven-plugin-annotations", transitive: false, version: "3.5"

	mavenDistribution classifier: "bin", ext: "zip", group: "org.apache.maven", name: "apache-maven", version: "3.3.9"

	testImplementation group: "com.liferay", name: "com.liferay.maven.executor", version: "1.0.2"
	testImplementation group: "com.sun.net.httpserver", name: "http", version: "20070405"
	testImplementation group: "org.apache.ant", name: "ant-testutil", version: "1.9.7"
	testImplementation group: "org.littleshoot", name: "littleproxy", version: "1.1.1"
}

liferayOSGi {
	expandCompileInclude = true
}

test {
	dependsOn publishToMavenLocal

	doFirst {
		systemProperty "maven.distribution.file.name", configurations.mavenDistribution.singleFile
	}

	systemProperty "http.nonProxyHosts", "localhost.localdomain"
	systemProperty "http.proxyHost", System.properties["http.proxyHost"]
	systemProperty "http.proxyPort", System.properties["http.proxyPort"]
	systemProperty "maven.debug", System.properties["maven.debug"]
	systemProperty "maven.repo.local", System.properties["maven.repo.local"]
	systemProperty "repository.url", System.properties["repository.url"]
	systemProperty "bundle.support.version", jar.archiveVersion.get()
}