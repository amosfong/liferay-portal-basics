@component-name = "portal-site-management"
definition {

	var baseURL = PropsUtil.get("portal.url");

	property custom.properties = "feature.flag.LPS-169981=true";
	property osgi.modules.includes = "osb-site-initializer-evp";
	property portal.release = "false";
	property portal.upstream = "true";
	property testray.main.component.name = "OSB Site Initializer EVP";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Enable private pages") {
			PagesAdmin.enablePrivatePages();
		}

		task ("Create a new EVP Site") {
			EVPUtils.addEVPSite();
		}

		task ("Add Liferay Manager Role to users can see Resquests and Organizations") {
			EVPUtils.addLiferayManagerUserRole();
		}

		task ("Create an organization") {
			var organizationId = JSONEVP.createOrganization(
				accountOwnerName = "User 4",
				bankingInfo = "Bank Test",
				city = "City Test",
				contactEmail = "OrganizationTest@liferay.com",
				contactName = "Contact Test",
				contactPhone = 12345,
				country = "Country Test",
				emailAddress = "employee@liferay.com",
				key_status = "awaitingApprovalOnEvp",
				organizationName = "Organization Test",
				organizationSite = "www.test.com.br",
				smallDescription = "Description Test",
				state = "State Test",
				status = "Awaiting Approval On EVP",
				street = "Street Test",
				taxId = 12345,
				zipCode = "12.345-67");

			EVPUtils.gotoEVP();
		}
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		ObjectAdmin.deleteAllCustomObjectsViaAPI();

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "EVP Portal");
		}
	}

	@description = "LPS-164733 - Verify that Only the Employee user can not see 'Review button' when organization are in 'Awaiting Approval On EVP' status"
	@priority = 5
	test EmployeeCanNotSeeReviewInAwaitingEVPStatus {
		task ("When the Employee user goes to organization page") {
			EVPNavigator.goToNavItem(navItem = "Organization");
		}

		task ("Then the employee user can not see Review option in kebab menu") {
			EVPOrganization.kebabMenuOrganization(organizationName = "Organization Test");

			MenuItem.viewNotPresent(menuItem = "Review");
		}
	}

	@description = "LPS-164733 - Verify that the Finance user can not see 'Review button' when organization are in 'Awaiting Approval On EVP' status"
	@priority = 5
	test FinanceCanNotSeeReviewInAwaitingEVPStatus {
		task ("Given that Finance user is logged") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "finance@liferay.com",
				userLoginFullName = "Finance User");
		}

		task ("When the finance user goes to organization page") {
			EVPUtils.gotoEVP();

			EVPNavigator.goToNavItem(navItem = "Organization");
		}

		task ("Then the finance user can not see Review option in kebab menu") {
			EVPOrganization.kebabMenuOrganization(organizationName = "Organization Test");

			MenuItem.viewNotPresent(menuItem = "Review");
		}
	}

	@description = "LPS-164733 - Verify that the EVP Manager can approve a new organization and see the comment in details page"
	@priority = 4
	test ManagerCanApproveOrganizationWithComment {
		task ("When Manager user approves the created organization") {
			EVPUtils.managerApproval(
				listingPage = "Organization",
				organizationName = "Organization Test");
		}

		task ("Then the status will be updated to 'Awaiting Finance Approval'") {
			EVPUtils.viewStatus(
				organizationName = "Organization Test",
				status = "Awaiting Finance Approval");
		}

		task ("When Manager user goes to details page") {
			EVPOrganization.kebabMenuOrganization(organizationName = "Organization Test");

			MenuItem.click(menuItem = "Details");
		}

		task ("Then the manager user can see the comment 'Your organization has been approved!' in the details page") {
			EVPUtils.assertHeadersTitle(listHeaderTitle = "Organization Test,Your organization has been approved!");
		}
	}

	@description = "LPS-164733 - Verify that the Manager can not see 'Review button' when organization is in 'Awaiting Finance Approval' status"
	@priority = 4
	test ManagerCannotSeeReviewButtonInAwaitingFinanceStatus {
		task ("Given that the created Organization has already been approved by Manager user") {
			EVPUtils.managerApproval(
				listingPage = "Organization",
				organizationName = "Organization Test");
		}

		task ("When the manager user clicks on kebab menu of an Organization with 'Awaiting Finance Approval' status") {
			EVPOrganization.kebabMenuOrganization(organizationName = "Organization Test");
		}

		task ("Then the manager user can't see the Review option") {
			MenuItem.viewNotPresent(menuItem = "Review");
		}
	}

	@description = "LPS-164733 - Verify that the EVP Staff can Reject a new organization"
	@priority = 5
	test ManagerCanRejectOrganization {
		task ("Given that Manager user is logged") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "manager@liferay.com",
				userLoginFullName = "Manager User");
		}

		task ("And go to organization page") {
			EVPUtils.gotoEVP();

			EVPNavigator.goToNavItem(navItem = "Organization");
		}

		task ("When the manager reject the organization created") {
			EVPOrganization.kebabMenuOrganization(organizationName = "Organization Test");

			MenuItem.click(menuItem = "Review");

			EVPUtils.changeStatus(
				button = "Reject",
				typeMessage = "Your organization has been Reject");
		}

		task ("Then the status will be updated to 'Rejected'") {
			EVPUtils.viewStatus(
				organizationName = "Organization Test",
				status = "Rejected");
		}
	}

}