@component-name = "portal-site-management"
definition {

	var baseURL = PropsUtil.get("portal.url");

	property custom.properties = "feature.flag.LPS-169981=true";
	property osgi.modules.includes = "osb-site-initializer-evp";
	property portal.release = "false";
	property portal.upstream = "true";
	property testray.main.component.name = "OSB Site Initializer EVP";

	static var requestValue = 100;
	static var totalFunds = 1000;

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Enable private pages") {
			PagesAdmin.enablePrivatePages();
		}

		task ("Create a new EVP Site") {
			EVPUtils.addEVPSite();
		}

		task ("Add Liferay Manager Role to users can see Resquests and Organizations") {
			EVPUtils.addLiferayManagerUserRole();
		}

		task ("Navigates to EVP Site and Create a Organization and a Request") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "employee@liferay.com",
				userLoginFullName = "Employee User");

			EVPUtils.gotoEVP();

			var organizationId = JSONEVP.createOrganization(
				accountOwnerName = "User 4",
				bankingInfo = "Bank Test",
				city = "City Test",
				contactEmail = "OrganizationTest@liferay.com",
				contactName = "Contact Test",
				contactPhone = 12345,
				country = "Country Test",
				emailAddress = "employee@liferay.com",
				key_status = "awaitingApprovalOnEvp",
				organizationName = "Organization Test",
				organizationSite = "www.test.com.br",
				smallDescription = "Description Test",
				state = "State Test",
				status = "Awaiting Approval On EVP",
				street = "Street Test",
				taxId = 12345,
				zipCode = "12.345-67");

			JSONEVP.createRequest(
				emailAddress = "employee@liferay.com",
				fullName = "Employee User",
				grantAmount = ${requestValue},
				grantRequestType = "Proposed",
				grantRequestTypeKey = "proposed",
				messageEVPManager = "Test",
				organizationId = ${organizationId},
				phoneNumber = 12345,
				requestDescription = "Description Test",
				requestStatus = "Awaiting Approval On Evp",
				requestStatusKey = "awaitingApprovalOnEvp",
				requestType = "Grant");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		ObjectAdmin.deleteAllCustomObjectsViaAPI();

		if (${testPortalInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "EVP Portal");
		}
	}

	@description = "LPS-164742 - Finance user can cancel the Payment Confirmation"
	@priority = 3
	test CanCancelPaymentConfirmation {
		task ("Given there is a created request in 'Awaiting Payment Confirmation' status") {
			EVPRequest.changeStatusToAwaitingPaymentConfirmation();
		}

		task ("And the Finance user is in the Payment Confirmation page") {
			EVPRequest.goToRequestOption(
				key_option = "Pay",
				organizationName = "Organization Test");

			EVPNavigator.assertActiveBreadcrumb(activeItem = "Payment Confirmation");
		}

		task ("When the Finance user clicks on Cancel Button") {
			EVPRequest.confirmPayment(
				cancelButton = "true",
				dateFormat = "MMddYYYY",
				dateRange = 0,
				document = "Document_1.pdf",
				paymentDate = "true",
				paymentValue = 100);
		}

		task ("Then Finance user can see that the Payment Confirmation was not saved and the status is still 'Awaiting Payment Confirmation'") {
			EVPUtils.viewStatus(
				organizationName = "Organization Test",
				status = "Awaiting Payment Confirmation");
		}
	}

	@description = "LPS-164742 - Finance user should Confirm Payment on requests that are on 'Awaiting Payment Confirmation' Status and assert payment date on details page"
	@priority = 5
	test CanConfirmPayment {
		task ("Given there is a created request in 'Awaiting Payment Confirmation' status") {
			EVPRequest.changeStatusToAwaitingPaymentConfirmation();
		}

		task ("When the Finance user do the Payment Confirmation") {
			EVPRequest.goToRequestOption(
				key_option = "Pay",
				organizationName = "Organization Test");

			EVPNavigator.assertActiveBreadcrumb(activeItem = "Payment Confirmation");

			for (var fieldName : list "Request ID,EVP Payment Date,Payment Value,Upload File") {
				EVPRequest.verifyFieldsName(key_field = ${fieldName});
			}

			EVPRequest.confirmPayment(
				dateFormat = "MMddYYYY",
				dateRange = 0,
				document = "Document_1.pdf",
				paymentDate = "true",
				paymentValue = 100);
		}

		task ("Then Finance user can see that the status change to 'Closed'") {
			EVPNavigator.goToNavItem(navItem = "Request");

			EVPUtils.viewStatus(
				organizationName = "Organization Test",
				status = "Closed");
		}

		task ("When Finance user goes to Request details page") {
			EVPRequest.goToRequestOption(
				key_option = "Details",
				organizationName = "Organization Test");
		}

		task ("Then Finance user can assert the Payment Confirmation date in details page") {
			EVPRequest.viewRequestDetails(paymentDate = ${paymentDate});
		}
	}

	@description = "LPS-180356 - Verify that  the 'phone number' field is enabled  if it's not registered and it is possible to fill this"
	@priority = 3
	test IsPossibleFillPhoneNumberField {
		task ("Given the Employee user doesn't have a phone number registered") {
			EVPUtils.gotoContactInformation(
				category = "User",
				contactUser = "Contact",
				enterUser = "Employee User",
				inforUser = "Contact Information",
				panel = "Control Panel",
				portlet = "Users and Organizations");

			EVPUtils.assertContactInformation(inforMessage = "This user does not have any phone numbers");
		}

		task ("When Employee user goes to Create Request Form") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "employee@liferay.com",
				userLoginFullName = "Employee User");

			EVPUtils.gotoEVP();

			EVPRequest.goToCreateRequest();
		}

		task ("Then the 'phone number' field is enabled and is possible to fill this") {
			EVPUtils.addUserPhoneNumber(
				assertNumber = 1234567890,
				key_number = 1234567890);
		}

		task ("Given the Employee user has a phone number registered") {
			EVPUtils.gotoContactInformation(
				category = "User",
				contactUser = "Contact",
				enterUser = "Employee User",
				inforUser = "Contact Information",
				panel = "Control Panel",
				portlet = "Users and Organizations");

			User.addContactInformation(
				contactType = "add-phone-number-link",
				number = 1234567890);
		}

		task ("When Employee user goes to Create Request Form") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "employee@liferay.com",
				userLoginFullName = "Employee User");

			EVPUtils.gotoEVP();

			EVPRequest.goToCreateRequest();
		}

		task ("Then the 'phone number' field is disabled and it's not possible to fill this") {
			EVPUtils.assertNotAllowed(notAllowed = "Phone Number");
		}
	}

	@description = "LPS-177444 - Verify that 'In flow' requests consume balance"
	@priority = 5
	test VerifyInFlowRequestsConsumeCap {
		task ("When Employee user checks how much is the consume cap") {
			EVPNavigator.goToNavItem(navItem = "Request");

			EVPRequest.goToCreateRequest();

			var availableFunds = ${totalFunds} - ${requestValue};

			AssertTextEquals.assertPartialText(
				key_value = ${availableFunds},
				locator1 = "EVPRequest#AVAILABLE_VALUE",
				value1 = ${availableFunds});
		}

		task ("And Employee user creates some requests") {
			for (var requestNum : list "1,2,3") {
				EVPRequest.goToCreateRequest();

				EVPRequest.fillRequestForm(
					grantAmount = ${requestValue},
					grantRequestType = "Proposed",
					organization = ${organizationId},
					phoneNumber = 12345,
					requestBehalf = "Global/Local Poor",
					requestDescription = "Request Description Test ${requestNum}",
					requestPurposes = "Basic Education",
					requestType = "Grant");
			}
		}

		task ("Then Employee user can see that all in flow requests are cosuming the cap") {
			var totalRequested = 6 * ${requestValue};

			var newAvailableFunds = ${availableFunds} - ${totalRequested};

			EVPNavigator.goToNavItem(navItem = "Request");

			EVPRequest.goToCreateRequest();

			AssertTextEquals.assertPartialText(
				key_value = ${newAvailableFunds},
				locator1 = "EVPRequest#AVAILABLE_VALUE",
				value1 = ${newAvailableFunds});
		}
	}

	@description = "LPS-181769 - Verify that a request of funds has 'Awaiting Approval on EVP' status when it's created"
	@priority = 5
	test VerifyStatusAfterCreatRequest {
		task ("When The employee user creates a funds request") {
			EVPUtils.gotoEVP();

			EVPRequest.goToCreateRequest();

			EVPRequest.fillRequestForm(
				grantAmount = 10,
				grantRequestType = "Proposed",
				organization = "Organization Test",
				phoneNumber = 12345,
				requestBehalf = "Global/Local Poor",
				requestDescription = "Request Description Test",
				requestPurposes = "Basic Education",
				requestType = "Grant");
		}

		task ("Then the Employee user can see the status on request listing") {
			EVPNavigator.goToNavItem(navItem = "Request");

			EVPUtils.viewStatus(
				organizationName = "Organization Test",
				status = "Awaiting Approval On EVP");
		}
	}

	@description = "LPS-164742 - The uploaded files are being stored on DM"
	@priority = 3
	test VerifyUploadedFileOnDM {
		task ("Given there is a created request in 'Awaiting Payment Confirmation' status") {
			EVPRequest.changeStatusToAwaitingPaymentConfirmation();
		}

		task ("When the Finance user do the Payment Confirmation") {
			EVPRequest.goToRequestOption(
				key_option = "Pay",
				organizationName = "Organization Test");

			EVPNavigator.assertActiveBreadcrumb(activeItem = "Payment Confirmation");

			for (var fieldName : list "Request ID,EVP Payment Date,Payment Value,Upload File") {
				EVPRequest.verifyFieldsName(key_field = ${fieldName});
			}

			EVPRequest.confirmPayment(
				dateFormat = "MMddYYYY",
				dateRange = 0,
				document = "Document_1.pdf",
				paymentDate = "true",
				paymentValue = 100);
		}

		task ("Then the Finance user can see the approved document in Documents and Media") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "test@liferay.com",
				userLoginFullName = "Test Test");

			ProductMenu.gotoPortlet(
				category = "Content & Data",
				portlet = "Documents and Media");

			DMNavigator.gotoFolder(dmFolderName = "EVPPaymentConfirmation");

			DMDocument.viewStatus(
				dmDocumentStatus = "Approved",
				dmDocumentTitle = "Document_1");
		}
	}

}