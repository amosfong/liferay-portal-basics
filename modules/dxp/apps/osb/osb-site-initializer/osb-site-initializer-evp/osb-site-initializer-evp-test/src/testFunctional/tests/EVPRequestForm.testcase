@component-name = "portal-site-management"
definition {

	var baseURL = PropsUtil.get("portal.url");

	property custom.properties = "feature.flag.LPS-169981=true";
	property osgi.modules.includes = "osb-site-initializer-evp";
	property portal.release = "false";
	property portal.upstream = "true";
	property testray.main.component.name = "OSB Site Initializer EVP";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Enable private pages") {
			PagesAdmin.enablePrivatePages();
		}

		task ("Create a new EVP Site") {
			EVPUtils.addEVPSite();
		}

		task ("Login with the Employee user and go to EVP Portal") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "employee@liferay.com",
				userLoginFullName = "Employee User");

			EVPUtils.gotoEVP();
		}

		task ("Add a new organization") {
			var organizationId = JSONEVP.createOrganization(
				accountOwnerName = "User 4",
				bankingInfo = "Bank Test",
				city = "City Test",
				contactEmail = "OrganizationTest@liferay.com",
				contactName = "Contact Test",
				contactPhone = 12345,
				country = "Country Test",
				emailAddress = "employee@liferay.com",
				key_status = "awaitingApprovalOnEvp",
				organizationName = "Organization test",
				organizationSite = "www.test.com.br",
				smallDescription = "Description Test",
				state = "State Test",
				status = "Awaiting Approval On EVP",
				street = "Street Test",
				taxId = 12345,
				zipCode = "12.345-67");
		}

		task ("Go to create request page") {
			EVPRequest.goToCreateRequest();
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		ObjectAdmin.deleteAllCustomObjectsViaAPI();

		if (${testPortalInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "EVP Portal");
		}
	}

	@description = "LPS-162902 - Verify that the user can cancel the request by clicking on 'Cancel' button"
	@priority = 3
	test CanCancelRequestCreation {
		task ("When the Employee user clicks on the 'Cancel' button") {
			Button.clickCancel();
		}

		task ("Then the Employee user can see that the request is not present in the list") {
			EVPUtils.viewNotPresentStatus(
				organizationName = "Organization Test",
				status = "Awaiting Approval On EVP");
		}
	}

	@description = "LPS-162902 - Verify that can create a Request by clicking on 'Create a Request'"
	@priority = 4
	test CanCreateRequestForm {
		task ("Given that the Employee user creates a new Request") {
			EVPRequest.fillRequestForm(
				grantAmount = 10,
				grantRequestType = "Proposed",
				organization = ${organizationId},
				phoneNumber = 12345,
				requestBehalf = "Global/Local Poor",
				requestDescription = "Request Description Test",
				requestPurposes = "Basic Education",
				requestType = "Grant");
		}

		task ("When Employee user goes to the request page") {
			EVPNavigator.goToNavItem(navItem = "Request");
		}

		task ("Then Employee user see the created request no request listing page") {
			EVPUtils.viewStatus(
				organizationName = "Organization Test",
				status = "Awaiting Approval On EVP");
		}
	}

	@description = "LPS-162902 - Verify that can not submit the request form without fill all required fields"
	@priority = 3
	test CannotSubmitFormWithoutRequiredFields {
		task ("When the Employee user clicks on submit button in the request form without filling in any required field") {
			Button.click(button = "Submit");
		}

		task ("Then the required field message appears") {
			AssertAttributeValue(
				key_text = "Phone Number",
				locator1 = "TextInput#ANY",
				value1 = "required",
				value2 = "true");
		}
	}

}