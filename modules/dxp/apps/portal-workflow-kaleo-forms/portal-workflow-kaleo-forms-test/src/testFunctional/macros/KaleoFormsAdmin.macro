definition {

	@summary = "Default summary"
	macro addFieldSet(kfFieldSetName = null) {
		if (IsElementPresent(locator1 = "Icon#HEADER_VERTICAL_ELLIPSIS")) {
			KaleoFormsNavigator.gotoManageFieldSets();

			LexiconEntry.gotoAdd();

			PortletEntry.inputName(name = ${kfFieldSetName});
		}
		else {
			KaleoFormsAdmin.viewNewProcessProgressBar(
				kfProgressName = "Fields",
				kfProgressStep = 2);

			AssertClick(
				locator1 = "KaleoFormsAdminEditProcess#ADD_FIELD_SET_BUTTON",
				value1 = "Add Field Set");

			SelectFrame(locator1 = "IFrame#MODAL_BODY");

			PortletEntry.inputName(name = ${kfFieldSetName});
		}
	}

	@summary = "Default summary"
	macro addForm(kfFormName = null) {
		SetWindowSize.maximizeWindow();

		SelectFrame(locator1 = "DDMEditStructure#IFRAME");

		LexiconEntry.gotoAddNoSelectFrame();

		Click(locator1 = "DDMEditStructure#NAME_FIELD");

		PortletEntry.inputName(name = ${kfFormName});

		for (var field : list "Fieldset,Paragraph,Separator") {
			DynamicDataMapping.viewCanAddField(field = ${field});
		}
	}

	@summary = "Default summary"
	macro addProcess() {
		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Kaleo Forms Admin");

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Kaleo Forms Admin");

		LexiconEntry.gotoAdd();
	}

	@summary = "Default summary"
	macro addProcessDetails(kfProcessName = null,kfProcessDescription = null) {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Details",
			kfProgressStep = 1);

		AssertTextEquals(
			locator1 = "KaleoFormsAdminEditProcess#NEXT_BUTTON_INACTIVE",
			value1 = "Next");

		var key_fieldName = "name";

		Type(
			locator1 = "FormFields#TEXT_FIELD",
			value1 = ${kfProcessName});

		KaleoFormsAdmin.editDescription(
			fieldName = "Description",
			fieldValue = ${kfProcessDescription});
	}

	@summary = "Default summary"
	macro addWorkflow(workflowDefinitionTitle = null) {
		SetWindowSize.maximizeWindow();

		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Workflow",
			kfProgressStep = 3);

		AssertClick(
			locator1 = "KaleoFormsAdminEditProcess#ADD_WORKFLOW_BUTTON",
			value1 = "Add Workflow");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		PortletEntry.inputTitle(title = ${workflowDefinitionTitle});
	}

	@summary = "Default summary"
	macro assertPaginationBarNotPresent() {
		AssertElementNotPresent(locator1 = "Pagination#PAGINATION_BAR");
	}

	@summary = "Default summary"
	macro assertPaginationBarPresent() {
		AssertElementPresent(locator1 = "Pagination#PAGINATION_BAR");
	}

	@summary = "Default summary"
	macro assignForm(workflowTask = null,kfPendingForm = null) {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Forms",
			kfProgressStep = 4);

		if (${kfPendingForm} == "true") {
			AssertTextEquals(
				locator1 = "Message#INFO",
				value1 = "Updating the field set or workflow will cause loss of data.");
		}

		WaitForLiferayEvent.initializeLiferayEventLog();

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Assign Form",
			rowEntry = ${workflowTask});
	}

	@summary = "Default summary"
	macro assignNewForm(workflowTask = null,kfFormNameNew = null,kfProcessName = null) {
		KaleoFormsAdmin.editProcess(kfProcessName = ${kfProcessName});

		KaleoFormsAdmin.gotoProgressStep(kfProgressName = "Forms");

		KaleoFormsAdmin.unassignForm(workflowTask = ${workflowTask});

		KaleoFormsAdmin.assignForm(workflowTask = ${workflowTask});

		KaleoFormsAdmin.addForm(kfFormName = ${kfFormNameNew});

		KaleoFormsAdmin.saveForm();

		KaleoFormsAdmin.chooseForm(kfFormName = ${kfFormNameNew});

		KaleoFormsAdmin.saveProcess();
	}

	@summary = "Default summary"
	macro cancelProcessRecordDetailsView() {
		AssertClick(
			locator1 = "Button#CANCEL",
			value1 = "Cancel");
	}

	@summary = "Default summary"
	macro checkboxesInProcessTable(checkProcessName = null) {
		Check(
			key_checkboxOption = ${checkProcessName},
			locator1 = "Checkbox#SPECIFIC_CHECKBOX_IN_TABLE");
	}

	@summary = "Default summary"
	macro chooseFieldSet(kfFieldSetName = null,kfPendingForm = null) {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Fields",
			kfProgressStep = 2);

		var key_kfFieldSetName = ${kfFieldSetName};

		if (${kfPendingForm} == "true") {
			AssertTextEquals(
				locator1 = "Message#INFO",
				value1 = "Updating the field set or workflow will cause loss of data.");
		}

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Choose",
			rowEntry = ${kfFieldSetName});

		AssertTextEquals(
			locator1 = "KaleoFormsAdminEditProcess#SELECTED_FIELD_SET",
			value1 = ${kfFieldSetName});
	}

	@summary = "Default summary"
	macro chooseForm(kfFormName = null) {
		DynamicDataMapping.selectDynamicDataMappingFrame();

		var key_kfFormName = ${kfFormName};

		AssertClick(
			locator1 = "KaleoFormsAdminEditProcessForm#FORM_TABLE_NAME_LINK",
			value1 = ${kfFormName});

		SelectFrame(value1 = "relative=top");

		WaitForLiferayEvent.initializeLiferayEventLog();
	}

	@summary = "Default summary"
	macro chooseWorkflow(workflowDefinitionTitleEdit = null,workflowDefinitionTitle = null,edit = null,kfPendingForm = null) {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Workflow",
			kfProgressStep = 3);

		Refresh();

		if (${kfPendingForm} == "true") {
			AssertTextEquals(
				locator1 = "Message#INFO",
				value1 = "Updating the field set or workflow will cause loss of data.");
		}

		if (isSet(workflowDefinitionTitleEdit)) {
			var key_workflowDefinitionTitle = ${workflowDefinitionTitleEdit};

			AssertTextEquals(
				locator1 = "KaleoFormsAdminEditProcess#WORKFLOW_TABLE_TITLE",
				value1 = ${workflowDefinitionTitleEdit});
		}
		else {
			var key_workflowDefinitionTitle = ${workflowDefinitionTitle};

			AssertTextEquals(
				locator1 = "KaleoFormsAdminEditProcess#WORKFLOW_TABLE_TITLE",
				value1 = ${workflowDefinitionTitle});
		}

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Choose",
			rowEntry = ${workflowDefinitionTitle});

		KaleoFormsAdmin.viewSelectedWorkflow(
			edit = ${edit},
			workflowDefinitionTitle = ${workflowDefinitionTitle});
	}

	@summary = "Default summary"
	macro closeWorkflowModal() {
		SelectFrame(value1 = "relative=top");

		if (IsElementPresent(locator1 = "IFrame#MODAL_BODY")) {
			Click(locator1 = "Modal#CLOSE_BUTTON");
		}

		WaitForLiferayEvent.initializeLiferayEventLog();
	}

	@summary = "Default summary"
	macro createKaleoFormsProcess(kfFieldSetName = null,workflowDefinitionTitle = null,kfFormName = null,workflowTask = null,kfProcessName = null,kfProcessDescription = null) {
		KaleoFormsAdmin.addProcessDetails(
			kfProcessDescription = ${kfProcessDescription},
			kfProcessName = ${kfProcessName});

		KaleoFormsAdmin.next();

		KaleoFormsAdmin.chooseFieldSet(
			kfFieldSetName = ${kfFieldSetName},
			kfProcessName = ${kfProcessName});

		KaleoFormsAdmin.next();

		KaleoFormsAdmin.chooseWorkflow(
			kfProcessName = ${kfProcessName},
			workflowDefinitionTitle = ${workflowDefinitionTitle});

		KaleoFormsAdmin.next();

		KaleoFormsAdmin.assignForm(
			kfProcessName = ${kfProcessName},
			workflowTask = ${workflowTask});

		KaleoFormsAdmin.addForm(kfFormName = ${kfFormName});

		KaleoFormsAdmin.saveForm();

		KaleoFormsAdmin.chooseForm(kfFormName = ${kfFormName});

		KaleoFormsAdmin.saveProcess(kfProcessName = ${kfProcessName});
	}

	@summary = "Default summary"
	macro deactivateWorkflow(workflowDefinitionTitle = null) {
		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Unpublish",
			rowEntry = ${workflowDefinitionTitle});

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro deleteForm(kfFieldSetName = null,kfFormName = null) {
		KaleoFormsNavigator.gotoManageForms(kfFieldSetName = ${kfFieldSetName});

		Search.searchCP(searchTerm = ${kfFormName});

		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Delete",
			rowEntry = ${kfFormName});

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro deleteProcess(kfProcessName = null) {
		if (isSet(kfProcessName)) {
			var key_rowEntry = ${kfProcessName};

			Click(locator1 = "Icon#ROW_VERTICAL_ELLIPSIS");
		}
		else {
			Click(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS");
		}

		MenuItem.clickNoError(menuItem = "Delete");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		if (IsElementNotPresent(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS")) {
			KaleoFormsAdmin.noProcessesWereFound();
		}
		else {
			AssertTextNotPresent(
				locator1 = "KaleoFormsAdmin#PROCESS_TABLE_NAME",
				value1 = ${kfProcessName});
		}
	}

	@summary = "Default summary"
	macro deleteProcessRecord(kfProcessFieldData = null) {
		var key_rowEntry = ${kfProcessFieldData};

		Click(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS");

		MenuItem.clickNoError(menuItem = "Delete");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();

		KaleoFormsAdmin.viewProcessRecordTableRecordNotPresent(kfProcessFieldData = ${kfProcessFieldData});
	}

	@summary = "Default summary"
	macro deleteViaDeleteIcon() {
		ClickNoError(
			key_text = "trash",
			locator1 = "ManagementBar#ANY_ICON");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro deleteWorkflowDraft(workflowDefinitionTitle = null) {
		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Delete",
			rowEntry = ${workflowDefinitionTitle});

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro editDescription(fieldName = null,fieldValue = null) {
		WaitForPageLoad();

		Type(
			key_fieldName = ${fieldName},
			locator1 = "KaleoFormsAdmin#DESCRIPTION_FIELD",
			value1 = ${fieldValue});
	}

	@summary = "Default summary"
	macro editFieldSet(kfFieldSetName = null,kfFieldSetNameEdit = null) {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfFieldSetName = ${kfFieldSetName},
			kfProgressName = "Fields",
			kfProgressStep = 2);

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = ${kfFieldSetName});

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		PortletEntry.inputName(name = ${kfFieldSetNameEdit});
	}

	@summary = "Default summary"
	macro editFieldSetPagination(paginationNumber = null) {
		Click(locator1 = "KaleoFormsAdminEditProcess#FIELD_SET_PAGINATION_SELECTOR_BUTTON");

		var key_paginationNumber = ${paginationNumber};

		MenuItem.click(menuItem = ${paginationNumber});

		AssertTextEquals.assertPartialText(
			locator1 = "KaleoFormsAdminEditProcess#FIELD_SET_PAGINATION_SELECTOR_BUTTON",
			value1 = "${paginationNumber} Entries");
	}

	@summary = "Default summary"
	macro editForm(kfFormNameEdit = null,kfFormName = null) {
		var key_kfFormName = ${kfFormName};

		AssertClick(
			locator1 = "KaleoFormsAdminEditProcessForm#FORM_TABLE_NAME",
			value1 = ${kfFormName});

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit Form",
			rowEntry = ${kfFormName});

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		PortletEntry.inputName(name = ${kfFormNameEdit});

		for (var field : list "Fieldset,Paragraph,Separator") {
			DynamicDataMapping.viewCanAddField(field = ${field});
		}
	}

	@summary = "Default summary"
	macro editProcess(kfProcessName = null) {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = ${kfProcessName});
	}

	@summary = "Default summary"
	macro editProcessDetails(kfProcessDescriptionEdit = null,kfPendingForm = null,kfProcessNameEdit = null) {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Details",
			kfProgressStep = 1);

		if (isSet(kfProcessNameEdit)) {
			var key_fieldName = "name";

			Type(
				locator1 = "FormFields#TEXT_FIELD",
				value1 = ${kfProcessNameEdit});
		}

		if (isSet(kfProcessDescriptionEdit)) {
			KaleoFormsAdmin.editDescription(
				fieldName = "Description",
				fieldValue = ${kfProcessDescriptionEdit});
		}

		if (${kfPendingForm} == "true") {
			AssertTextEquals(
				locator1 = "Message#INFO",
				value1 = "Updating the field set or workflow will cause loss of data.");
		}
	}

	@summary = "Default summary"
	macro editWorkflow(workflowDefinitionTitleEdit = null,workflowDefinitionTitle = null) {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Workflow",
			kfProgressStep = 3);

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = ${workflowDefinitionTitle});

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		PortletEntry.inputTitle(title = ${workflowDefinitionTitleEdit});
	}

	@summary = "Default summary"
	macro editWorkflowDraft(workflowDefinitionTitle = null) {
		var key_workflowDefinitionTitle = ${workflowDefinitionTitle};

		AssertTextEquals(
			locator1 = "KaleoFormsAdminEditProcess#WORKFLOW_TABLE_TITLE",
			value1 = ${workflowDefinitionTitle});

		WaitForLiferayEvent.initializeLiferayEventLog();

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = ${workflowDefinitionTitle});
	}

	@summary = "Default summary"
	macro exportRecords(fileExtension = null,approvedOnly = null,kfProcessName = null) {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Export",
			rowEntry = ${kfProcessName});

		Select(
			locator1 = "KaleoFormsAdminViewProcessRecords#EXPORT_DIALOG_BOX_FILE_EXTENSION_SELECT",
			value1 = ${fileExtension});

		if (${approvedOnly} == "true") {
			Check(locator1 = "KaleoFormsAdminViewProcessRecords#EXPORT_DIALOG_BOX_APPROVED_SUBMISSIONS_CHECKBOX");
		}

		AssertClick(
			locator1 = "KaleoFormsAdminViewProcessRecords#EXPORT_DIALOG_BOX_OK_BUTTON",
			value1 = "OK");

		DownloadTempFile();
	}

	@summary = "Default summary"
	macro gotoProgressStep(kfProgressName = null) {
		var key_kfProgressName = ${kfProgressName};

		Click(locator1 = "KaleoFormsAdminEditProcess#FORM_NAVIGATOR_PROGRESS_BAR_NAME");
	}

	@summary = "Default summary"
	macro next() {
		AssertElementNotPresent(locator1 = "KaleoFormsAdminEditProcess#NEXT_BUTTON_INACTIVE");

		AssertClick(
			locator1 = "Button#NEXT",
			value1 = "Next");
	}

	@summary = "Default summary"
	macro noProcessesWereFound() {
		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No processes were found.");
	}

	@summary = "Default summary"
	macro populateFieldDate(fieldName = null,kfProcessFieldData = null) {
		var key_fieldName = ${fieldName};

		Type(
			locator1 = "TextInput#DDM",
			value1 = ${kfProcessFieldData});

		Click(locator1 = "TextInput#DDM");
	}

	@summary = "Default summary"
	macro populateFieldSelect(fieldName = null,kfProcessFieldData = null) {
		var key_fieldName = ${fieldName};

		Select(
			locator1 = "DDMField#SELECT",
			value1 = ${kfProcessFieldData});
	}

	@summary = "Default summary"
	macro populateFieldText(fieldName = null,kfProcessFieldData = null) {
		var key_fieldName = ${fieldName};

		Type(
			locator1 = "TextInput#DDM",
			value1 = ${kfProcessFieldData});
	}

	@summary = "Default summary"
	macro populateFieldTextBox(kfProcessFieldData = null) {
		var fieldName = StringUtil.replace(${fieldName}, " ", "_");

		var key_fieldName = ${fieldName};

		Type(
			locator1 = "DDLEditRecord#TEXT_BOX_FIELD",
			value1 = ${kfProcessFieldData});
	}

	@summary = "Default summary"
	macro publishWorkflow() {
		Click(locator1 = "Button#PUBLISH");

		Alert.viewSuccessMessage();

		SelectFrame(value1 = "relative=top");

		if (IsElementPresent(locator1 = "IFrame#MODAL_BODY")) {
			Click(locator1 = "Modal#CLOSE_BUTTON");
		}

		WaitForLiferayEvent.initializeLiferayEventLog();
	}

	@summary = "Default summary"
	macro reactivateInactiveWorkflowDefinition(workflowDefinitionTitle = null) {
		WaitForLiferayEvent.initializeLiferayEventLog();

		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Edit",
			rowEntry = ${workflowDefinitionTitle});

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		KaleoFormsAdmin.publishWorkflow();
	}

	@summary = "Default summary"
	macro saveAndContinueForm() {
		DynamicDataMapping.selectDynamicDataMappingFrame();

		AssertClick(
			locator1 = "Button#SAVE_AND_CONTINUE",
			value1 = "Save and Continue");

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro saveFieldSet() {
		DDLDataDefinition.save();

		SelectFrame(value1 = "relative=top");

		if (IsElementPresent(locator1 = "IFrame#MODAL_BODY")) {
			Click(locator1 = "KaleoFormsAdmin#CLOSE_BUTTON");
		}

		WaitForLiferayEvent.initializeLiferayEventLog();
	}

	@summary = "Default summary"
	macro saveForm() {
		DynamicDataMapping.selectDynamicDataMappingFrame();

		Workflow.saveDefinition();
	}

	@summary = "Default summary"
	macro saveFormEdit(kfFormName = null) {
		DynamicDataMapping.selectDynamicDataMappingFrame();

		PortletEntry.save();

		SelectFrame(value1 = "relative=top");

		Click(locator1 = "Modal#CLOSE_BUTTON");

		var key_kfFormName = ${kfFormName};

		AssertTextEquals(
			locator1 = "KaleoFormsAdminEditProcessForm#FORM_TABLE_NAME",
			value1 = ${kfFormName});
	}

	@summary = "Default summary"
	macro saveProcess() {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Forms",
			kfProgressStep = 4);

		if (IsElementNotPresent(key_text = "Save", locator1 = "Button#ANY")) {
			Refresh();
		}

		Button.clickSave();
	}

	@summary = "Default summary"
	macro saveProcessRecord() {
		PortletEntry.save();
	}

	@summary = "Default summary"
	macro submitNewProcess(kfProcessName = null) {
		KaleoFormsAdmin.viewProcessRecord(kfProcessName = ${kfProcessName});

		LexiconEntry.gotoAdd();
	}

	@summary = "Default summary"
	macro tearDownProcess() {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Kaleo Forms Admin");

		while (IsElementPresent(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS")) {
			KaleoFormsAdmin.deleteProcess();
		}

		KaleoFormsAdmin.noProcessesWereFound();
	}

	@summary = "Default summary"
	macro unassignForm(workflowTask = null) {
		KaleoFormsAdmin.gotoProgressStep(kfProgressName = "Forms");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Unassign Form",
			rowEntry = ${workflowTask});
	}

	@summary = "Default summary"
	macro updateWorkflow() {
		Click.mouseDownMouseUp(
			key_text = "Update",
			locator1 = "Button#ANY");

		SelectFrame(value1 = "relative=top");

		Click(locator1 = "Modal#CLOSE_BUTTON");
	}

	@summary = "Default summary"
	macro viewExportRecords(kfProcessName = null) {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Export",
			rowEntry = ${kfProcessName});

		AssertElementPresent(locator1 = "KaleoFormsAdminViewProcessRecords#EXPORT_TABLE_MODAL");

		AssertElementPresent(locator1 = "KaleoFormsAdminViewProcessRecords#EXPORT_DIALOG_BOX_FILE_EXTENSION_SELECT");
	}

	@summary = "Default summary"
	macro viewFieldRequired(kfFieldSetName = null,workflowDefinitionTitle = null,kfFormName = null,progressTitle = null,workflowTask = null,kfProcessName = null) {
		if (${progressTitle} == "Details") {
			KaleoFormsAdmin.viewNewProcessProgressBar(
				kfProgressName = ${progressTitle},
				kfProgressStep = 1);

			AssertElementPresent(locator1 = "KaleoFormsAdminEditProcess#FIELD_NAME_REQUIRED");

			AssertElementPresent(locator1 = "KaleoFormsAdminEditProcess#NEXT_BUTTON_INACTIVE");

			Type(
				key_fieldName = "name",
				locator1 = "FormFields#TEXT_FIELD",
				value1 = ${kfProcessName});

			AssertElementPresent(locator1 = "Button#NEXT");
		}
		else if (${progressTitle} == "Fields") {
			KaleoFormsAdmin.viewNewProcessProgressBar(
				kfProgressName = ${progressTitle},
				kfProgressStep = 2);

			AssertElementPresent(locator1 = "KaleoFormsAdminEditProcess#NEXT_BUTTON_INACTIVE");

			KaleoFormsAdmin.chooseFieldSet(
				kfFieldSetName = ${kfFieldSetName},
				kfProcessName = ${kfProcessName});

			AssertElementPresent(locator1 = "Button#NEXT");
		}
		else if (${progressTitle} == "Workflow") {
			KaleoFormsAdmin.viewNewProcessProgressBar(
				kfProgressName = ${progressTitle},
				kfProgressStep = 3);

			AssertElementPresent(locator1 = "KaleoFormsAdminEditProcess#NEXT_BUTTON_INACTIVE");

			KaleoFormsAdmin.chooseWorkflow(
				kfProcessName = ${kfProcessName},
				workflowDefinitionTitle = ${workflowDefinitionTitle});

			AssertElementPresent(locator1 = "Button#NEXT");
		}
		else {
			KaleoFormsAdmin.viewNewProcessProgressBar(
				kfProgressName = ${progressTitle},
				kfProgressStep = 4);

			AssertElementPresent(locator1 = "KaleoFormsAdminEditProcess#SAVE_BUTTON_INACTIVE");

			KaleoFormsAdmin.assignForm(
				kfProcessName = ${kfProcessName},
				workflowTask = ${workflowTask});

			KaleoFormsAdmin.addForm(kfFormName = ${kfFormName});

			KaleoFormsAdmin.saveForm();

			KaleoFormsAdmin.chooseForm(kfFormName = ${kfFormName});

			AssertElementPresent(locator1 = "Button#SAVE");

			KaleoFormsAdmin.saveProcess(kfProcessName = ${kfProcessName});

			Alert.viewSuccessMessage();
		}
	}

	@summary = "Default summary"
	macro viewFormEdit(kfFormNameEdit = null) {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit Form",
			rowEntry = ${kfFormNameEdit});

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		AssertTextEquals(
			locator1 = "DDMEditStructure#NAME_FIELD",
			value1 = ${kfFormNameEdit});
	}

	@summary = "Default summary"
	macro viewFormsNotPresent(workflowTask = null) {
		DynamicDataMapping.selectDynamicDataMappingFrame();

		var key_workflowTask = ${workflowTask};

		AssertElementNotPresent(locator1 = "KaleoFormsAdminEditProcess#FORMS_TABLE_TASK");

		PortletEntry.viewEmptyMessage(entity = "Templates");

		SelectFrame(value1 = "relative=top");
	}

	@summary = "Default summary"
	macro viewInvalidDefinitionMessage() {
		Alert.viewRequestFailedToComplete();

		AssertTextEquals.assertPartialText(
			locator1 = "Message#ERROR_1",
			value1 = "Please add at least one field.");
	}

	@summary = "Default summary"
	macro viewNewProcessProgressBar(kfProgressName = null,kfProgressStep = null) {
		var key_kfProgressName = ${kfProgressName};

		AssertTextEquals(
			locator1 = "KaleoFormsAdminEditProcess#FORM_NAVIGATOR_PROGRESS_BAR_NAME",
			value1 = ${kfProgressName});

		var key_kfProgressName = StringUtil.lowerCase(${kfProgressName});

		AssertTextEquals(
			locator1 = "KaleoFormsAdminEditProcess#FORM_NAVIGATOR_PROGRESS_BAR_STEP",
			value1 = ${kfProgressStep});
	}

	@summary = "Default summary"
	macro viewNextFieldSetPage() {
		Click(locator1 = "Pagination#NEXT_LINK");
	}

	@summary = "Default summary"
	macro viewNoDuplicateForms(kfFormName = null) {
		var key_kfFormName = ${kfFormName};

		AssertTextEquals(
			locator1 = "KaleoFormsAdminEditProcessForm#FORM_TABLE_NAME",
			value1 = ${kfFormName});

		AssertElementNotPresent(locator1 = "KaleoFormsAdminEditProcessForm#FORM_TABLE_NAME_2");
	}

	@summary = "Default summary"
	macro viewNoPublishedWorkflowDefinitions() {
		AssertVisible(locator1 = "KaleoFormsAdmin#KALEO_WORKFLOW_PUBLISHED_EMPTY_MESSAGE");
	}

	@summary = "Default summary"
	macro viewNotSelectedFieldSet(kfFieldSetName = null,key_kfFieldSetName = null) {
		AssertElementNotPresent(
			key_kfFieldSetName = ${kfFieldSetName},
			locator1 = "KaleoFormsAdminEditProcess#SELECTED_FIELD_SET",
			value1 = ${key_kfFieldSetName});
	}

	@summary = "Default summary"
	macro viewNotSelectedWorkflow(workflowDefinitionTitle = null) {
		AssertTextNotEquals(
			locator1 = "KaleoFormsAdminEditProcess#SELECTED_WORKFLOW",
			value1 = ${workflowDefinitionTitle});
	}

	@summary = "Default summary"
	macro viewNoUnpublishedWorkflowDefinitions() {
		KaleoFormsAdmin.viewUnpublishedWorkflowDefinitions();

		AssertVisible(locator1 = "KaleoFormsAdmin#KALEO_WORKFLOW_UNPUBLISHED_EMPTY_MESSAGE");
	}

	@summary = "Default summary"
	macro viewPermissionsAndAssertModal() {
		LexiconEntry.gotoEllipsisMenuItem(menuItem = "Permissions");

		AssertElementPresent(
			locator1 = "Portlet#MODAL_TITLE",
			value1 = "Permissions");

		AssertElementPresent(locator1 = "WorkflowDefinition#DEFINITION_TABLE_TITLE_PERMISSIONS");

		IFrame.selectPermissionsFrame();

		PortletEntry.save();

		Alert.viewSuccessMessage();
	}

	@summary = "Default summary"
	macro viewPreviousFieldSetPage() {
		Click(locator1 = "Pagination#FIRST_LINK");
	}

	@summary = "Default summary"
	macro viewProcess(kfProcessName = null) {
		var key_kfProcessName = ${kfProcessName};

		AssertTextEquals(
			locator1 = "KaleoFormsAdmin#PROCESS_TABLE_NAME",
			value1 = ${kfProcessName});

		var key_rowEntry = ${kfProcessName};

		AssertElementPresent(locator1 = "Icon#ROW_VERTICAL_ELLIPSIS");
	}

	@summary = "Default summary"
	macro viewProcessByPosition(title = null,order = null) {
		AssertElementPresent(
			key_order = ${order},
			key_title = ${title},
			locator1 = "AssetWorkflow#DETAILS_WORKFLOW_TITLE_ORDER");
	}

	@summary = "Default summary"
	macro viewProcessDetails(kfProcessName = null,kfProcessDescription = null) {
		KaleoFormsAdmin.editProcess(kfProcessName = ${kfProcessName});

		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Details",
			kfProgressStep = 1);

		var key_fieldName = "name";

		AssertTextEquals(
			locator1 = "FormFields#TEXT_FIELD",
			value1 = ${kfProcessName});

		var key_fieldName = "Description";

		AssertTextEquals(
			locator1 = "KaleoFormsAdmin#DESCRIPTION_FIELD",
			value1 = ${kfProcessDescription});

		KaleoFormsAdmin.next();
	}

	@summary = "Default summary"
	macro viewProcessFieldSet(kfFieldSetName = null) {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Fields",
			kfProgressStep = 2);

		var key_kfFieldSetName = ${kfFieldSetName};

		AssertTextEquals(
			locator1 = "KaleoFormsAdminEditProcess#SELECTED_FIELD_SET",
			value1 = ${kfFieldSetName});

		KaleoFormsAdmin.next();
	}

	@summary = "Default summary"
	macro viewProcessForms(kfFormName = null) {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Forms",
			kfProgressStep = 4);

		var key_kfFormName = ${kfFormName};

		AssertTextEquals(
			locator1 = "KaleoFormsAdminEditProcessForm#FORM_TABLE_NAME",
			value1 = ${kfFormName});

		AssertElementNotPresent(locator1 = "Button#SAVE_INACTIVE");

		AssertElementPresent(locator1 = "Button#SAVE");
	}

	@summary = "Default summary"
	macro viewProcessFormsDeleted(kfFormName = null,workflowTask = null,kfPendingForm = null,kfProcessName = null) {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Forms",
			kfProgressStep = 4);

		KaleoFormsAdmin.assignForm(
			kfPendingForm = ${kfPendingForm},
			kfProcessName = ${kfProcessName},
			workflowTask = ${workflowTask});

		AssertElementNotPresent(
			key_kfFormName = ${kfFormName},
			locator1 = "KaleoFormsAdminEditProcessForm#FORM_TABLE_NAME_LINK");
	}

	@summary = "Default summary"
	macro viewProcessNameByColumns(kfInstantMessenger = null,kfStatus = null,kfFirstName = null,kfEmailName = null,kfCompanyName = null,kfInstantMessengerService = null,kfAuthor = null) {
		if (isSet(kfCompanyName)) {
			AssertElementPresent(
				key_itemPosition = 2,
				key_kfProcessName = ${kfCompanyName},
				locator1 = "KaleoFormsAdmin#PROCESS_TABLE_NAME_BY_INDEX");
		}

		if (isSet(kfEmailName)) {
			AssertElementPresent(
				key_itemPosition = 3,
				key_kfProcessName = ${kfEmailName},
				locator1 = "KaleoFormsAdmin#PROCESS_TABLE_NAME_BY_INDEX");
		}

		if (isSet(kfFirstName)) {
			AssertElementPresent(
				key_itemPosition = 4,
				key_kfProcessName = ${kfFirstName},
				locator1 = "KaleoFormsAdmin#PROCESS_TABLE_NAME_BY_INDEX");
		}

		if (isSet(kfInstantMessengerService)) {
			AssertElementPresent(
				key_itemPosition = 5,
				key_kfProcessName = ${kfInstantMessengerService},
				locator1 = "KaleoFormsAdmin#PROCESS_TABLE_NAME_BY_INDEX");
		}

		if (isSet(kfInstantMessenger)) {
			AssertElementPresent(
				key_itemPosition = 6,
				key_kfProcessName = ${kfInstantMessenger},
				locator1 = "KaleoFormsAdmin#PROCESS_TABLE_NAME_BY_INDEX");
		}

		if (isSet(kfStatus)) {
			AssertElementPresent(
				key_itemPosition = 7,
				key_kfProcessName = ${kfStatus},
				locator1 = "KaleoFormsAdmin#PROCESS_TABLE_NAME_BY_INDEX");
		}

		if (isSet(kfAuthor)) {
			AssertElementPresent(
				key_itemPosition = 9,
				key_kfProcessName = ${kfAuthor},
				locator1 = "KaleoFormsAdmin#PROCESS_TABLE_NAME_BY_INDEX");
		}
	}

	@summary = "Default summary"
	macro viewProcessNoRecords(kfProcessName = null) {
		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No ${kfProcessName} records were found");
	}

	@summary = "Default summary"
	macro viewProcessRecord(kfProcessName = null) {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "View",
			rowEntry = ${kfProcessName});
	}

	@summary = "Default summary"
	macro viewProcessRecordDetails(kfStatus = null,kfProcessFieldData = null) {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "View",
			rowEntry = ${kfProcessFieldData});

		AssertTextEquals(
			locator1 = "AssetWorkflow#WORKFLOW_STATUS",
			value1 = ${kfStatus});
	}

	@summary = "Default summary"
	macro viewProcessRecordDetailsField(fieldName = null,kfProcessFieldData = null) {
		var key_fieldName = ${fieldName};
		var key_kfProcessFieldData = ${kfProcessFieldData};

		AssertTextEquals.assertPartialText(
			locator1 = "KaleoFormsAdminViewProcessRecordsDetails#CURRENT_RECORD_FIELD",
			value1 = ${kfProcessFieldData});
	}

	@summary = "Default summary"
	macro viewProcessRecordTable(kfStatus = null,kfAuthor = null,kfProcessFieldData = null) {
		var key_kfAuthor = ${kfAuthor};

		AssertTextEquals(
			locator1 = "KaleoFormsAdminViewProcessRecords#RECORD_TABLE_AUTHOR",
			value1 = ${kfAuthor});

		var key_kfProcessFieldData = ${kfProcessFieldData};

		AssertTextEquals(
			locator1 = "KaleoFormsAdminViewProcessRecords#RECORD_TABLE_RECORD",
			value1 = ${kfProcessFieldData});

		var key_kfStatus = ${kfStatus};

		AssertTextEquals(
			locator1 = "KaleoFormsAdminViewProcessRecords#RECORD_TABLE_STATUS",
			value1 = ${kfStatus});
	}

	@summary = "Default summary"
	macro viewProcessRecordTableRecordNotPresent(kfProcessFieldData = null) {
		var key_kfProcessFieldData = ${kfProcessFieldData};

		AssertElementNotPresent(locator1 = "KaleoFormsAdminViewProcessRecords#RECORD_TABLE_RECORD");
	}

	@summary = "Default summary"
	macro viewProcessWorkflow(workflowDefinitionTitle = null) {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Workflow",
			kfProgressStep = 3);

		AssertTextEquals(
			locator1 = "KaleoFormsAdminEditProcess#SELECTED_WORKFLOW",
			value1 = ${workflowDefinitionTitle});

		KaleoFormsAdmin.next();
	}

	@summary = "Default summary"
	macro viewProgressBarIsShownCorrectly(iconType = null,stepProcess = null) {
		AssertElementPresent(
			key_iconType = ${iconType},
			key_stepProcess = ${stepProcess},
			locator1 = "KaleoFormsAdminEditProcess#FORM_NAVIGATOR_MULTI_STEP_PROGRESS_BAR");
	}

	@summary = "Default summary"
	macro viewPublishedWorkflowDefinitions() {
		AssertClick(
			key_assetTitle = "Published",
			locator1 = "NavBar#ASSET_TITLE",
			value1 = "Published");
	}

	@summary = "Default summary"
	macro viewSelectedFieldSet(kfFieldSetName = null) {
		var key_kfFieldSetName = ${kfFieldSetName};

		AssertTextEquals(
			locator1 = "KaleoFormsAdminEditProcess#SELECTED_FIELD_SET",
			value1 = ${kfFieldSetName});
	}

	@summary = "Default summary"
	macro viewSelectedLanguage(defaultLanguage = null,infoLaguage = null,dropdownTagLanguage = null) {
		Click(
			key_locale = ${defaultLanguage},
			locator1 = "Translation#CURRENT_LOCALE_NAME");

		AssertElementPresent(
			key_infoLaguage = ${infoLaguage},
			key_tagLanguage = ${dropdownTagLanguage},
			locator1 = "KaleoFormsAdminEditProcess#DROPDOWN_LANGUAGE");

		Click(
			key_locale = ${defaultLanguage},
			locator1 = "Translation#CURRENT_LOCALE_DESCRIPTION");

		AssertElementPresent(
			key_infoLaguage = ${infoLaguage},
			key_tagLanguage = ${dropdownTagLanguage},
			locator1 = "KaleoFormsAdminEditProcess#DROPDOWN_LANGUAGE");
	}

	@summary = "Default summary"
	macro viewSelectedWorkflow(workflowDefinitionTitle = null,edit = null) {
		if (${edit} == "true") {
			AssertTextEquals(
				locator1 = "KaleoFormsAdminEditProcess#SELECTED_WORKFLOW",
				value1 = ${workflowDefinitionTitle});
		}
		else {
			AssertTextEquals(
				locator1 = "KaleoFormsAdminEditProcess#SELECTED_WORKFLOW",
				value1 = ${workflowDefinitionTitle});
		}
	}

	@summary = "Default summary"
	macro viewTableDisplaysColumns() {
		for (var tableHeader : list "ID,Name,Description,Modified Date") {
			AssertElementPresent(
				key_tableHeader = ${tableHeader},
				locator1 = "LexiconTable#TABLE_HEADER");
		}
	}

	@summary = "Default summary"
	macro viewTaskNotPresent(workflowTask = null) {
		var key_workflowTask = ${workflowTask};

		AssertElementNotPresent(locator1 = "KaleoFormsAdminEditProcess#FORMS_TABLE_TASK");
	}

	@summary = "Default summary"
	macro viewTaskPresent(workflowTask = null) {
		var key_workflowTask = ${workflowTask};

		AssertElementPresent(locator1 = "KaleoFormsAdminEditProcess#FORMS_TABLE_TASK");
	}

	@summary = "Default summary"
	macro viewUnpublishedWorkflowDefinitions() {
		AssertClick(
			key_assetTitle = "Unpublished",
			locator1 = "NavBar#ASSET_TITLE",
			value1 = "Unpublished");
	}

	@summary = "Default summary"
	macro viewWorkflowCannotBeUnpublish(workflowDefinitionTitle = null) {
		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Unpublish",
			rowEntry = ${workflowDefinitionTitle});

		Alert.viewErrorMessage(errorMessage = "Unable to delete the workflow definition that is in use.");
	}

}