import com.liferay.gradle.plugins.patcher.PatchTask
import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.OSDetector

import groovy.io.FileType

apply plugin: "com.liferay.patcher"

task deleteSrcDirectory(type: Delete)
task downloadSrc(type: Exec)
task jarManifest
task jarPatched(type: Zip)
task patch(type: PatchTask)
task unzip(type: Copy)

String openSearchRestClientVersion = "2.7.0"
String srcUrl = "https://repo1.maven.org/maven2/org/opensearch/client/opensearch-rest-client/${openSearchRestClientVersion}/opensearch-rest-client-${openSearchRestClientVersion}-sources.jar"
String tempDir ="build/tmp/zip"

compileJava {
	dependsOn downloadSrc
	dependsOn unzip
}

deleteSrcDirectory {
	delete file('src/main/java/opensearch')
}

dependencies {
	api group: "commons-codec", name: "commons-codec", version: "1.15"
	api group: "commons-logging", name: "commons-logging", version: "1.2"
	api group: "org.apache.httpcomponents", name: "httpasyncclient", version: "4.1.5"
	api group: "org.apache.httpcomponents", name: "httpclient", version: "4.5.13"
	api group: "org.apache.httpcomponents", name: "httpcore", version: "4.4.15"
	api group: "org.apache.httpcomponents", name: "httpcore-nio", version: "4.4.15"

	compileOnly group: "org.opensearch.client", name: "opensearch-rest-client", transitive: false, version: "2.7.0"
}

downloadSrc {
	File srcTmpDir = new File(temporaryDir, "src")

	if (OSDetector.windows) {
		executable "cmd.exe"

		args "/c"
	}
	else {
		executable "/bin/sh"

		args "-c"
	}

	args "wget '${srcUrl}' -P '${tempDir}'"
}

jar {
	dependsOn downloadSrc
	dependsOn unzip
	dependsOn compileJava
	dependsOn jarPatched
	finalizedBy deleteSrcDirectory
	setActions([])
}

jarPatched {
	archiveFileName = jar.archiveFileName
	destinationDirectory = jar.destinationDirectory
	duplicatesStrategy = 'exclude'

	exclude "module-info.class"

	from sourceSets.main.output

	from {
		zipTree(configurations.compileOnly.singleFile)
	}

	mustRunAfter compileJava
	mustRunAfter generatePomFileForMavenPublication
	mustRunAfter processResources
}

jarSources {
	mustRunAfter patch
}

patch {
	ext {
		autoClean = false
	}

	fileNames "org/opensearch/client/RestClient.java"

	originalLibModuleName = "opensearch-rest-client"
}

unzip {
	dependsOn downloadSrc

	doLast {
		File srcTmpDir = new File("${tempDir}/src/org")

		srcTmpDir.eachFileRecurse(FileType.FILES) {
			if (it.name == "RestClient.java") {
				return
			}

			if (it.name.endsWith(".java")) {
				File destFile = new File(file("src/main/java"), FileUtil.relativize(it, srcTmpDir))

				destFile.parentFile.mkdirs()

				it.renameTo(destFile)
			}
		}
	}

	ext {
		autoClean = false
	}

	from zipTree(file("${tempDir}/opensearch-rest-client-${openSearchRestClientVersion}-sources.jar"))
	into file("${tempDir}/src")
}