task jarOverwrite(type: Jar)

String gradleVersion = version.substring(0, version.lastIndexOf('.'))

if (gradleVersion != gradle.gradleVersion) {
	throw new GradleException("Unable to use Gradle " + gradle.gradleVersion + ", " + gradleVersion + " is required")
}

tasks.eclipse {
	ext.gradleVersion = gradleVersion
}

dependencies {
	api fileTree(dir: gradle.gradleHomeDir, include: "lib/**/*.jar")
	api group: "com.liferay", name: "com.liferay.gradle.util", version: "1.0.49"
	api group: "org.apache.maven", name: "maven-artifact", version: "3.6.3"
}

jar {
	setActions([])

	dependsOn jarOverwrite
}

jarOverwrite {
	archiveBaseName = "org-gradle-resources-http"
	duplicatesStrategy = "exclude"
	from sourceSets.main.output

	from zipTree(
		{
			configurations.runtimeClasspath.find {
				it.name.startsWith "gradle-resources-http-"
			}
		})

	mustRunAfter generatePomFileForMavenPublication
}