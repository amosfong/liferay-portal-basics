import com.liferay.gradle.plugins.patcher.PatchTask
import com.liferay.gradle.util.FileUtil

apply plugin: "com.liferay.patcher"

task createJar(type: Jar)
task downloadSrc(type: Copy)
task patch(type: PatchTask)
task unzip(type: Copy)

createJar {
	dependsOn downloadSrc
	dependsOn unzip

	archiveFileName = 'com-liferay-daisydiff-1.2-sources.jar'
	destinationDirectory = file("$buildDir")

	from "$buildDir/src/daisydiff/tags/v1.2/src/java"
}

dependencies {
	api group: "com.liferay", name: "org.outerj.daisy.daisydiff", version: "1.2"

	compileOnly group: "net.sourceforge.htmlunit", name: "neko-htmlunit", transitive: false, version: "2.66.0"
	compileOnly group: "xerces", name: "xercesImpl", transitive: false, version: "2.12.1"
}

downloadSrc {
	from FileUtil.get(project, "https://storage.googleapis.com/google-code-archive-source/v2/code.google.com/daisydiff/source-archive.zip")
	into new File("$buildDir")
}

jarSources {
	mustRunAfter patch
}

patch {
	dependsOn downloadSrc
	dependsOn unzip
	dependsOn createJar

	ext {
		autoClean = false
	}

	fileNames "org/outerj/daisy/diff/helper/NekoHtmlParser.java"

	originalLibModuleName = "org.outerj.daisy.daisydiff"
	originalLibSrcFile = "$buildDir/com-liferay-daisydiff-1.2-sources.jar"
}

unzip {
	dependsOn downloadSrc

	from zipTree(file("$buildDir/source-archive.zip"))
	into file("$buildDir/src")
}