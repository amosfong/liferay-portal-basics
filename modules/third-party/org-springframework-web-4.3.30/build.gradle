import com.liferay.gradle.plugins.patcher.PatchTask
apply plugin: "com.liferay.patcher"

task jarManifest
task jarPatched(type: Zip)
task patch(type: PatchTask)

dependencies {
	api group: "commons-logging", name: "commons-logging", version: "1.2"
	api group: "org.apache.felix", name: "org.apache.felix.http.servlet-api", version: "1.1.2"
	api group: "org.springframework", name: "spring-core", version: "4.3.30.RELEASE"

	compileOnly group: "org.springframework", name: "spring-web", transitive: false, version: "4.3.30.RELEASE"
}

jar {
	setActions([])

	dependsOn jarPatched
	finalizedBy jarManifest
}

jarManifest {
	doLast {
		ant.jar(destfile: jar.archivePath, update: true)
	}
}

jarPatched {
	archiveFileName = jar.archiveFileName
	destinationDirectory = jar.destinationDirectory
	duplicatesStrategy = "exclude"

	exclude "org/springframework/web/util/UriComponentsBuilder*.class"

	from sourceSets.main.output

	from {
		zipTree(configurations.compileOnly.singleFile)
	}

	mustRunAfter compileJava
	mustRunAfter generatePomFileForMavenPublication
	mustRunAfter processResources
}

jarSources {
	mustRunAfter patch
}

patch {
	ext {
		autoClean = false
	}

	fileNames "org/springframework/http/HttpHeaders.java"
	fileNames "org/springframework/web/context/request/ServletWebRequest.java"

	originalLibModuleName = "spring-web"
}