import com.liferay.gradle.plugins.patcher.PatchTask

apply plugin: "com.liferay.patcher"

task deployJar(type: Copy)
task jarManifest
task jarPatched(type: Zip)
task patch(type: PatchTask)

dependencies {
	api group: "org.hibernate", name: "hibernate-core", version: "5.6.7.Final"
	api group: "org.springframework", name: "spring-orm", version: "5.3.39"
}

deploy {
	finalizedBy deployJar
}

deployJar {
	ext {
		autoClean = false
	}

	from jar
	into "../../../lib/portal"

	rename {
		"spring-orm.jar"
	}
}

jar {
	setActions([])

	dependsOn jarPatched
}

jarManifest {
	doLast {
		ant.jar(destfile: jar.archivePath, update: true)
	}
}

jarPatched {
	archiveFileName = jar.archiveFileName
	destinationDirectory = jar.destinationDirectory
	duplicatesStrategy = "exclude"

	from copyPatchOriginalLibClasses

	mustRunAfter compileJava
	mustRunAfter generatePomFileForMavenPublication
	mustRunAfter processResources
}

jarSources {
	mustRunAfter patch
}

javadoc {
	enabled = false
}

patch {
	ext {
		autoClean = false
	}

	fileNames "org/springframework/orm/hibernate5/SessionFactoryUtils.java"
	fileNames "org/springframework/orm/hibernate5/SpringSessionSynchronization.java"

	originalLibModuleName = "spring-orm"
}