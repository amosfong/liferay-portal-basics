task jarManifest
task jarPatched(type: Zip)

dependencies {
	compileOnly group: "org.apache.logging.log4j", name: "log4j-api", transitive: false, version: "2.17.1"
}

jar {
	setActions([])

	dependsOn jarPatched
	finalizedBy jarManifest
}

jarManifest {
	doLast {
		ant.jar(destfile: jar.archivePath, update: true)
	}
}

jarPatched {
	archiveFileName = jar.archiveFileName
	destinationDirectory = jar.destinationDirectory
	duplicatesStrategy = "exclude"

	exclude "META-INF/versions/9/module-info.class"
	exclude "META-INF/versions/9/org/apache/logging/log4j/util/StackLocator.class"
	exclude "META-INF/versions/9/org/apache/logging/log4j/util/StackLocator\$FqcnCallerLocator*"
	exclude "META-INF/versions/9/org/apache/logging/log4j/util/ProcessIdUtil.class"
	exclude "META-INF/versions/9/org/apache/logging/log4j/util/Base64Util.class"
	exclude "META-INF/versions/9/org/apache/logging/log4j/util/internal/DefaultObjectInputFilter.class"

	from sourceSets.main.output

	from {
		zipTree(configurations.compileOnly.singleFile)
	}

	mustRunAfter compileJava
	mustRunAfter generatePomFileForMavenPublication
	mustRunAfter processResources
}